100 REMark $$asmb=dev1_booty_outptr_bin,0,10
110 REMark $$asmb=dev1_booty_qptr_bin,50,172
120 REMark $$asmb=dev8_extras_exe_source_SMSQEMake_bin,0,10
130 REMark $$stak=20000
140 compiled=NOT IS_OPEN(#0)
150 IF NOT compiled
160   SCR_SIZE#0, xsz%,ysz%
170   OUTLN#0,xsz%,ysz%-28,0,28
180   WSET -1
190 END IF
200 init
210 main
220 STOP
230 DEFine PROCedure make_new_dir (poste%)
240 REMark get athe new dir where the files are located
250 LOCal quel_rep$
260   IF MENU_OK
270     quel_rep$= DIR_SELECT$("The Files are where?",,10,10,main_coul%,app_coul%)
280   ELSE
290     quel_rep$=getstr$(poste%,0,main_defn,a_lire$)       : REMark use menu extns if possible
300   END IF
310   IF LEN (quel_rep$) >= 5
320      rep$=quel_rep$(1 TO 5)
330   END IF
340   CH_ITEM main_defn,-1,poste%,-1,"",rep$
350   main_lfl%(poste%)=main_lfl%(poste%)+1
360 END DEFine make_new_dir
370 :
380 DEFine PROCedure main
390 LOCal lp%,item%,event%,swnum%,xrel%,yrel%,item%,lp2%,tot%
400   IF compiled
410      OPEN#1,'con'
420      SCR_SIZE#1,xsz%,ysz%
430      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
440   ELSE
450      SCR_SIZE#1,xsz%,ysz%
460      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
470   END IF
480   POS_PTR main_defn,40*65536+20,1
490   REPeat lp%
500     RD_PTR main_defn,item%,swnum%,event%,xrel%,yrel%,main_lfl%,main_mfl%,main_cty%
510     IF swnum%= -1
520       SELect ON item%
530               =esc%     : EXIT lp%      : REMark quit prog
540               =move_it% : CH_WIN main_defn: REMark move
550               =size_it% : change_size xrel%,yrel% : REMark change size
560               =sleep_it%: button main_defn,main_lfl%,main_mfl%,main_cty%,"SMSQ/E Make"
570               =all_it%
580                  set_statusses          : REMark set all appsub item statusses
590                  main_cty%(0,1)=1       : REMark and redraw
600               =go_it%   : do_the_work
610               =dir_it%: make_new_dir item%
620               =target_its% TO target_its%+4
630                           start%=1:machines%=0
640                           FOR lp2%=target_its% TO target_its%+4
650                               IF main_lfl%(lp2%):machines%=machines%+start%
660                               start%=start%*2
670                           END FOR lp2%
680                           make_my_data machines%        : REMark make data arrays
690                           mk_main_app                   : REMark make appsub wdw
700                           main_app_list=app_addr+180
710                           AP_IN main_defn,main_app_list : REMark put it in
720                           DR_WDRW main_defn:main_cty%(0,1)=1 : REMark and redraw entire wdw
730       END SELect
740     END IF
750   END REPeat lp%
760   DR_UNST main_defn
770   IF compiled:CLOSE#1
780   RECHP espace_vide
790 END DEFine main
800 :
810 DEFine PROCedure button (def_trav,menu_drap%,app_drap%,control%,chaine$)
820 REMark set up as button
830   DR_UNST def_trav                      : REMark no more working defn
840   IF compiled: CLOSE#1                  : REMark more wdw
850   BUTTON_WAIT chaine$                   : REMark use menu extensions
860   IF compiled
870     OPEN#1,"CON"
880     DR_PPOS def_trav,-1,-1,menu_drap%,app_drap%,control%
890   ELSE
900     DR_PULD def_trav,-1,-1,menu_drap%,app_drap%,control%
910   END IF
920 END DEFine button
930 :
940 DEFine PROCedure mk_main_app
950 REMark make main application sub window
960 LOCal t2%,temp%,sections%,att_phys%(3)
970   DIM main_cty%(0):DIM main_mfl%(0,0)
980   sections%=2
990   temp%=sections% :t2%=rangees
1000   att_phys%(0)=xsize%-16                : REMark xsize calculated on main wdw x size
1010   att_phys%(1)=t2%                      : REMark y size,nominal,in rows
1020   att_phys%(2)=4                        : REMark x origin x
1030   att_phys%(3)=inf1y +appwinoffset%     : REMark y origin
1040   app_addr=fen_app2(app_addr,mdata$,temp%,1,att_phys%,"DO ")
1050   IF app_addr
1060       DIM main_cty%(sections%,2)        : REMark ctrl defn
1070       main_cty%(0,0)=1                  : REMark show 1 section
1080       main_cty%(1,2)=t2%
1090       IF sections%=2:main_cty%(2,2)=t2% : REMark but allow 2
1100       IF temp%
1110            DIM main_mfl%(temp%-1,0)     : REMark flag array
1120            set_statusses                : REMark and set all statusses
1130       ELSE
1140            POKE_W app_addr,PEEK_W(app_addr)+8
1150       END IF
1160    REMark   scroll_arrows t2%,temp%,app_addr,main_cty%
1170   END IF
1180 END DEFine mk_main_app
1190 :
1200 DEFine PROCedure set_statusses
1210 REMark set all statusses of menu appsub objects
1220 LOCal temp%,lp%,stat%
1230   temp%=DIMN(main_mfl%,1)               : REMark how many are there?
1240   stat%=main_lfl%(all_it%)              : REMark status of "all" item
1250   FOR lp%=0 TO temp%
1260     IF mdata$(lp%+1,1)='*'
1270       main_mfl%(lp%,0)=16               : REMark comments set to unavailable
1280     ELSE
1290       main_mfl%(lp%,0)=stat%            : REMark all other to status
1300     END IF
1310   END FOR lp%
1320 END DEFine set_statusses
1330 :
1340 DEFine PROCedure make_my_data (what_machine%)
1350 REMark what_machine% = 1 -> generic
1360 REMark what_machine% = 2 -> atari
1370 REMark what_machine% = 4 -> Gold Card
1380 REMark what_machine% = 8 -> Qx0
1390 REMark what_machine% = 16 -> QXL
1400 REMark these may be combined
1410   DIM mdata$(100,50)
1420   REMark first make generic data
1430   data_count%=1                         : REMark general data count (global var)
1440   IF what_machine%&&1
1450     generic_data                        : REMark set up generic data
1460     read_my_datas                       : REMark and read it
1470   END IF
1480   IF what_machine%&&2
1490      atari_data                         : REMark set up machine specific data....
1500      read_my_datas                      : REMark and read it
1510   END IF
1520   IF what_machine%&&4
1530      gc_data                            : REMark set up machine specific data....
1540      read_my_datas                      : REMark and read it
1550   END IF
1560   IF what_machine%&&8
1570      q40_data                           : REMark set up machine specific data....
1580      read_my_datas                      : REMark and read it
1590   END IF
1600   IF what_machine%&&16
1610      qxl_data                           : REMark set up machine specific data....
1620      read_my_datas                      : REMark and read it
1630   END IF
1640 END DEFine make_my_data
1650 :
1660 DEFine PROCedure read_my_datas
1670 REMark reads datas into the global var mdata$
1680 REMark uses data_count% from make_my_data
1690 LOCal lp%
1700   REPeat lp%
1710     READ mdata$(data_count%)
1720     IF mdata$(data_count%)="":EXIT lp%
1730     data_count%=data_count%+1
1740   END REPeat lp%
1750 END DEFine read_my_datas
1760 :
1770 DEFine PROCedure generic_data
1780 RESTORE 1780
1790   DATA '**** -- SMSQ Generic -- ****'
1800   DATA 'smsq_smsq_loader_link'
1810   DATA 'smsq_smsq_fh_link'
1820   DATA 'smsq_smsq_link'
1830   DATA 'smsq_smsq_cache_link'
1840   DATA 'smsq_sbas_link'
1850   DATA 'smsq_smsq_lang_link'
1860   DATA 'smsq_sbas_lang_link'
1870   DATA 'smsq_sbas_procs_link'
1880   DATA 'smsq_smsq_wman_link'
1890   DATA 'smsq_smsq_hotkey_link'
1900   DATA 'smsq_smsq_vers_link'
1910   DATA 'smsq_smsq_1mb_link',''
1920 END DEFine generic_data
1930 :
1940 DEFine PROCedure q40_data
1950 RESTORE 1950
1960   DATA '**** -- Q40/Q60 -- ****'
1970   DATA 'smsq_q40_hwinit_link'
1980   DATA 'smsq_q40_nasty_link'
1990   DATA 'smsq_q40_cache_link'
2000   DATA 'smsq_q40_cachemode_link'
2010   DATA 'smsq_smsq_cache40c_link'
2020   DATA 'smsq_q40_driver_ser_link'
2030   DATA 'smsq_q40_driver_dv3_link'
2040   DATA 'smsq_q40_driver_ql_link'
2050   DATA 'smsq_q40_driver_16_link'
2060   DATA 'smsq_q40_kbd_lang_link'
2070   DATA 'smsq_sbas_procs_q60_link'
2080   DATA 'smsq_q40_sysspr_link',''
2090 END DEFine q40_data
2100 :
2110 DEFine PROCedure atari_data
2120 RESTORE 2120
2130   DATA '**** -- Atari -- ****'
2140   DATA 'smsq_atari_hwinit_link'
2150   DATA 'smsq_atari_nasty_link'
2160   DATA 'smsq_atari_driver_ql_link'
2170   DATA 'smsq_atari_driver_mo_link'
2180   DATA 'smsq_atari_driver_ser_link'
2190   DATA 'smsq_atari_driver_dv3_link'
2200   DATA 'smsq_atari_kbd_lang_link'
2210   DATA 'smsq_atari_sysspr_link'
2220   DATA 'sys_boot_st_host_link',''
2230 END DEFine atari_data
2240 :
2250 DEFine PROCedure gc_data
2260 RESTORE 2260
2270   DATA '**** -- (Super)GoldCard -- ****'
2280   DATA 'smsq_gold_host_link'
2290   DATA 'smsq_gold_hwinit_link'
2300   DATA 'smsq_gold_nasty_link'
2310   DATA 'smsq_gold_nasty_s_link'
2320   DATA 'smsq_gold_driver_ql_link'
2330   DATA 'smsq_gold_driver_most_link'
2340   DATA 'smsq_gold_driver_nd_link'
2350   DATA 'smsq_gold_driver_nds_link'
2360   DATA 'smsq_gold_driver_dv3_link'
2370   DATA 'smsq_gold_kbd_lang_link'
2380   DATA 'smsq_gold_kbd_abc_lng_link'
2390   DATA 'smsq_gold_kbd_abc_link'
2400   DATA 'smsq_gold_roms_link'
2410   DATA 'smsq_gold_qimi_link'
2420   DATA 'smsq_gold_sysspr_link',''
2430 END DEFine gc_data
2440 :
2450 DEFine PROCedure qxl_data
2460 RESTORE 2460
2470   DATA '**** -- QXL -- ****'
2480   DATA 'smsq_qxl_host_link'
2490   DATA 'smsq_qxl_hwinit_link'
2500   DATA 'smsq_qxl_nasty_e_link'
2510   DATA 'smsq_qxl_driver_ql_link'
2520   DATA 'smsq_qxl_driver_16_link'
2530   DATA 'smsq_qxl_driver_most_link'
2540   DATA 'smsq_qxl_driver_nd_link'
2550   DATA 'smsq_qxl_procs_link'
2560   DATA 'smsq_qxl_driver_dv3e_link'
2570   DATA 'smsq_qxl_kbd_lang_link'
2580   DATA 'smsq_qxl_ecache_link'
2590   DATA 'smsq_qxl_sysspr_link',''
2600 END DEFine qxl_data
2610 :
2620 DEFine PROCedure do_the_work
2630 REMark this tries to compile all of the selected files
2640 REMark if the file was compiled OK, status (ogf the link file) set to available
2650 REMark if the file was not compiled ok, status left selected
2660 LOCal lp%,bound%,a$,my_error%,tot_err%
2670   bound%=DIMN(main_mfl%,1)              : REMark number of items to check
2680   tot_err%=0
2690   FOR lp%=0 TO bound%
2700     a$=mdata$(lp%+1)                    : REMark potential file to compile
2710     IF a$(1)<>"*" AND main_mfl%(lp%,0)
2720        a$=rep$&a$                       : REMark add dir
2730        my_error%=compile_and_check% (a$): REMark try to compile
2740        IF NOT my_error%
2750            main_mfl%(lp%,0)=1
2760            DR_ADRW main_defn,0,main_mfl%,main_cty%
2770        END IF
2780        tot_err%=tot_err%+my_error%
2790     END IF
2800   END FOR lp%
2810   main_cty%(0,1)=1
2820   main_lfl%(go_it%)=1
2830   IF main_lfl%(make_it%)=128 AND NOT tot_err%:make_targets
2840 END DEFine do_the_work
2850 :
2860 DEFine FuNction compile_and_check% (a$)
2870 REMark this tries to compile a file, using the "make" prog
2880 REMark this returns 1 if errors, else 0
2890 LOCal chan%,is_ok$,ch_len
2900   EXEP_W "make";a$&" -w -l"             : REMark exec make & wait
2910   is_ok$=a$(1 TO LEN(a$)-4)             : REMark file name w/o extension
2920   chan%=FOP_IN(is_ok$&'log')            : REMark open log file
2930   IF chan%<0:RETurn 1                   : REMark can't even open it!
2940   ch_len=FLEN(#chan%)                   : REMark file length
2950   GET#chan%\ch_len-10                   : REMark this is where error report is
2960   INPUT#chan%,is_ok$                    : REMark get it
2970   CLOSE#chan%
2980   IF NOT is_ok$=="No errors"
2990     RETurn 1                            : REMark anything other than "no errors" signals errors
3000   ELSE
3010     RETurn 0
3020   END IF
3030 END DEFine compile_and_check%
3040 :
3050 DEFine PROCedure make_targets
3060 REMark targets are only made if there was no error during compilation
3070 REMark to achieve this, a primitive approach is used:
3080 REMark a check is made whether an appsub menu item status is still selected
3090 REMark if it is, then the
3100   LOCal lp%,current_target%,tot_targets%
3110   tot_targets%=0                        : REMark no targets to make yet
3120   current_target%=0                     : REMark and no target yet
3130   FOR lp%=0 TO data_count%-2
3140     IF main_mfl%(lp%,0)=16
3150       REMark we're covering a new target
3160       tot_targets%=tot_targets%+current_target% : REMark show old target
3170       current_target%=find_target%(lp%+1) : REMark get new target we're covering
3180     ELSE
3190       IF main_mfl%(lp%,0)<>0:current_target%=0
3200     END IF
3210   END FOR lp%
3220   tot_targets%=tot_targets%+current_target%
3230   REMark at the end, here, tot_targets holds a bitmap
3240   REMark of targets for which compilation was ok
3250   IF (machines%&&1) AND NOT (tot_targets% && 1):RETurn : REMark generic was asked for but not compiled correctly!
3260   IF tot_targets%&&2:atari_target
3270   IF tot_targets%&&4:gc_target
3280   IF tot_targets%&&8:qx0_target
3290   IF tot_targets%&&16:qxl_target
3300 END DEFine make_targets
3310 :
3320 DEFine PROCedure atari_target
3330   cmd$='m'
3340   p$=rep$&'sys_boot_st_flp'
3350   f$=rep$&'sys_boot_file'
3360   ah$=rep$&'sys_boot_st_host'
3370   ld$=rep$&'smsq_smsq_loader'
3380   hi$=rep$&'smsq_atari_hwinit'
3390   os1f$=rep$&'smsq_smsq_fh_os'
3400   os1$=rep$&'smsq_smsq_os'
3410   ca$=rep$&'smsq_smsq_cache'
3420   os2$=rep$&'smsq_sbas_control'
3430   ns1$=rep$&'smsq_atari_nasty'
3440   ln1$=rep$&'smsq_smsq_lang'
3450   ln2$=rep$&'smsq_atari_kbd_lang'
3460   ln3$=rep$&'smsq_sbas_lang'
3470   ex1$=rep$&'smsq_sbas_procs_x'
3480   ex2$=rep$&'smsq_atari_driver_dv3'
3490   ex3$=rep$&'smsq_atari_driver_ser'
3500   ex4$=rep$&'smsq_atari_driver_mono'
3510   ex5$=rep$&'smsq_atari_driver_ql'
3520   ex6$=rep$&'smsq_smsq_wman'
3530   ex7$=rep$&'smsq_smsq_hotkey'
3540   ex8$=rep$&'smsq_atari_sysspr'
3550   nl$=rep$&'sys_boot_null'
3560   IF 'm' INSTR cmd$ = 0: ex4$ = nl$
3570   EW f$, ah$,ld$,hi$,os1f$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4$,ex5$,ex6$,ex7$,ex8$, rep$&'smsq_atari_SMSQ.PRG'
3580 END DEFine atari_target
3590 :
3600 DEFine PROCedure gc_target
3610   f$=rep$&'sys_boot_file'
3620   v$=rep$&'smsq_smsq_vers'
3630   h$=rep$&'smsq_gold_host'
3640   ld$=rep$&'smsq_smsq_loader'
3650   hi$=rep$&'smsq_gold_hwinit'
3660   os1$=rep$&'smsq_smsq_os'
3670   ca$=rep$&'smsq_smsq_cache'
3680   os2$=rep$&'smsq_sbas_control'
3690   ns1$=rep$&'smsq_gold_nasty'
3700   ns2$=rep$&'smsq_gold_nasty_s'
3710   ln1$=rep$&'smsq_smsq_lang'
3720   ln2$=rep$&'smsq_gold_kbd_lang'
3730   ln2a$=rep$&'smsq_gold_kbd_abc_lang'
3740   ln3$=rep$&'smsq_sbas_lang'
3750   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
3760   ex2a$=rep$&'smsq_gold_kbd_abc'
3770   ex2$=rep$&'smsq_gold_driver_most'
3780   ex3a$=rep$&'smsq_gold_driver_ql'
3790   ex3q$=rep$&'smsq_gold_qimi'
3800   ex3w$=rep$&'smsq_smsq_wman'
3810   ex4$=rep$&'smsq_gold_driver_dv3'
3820   ex5$=rep$&'smsq_gold_driver_nd'
3830   ex6$=rep$&'smsq_gold_driver_nds'
3840   ex7$=rep$&'smsq_smsq_hotkey'
3850   ex8$=rep$&'smsq_gold_sysspr'
3860   nl$=rep$&'sys_boot_null'
3870   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex3a$,ex3q$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,ex1$, rep$&'SMSQ_GOLD_gold'
3880 END DEFine gc_target
3890 :
3900 DEFine PROCedure qx0_target
3910   nl$=rep$&'sys_boot_null'
3920   f$=rep$&'sys_boot_file'
3930   qr$=rep$&'sys_boot_q40_rom'
3940   ld$=rep$&'smsq_smsq_loader'
3950   hi$=rep$&'smsq_q40_hwinit'
3960   os1$=rep$&'smsq_smsq_1mb_os' : REMark change for fast memory
3970   ca$=rep$&'smsq_smsq_cache40c'
3980   caq$=rep$&'smsq_q40_cache'
3990   caq4$=rep$&'smsq_q40_cachemode'
4000   os2$=rep$&'smsq_sbas_control'
4010   ns2$=rep$&'smsq_q40_nasty'
4020   ln1$=rep$&'smsq_smsq_lang'
4030   ln2$=rep$&'smsq_q40_kbd_lang'
4040   ln3$=rep$&'smsq_sbas_lang'
4050   ex1$=rep$&'smsq_sbas_procs_x_q60'
4060   ex2$=rep$&'smsq_q40_driver_dv3'
4070   ex3$=rep$&'smsq_q40_driver_ser'
4080   ex4a$=rep$&'smsq_q40_driver_ql'
4090   ex4b$=rep$&'smsq_q40_driver_16'
4100   ex4w$=rep$&'smsq_smsq_wman'
4110   ex5$=rep$&'smsq_q40_sysspr'
4120   ex9$=rep$&'smsq_smsq_hotkey'
4130   EW f$, qr$,ld$,hi$,os1$,ca$,caq$,os2$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4a$,ex4b$,ex4w$,ex5$,ex9$,ns2$,caq4$, rep$&'smsq_q40_rom'; p$
4140 END DEFine qx0_target
4150 :
4160 DEFine PROCedure qxl_target
4170   IF FOP_IN (rep$&'smsq_qxl_qxlh.exe') <> -7: INPUT 'QXLH.EXE exists'; cmd$: QUIT
4180   p$   = rep$&'sys_boot_file'
4190   bl$  = rep$&'smsq_qxl_qxl2_exe'
4200   hst$ = rep$&'smsq_qxl_host'
4210   hi$  = rep$&'smsq_qxl_hwinit'
4220   ld$  = rep$&'smsq_smsq_loader'
4230   os1$ = rep$&'smsq_smsq_os'
4240   ca$  = rep$&'smsq_smsq_cache'
4250   os2$ = rep$&'smsq_sbas_control'
4260   ns1$ = rep$&'smsq_qxl_nasty_e'
4270   ln1$ = rep$&'smsq_smsq_lang'
4280   ln2$ = rep$&'smsq_qxl_kbd_lang'
4290   ln3$ = rep$&'smsq_sbas_lang'
4300   ex1$ = rep$&'smsq_qxl_procs_x'
4310   ex2$ = rep$&'smsq_qxl_driver_most'  : REMark Keyboard before CON
4320   ex3$ = rep$&'smsq_qxl_driver_ql'
4330   ex3b$ = rep$&'smsq_qxl_driver_16'
4340   ex3w$ = rep$&'smsq_smsq_wman'
4350   ex4$ = rep$&'smsq_qxl_driver_nd'
4360   ex5$ = rep$&'smsq_qxl_driver_dv3e'
4370   ex6$= rep$&'smsq_smsq_hotkey'
4380   ex7$ = rep$&'smsq_qxl_sysspr'
4390   last$ = rep$&'smsq_qxl_ecache'
4400   dml$ = rep$&'smsq_qxl_dummy'
4410   qm$  = rep$&'qmon_qxl_bin'
4420   r$='ram1_smsqe.exe' : f$=rep$&'smsq_qxl_smsqe.exe'
4430   DELETE f$
4440   EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3w$,ex4$,ex5$,ex6$,ex7$,last$, r$
4450   EW cct, bl$,r$, f$
4460   DELETE r$
4470   OPEN #4,f$
4480   lenf=FLEN(#4)
4490   lens% = INT ((lenf+511)/512)
4500   lenb% = lenf - lens% * 512
4510   IF lenb%: lenb% = lenb% + 512
4520   BPUT #4\2, lenb% MOD 256, lenb% DIV 256, lens% MOD 256, lens% DIV 256
4530   CLOSE #4
4540 END DEFine qxl_target
4550 :
4560 DEFine FuNction find_target% (current%)
4570   IF "SMSQ Generic" INSTR mdata$(current%):RETurn 1
4580   IF "Atari" INSTR mdata$(current%):RETurn 2
4590   IF "GoldCard" INSTR mdata$(current%):RETurn 4
4600   IF "Q40" INSTR mdata$(current%):RETurn 8
4610   IF "QXL" INSTR mdata$(current%):RETurn 16
4620   RETurn 0                             : REMark huh???
4630 END DEFine find_target%
4640 :
4650 DEFine PROCedure change_size   (x%,y%)
4660 LOCal xrel%,yrel%,temp,a$,ttemp%(main_lit%),abs%,yabs%,t%,bt$
4670   main_lfl%(size_it%)=1                 : REMark reset to available
4680   ARRCOPY main_lfl%,ttemp%              : REMark keep statusses
4690   CH_WIN main_defn,xrel%,yrel%          : REMark ptr displacement
4700   xsize%=xsize%-xrel%
4710   ysize%=ysize%-yrel%                   : REMark new sizes
4720   t%=2^4+2^5
4730   RPTR xabs%,yabs%,t%,t%,xrel%,yrel%,bt$: REMark get absolute coords
4740   IF xsize%>xsz%-16:xsize%=xsz%-16
4750   IF ysize%>ysz%-16:ysize%=ysz%-16      : REMark max values
4760   DR_UNST main_defn                     : REMark no more wdw defn...
4770   DR_REMV main_defn                     : REMark .... now!
4780   app_addr=0                            : REMark nor any appsub wdw
4790   make_main_wdw                         : REMark make window anew
4800   ARRCOPY ttemp%,main_lfl%
4810   t%=lxs*3
4820   xabs%=xabs%-t%:IF xabs%<0:xabs%=0     : REMark position ptr nicely
4830   yabs%=yabs%-8:IF yabs%<0:yabs%=0
4840   SPTR xabs%,yabs%,0                    : REMark now
4850   IF compiled
4860      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
4870   ELSE
4880      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
4890   END IF
4900    POS_PTR main_defn,t%*65536+8,1       : REMark position pointer in wdw
4910 END DEFine change_size
4920 :
4930 REMark as of here, standard functions/procs (except init)
4940 :
4950 DEFine FuNction warning_pos% (mess$,channel%,title$,pos)
4960 REMark warning wdw with positioning
4970   POS_PTR main_defn,pos,-1
4980   RETurn warning%(mess$,channel%,title$)
4990 END DEFine warning_pos%
5000 :
5010 DEFine FuNction warning%(mess$,channel%,title$)
5020 REMark shows a warning message (mess$) in a small window. Hit ESC or OK
5030 REMark to return; returns 0 if ESC, 1 if OK  hit
5040 LOCal item%,lp%,chan%,y%,swnum%
5050   IF channel%
5060     chan%=channel%
5070   ELSE
5080     chan%=FOPEN("con")
5090   END IF
5100   CH_ITEM warn_defn,-3,0,-1,'','Attention'
5110   CH_ITEM warn_defn,-1,0,-1,cancel$,'ESC'
5120   CH_ITEM warn_defn,-1,1,-1,'O','OK'
5130   warn_lfl%(0)=1:warn_lfl%(1)=1
5140   POKE_W warn_defn+46,pink                        : REMark paper colour of wdw
5150   POKE_W warn_defn+44,red                         : REMark border around wdw
5160   DR_PULD warn_defn,-1,-1
5170   DR_IWDF #chan%,warn_defn,0
5180   PAPER#chan%,white :INK#chan%,red
5190   IF title$<>"":PRINT#chan%,title$;" :"
5200   PRINT #chan%,mess$
5210   POS_PTR warn_defn,160*65536+10,-1
5220   REPeat lp%
5230     RD_PTR warn_defn,item%,swnum%,y%,y%,y%,warn_lfl%
5240     IF swnum%=-1:EXIT lp%
5250   END REPeat lp%
5260   IF NOT channel%:CLOSE#chan%
5270   DR_UNST warn_defn
5280   RETurn item%
5290 END DEFine warning%
5300 :
5310 DEFine PROCedure info_pos (mess$,channel%,pos)
5320 REMark warning wdw with positioning
5330   POS_PTR main_defn,pos,-1
5340   info mess$,channel%
5350 END DEFine info_pos
5360 :
5370 DEFine PROCedure info(mess$,channel%)
5380 REMark shows window containing mess$
5390   LOCal chan%
5400   IF channel%
5410     chan%=channel%
5420   ELSE
5430     chan%=FOPEN("con")
5440   END IF
5450   CH_ITEM warn_defn,-3,0,-1,"",'  Info.  '
5460   CH_ITEM warn_defn,-1,0,-1,"",'Info'
5470   CH_ITEM warn_defn,-1,1,-1,"",'Info'
5480   POKE_W warn_defn+44,green                       : REMark border
5490   POKE_W warn_defn+46,light_green                  : REMark paper col
5500   DR_PULD warn_defn,-1,-1
5510   POS_PTR warn_defn,90*65536+70,-1
5520   DR_IWDF #chan%,warn_defn,0
5530   PAPER#chan%,white:INK#chan%,black
5540   PRINT #chan%,mess$
5550   IF NOT channel%:CLOSE#chan%
5560 END DEFine info
5570 :
5580 DEFine PROCedure make_colours (colourway%)
5590 REMark this initialises the GLOBAL variables menu_pap, menu_brd, menu_inv,
5600 REMark menu_ink and menu_bar according to the colourway passed as parameter.
5610 REMark The GLOBAl variable mycolour_mode% must have been set prior to calling
5620 REMark this function, preferrably via the function set_colours%.
5630 REMark Moreover the colours black, white,red, green,wg_stripes, etc...
5640 REMark must have been set prior to calling this procedure:
5650 REMark Preferrably, they were also set via the function set_colours%
5660 :
5670 REMark this function needs the extension WL_MK16 (outptr_bin)
5680 :
5690 REMark the colourways% are:
5700 REMark 0= white/green
5710 REMark 1= black/red
5720 REMark 2= white/red
5730 REMark 3= black/green
5740 REMark 4= use system palette (only available in modes 32 and 33)
5750 :
5760 REMark the Global war mycolour_mode% is:
5770 REMark 0 = we are in QL mode 4
5780 REMark 1 = we are in PAL mode (256 colours)
5790 REMark 2 - we are in 24 bit colour mode
5800 REMark preferrably, this is set via the function set_colours%
5810 :
5820   IF colourway%>7 OR colourway%<0: colourway%=0: REMark must be between 0 & 7
5830 :
5840 REMark first make sure that what we request is possible.
5850 REMark colourway 4 is only possible if we are in 24 bit colour mode
5860 REMark if it is requested in another mode, it is set to 0
5870 :
5880   IF colourway%>3
5890     IF mycolour_mode%< 2
5900        colourway%=0
5910      ELSE
5920        make_my_palette colourway%-4:RETurn   : REMark set colours according to system palette
5930     END IF
5940   END IF
5950 :
5960   SELect ON mycolour_mode%
5970     =0                                  : REMark QL 4 colours
5980       SELect ON colourway%
5990           =0:                           : REMark white/green
6000                 menu_pap=white
6010                 menu_brd=green
6020                 menu_ink=black
6030                 menu_bar=wg_striped
6040                 menu_inv=red
6050           =1:                           : REMark black/red
6060                 menu_pap=black
6070                 menu_brd=red
6080                 menu_ink=white
6090                 menu_bar=br_striped
6100                 menu_inv=green
6110           =2:                           : REMark white/red
6120                 menu_pap=white
6130                 menu_brd=red
6140                 menu_ink=black
6150                 menu_bar=wr_striped
6160                 menu_inv=green
6170           =3:                           : REMark black/green
6180                 menu_pap=black
6190                 menu_brd=green
6200                 menu_ink=white
6210                 menu_bar=bg_striped
6220                 menu_inv=red
6230       END SELect
6240     =1                                  : REMark pal colours
6250       SELect ON colourway%
6260           =0:                           : REMark white/green
6270                 menu_pap=white+256
6280                 menu_brd=green+256
6290                 menu_ink=black+256
6300                 menu_bar=wg_striped+256
6310                 menu_inv=red +256
6320           =1:                           : REMark black/red
6330                 menu_pap=black+256
6340                 menu_brd=red  +256
6350                 menu_ink=white +256
6360                 menu_bar=br_striped +256
6370                 menu_inv=green +256
6380                 omenu_pap=black+256
6390           =2:                           : REMark white/red
6400                 menu_pap=white +256
6410                 menu_brd=red   +256
6420                 menu_ink=black +256
6430                 menu_bar=wr_striped +256
6440                 menu_inv=green +256
6450           =3:                           : REMark black/green
6460                 menu_pap=black +256
6470                 menu_brd=green +256
6480                 menu_ink=white +256
6490                 menu_bar=bg_striped +256
6500                 menu_inv=red   +256
6510       END SELect
6520     =2                                  : REMark 24 bit colours
6530       SELect ON colourway%
6540           =0:                           : REMark white/green
6550                 menu_pap=WL_MK16(white)
6560                 menu_brd=WL_MK16(green)
6570                 menu_ink=WL_MK16(black)
6580                 menu_bar=WL_MK16(wg_striped)
6590                 menu_inv=WL_MK16(red)
6600           =1:                           : REMark black/red
6610                 menu_pap=WL_MK16(black)
6620                 menu_brd=WL_MK16(red)
6630                 menu_ink=WL_MK16(white)
6640                 menu_bar=WL_MK16(br_striped)
6650                 menu_inv=WL_MK16(green)
6660           =2:                           : REMark white/red
6670                 menu_pap=WL_MK16(white)
6680                 menu_brd=WL_MK16(red)
6690                 menu_ink=WL_MK16(black)
6700                 menu_bar=WL_MK16(wr_striped)
6710                 menu_inv=WL_MK16(green)
6720           =3:                           : REMark black/green
6730                 menu_pap=WL_MK16(black)
6740                 menu_brd=WL_MK16(green)
6750                 menu_ink=WL_MK16(white)
6760                 menu_bar=WL_MK16(bg_striped)
6770                 menu_inv=WL_MK16(red)
6780       END SELect
6790   END SELect
6800 :
6810   make_my_old_palette
6820 END DEFine make_colours
6830 :
6840 DEFine PROCedure make_my_palette (cw%)
6850   sp.winbd            = HEX('0200') : REMark Window border
6860   sp.winbg            = HEX('0201') : REMark Window background
6870   sp.winfg            = HEX('0202') : REMark Window foreground
6880   sp.winmg            = HEX('0203') : REMark Window middleground
6890   sp.titlebg          = HEX('0204') : REMark Title background
6900   sp.titletextbg      = HEX('0205') : REMark Title text background
6910   sp.titlefg          = HEX('0206') : REMark Title foreground
6920   sp.litemhigh        = HEX('0207') : REMark Loose item highlight
6930   sp.litemavabg       = HEX('0208') : REMark Loose item available background
6940   sp.litemavafg       = HEX('0209') : REMark Loose item available foreground
6950   sp.litemselbg       = HEX('020a') : REMark Loose item selected background
6960   sp.litemselfg       = HEX('020b') : REMark Loose item selected foreground
6970   sp.litemunabg       = HEX('020c') : REMark Loose item unavailable background
6980   sp.litemunafg       = HEX('020d') : REMark Loose item unavailable foreground
6990   sp.infwinbd         = HEX('020e') : REMark Information window border
7000   sp.infwinbg         = HEX('020f') : REMark Information window background
7010   sp.infwinfg         = HEX('0210') : REMark Information window foreground
7020   sp.infwinmg         = HEX('0211') : REMark Information window middleground
7030   sp.subinfbd         = HEX('0212') : REMark Subsidiary information window border
7040   sp.subinfbg         = HEX('0213') : REMark Subsidiary information window background
7050   sp.subinffg         = HEX('0214') : REMark Subsidiary information window foreground
7060   sp.subinfmg         = HEX('0215') : REMark Subsidiary information window middleground
7070   sp.appbd            = HEX('0216') : REMark Application window border
7080   sp.appbg            = HEX('0217') : REMark Application window background
7090   sp.appfg            = HEX('0218') : REMark Application window foreground
7100   sp.appmg            = HEX('0219') : REMark Application window middleground
7110   sp.appihigh         = HEX('021a') : REMark Application window item highlight
7120   sp.appiavabg        = HEX('021b') : REMark Application window item available background
7130   sp.appiavafg        = HEX('021c') : REMark Application window item available foreground
7140   sp.appiselbg        = HEX('021d') : REMark Application window item selected background
7150   sp.appiselfg        = HEX('021e') : REMark Application window item selected foreground
7160   sp.appiunabg        = HEX('021f') : REMark Application window item unavailable background
7170   sp.appiunafg        = HEX('0220') : REMark Application window item unavailable foreground
7180   sp.scrbar           = HEX('0221') : REMark Pan/scroll bar
7190   sp.scrbarsec        = HEX('0222') : REMark Pan/scroll bar section
7200   sp.scrbararr        = HEX('0223') : REMark Pan/scroll bar arrow
7210   sp.buthigh          = HEX('0224') : REMark Button highlight
7220   sp.butbd            = HEX('0225') : REMark Button border
7230   sp.butbg            = HEX('0226') : REMark Button background
7240   sp.butfg            = HEX('0227') : REMark Button foreground
7250   sp.hintbd           = HEX('0228') : REMark Hint border
7260   sp.hintbg           = HEX('0229') : REMark Hint background
7270   sp.hintfg           = HEX('022a') : REMark Hint foreground
7280   sp.hintmg           = HEX('022b') : REMark Hint middleground
7290   sp.errbg            = HEX('022c') : REMark Error message background
7300   sp.errfg            = HEX('022d') : REMark Error message foreground
7310   sp.errmg            = HEX('022e') : REMark Error message middleground
7320   sp.shaded           = HEX('022f') : REMark sp.shaded area
7330   sp.3ddark           = HEX('0230') : REMark Dark 3D border shade
7340   sp.3dlight          = HEX('0231') : REMark Light 3D border shade
7350   sp.vertfill         = HEX('0232') : REMark Vertical area fill
7360   IF NOT (IS_OPEN(#0) OR IS_OPEN(#1))
7370     OPEN#1,'con'
7380     SP_JOBPAL -1,cw%
7390     CLOSE#1
7400   ELSE
7410     SP_JOBPAL -1,cw%
7420   END IF
7430 END DEFine make_my_palette
7440 :
7450 DEFine PROCedure make_my_old_palette
7460 REMark this translates the colours used in the wdws into 4 colour mode...
7470   sp.winbd          = menu_brd          : REMark Window border
7480   sp.winbg          = menu_pap          : REMark Window background
7490   sp.winfg          = menu_pap          : REMark Window foreground
7500   sp.titletextbg    = menu_pap          : REMark Title text background
7510   sp.titlefg        = menu_ink          : REMark Title foreground
7520   sp.titlebg        = menu_bar          : REMark Title background
7530   sp.litemhigh      = menu_ink          : REMark Loose item highlight
7540   sp.litemavabg     = menu_pap          : REMark Loose item available background
7550   sp.litemavafg     = menu_ink          : REMark Loex fifiose item available foreground
7560   sp.litemselbg     = menu_brd          : REMark Loose item selected background
7570   sp.litemselfg     = menu_ink          : REMark Loose item selected foreground
7580   sp.litemunabg     = menu_pap          : REMark Loose item unavailable background
7590   sp.litemunafg     = menu_brd          : REMark Loose item unavailable foreground
7600   sp.infwinbd       = menu_brd          : REMark Information window border
7610   sp.infwinbg       = menu_pap          : REMark Information window background
7620   sp.infwinfg       = menu_ink          : REMark Information window foreground
7630   sp.appbd          = menu_brd          : REMark Application window border
7640   sp.appbg          = menu_pap          : REMark Application window background
7650   sp.appfg          = menu_ink          : REMark Application window foreground
7660   sp.appmg          = menu_pap          : REMark Application window middleground
7670   sp.appihigh       = menu_ink          : REMark Application window item highlight
7680   sp.appiavabg      = menu_pap          : REMark Application window item available background
7690   sp.appiavafg      = menu_ink          : REMark Application window item available foreground
7700   sp.appiselbg      = menu_brd          : REMark Application window item selected background
7710   sp.appiselfg      = menu_ink          : REMark Application window item selected foreground
7720   sp.appiunabg      = menu_pap          : REMark Application window item unavailable background
7730   sp.appiunafg      = menu_brd          : REMark Application window item unavailable foreground
7740   sp.scrbar         = menu_ink          : REMark Pan/scroll bar
7750   sp.scrbararr      = menu_brd          : REMark Pan/scroll bar arrow
7760   sp.buthigh        = menu_ink          : REMark Button highlight
7770   sp.butbd          = menu_brd          : REMark Button border
7780   sp.butbg          = menu_pap          : REMark Button background
7790   sp.butfg          = menu_ink          : REMark Button foreground
7800 END DEFine mk_my_old_palette
7810 :
7820 DEFine FuNction getstr$(poste%,drap%,defn,a$)
7830 REMark ouvre une fenëtre sur un poste de menu dÉliÉ et propose
7840 REMark une chaïne ç Éditer
7850 REMark les paramêtres sont
7860 REMark - le numÉro du poste
7870 REMark - un drapeau si le rÉsultat doit ëtre un chiffre (si drap%<>0)
7880 REMark - la dÉfinition de travail dans laquelle se trouve le poste
7890 REMark - la chaïne qu'on peut Éditer
7900 REMark returns the string or ""
7910 REMark uses the global (!) vars menu_pap% & menu_brd%
7920 REMark
7930   LOCal chan%,b$,res
7940   b$=" ":b$=a$   : REMark c'est curieusement nÉcessaire
7950   chan%=FOPEN("con")
7960   PAPER#chan%,menu_brd
7970   INK#chan%,menu_ink
7980   DR_LWDF#chan%,defn,poste%             : REMark open channel over item
7990   CLS#chan%
8000   res=GET_STR(#chan%,drap%,b$)          : REMark get string
8010   CLOSE#chan%
8020   IF res<0:b$=""                        : REMark ooops
8030   CH_ITEM defn,-1,poste%,-1,"",b$
8040   RETurn b$
8050 END DEFine getstr$
8060 :
8070 DEFine FuNction showcont$(string$,length%)
8080 REMark this adds '...' at the end of a string, to show that it continues
8090 LOCal l%,lengt%
8100   l%=LEN(string$)
8110   IF NOT l%:RETurn ""                   : REMark empty string
8120   lengt%=length% DIV 6
8130   IF l%<=lengt%:RETurn string$          : REMark entire string fits wdw
8140   RETurn string$(1 TO lengt%-3)&"..."
8150 END DEFine showcont$
8160 :
8170 DEFine FuNction get_free_text$(defn,item%,prompt$,length%)
8180 REMark this opens wdw over loose-item item% of the working definition defn
8190 REMark allows free text to be entered
8200 REMark params: working defintion,item over which wdw is to be opened
8210 REMark         first value of string, length of max string in wdw, if this is...
8220 REMark            -1, the length will be determined here
8230 LOCal chan%,string$,tempsizes%(3)
8240   chan%=FOPEN('con')
8250   IF chan%<0:RETurn prompt$
8260   DR_LWDF#chan%,defn,item%              : REMark open wdw over item
8270   PAPER#chan%,menu_brd
8280   INK#chan%,menu_ink
8290   CLS#chan%
8300   string$=WEDIT$(#chan%,prompt$)
8310   IF length%=-1
8320      WSIZE#chan%,tempsizes%
8330      length%=tempsizes%(0)
8340   END IF
8350   CLOSE#chan%
8360   IF length%
8370     CH_ITEM defn,-1,item%,-1,"",showcont$(string$,length%)
8380   ELSE
8390     CH_ITEM defn,-1,item%,-1,"",string$
8400   END IF
8410   RETurn string$
8420 END DEFine get_free_text$
8430 :
8440 DEFine FuNction do_caps(a$,caps%)
8450 REMark set string to capitales etc,caps% determines whether the string....
8460 REMark ...must be lower case (=0), upper case(=1), all words capitalized (=2),
8470 REMark ... first word only capitalized (=3), or nothing(=-1)
8480   SELect ON caps%
8490    =0: MIN a$
8500    =1: MAJ a$
8510    =2: CAP2 a$
8520    =3: CAP a$
8530   END SELect
8540   RETurn a$
8550 END DEFine do_caps
8560 :
8570 DEFine FuNction centr$(ctr$,leng)
8580 REMark this "centres" the string ctr$ (i.e. left padds it with spaces) so that
8590 REMark it is centred in a window of leng width (leng is length in chars)
8600   LOCal l%,a$
8610   a$=ctr$
8620   l%=INT(leng-LEN(a$))/2
8630   IF l%<0:RETurn a$
8640   a$=FILL$(' ',l%)&a$&FILL$(' ',l%)
8650   RETurn a$
8660 END DEFine centr$
8670 :
8680 DEFine FuNction set_colours% (force_mode%)
8690 REMark this sets colour variables according to the colour mode wished, if possible.
8700 REMark force_mode%=
8710 REMark     0 if ql colours wished (mode 4 only)
8720 REMark     1 if pal colours wished
8730 REMark     2 if 24 bit colours wished
8740 REMark This function returns the true QL mode set (4,8,32,33 etc.).
8750 REMark This function also sets the GLOBAL variable mycolour_mode% to the
8760 REMark mode 0,1,2 as above - whatever is possible on the machine, i.e.
8770 REMark if colour_24 is wished on a 4 colour machine (which won't succeed)
8780 REMark this function will set set that variable to 0, i.e. QL mode.
8790 REMark
8800 REMark This function needs the outptr toolkit
8810 LOCal mode%
8820   mode%=RMODE                           : REMark get current mode now
8830   SELect ON force_mode%
8840       = 0                               : REMark mode 4 wished
8850        SELect ON mode%
8860           = 4                           : REMark already on, so do nothing
8870           = 0,1,8 : MODE 4              : REMark old modes, set to 4 colours
8880           = REMAINDER
8890             COLOUR_QL                   : REMark if higher colour mode, set to "mode 4"
8900             MODE 4
8910        END SELect
8920        mode%=0                          : REMark new mode we're in  - 4 colour mode
8930       = 1                               : REMark PALette wished
8940        SELect ON mode%
8950           = 16,32,33 : COLOUR_PAL:mode%=1: REMark we're in high colour mode, but PAL colours wished
8960           = REMAINDER
8970             IF mode%<>4:MODE 4
8980             mode%=0
8990        END SELect
9000       = 2
9010        SELect ON mode%
9020           = 32,33 : COLOUR_24 :mode%=2  : REMark we're in high col mode 24 bit colours
9030           = 16 : COLOUR_PAL : mode%=1
9040           = REMAINDER
9050             IF mode%<>4:MODE 4
9060             mode%=0
9070        END SELect
9080   END SELect
9090   mycolour_mode%=mode%                  : REMark set this global variable now:
9100   REMark now set some colour variables
9110   SELect ON mode%
9120        = 0                              : REMark Ql 4 mode colours
9130         black          = 0
9140         white          = 7
9150         red            = 2
9160         green          = 4
9170         grey           = 255
9180         dull_pink      = 19
9190         Blue           = 0
9200         Magenta        = 2
9210         Yellow         = 7
9220         Cyan           = 0
9230         Dark_slate     = 0
9240         Slate_grey     = 255
9250         Dark_grey      = 255
9260         Light_grey     = 255
9270         Ash_grey       = 255
9280         Dark_red       = 210
9290         light_green    = 220
9300         Mustard        = 242
9310         Dark_green     = 228
9320         Sea_blue       = 0
9330         Dark_blue      = 0
9340         Purple         = 0
9350         Shocking_pink  = 234
9360         Orange         = 234
9370         Lime_green     = 4
9380         Apple_green    = 4
9390         Bright_blue    = 0
9400         Mauve          = 0
9410         Peach          = 7
9420         Light_yellow   = 7
9430         Light_blue     = 0
9440         Sky_blue       = 0
9450         Rose_pink      = 234
9460         pink           = 234
9470         Beige          = 7
9480         Pastel_pink    = 234
9490         Pastel_yellow  = 7
9500         Pastel_green   = 4
9510         Pastel_cyan    = 0
9520         Pastel_blue    = 0
9530         Pastel_rose    = 234
9540         Brick          = 210
9550         Light_khaki    = 7
9560         Dull_green     = 228
9570         Dull_cyan      = 0
9580         Steel_blue     = 0
9590         dull_pink      = 234
9600         Brown          = 0
9610         Khaki          = 7
9620         Dusky_green    = 4
9630         Dusky_blue     = 0
9640         Midnight_blue  = 0
9650         Plum           = 0
9660         Dusky_pink     = 234
9670         Buff           = 0
9680         Avocado        = 4
9690         Dull_turquoise = 4
9700         Dull_blue      = 0
9710         Faded_purple   = 234
9720         Cerise         = 2
9730         Tan_normal     = 0
9740         Grass_green    = 4
9750         Sea_green      = 4
9760         Ultramarine    = 0
9770         Deep_purple    = 234
9780         Light_orange   = 7
9790         light_red      = 2
9800         wg_striped     = 92
9810         br_striped     = 82
9820         wr_striped     = 107
9830         bg_striped     = 100
9840        = 1                      : REMark Colour_pal
9850         black          = 0
9860         white          = 1
9870         red            = 2
9880         green          = 3
9890         Blue           = 4
9900         Magenta        = 5
9910         Yellow         = 6
9920         Cyan           = 7
9930         Dark_slate     = 8
9940         Slate_grey     = 9
9950         Dark_grey      = 10
9960         grey           = 11
9970         Light_grey     = 12
9980         Ash_grey       = 13
9990         Dark_red       = 14
10000         light_green    = 15
10010         Mustard        = 16
10020         Dark_green     = 17
10030         Sea_blue       = 18
10040         Dark_blue      = 19
10050         Purple         = 20
10060         Shocking_pink  = 21
10070         Orange         = 22
10080         Lime_green     = 23
10090         Apple_green    = 24
10100         Bright_blue    = 25
10110         Mauve          = 26
10120         Peach          = 27
10130         Light_yellow   = 28
10140         Light_blue     = 29
10150         Sky_blue       = 30
10160         Rose_pink      = 31
10170         pink           = 32
10180         Beige          = 33
10190         Pastel_pink    = 34
10200         Pastel_yellow  = 35
10210         Pastel_green   = 36
10220         Pastel_cyan    = 37
10230         Pastel_blue    = 38
10240         Pastel_rose    = 39
10250         Brick          = 40
10260         Light_khaki    = 41
10270         Dull_green     = 42
10280         Dull_cyan      = 43
10290         Steel_blue     = 44
10300         dull_pink      = 45
10310         Brown          = 46
10320         Khaki          = 47
10330         Dusky_green    = 48
10340         Dusky_blue     = 49
10350         Midnight_blue  = 50
10360         Plum           = 51
10370         Dusky_pink     = 52
10380         Buff           = 53
10390         Avocado        = 54
10400         Dull_turquoise = 55
10410         Dull_blue      = 56
10420         Faded_purple   = 57
10430         Cerise         = 58
10440         Tan_normal     = 59
10450         Grass_green    = 60
10460         Sea_green      = 61
10470         Ultramarine    = 62
10480         Deep_purple    = 63
10490         pinkish        = 77
10500         light_red      = 109
10510         Light_orange   = 208
10520         wg_striped     = light_green
10530         br_striped     = Dark_red
10540         wr_striped     = light_red
10550         bg_striped     = Dark_green
10560        = 2                      : REMark colour_24
10570         black          = HEX('000000')
10580         white          = HEX('FFFFFF')
10590         red            = HEX('FF0000')
10600         green          = HEX('00FF00')
10610         Blue           = HEX('0000FF')
10620         Magenta        = HEX('FF00FF')
10630         Yellow         = HEX('FFFF00')
10640         Cyan           = HEX('00FFFF')
10650         Dark_slate     = HEX('242424')
10660         Slate_grey     = HEX('494949')
10670         Dark_grey      = HEX('6D6D6D')
10680         grey           = HEX('929292')
10690         Light_grey     = HEX('B6B6B6')
10700         Ash_grey       = HEX('DBDBDB')
10710         Dark_red       = HEX('920000')
10720         light_green    = HEX('B6FFB6')
10730         Mustard        = HEX('929200')
10740         Dark_green     = HEX('009200')
10750         Sea_blue       = HEX('009292')
10760         Dark_blue      = HEX('000092')
10770         Purple         = HEX('920092')
10780         Shocking_pink  = HEX('FF0092')
10790         Orange         = HEX('FF9200')
10800         Lime_green     = HEX('92FF00')
10810         Apple_green    = HEX('00FF92')
10820         Bright_blue    = HEX('0092FF')
10830         Mauve          = HEX('9200FF')
10840         Peach          = HEX('FFB6B6')
10850         Light_yellow   = HEX('FFFFB6')
10860         Light_blue     = HEX('B6FFFF')
10870         Sky_blue       = HEX('B6B6FF')
10880         Rose_pink      = HEX('FFB6FF')
10890         pink           = HEX('FFB6DB')
10900         Beige          = HEX('FFDBB6')
10910         Pastel_pink    = HEX('FFDBDB')
10920         Pastel_yellow  = HEX('FFFFDB')
10930         Pastel_green   = HEX('DBFFDB')
10940         Pastel_cyan    = HEX('DBFFFF')
10950         Pastel_blue    = HEX('DBDBFF')
10960         Pastel_rose    = HEX('FFDBFF')
10970         Brick          = HEX('B66D6D')
10980         Light_khaki    = HEX('B6B66D')
10990         Dull_green     = HEX('6DB66D')
11000         Dull_cyan      = HEX('6DB6B6')
11010         Steel_blue     = HEX('6D6DB6')
11020         dull_pink      = HEX('B66DB6')
11030         Brown          = HEX('6D2424')
11040         Khaki          = HEX('6D6D24')
11050         Dusky_green    = HEX('246D24')
11060         Dusky_blue     = HEX('246D6D')
11070         Midnight_blue  = HEX('24246D')
11080         Plum           = HEX('6D246D')
11090         Dusky_pink     = HEX('B64992')
11100         Buff           = HEX('B69249')
11110         Avocado        = HEX('92B649')
11120         Dull_turquoise = HEX('49B692')
11130         Dull_blue      = HEX('4992B6')
11140         Faded_purple   = HEX('9249B6')
11150         Cerise         = HEX('920049')
11160         Tan_normal     = HEX('924900')
11170         Grass_green    = HEX('499200')
11180         Sea_green      = HEX('009249')
11190         Ultramarine    = HEX('004992')
11200         Deep_purple    = HEX('490092')
11210         Light_orange   = HEX('ffb624')
11220         pinkish        = HEX('C0037B')
11230         light_red      = HEX('FC1729')
11240         wg_striped     = light_green
11250         br_striped     = Dark_red
11260         wr_striped     = light_red
11270         bg_striped     = Dark_green
11280   END SELect
11290   mode%=RMODE
11300   RETurn mode%                     : REMark return current screen mode now
11310 END DEFine set_colours%
11320 :
11330 DEFine FuNction fen_app2 (adresse,tableau$,nr_sects%,selk%,phys%)
11340 REMark les paramêtres sont: l'adresse (0 la premiêre fois)
11350 REMark le tableau de chaïne, le nombre de sections,
11360 REMark le drapeau si on veut des touches de sÉlection et si sÉlection=action,
11370 REMark un tableau d'entiers contenant les tailles x,y et les origines
11380 REMark et les touches ç exclure des touches de sÉlection
11390 LOCal app_attributs%(3,1),temp%,bord%,pap%,ori_x%,ori_y%
11400 LOCal tail_x%,tail_y%,fleches%,barrre%
11410 :
11420   IF adresse:RECHP adresse              : REMark si c'est un 2nd appel, on enlêve l'ancienne
11430   tail_x%=phys%(0)                      : REMark (largeur)    Ceci est pour la clartÉ
11440   tail_y%=phys%(1)                      : REMark (hauteur)    seulement, on pourrait
11450   ori_x%=phys%(2)                       : REMark            utiliser directement le
11460   ori_y%=phys%(3)                       : REMark origs x,y    tableau phys% dans AP_MAKE
11470 :
11480   adresse=AP_MAKE2(selk%,tail_x%,tail_y%,ori_x%,ori_y%,nr_sects%,tableau$)
11490   IF adresse<0: RETurn 0                : REMark si erreur, on retourne 0
11500                                         : REMark Maintenant on dÉtermine les attributs
11510   bord%=sp.appbd                        : REMark couleur bordure
11520   pap%=sp.appbg                         : REMark couleur "papier"
11530   touche_sel%=9                         : REMark touche de sÉlection - TAB
11540   fleches%=sp.scrbararr                 : REMark couleur des flêches
11550   barre%=sp.scrbar                      : REMark couleur de la barre d'ascenseur
11560 :
11570   app_attributs%(0,0)=1
11580   app_attributs%(0,1)=sp.appihigh       : REMark larg. & coul. trait poste courant
11590 :
11600   app_attributs%(1,0)=sp.appiunabg
11610   app_attributs%(1,1)=sp.appiunafg      : REMark unavailable
11620 :
11630   app_attributs%(2,0)=sp.appiavabg
11640   app_attributs%(2,1)=sp.appiavafg      : REMark disponible
11650 :
11660   app_attributs%(3,0)=sp.appiselbg
11670   app_attributs%(3,1)=sp.appiselfg      : REMark sÉlectionnÉ
11680 :
11690   er=AP_PARS(adresse,0,bord%,pap%,touche_sel%,fleches%,barre%,app_attributs%)
11700 :
11710   IF er:RETurn 0
11720 :
11730   RETurn adresse  : REMark on retourne l'adresse
11740 END DEFine fen_app
11750 :
11760 DEFine PROCedure scroll_arrows(rangees%,nbr_objets%,appfen,cty%)
11770 REMark Cette procÉdure dÉcide s'il faut laisser les flêches de dÉfilement
11780 REMark params: le nombre de rangÉes, le nombre total d'objets
11790 REMark le pointer vers la sous-fen d'app et la dÉfinition de controle
11800   IF nbr_objets%<=rangees% +1
11810     POKE_W appfen+150,0
11820     cty%(1,2)=nbr_objets%
11830   ELSE
11840     POKE_W appfen+150,6
11850 REMark    cty%(1,2)=rangees%-1             : rem if scroll arrows are there, there is one line less in the window
11860     cty%(1,2)=rangees%
11870  END IF
11880  IF DIMN(cty%,1)=2:cty%(2,2)=cty%(1,2)
11890  cty%(0,1)=1                            : REMark show that definitionhas changed
11900 END DEFine scroll_arrows
11910 :
11920 DEFine PROCedure init
11930 LOCal temp%
11940   text=0:sprite=2:blob=4:pattern=6
11950   retr=256:retn=-256:sous1=254:sous2=252
11960   REMark normal: hit = selects/deselects, do = returns & leaves selected
11970   REMark retr = return even on hit, reset item to available after,
11980   REMark retn = return even on hit, hit selects/deselects, do selects
11990 :
12000   hit$=CHR$(1):do$=CHR$(2):cancel$=CHR$(3):sleep$=CHR$(7):wake$=CHR$(8)
12010   help$=CHR$(4):move$=CHR$(5):size$=CHR$(6):cr$=CHR$(10)
12020 :
12030   appwinoffset%=16                      : REMark appsub win additional y offset
12040   xsize%=400                            : REMark x size divisible by 4!
12050   ysize%=200                            : REMark provisional y size%
12060   lxs=24:lys=12                         : REMark standard item x,y, sizes
12070   inf1y=lys+4                           : REMark y orig of inner border
12080   xesp=lxs+2                            : REMark loose items x spacing
12090   nbr_post%=3                           : REMark nbr of item right & left in title bar
12100   main_coul%=SMSCFG(2)                  : REMark colourways
12110   app_coul%=main_coul%                  : REMark preset colourways
12120   temp%=set_colours% (2)                : REMark always try to set highest colours
12130   IF temp%<2 AND main_coul%>3:main_coul%=0 : REMark old wman
12140   make_colours main_coul%               : REMark set all colours now
12150 :
12160   rep$=SMSCFG(1)                        : REMark dir with files
12170   titre$="Dir: "&rep$&" "
12180 :
12190 RESTORE 12190
12200 :
12210 : REMark make loose item attributes
12220 :
12230   DIM std_iattr(3,3)
12240   RD_IATT std_iattr
12250     DATA 1,sp.litemhigh : REMark current item border size & colour
12260     DATA sp.litemunabg,sp.litemunafg,0,0 : REMark unavailable item paper & ink
12270     DATA sp.litemavabg,sp.litemavafg,0,0 : REMark   available         "    "       "
12280     DATA sp.litemselbg,sp.litemselfg,0,0 : REMark   selected
12290 :
12300 : REMark make data for sprites - uses the modified RD_SPRT function
12310 : REMark
12320 : REMark
12330 :
12340   espace_vide=ALCHP(100)                : REMark GLOBAL var for 0 mask sprites!
12350 :
12360   main_sprite=RD_SPRT(0)                : REMark 0 = normal mask
12370     DATA 11,13,6,5,4
12380     DATA '     www     '
12390     DATA '     waw     '
12400     DATA '     waw     '
12410     DATA '     waw     '
12420     DATA 'wwwwwa awwwww'
12430     DATA 'waaaa   aaaaw'
12440     DATA 'wwwwwa awwwww'
12450     DATA '     waw     '
12460     DATA '     waw     '
12470     DATA '     waw     '
12480     DATA '     www     '
12490 :
12500   move_sprite=RD_SPRT(1)                : REMark 1 = 0 mask
12510   IF temp%>1:move_sprite=6              : REMark or system sprite!
12520     DATA 10,14,5,4,4
12530     DATA 'aaaaaaaaaa    '
12540     DATA 'awwwwwwwwa    '
12550     DATA 'awwrrrrwwa    '
12560     DATA 'awwrrrrwwaaaaa'
12570     DATA 'awwrrwwwwwwwwa'
12580     DATA 'awwwwwwwwrrwwa'
12590     DATA 'aaaaawwrrrrwwa'
12600     DATA '    awwrrrrwwa'
12610     DATA '    awwwwwwwwa'
12620     DATA '    aaaaaaaaaa'
12630 :
12640   zzz_sprite= RD_SPRT(1)
12650   IF temp%>1:zzz_sprite=10
12660     DATA 7,15,0,0,4
12670     DATA 'wwwww          '
12680     DATA '   w           '
12690     DATA '  w  wwww      '
12700     DATA ' w     w       '
12710     DATA 'wwwww w  wwww  '
12720     DATA '     wwww  w   '
12730     DATA '          wwww '
12740 :
12750  size_sprite=RD_SPRT(1)
12760  IF temp%>1:size_sprite=7
12770     DATA 9,14,5,4,4
12780     DATA 'aaaaaaaaaaaaaa'
12790     DATA 'awwwwwwwwwwwwa'
12800     DATA 'awwrrrrrrrrwwa'
12810     DATA 'awwrrrrrrrrwwa'
12820     DATA 'awwrrwwwwwwwwa'
12830     DATA 'awwrrwwrrrrwwa'
12840     DATA 'awwrrwwrrrrwwa'
12850     DATA 'awwwwwwwwwwwwa'
12860     DATA 'aaaaaaaaaaaaaa'
12870 :
12880   make_my_data 1
12890   make_main_wdw
12900   main_lfl%(target_its%)=128
12910 :
12920 END DEFine init
12930 :
12940 DEFine PROCedure make_main_wdw
12950 REMark make the main window
12960 LOCal main_lot,main_iwt,main_iot1
12970 :
12980   REMark first make sure of the wdw sizes (we also come here from change_size!)
12990   xsize%=xsize%-(xsize% MOD 4)                    : REMark make into nbr divisible by 4
13000   IF xsize%<360:xsize%=360
13010 :
13020   REMark calculate the y size of the wdw so that it always looks nice, in that
13030   REMark the rows in the appsub always fill the appsub entirely
13040   ysize%=ysize%+(ysize%&&1)             : REMark make into even number
13050   IF ysize%<100:ysize%=100              : REMark predetermioned min ysize% before calculations
13060   ysize%=ysize%-inf1y-14-appwinoffset%
13070   rangees=ysize% DIV 12                 : REMark GLOBAL var: nbr of rows in appsub
13080   IF rangees>data_count%-2:rangees=data_count%-2
13090   IF rangees<3:rangees=3
13100   ysize%=rangees*12+inf1y+14 +appwinoffset%
13110 :
13120   REMark first calc info obj size - this also influences one loose item, used
13130   REMark in the title bar
13140   longueur_titre=LEN(titre$)*6          : REMark length in pixels
13150   titre_x=INT((xsize%-longueur_titre)/2): REMark x orig
13160 :
13170   REMark first the loose items
13180 :
13190   yinf1y=inf1y+4:
13200   esc%=0:move_it%=esc%+1:size_it%=move_it%+1
13210   sleep_it%=size_it%+1:all_it%=sleep_it%+1:go_it%=all_it%+1
13220   dir_it%=go_it%+1:target_its%=dir_it%+1:make_it%=target_its%+5
13230 :
13240 RESTORE 13240
13250   REMark reset data pointer to here (The Basic Linker does that automatically)
13260 :
13270   temp%=xsize% DIV 6
13280   main_lit%=12:DIM main_lfl%(main_lit%)   : REMark there are 13 items!
13290   main_lot=RD_LOT(std_iattr,main_lit%)
13300     DATA lxs,lys,4+xesp*0,2,0,0,cancel$,text+retr,'ESC'
13310     DATA lxs,lys,4+xesp*1,2,0,0,move$,sprite+retr,move_sprite
13320     DATA lxs,lys,4+xesp*2,2,0,0,size$,sprite+retn,size_sprite
13330     DATA lxs,lys,xsize%-2-xesp*1,2,0,0,sleep$,sprite+retr,zzz_sprite
13340     DATA lxs,lys,xsize%-2-xesp*2,2,0,0,'A',text+retn,"All"
13350     DATA lxs,lys,xsize%-2-xesp*3,2,0,0,'O',sous1+retn,"OK"
13360     DATA 6*6,lys-2,titre_x+30,3,0,0,'D',text+retn,rep$
13370     DATA 8*6,lys,8,inf1y+2,0,0,'G',sous1+retn,"Generic"
13380     DATA 8*6,lys,8+temp%,inf1y+2,0,0,'T',sous2+retn,"Atari"
13390     DATA 8*6,lys,8+temp%*2,inf1y+2,0,0,'G',sous1+retn,"GoldCard"
13400     DATA 8*6,lys,8+temp%*3,inf1y+2,0,0,'Q',sous1+retn,"Q40/Q60"
13410     DATA 8*6,lys,8+temp%*4,inf1y+2,0,0,'X',sous2+retn,"QXL"
13420     DATA 8*6,lys,8+temp%*5,inf1y+2,0,0,'M',sous2+retn,"Make"
13430 :
13440   REMark now info wdw objects - here only one, the title
13450   REMark make the title data: calculate size and x position of this object
13460 :
13470   main_iot1=RD_IOT(0)
13480     DATA longueur_titre,10,4,INT((lys-10)/2),sous1,sp.titlefg,0,0,titre$
13490 :
13500   REMark now the info wdws
13510 :
13520   main_iwt=RD_IWT(2)                              : REMark there are 3
13530 :
13540 REMark first the title bar
13550 :
13560     DATA xsize%-8-(2*nbr_post%*xesp),lys+2,xesp*nbr_post%+4,0         : REMark x,y sizes & origs
13570     DATA 0,0,sp.infwinbd,sp.titlebg     : REMark shadow, border size & col,paper col
13580     DATA 0                              : REMark object pointer (none)
13590 :
13600 REMark now wdw containing title itself
13610 :
13620     DATA longueur_titre+8,lys,titre_x-4,2
13630     DATA 0,0,sp.infwindbd,sp.infwinbg
13640     DATA main_iot1                      : REMark one object
13650 :
13660 REMark inner border
13670 :
13680     DATA xsize%-8,ysize%-inf1y-2,4,inf1y
13690     DATA 0,1,sp.infwinbd,sp.infwinbg
13700     DATA 0
13710 :
13720 REMark now the appsub wdw - use the OUTPTR functions for this
13730 :
13740   app_addr=0                            : REMark there is none yet
13750   mk_main_app 1                         : REMark (this destroys my DATA pointer)
13760   main_app_list = app_addr+180          : REMark list built by OUTPTR
13770 :
13780 : REMark  working defn
13790 :
13800 RESTORE 13800
13810                                         : REMark reset DATA pointer (due to Basic Linker, restore must be ALONE on the line)
13820   main_defn=RD_WDEF
13830     DATA 2,1,sp.winbd,sp.winbg          : REMark shad,border size & col,pap col
13840     DATA xsize%,ysize%,0,0    : REMark x,y size & initial ptr pos
13850     DATA main_sprite,main_lot,main_iwt,main_app_list : REMark sprite,loose items, info wins, appsubs
13860 END DEFine make_main_wdw
13870 :
13880 DEFine PROCedure warn_make
13890 REMark makes warning & info window
13900 REMark uses global vars green etc...
13910 REMark SETS the global var warning_width% (nbr of chars in one line in wdw)
13920 LOCal lys%,warn_lot,warn_iot1,warn_iwt
13930 LOCal x%,y%,warning_paper,warning_brd
13940   RESTORE 13940
13950   x%=188:y%=82
13960   warning_paper=menu_pap
13970   warning_brd=pink                      : REMark pink
13980 REMark  if menu_brd=4:warning_brd=light_green      : rem light green
13990   lys%=10
14000   DIM warn_lfl%(1)
14010   warn_lot=RD_LOT(std_iattr,1)
14020     DATA 30,lys%,4,4,0,0,cancel$,text+retr,'ESC'
14030     DATA 30,lys%,x%-34,4,0,0,'O',text+retr,"OK"
14040 :
14050   warn_iot1=RD_IOT (0)
14060     DATA 12*9,10,0,1,text,red,2,0,'Attention'
14070 :
14080   warn_iwt=RD_IWT(1)
14090     DATA x%-16,y%-32,8,24
14100     DATA 0,1,red,warning_paper
14110     DATA 0
14120 :
14130     DATA 120,12,(x%-112) DIV 2,4
14140     DATA 0,1,menu_brd,warning_paper
14150     DATA warn_iot1
14160 :
14170   warn_defn=RD_WDEF
14180     DATA 2,1,menu_brd,warning_brd
14190     DATA x%,y%,0,0
14200     DATA main_sprite,warn_lot,warn_iwt,0
14210 :
14220   warning_width%=(x%-16) DIV 6                    : REMark width of warning winfow in chars
14230 :
14240 END DEFine warn_make
14250 :
14260 DEFine FuNction RD_SPRT(drap%)
14270 REMark drap% determine si masque 0
14280   LOCal tmp_patt$(32,32)
14290   LOCal xs,ys,xo,yo, md, l
14300   READ ys,xs,xo,yo,md
14310   DIM tmp_patt$(ys,xs)
14320   FOR l=0 TO ys-1:READ tmp_patt$(l)
14330   l=ALCHP(SPRSP(xs,ys))
14340   SPSET l,xo,yo,md,tmp_patt$(0 TO ys-1,1 TO xs)
14350   IF drap%
14360     POKE_L l+16,espace_vide-(l+16)
14370   END IF
14380   RETurn l
14390 END DEFine RD_SPRT
14400 :
14410 DEFine FuNction RD_WDEF
14420 REMark read window defn
14430   LOCal ldef%(3), lspr, lloose, linfo, lappl, lattr%(3)
14440   RD_WATT lattr%
14450   READ ldef%(0), ldef%(1), ldef%(2), ldef%(3)
14460   READ lspr, lloose, linf, lappl
14470   RETurn MK_WDEF (ldef%, lattr%, lspr, lloose, linf, lappl)
14480 END DEFine RD_WDEF
14490 :
14500 DEFine FuNction RD_LOT (lattr,nitem)
14510   LOCal count(3)
14520   LOCal item, ltyp, a$, lsk$
14530   LOCal ldef%(nitem,6), lptr(3,nitem), lstr$(nitem,85)
14540   lsk$=''
14550   FOR item = 0 TO nitem
14560     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
14570     READ ldef%(item,4), ldef%(item,5)
14580     READ a$: lsk$=lsk$ & a$
14590     READ ltyp
14600     ldef%(item,6)=ltyp: ltyp=(ltyp MOD 256)/2
14610     IF ltyp>10 OR ltyp <0::ltyp=0
14620     IF ltyp
14630       READ lptr(ltyp,count(ltyp))
14640     ELSE
14650       READ lstr$(count(0))
14660     END IF
14670     count(ltyp)=count(ltyp)+1
14680   END FOR item
14690   RETurn MK_LIL (lattr, ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), ldef%(TO, 4 TO 5), lsk$, ldef%(TO, 6), lstr$, lptr(1), lptr(2), lptr(3))
14700 END DEFine RD_LOT
14710 :
14720 DEFine FuNction RD_IWT(nitem)
14730   LOCal item,mc1
14740   LOCal ldef%(nitem,3), latt%(nitem,3), lptr(nitem)
14750   FOR item = 0 TO nitem
14760     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
14770     READ latt%(item,0), latt%(item,1), latt%(item,2), latt%(item,3)
14780     READ lptr(item)
14790   END FOR item
14800   RETurn MK_IWL (ldef%, latt%, lptr)
14810 END DEFine RD_IWT
14820 :
14830 DEFine FuNction RD_IOT(nitem)
14840   LOCal count(3)
14850   LOCal item, ltyp, work1, work2
14860   LOCal ldef%(nitem,4), lptr(3,nitem), lstr$(nitem,85)
14870   FOR item = 0 TO nitem
14880     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
14890     READ ltyp
14900     ldef%(item,4)=ltyp:  ltyp=(ltyp MOD 256)/2
14910     IF ltyp >10:ltyp=0
14920     IF ltyp
14930       READ lptr(0,item),lptr(ltyp,count(ltyp))
14940     ELSE
14950       IF mycolour_mode%=2
14960           READ work1,work2,work3
14970           lptr(0,item)=WL_4_IOL(work1,work2,work3)
14980       ELSE
14990           READ work1
15000           READ work2: work1=work1*256+work2
15010           READ work2: lptr(0,item)=work1*256+work2
15020       END IF
15030       READ lstr$(count(0))
15040     END IF
15050     count(ltyp) = count(ltyp) + 1
15060   END FOR item
15070   RETurn MK_IOL (ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), lptr(0), ldef%(TO, 4), lstr$, lptr(1), lptr(2), lptr(3))
15080 END DEFine RD_IOT
15090 :
15100 DEFine PROCedure RD_IATT (lattr)
15110   LOCal i
15120   READ lattr(0,0), lattr(0,1)
15130   FOR i=1 TO 3: READ lattr(i,0), lattr(i,1), lattr(i,2), lattr(i,3)
15140 END DEFine RD_IATT
15150 :
15160 DEFine PROCedure RD_WATT (lattr%)
15170   READ lattr%(0), lattr%(1),lattr%(2),lattr%(3)
15180 END DEFine RD_WATT
15190 :
