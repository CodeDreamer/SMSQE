100 REMark $$asmb=dev1_booty_outptr_bin,0,10
110 REMark $$asmb=dev1_booty_qptr_bin,50,172
120 REMark $$asmb=dev8_extras_exe_source_SMSQEMake_bin,0,10
130 REMark $$stak=20000
140 compiled=NOT IS_OPEN(#0)
150 IF NOT compiled
160   SCR_SIZE#0, xsz%,ysz%
170   OUTLN#0,xsz%,ysz%-28,0,28
180   WSET -1
190 END IF
200 init
210 main
220 STOP
230 DEFine PROCedure make_new_dir (poste%)
240 REMark get athe new dir where the files are located
250 LOCal quel_rep$
260   IF MENU_OK
270     quel_rep$= DIR_SELECT$("The Files are where?",,10,10,main_coul%,app_coul%)
280   ELSE
290     quel_rep$=getstr$(poste%,0,main_defn,a_lire$)       : REMark use menu extns if possible
300   END IF
310   IF LEN (quel_rep$) >= 5
320      rep$=quel_rep$(1 TO 5)
330   END IF
340   CH_ITEM main_defn,-1,poste%,-1,"",rep$
350   main_lfl%(poste%)=main_lfl%(poste%)+1
360 END DEFine make_new_dir
370 :
380 DEFine PROCedure main
390 LOCal lp%,item%,event%,swnum%,xrel%,yrel%,item%,lp2%,tot%
400   IF compiled
410      OPEN#1,'con'
420      SCR_SIZE#1,xsz%,ysz%
430      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
440   ELSE
450      SCR_SIZE#1,xsz%,ysz%
460      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
470   END IF
480   POS_PTR main_defn,40*65536+20,1
490   REPeat lp%
500     RD_PTR main_defn,item%,swnum%,event%,xrel%,yrel%,main_lfl%,main_mfl%,main_cty%
510     IF swnum%= -1
520       SELect ON item%
530               =esc%     : EXIT lp%      : REMark quit prog
540               =move_it% : CH_WIN main_defn: REMark move
550               =size_it% : change_size xrel%,yrel% : REMark change size
560               =sleep_it%: button main_defn,main_lfl%,main_mfl%,main_cty%,"SMSQ/E Make"
570               =all_it%
580                  set_statusses          : REMark set all appsub item statusses
590                  main_cty%(0,1)=1       : REMark and redraw
600               =go_it%   : do_the_work
610               =dir_it%: make_new_dir item%
620               =target_its% TO target_its%+5
630                           start%=1:machines%=0
640                           FOR lp2%=target_its% TO target_its%+5
650                               IF main_lfl%(lp2%):machines%=machines%+start%
660                               start%=start%*2
670                           END FOR lp2%
680                           make_my_data machines%        : REMark make data arrays
690                           mk_main_app                   : REMark make appsub wdw
700                           main_app_list=app_addr+180
710                           AP_IN main_defn,main_app_list : REMark put it in
720                           DR_WDRW main_defn:main_cty%(0,1)=1 : REMark and redraw entire wdw
730       END SELect
740     END IF
750   END REPeat lp%
760   DR_UNST main_defn
770   IF compiled:CLOSE#1
780   RECHP espace_vide
790 END DEFine main
800 :
810 DEFine PROCedure button (def_trav,menu_drap%,app_drap%,control%,chaine$)
820 REMark set up as button
830   DR_UNST def_trav                      : REMark no more working defn
840   IF compiled: CLOSE#1                  : REMark more wdw
850   BUTTON_WAIT chaine$                   : REMark use menu extensions
860   IF compiled
870     OPEN#1,"CON"
880     DR_PPOS def_trav,-1,-1,menu_drap%,app_drap%,control%
890   ELSE
900     DR_PULD def_trav,-1,-1,menu_drap%,app_drap%,control%
910   END IF
920 END DEFine button
930 :
940 DEFine PROCedure mk_main_app
950 REMark make main application sub window
960 LOCal t2%,temp%,sections%,att_phys%(3)
970   DIM main_cty%(0):DIM main_mfl%(0,0)
980   sections%=2
990   temp%=sections% :t2%=rangees
1000   att_phys%(0)=xsize%-16                : REMark xsize calculated on main wdw x size
1010   att_phys%(1)=t2%                      : REMark y size,nominal,in rows
1020   att_phys%(2)=4                        : REMark x origin x
1030   att_phys%(3)=inf1y +appwinoffset%     : REMark y origin
1040   app_addr=fen_app2(app_addr,mdata$,temp%,1,att_phys%,"DO ")
1050   IF app_addr
1060       DIM main_cty%(sections%,2)        : REMark ctrl defn
1070       main_cty%(0,0)=1                  : REMark show 1 section
1080       main_cty%(1,2)=t2%
1090       IF sections%=2:main_cty%(2,2)=t2% : REMark but allow 2
1100       IF temp%
1110            DIM main_mfl%(temp%-1,0)     : REMark flag array
1120            set_statusses                : REMark and set all statusses
1130       ELSE
1140            POKE_W app_addr,PEEK_W(app_addr)+8
1150       END IF
1160    REMark   scroll_arrows t2%,temp%,app_addr,main_cty%
1170   END IF
1180 END DEFine mk_main_app
1190 :
1200 DEFine PROCedure set_statusses
1210 REMark set all statusses of menu appsub objects
1220 LOCal temp%,lp%,stat%
1230   temp%=DIMN(main_mfl%,1)               : REMark how many are there?
1240   stat%=main_lfl%(all_it%)              : REMark status of "all" item
1250   FOR lp%=0 TO temp%
1260     IF mdata$(lp%+1,1)='*'
1270       main_mfl%(lp%,0)=16               : REMark comments set to unavailable
1280     ELSE
1290       main_mfl%(lp%,0)=stat%            : REMark all other to status
1300     END IF
1310   END FOR lp%
1320 END DEFine set_statusses
1330 :
1340 DEFine PROCedure make_my_data (what_machine%)
1350 REMark what_machine% = 1 -> generic
1360 REMark what_machine% = 2 -> atari
1370 REMark what_machine% = 4 -> Gold Card
1380 REMark what_machine% = 8 -> Qx0
1390 REMark what_machine% = 16 -> QXL
1400 REMark what_machine% = 32 -> aurora
1410 REMark these may be combined
1420   DIM mdata$(150,50)
1430   REMark first make generic data
1440   data_count%=1                         : REMark general data count (global var)
1450   IF what_machine%&&1
1460     generic_data                        : REMark set up generic data
1470     read_my_datas                       : REMark and read it
1480   END IF
1490   IF what_machine%&&2
1500      atari_data                         : REMark set up machine specific data....
1510      read_my_datas                      : REMark and read it
1520   END IF
1530   IF what_machine%&&4
1540      gc_data                            : REMark set up machine specific data....
1550      read_my_datas                      : REMark and read it
1560   END IF
1570   IF what_machine%&&8
1580      q40_data                           : REMark set up machine specific data....
1590      read_my_datas                      : REMark and read it
1600   END IF
1610   IF what_machine%&&16
1620      qxl_data                           : REMark set up machine specific data....
1630      read_my_datas                      : REMark and read it
1640   END IF
1650   IF what_machine%&&32
1660      aurora_data                        : REMark set up machine specific data....
1670      read_my_datas                      : REMark and read it
1680   END IF
1690 END DEFine make_my_data
1700 :
1710 DEFine PROCedure read_my_datas
1720 REMark reads datas into the global var mdata$
1730 REMark uses data_count% from make_my_data
1740 LOCal lp%
1750   REPeat lp%
1760     READ mdata$(data_count%)
1770     IF mdata$(data_count%)="":EXIT lp%
1780     data_count%=data_count%+1
1790   END REPeat lp%
1800 END DEFine read_my_datas
1810 :
1820 DEFine PROCedure generic_data
1830 RESTORE 1830
1840   DATA '**** -- SMSQ Generic -- ****'
1850   DATA 'smsq_smsq_loader_link'
1860   DATA 'smsq_smsq_fh_link'
1870   DATA 'smsq_smsq_link'
1880   DATA 'smsq_smsq_cache_link'
1890   DATA 'smsq_sbas_link'
1900   DATA 'smsq_smsq_lang_link'
1910   DATA 'smsq_sbas_lang_link'
1920   DATA 'smsq_sbas_procs_link'
1930   DATA 'smsq_smsq_wman_link'
1940   DATA 'smsq_smsq_hotkey_link'
1950   DATA 'smsq_smsq_vers_link'
1960   DATA 'smsq_home_link'
1970   DATA 'smsq_smsq_1mb_link',''
1980 END DEFine generic_data
1990 :
2000 DEFine PROCedure q40_data
2010 RESTORE 2010
2020   DATA '**** -- Q40/Q60 -- ****'
2030   DATA 'smsq_q40_hwinit_link'
2040   DATA 'smsq_q40_nasty_link'
2050   DATA 'smsq_q40_cache_link'
2060   DATA 'smsq_q40_cachemode_link'
2070   DATA 'smsq_smsq_cache40c_link'
2080   DATA 'smsq_q40_driver_ser_link'
2090   DATA 'smsq_q40_driver_dv3_link'
2100   DATA 'smsq_q40_driver_ql_link'
2110   DATA 'smsq_q40_driver_16_link'
2120   DATA 'smsq_q40_kbd_lang_link'
2130   DATA 'smsq_sbas_procs_link'
2140   DATA 'smsq_q40_sysspr_link',''
2150 END DEFine q40_data
2160 :
2170 DEFine PROCedure atari_data
2180 RESTORE 2180
2190   DATA '**** -- Atari -- ****'
2200   DATA 'smsq_atari_hwinit_link'
2210   DATA 'smsq_atari_nasty_link'
2220   DATA 'smsq_atari_driver_ql_link'
2230   DATA 'smsq_atari_driver_mo_link'
2240   DATA 'smsq_atari_driver_ser_link'
2250   DATA 'smsq_atari_driver_dv3_link'
2260   DATA 'smsq_atari_kbd_lang_link'
2270   DATA 'smsq_atari_sysspr_link'
2280   DATA 'sys_boot_st_host_link',''
2290 END DEFine atari_data
2300 :
2310 DEFine PROCedure gc_data
2320 RESTORE 2320
2330   DATA '**** -- (Super)GoldCard -- ****'
2340   DATA 'smsq_gold_host_link'
2350   DATA 'smsq_gold_hwinit_link'
2360   DATA 'smsq_gold_nasty_link'
2370   DATA 'smsq_gold_nasty_s_link'
2380   DATA 'smsq_gold_driver_ql_link'
2390   DATA 'smsq_gold_driver_most_link'
2400   DATA 'smsq_gold_driver_nd_link'
2410   DATA 'smsq_gold_driver_nds_link'
2420   DATA 'smsq_gold_driver_dv3_link'
2430   DATA 'smsq_gold_kbd_lang_link'
2440   DATA 'smsq_gold_kbd_abc_lng_link'
2450   DATA 'smsq_gold_kbd_abc_link'
2460   DATA 'smsq_gold_roms_link'
2470   DATA 'smsq_gold_qimi_link'
2480   DATA 'smsq_gold_sysspr_link',''
2490 END DEFine gc_data
2500 :
2510 DEFine PROCedure aurora_data
2520 RESTORE 2520
2530   DATA '**** -- Aurora -- ****'
2540   DATA 'smsq_gold_host_link'
2550   DATA 'smsq_gold_hwinit_link'
2560   DATA 'smsq_gold_nasty_link'
2570   DATA 'smsq_gold_nasty_s_link'
2580   DATA 'smsq_aurora_driver_8_link'
2590   DATA 'smsq_gold_driver_ql_link'
2600   DATA 'smsq_gold_driver_most_link'
2610   DATA 'smsq_gold_driver_nd_link'
2620   DATA 'smsq_gold_driver_nds_link'
2630   DATA 'smsq_gold_driver_dv3_link'
2640   DATA 'smsq_gold_kbd_lang_link'
2650   DATA 'smsq_gold_kbd_abc_lng_link'
2660   DATA 'smsq_gold_kbd_abc_link'
2670   DATA 'smsq_gold_roms_link'
2680   DATA 'smsq_gold_qimi_link'
2690   DATA 'smsq_aurora_sysspr_link'
2700   DATA 'smsq_gold_sysspr_link',''
2710 END DEFine aurora_data
2720 :
2730 DEFine PROCedure qxl_data
2740 RESTORE 2740
2750   DATA '**** -- QXL -- ****'
2760   DATA 'smsq_qxl_host_link'
2770   DATA 'smsq_qxl_hwinit_link'
2780   DATA 'smsq_qxl_nasty_e_link'
2790   DATA 'smsq_qxl_driver_ql_link'
2800   DATA 'smsq_qxl_driver_16_link'
2810   DATA 'smsq_qxl_driver_most_link'
2820   DATA 'smsq_qxl_driver_nd_link'
2830   DATA 'smsq_qxl_procs_link'
2840   DATA 'smsq_qxl_driver_dv3e_link'
2850   DATA 'smsq_qxl_kbd_lang_link'
2860   DATA 'smsq_qxl_ecache_link'
2870   DATA 'smsq_qxl_sysspr_link',''
2880 END DEFine qxl_data
2890 :
2900 DEFine PROCedure do_the_work
2910 REMark this tries to compile all of the selected files
2920 REMark if the file was compiled OK, status (ogf the link file) set to available
2930 REMark if the file was not compiled ok, status left selected
2940 LOCal lp%,bound%,a$,my_error%,tot_err%
2950   bound%=DIMN(main_mfl%,1)              : REMark number of items to check
2960   tot_err%=0
2970   FOR lp%=0 TO bound%
2980     a$=mdata$(lp%+1)                    : REMark potential file to compile
2990     IF a$(1)<>"*" AND main_mfl%(lp%,0)
3000        a$=rep$&a$                       : REMark add dir
3010        my_error%=compile_and_check% (a$): REMark try to compile
3020        IF NOT my_error%
3030            main_mfl%(lp%,0)=1
3040            DR_ADRW main_defn,0,main_mfl%,main_cty%
3050        END IF
3060        tot_err%=tot_err%+my_error%
3070     END IF
3080   END FOR lp%
3090   main_cty%(0,1)=1
3100   main_lfl%(go_it%)=1
3110   IF main_lfl%(make_it%)=128 AND NOT tot_err%:make_targets
3120 END DEFine do_the_work
3130 :
3140 DEFine FuNction compile_and_check% (a$)
3150 REMark this tries to compile a file, using the "make" prog
3160 REMark this returns 1 if errors, else 0
3170 LOCal chan%,is_ok$,ch_len
3180   EXEP_W "make";a$&" -w -l"             : REMark exec make & wait
3190   is_ok$=a$(1 TO LEN(a$)-4)             : REMark file name w/o extension
3200   chan%=FOP_IN(is_ok$&'log')            : REMark open log file
3210   IF chan%<0:RETurn 1                   : REMark can't even open it!
3220   ch_len=FLEN(#chan%)                   : REMark file length
3230   GET#chan%\ch_len-10                   : REMark this is where error report is
3240   INPUT#chan%,is_ok$                    : REMark get it
3250   CLOSE#chan%
3260   IF NOT is_ok$=="No errors"
3270     RETurn 1                            : REMark anything other than "no errors" signals errors
3280   ELSE
3290     RETurn 0
3300   END IF
3310 END DEFine compile_and_check%
3320 :
3330 DEFine PROCedure make_targets
3340 REMark targets are only made if there was no error during compilation
3350 REMark to achieve this, a primitive approach is used:
3360 REMark a check is made whether an appsub menu item status is still selected
3370 REMark if it is, then the
3380   LOCal lp%,current_target%,tot_targets%
3390   tot_targets%=0                        : REMark no targets to make yet
3400   current_target%=0                     : REMark and no target yet
3410   FOR lp%=0 TO data_count%-2
3420     IF main_mfl%(lp%,0)=16
3430       REMark we're covering a new target
3440       tot_targets%=tot_targets%+current_target% : REMark show old target
3450       current_target%=find_target%(lp%+1) : REMark get new target we're covering
3460     ELSE
3470       IF main_mfl%(lp%,0)<>0:current_target%=0
3480     END IF
3490   END FOR lp%
3500   tot_targets%=tot_targets%+current_target%
3510   REMark at the end, here, tot_targets holds a bitmap
3520   REMark of targets for which compilation was ok
3530   IF (machines%&&1) AND NOT (tot_targets% && 1):RETurn : REMark generic was asked for but not compiled correctly!
3540   IF tot_targets%&&2:atari_target
3550   IF tot_targets%&&4:gc_target
3560   IF tot_targets%&&8:qx0_target
3570   IF tot_targets%&&16:qxl_target
3580   IF tot_targets%&&32:aurora_target
3590 END DEFine make_targets
3600 :
3610 DEFine PROCedure atari_target
3620   cmd$='m'
3630   p$=rep$&'sys_boot_st_flp'
3640   f$=rep$&'sys_boot_file'
3650   ah$=rep$&'sys_boot_st_host'
3660   ld$=rep$&'smsq_smsq_loader'
3670   hi$=rep$&'smsq_atari_hwinit'
3680   os1f$=rep$&'smsq_smsq_fh_os'
3690   os1$=rep$&'smsq_smsq_os'
3700   ca$=rep$&'smsq_smsq_cache'
3710   os2$=rep$&'smsq_sbas_control'
3720   ns1$=rep$&'smsq_atari_nasty'
3730   ln1$=rep$&'smsq_smsq_lang'
3740   ln2$=rep$&'smsq_atari_kbd_lang'
3750   ln3$=rep$&'smsq_sbas_lang'
3760   ex1$=rep$&'smsq_sbas_procs_x'
3770   ex2$=rep$&'smsq_atari_driver_dv3'
3780   ex3$=rep$&'smsq_atari_driver_ser'
3790   ex4$=rep$&'smsq_atari_driver_mono'
3800   ex5$=rep$&'smsq_atari_driver_ql'
3810   ex6$=rep$&'smsq_smsq_wman'
3820   ex7$=rep$&'smsq_smsq_hotkey'
3830   ex8$=rep$&'smsq_atari_sysspr'
3840   ex9$=rep$&'smsq_home_home'
3850   nl$=rep$&'sys_boot_null'
3860   IF 'm' INSTR cmd$ = 0: ex4$ = nl$
3870   EW f$, ah$,ld$,hi$,os1f$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$, rep$&'smsq_atari_SMSQ.PRG'
3880 END DEFine atari_target
3890 :
3900 DEFine PROCedure gc_target
3910   f$=rep$&'sys_boot_file'
3920   v$=rep$&'smsq_smsq_vers'
3930   h$=rep$&'smsq_gold_host'
3940   ld$=rep$&'smsq_smsq_loader'
3950   hi$=rep$&'smsq_gold_hwinit'
3960   os1$=rep$&'smsq_smsq_os'
3970   ca$=rep$&'smsq_smsq_cache'
3980   os2$=rep$&'smsq_sbas_control'
3990   ns1$=rep$&'smsq_gold_nasty'
4000   ns2$=rep$&'smsq_gold_nasty_s'
4010   ln1$=rep$&'smsq_smsq_lang'
4020   ln2$=rep$&'smsq_gold_kbd_lang'
4030   ln2a$=rep$&'smsq_gold_kbd_abc_lang'
4040   ln3$=rep$&'smsq_sbas_lang'
4050   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
4060   ex2a$=rep$&'smsq_gold_kbd_abc'
4070   ex2$=rep$&'smsq_gold_driver_most'
4080   ex3a$=rep$&'smsq_gold_driver_ql'
4090   ex3q$=rep$&'smsq_gold_qimi'
4100   ex3w$=rep$&'smsq_smsq_wman'
4110   ex4$=rep$&'smsq_gold_driver_dv3'
4120   ex5$=rep$&'smsq_gold_driver_nd'
4130   ex6$=rep$&'smsq_gold_driver_nds'
4140   ex7$=rep$&'smsq_smsq_hotkey'
4150   ex8$=rep$&'smsq_gold_sysspr'
4160   ex9$=rep$&'smsq_home_home'
4170   nl$=rep$&'sys_boot_null'
4180   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex3a$,ex3q$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,ex1$,ex9$, rep$&'SMSQ_GOLD_gold'
4190 END DEFine gc_target
4200 :
4210 DEFine PROCedure aurora_target
4220   f$='dev8_sys_boot_file'
4230   v$='dev8_smsq_smsq_vers'
4240   h$='dev8_smsq_gold_host'
4250   ld$='dev8_smsq_smsq_loader'
4260   hi$='dev8_smsq_gold_hwinit'
4270   os1$='dev8_smsq_smsq_os'
4280   ca$='dev8_smsq_smsq_cache'
4290   os2$='dev8_smsq_sbas_control'
4300   ns1$='dev8_smsq_gold_nasty'
4310   ns2$='dev8_smsq_gold_nasty_s'
4320   ln1$='dev8_smsq_smsq_lang'
4330   ln2$='dev8_smsq_gold_kbd_lang'
4340   ln2a$='dev8_smsq_gold_kbd_abc_lang'
4350   ln3$='dev8_smsq_sbas_lang'
4360   ex1$='dev8_smsq_sbas_procs_x'   : REMark this must be after "roms"
4370   ex2a$='dev8_smsq_gold_kbd_abc'
4380   ex2$='dev8_smsq_gold_driver_most'
4390   ex3a$='dev8_smsq_gold_driver_ql'
4400   ex3b$='dev8_smsq_aurora_driver_8'
4410   ex3q$='dev8_smsq_gold_qimi'
4420   ex3w$='dev8_smsq_smsq_wman'
4430   ex3sq$='dev8_smsq_aurora_sysspr'
4440   ex3sh$='dev8_smsq_gold_sysspr'
4450   ex4$='dev8_smsq_gold_driver_dv3'
4460   ex5$='dev8_smsq_gold_driver_nd'
4470   ex6$='dev8_smsq_gold_driver_nds'
4480   ex8$="dev8_smsq_home_home"
4490   ex9$='dev8_smsq_smsq_hotkey'
4500   ex10$='dev8_smsq_gold_roms'
4510   nl$='dev8_sys_boot_null'
4520   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex3a$,ex3b$,ex3q$,ex3w$,ex3sh$,ex4$,ex5$,ex6$,ex9$,ex10$,ex8$,ex1$, 'dev8_SMSQ_Aurora_SMSQE'
4530 END DEFine aurora_target
4540 :
4550 DEFine PROCedure qx0_target
4560   nl$=rep$&'sys_boot_null'
4570   f$=rep$&'sys_boot_file'
4580   qr$=rep$&'sys_boot_q40_rom'
4590   ld$=rep$&'smsq_smsq_loader'
4600   hi$=rep$&'smsq_q40_hwinit'
4610   os1$=rep$&'smsq_smsq_1mb_os' : REMark change for fast memory
4620   ca$=rep$&'smsq_smsq_cache40c'
4630   caq$=rep$&'smsq_q40_cache'
4640   caq4$=rep$&'smsq_q40_cachemode'
4650   os2$=rep$&'smsq_sbas_control'
4660   ns2$=rep$&'smsq_q40_nasty'
4670   ln1$=rep$&'smsq_smsq_lang'
4680   ln2$=rep$&'smsq_q40_kbd_lang'
4690   ln3$=rep$&'smsq_sbas_lang'
4700   ex1$=rep$&'smsq_sbas_procs_x'
4710   ex2$=rep$&'smsq_q40_driver_dv3'
4720   ex3$=rep$&'smsq_q40_driver_ser'
4730   ex4a$=rep$&'smsq_q40_driver_ql'
4740   ex4b$=rep$&'smsq_q40_driver_16'
4750   ex4w$=rep$&'smsq_smsq_wman'
4760   ex5$=rep$&'smsq_q40_sysspr'
4770   ex9$=rep$&'smsq_smsq_hotkey'
4780   ex10$=rep$&'smsq_home_home'
4790   EW f$, qr$,ld$,hi$,os1$,ca$,caq$,os2$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4a$,ex4b$,ex4w$,ex5$,ex9$,ex10$,ns2$,caq4$, rep$&'smsq_q40_rom'
4800 END DEFine qx0_target
4810 :
4820 DEFine PROCedure qxl_target
4830   IF FOP_IN (rep$&'smsq_qxl_qxlh.exe') <> -7: INPUT 'QXLH.EXE exists'; cmd$: QUIT
4840   p$   = rep$&'sys_boot_file'
4850   bl$  = rep$&'smsq_qxl_qxl2_exe'
4860   hst$ = rep$&'smsq_qxl_host'
4870   hi$  = rep$&'smsq_qxl_hwinit'
4880   ld$  = rep$&'smsq_smsq_loader'
4890   os1$ = rep$&'smsq_smsq_os'
4900   ca$  = rep$&'smsq_smsq_cache'
4910   os2$ = rep$&'smsq_sbas_control'
4920   ns1$ = rep$&'smsq_qxl_nasty_e'
4930   ln1$ = rep$&'smsq_smsq_lang'
4940   ln2$ = rep$&'smsq_qxl_kbd_lang'
4950   ln3$ = rep$&'smsq_sbas_lang'
4960   ex1$ = rep$&'smsq_qxl_procs_x'
4970   ex2$ = rep$&'smsq_qxl_driver_most'  : REMark Keyboard before CON
4980   ex3$ = rep$&'smsq_qxl_driver_ql'
4990   ex3b$ = rep$&'smsq_qxl_driver_16'
5000   ex3w$ = rep$&'smsq_smsq_wman'
5010   ex4$ = rep$&'smsq_qxl_driver_nd'
5020   ex5$ = rep$&'smsq_qxl_driver_dv3e'
5030   ex6$= rep$&'smsq_smsq_hotkey'
5040   ex7$ = rep$&'smsq_qxl_sysspr'
5050   ex8$=rep$&'smsq_home_home'
5060   last$ = rep$&'smsq_qxl_ecache'
5070   dml$ = rep$&'smsq_qxl_dummy'
5080   qm$  = rep$&'qmon_qxl_bin'
5090   r$='ram1_smsqe.exe' : f$=rep$&'smsq_qxl_smsqe.exe'
5100   DELETE f$
5110   EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,last$, r$
5120   EW cct, bl$,r$, f$
5130   DELETE r$
5140   OPEN #4,f$
5150   lenf=FLEN(#4)
5160   lens% = INT ((lenf+511)/512)
5170   lenb% = lenf - lens% * 512
5180   IF lenb%: lenb% = lenb% + 512
5190   BPUT #4\2, lenb% MOD 256, lenb% DIV 256, lens% MOD 256, lens% DIV 256
5200   CLOSE #4
5210 END DEFine qxl_target
5220 :
5230 DEFine FuNction find_target% (current%)
5240   IF "SMSQ Generic" INSTR mdata$(current%):RETurn 1
5250   IF "Atari" INSTR mdata$(current%):RETurn 2
5260   IF "GoldCard" INSTR mdata$(current%):RETurn 4
5270   IF "Q40" INSTR mdata$(current%):RETurn 8
5280   IF "QXL" INSTR mdata$(current%):RETurn 16
5290   IF "Aurora" INSTR mdata$(current%):RETurn 32
5300   RETurn 0                             : REMark huh???
5310 END DEFine find_target%
5320 :
5330 DEFine PROCedure change_size   (x%,y%)
5340 LOCal xrel%,yrel%,temp,a$,ttemp%(main_lit%),abs%,yabs%,t%,bt$
5350   main_lfl%(size_it%)=1                 : REMark reset to available
5360   ARRCOPY main_lfl%,ttemp%              : REMark keep statusses
5370   CH_WIN main_defn,xrel%,yrel%          : REMark ptr displacement
5380   xsize%=xsize%-xrel%
5390   ysize%=ysize%-yrel%                   : REMark new sizes
5400   t%=2^4+2^5
5410   RPTR xabs%,yabs%,t%,t%,xrel%,yrel%,bt$: REMark get absolute coords
5420   IF xsize%>xsz%-16:xsize%=xsz%-16
5430   IF ysize%>ysz%-16:ysize%=ysz%-16      : REMark max values
5440   DR_UNST main_defn                     : REMark no more wdw defn...
5450   DR_REMV main_defn                     : REMark .... now!
5460   app_addr=0                            : REMark nor any appsub wdw
5470   make_main_wdw                         : REMark make window anew
5480   ARRCOPY ttemp%,main_lfl%
5490   t%=lxs*3
5500   xabs%=xabs%-t%:IF xabs%<0:xabs%=0     : REMark position ptr nicely
5510   yabs%=yabs%-8:IF yabs%<0:yabs%=0
5520   SPTR xabs%,yabs%,0                    : REMark now
5530   IF compiled
5540      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
5550   ELSE
5560      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
5570   END IF
5580    POS_PTR main_defn,t%*65536+8,1       : REMark position pointer in wdw
5590 END DEFine change_size
5600 :
5610 REMark as of here, standard functions/procs (except init)
5620 :
5630 DEFine FuNction warning_pos% (mess$,channel%,title$,pos)
5640 REMark warning wdw with positioning
5650   POS_PTR main_defn,pos,-1
5660   RETurn warning%(mess$,channel%,title$)
5670 END DEFine warning_pos%
5680 :
5690 DEFine FuNction warning%(mess$,channel%,title$)
5700 REMark shows a warning message (mess$) in a small window. Hit ESC or OK
5710 REMark to return; returns 0 if ESC, 1 if OK  hit
5720 LOCal item%,lp%,chan%,y%,swnum%
5730   IF channel%
5740     chan%=channel%
5750   ELSE
5760     chan%=FOPEN("con")
5770   END IF
5780   CH_ITEM warn_defn,-3,0,-1,'','Attention'
5790   CH_ITEM warn_defn,-1,0,-1,cancel$,'ESC'
5800   CH_ITEM warn_defn,-1,1,-1,'O','OK'
5810   warn_lfl%(0)=1:warn_lfl%(1)=1
5820   POKE_W warn_defn+46,pink                        : REMark paper colour of wdw
5830   POKE_W warn_defn+44,red                         : REMark border around wdw
5840   DR_PULD warn_defn,-1,-1
5850   DR_IWDF #chan%,warn_defn,0
5860   PAPER#chan%,white :INK#chan%,red
5870   IF title$<>"":PRINT#chan%,title$;" :"
5880   PRINT #chan%,mess$
5890   POS_PTR warn_defn,160*65536+10,-1
5900   REPeat lp%
5910     RD_PTR warn_defn,item%,swnum%,y%,y%,y%,warn_lfl%
5920     IF swnum%=-1:EXIT lp%
5930   END REPeat lp%
5940   IF NOT channel%:CLOSE#chan%
5950   DR_UNST warn_defn
5960   RETurn item%
5970 END DEFine warning%
5980 :
5990 DEFine PROCedure info_pos (mess$,channel%,pos)
6000 REMark warning wdw with positioning
6010   POS_PTR main_defn,pos,-1
6020   info mess$,channel%
6030 END DEFine info_pos
6040 :
6050 DEFine PROCedure info(mess$,channel%)
6060 REMark shows window containing mess$
6070   LOCal chan%
6080   IF channel%
6090     chan%=channel%
6100   ELSE
6110     chan%=FOPEN("con")
6120   END IF
6130   CH_ITEM warn_defn,-3,0,-1,"",'  Info.  '
6140   CH_ITEM warn_defn,-1,0,-1,"",'Info'
6150   CH_ITEM warn_defn,-1,1,-1,"",'Info'
6160   POKE_W warn_defn+44,green                       : REMark border
6170   POKE_W warn_defn+46,light_green                  : REMark paper col
6180   DR_PULD warn_defn,-1,-1
6190   POS_PTR warn_defn,90*65536+70,-1
6200   DR_IWDF #chan%,warn_defn,0
6210   PAPER#chan%,white:INK#chan%,black
6220   PRINT #chan%,mess$
6230   IF NOT channel%:CLOSE#chan%
6240 END DEFine info
6250 :
6260 DEFine PROCedure make_colours (colourway%)
6270 REMark this initialises the GLOBAL variables menu_pap, menu_brd, menu_inv,
6280 REMark menu_ink and menu_bar according to the colourway passed as parameter.
6290 REMark The GLOBAl variable mycolour_mode% must have been set prior to calling
6300 REMark this function, preferrably via the function set_colours%.
6310 REMark Moreover the colours black, white,red, green,wg_stripes, etc...
6320 REMark must have been set prior to calling this procedure:
6330 REMark Preferrably, they were also set via the function set_colours%
6340 :
6350 REMark this function needs the extension WL_MK16 (outptr_bin)
6360 :
6370 REMark the colourways% are:
6380 REMark 0= white/green
6390 REMark 1= black/red
6400 REMark 2= white/red
6410 REMark 3= black/green
6420 REMark 4= use system palette (only available in modes 32 and 33)
6430 :
6440 REMark the Global war mycolour_mode% is:
6450 REMark 0 = we are in QL mode 4
6460 REMark 1 = we are in PAL mode (256 colours)
6470 REMark 2 - we are in 24 bit colour mode
6480 REMark preferrably, this is set via the function set_colours%
6490 :
6500   IF colourway%>7 OR colourway%<0: colourway%=0: REMark must be between 0 & 7
6510 :
6520 REMark first make sure that what we request is possible.
6530 REMark colourway 4 is only possible if we are in 24 bit colour mode
6540 REMark if it is requested in another mode, it is set to 0
6550 :
6560   IF colourway%>3
6570     IF mycolour_mode%< 2
6580        colourway%=0
6590      ELSE
6600        make_my_palette colourway%-4:RETurn   : REMark set colours according to system palette
6610     END IF
6620   END IF
6630 :
6640   SELect ON mycolour_mode%
6650     =0                                  : REMark QL 4 colours
6660       SELect ON colourway%
6670           =0:                           : REMark white/green
6680                 menu_pap=white
6690                 menu_brd=green
6700                 menu_ink=black
6710                 menu_bar=wg_striped
6720                 menu_inv=red
6730           =1:                           : REMark black/red
6740                 menu_pap=black
6750                 menu_brd=red
6760                 menu_ink=white
6770                 menu_bar=br_striped
6780                 menu_inv=green
6790           =2:                           : REMark white/red
6800                 menu_pap=white
6810                 menu_brd=red
6820                 menu_ink=black
6830                 menu_bar=wr_striped
6840                 menu_inv=green
6850           =3:                           : REMark black/green
6860                 menu_pap=black
6870                 menu_brd=green
6880                 menu_ink=white
6890                 menu_bar=bg_striped
6900                 menu_inv=red
6910       END SELect
6920     =1                                  : REMark pal colours
6930       SELect ON colourway%
6940           =0:                           : REMark white/green
6950                 menu_pap=white+256
6960                 menu_brd=green+256
6970                 menu_ink=black+256
6980                 menu_bar=wg_striped+256
6990                 menu_inv=red +256
7000           =1:                           : REMark black/red
7010                 menu_pap=black+256
7020                 menu_brd=red  +256
7030                 menu_ink=white +256
7040                 menu_bar=br_striped +256
7050                 menu_inv=green +256
7060                 omenu_pap=black+256
7070           =2:                           : REMark white/red
7080                 menu_pap=white +256
7090                 menu_brd=red   +256
7100                 menu_ink=black +256
7110                 menu_bar=wr_striped +256
7120                 menu_inv=green +256
7130           =3:                           : REMark black/green
7140                 menu_pap=black +256
7150                 menu_brd=green +256
7160                 menu_ink=white +256
7170                 menu_bar=bg_striped +256
7180                 menu_inv=red   +256
7190       END SELect
7200     =2                                  : REMark 24 bit colours
7210       SELect ON colourway%
7220           =0:                           : REMark white/green
7230                 menu_pap=WL_MK16(white)
7240                 menu_brd=WL_MK16(green)
7250                 menu_ink=WL_MK16(black)
7260                 menu_bar=WL_MK16(wg_striped)
7270                 menu_inv=WL_MK16(red)
7280           =1:                           : REMark black/red
7290                 menu_pap=WL_MK16(black)
7300                 menu_brd=WL_MK16(red)
7310                 menu_ink=WL_MK16(white)
7320                 menu_bar=WL_MK16(br_striped)
7330                 menu_inv=WL_MK16(green)
7340           =2:                           : REMark white/red
7350                 menu_pap=WL_MK16(white)
7360                 menu_brd=WL_MK16(red)
7370                 menu_ink=WL_MK16(black)
7380                 menu_bar=WL_MK16(wr_striped)
7390                 menu_inv=WL_MK16(green)
7400           =3:                           : REMark black/green
7410                 menu_pap=WL_MK16(black)
7420                 menu_brd=WL_MK16(green)
7430                 menu_ink=WL_MK16(white)
7440                 menu_bar=WL_MK16(bg_striped)
7450                 menu_inv=WL_MK16(red)
7460       END SELect
7470   END SELect
7480 :
7490   make_my_old_palette
7500 END DEFine make_colours
7510 :
7520 DEFine PROCedure make_my_palette (cw%)
7530   sp.winbd            = HEX('0200') : REMark Window border
7540   sp.winbg            = HEX('0201') : REMark Window background
7550   sp.winfg            = HEX('0202') : REMark Window foreground
7560   sp.winmg            = HEX('0203') : REMark Window middleground
7570   sp.titlebg          = HEX('0204') : REMark Title background
7580   sp.titletextbg      = HEX('0205') : REMark Title text background
7590   sp.titlefg          = HEX('0206') : REMark Title foreground
7600   sp.litemhigh        = HEX('0207') : REMark Loose item highlight
7610   sp.litemavabg       = HEX('0208') : REMark Loose item available background
7620   sp.litemavafg       = HEX('0209') : REMark Loose item available foreground
7630   sp.litemselbg       = HEX('020a') : REMark Loose item selected background
7640   sp.litemselfg       = HEX('020b') : REMark Loose item selected foreground
7650   sp.litemunabg       = HEX('020c') : REMark Loose item unavailable background
7660   sp.litemunafg       = HEX('020d') : REMark Loose item unavailable foreground
7670   sp.infwinbd         = HEX('020e') : REMark Information window border
7680   sp.infwinbg         = HEX('020f') : REMark Information window background
7690   sp.infwinfg         = HEX('0210') : REMark Information window foreground
7700   sp.infwinmg         = HEX('0211') : REMark Information window middleground
7710   sp.subinfbd         = HEX('0212') : REMark Subsidiary information window border
7720   sp.subinfbg         = HEX('0213') : REMark Subsidiary information window background
7730   sp.subinffg         = HEX('0214') : REMark Subsidiary information window foreground
7740   sp.subinfmg         = HEX('0215') : REMark Subsidiary information window middleground
7750   sp.appbd            = HEX('0216') : REMark Application window border
7760   sp.appbg            = HEX('0217') : REMark Application window background
7770   sp.appfg            = HEX('0218') : REMark Application window foreground
7780   sp.appmg            = HEX('0219') : REMark Application window middleground
7790   sp.appihigh         = HEX('021a') : REMark Application window item highlight
7800   sp.appiavabg        = HEX('021b') : REMark Application window item available background
7810   sp.appiavafg        = HEX('021c') : REMark Application window item available foreground
7820   sp.appiselbg        = HEX('021d') : REMark Application window item selected background
7830   sp.appiselfg        = HEX('021e') : REMark Application window item selected foreground
7840   sp.appiunabg        = HEX('021f') : REMark Application window item unavailable background
7850   sp.appiunafg        = HEX('0220') : REMark Application window item unavailable foreground
7860   sp.scrbar           = HEX('0221') : REMark Pan/scroll bar
7870   sp.scrbarsec        = HEX('0222') : REMark Pan/scroll bar section
7880   sp.scrbararr        = HEX('0223') : REMark Pan/scroll bar arrow
7890   sp.buthigh          = HEX('0224') : REMark Button highlight
7900   sp.butbd            = HEX('0225') : REMark Button border
7910   sp.butbg            = HEX('0226') : REMark Button background
7920   sp.butfg            = HEX('0227') : REMark Button foreground
7930   sp.hintbd           = HEX('0228') : REMark Hint border
7940   sp.hintbg           = HEX('0229') : REMark Hint background
7950   sp.hintfg           = HEX('022a') : REMark Hint foreground
7960   sp.hintmg           = HEX('022b') : REMark Hint middleground
7970   sp.errbg            = HEX('022c') : REMark Error message background
7980   sp.errfg            = HEX('022d') : REMark Error message foreground
7990   sp.errmg            = HEX('022e') : REMark Error message middleground
8000   sp.shaded           = HEX('022f') : REMark sp.shaded area
8010   sp.3ddark           = HEX('0230') : REMark Dark 3D border shade
8020   sp.3dlight          = HEX('0231') : REMark Light 3D border shade
8030   sp.vertfill         = HEX('0232') : REMark Vertical area fill
8040   IF NOT (IS_OPEN(#0) OR IS_OPEN(#1))
8050     OPEN#1,'con'
8060     SP_JOBPAL -1,cw%
8070     CLOSE#1
8080   ELSE
8090     SP_JOBPAL -1,cw%
8100   END IF
8110 END DEFine make_my_palette
8120 :
8130 DEFine PROCedure make_my_old_palette
8140 REMark this translates the colours used in the wdws into 4 colour mode...
8150   sp.winbd          = menu_brd          : REMark Window border
8160   sp.winbg          = menu_pap          : REMark Window background
8170   sp.winfg          = menu_pap          : REMark Window foreground
8180   sp.titletextbg    = menu_pap          : REMark Title text background
8190   sp.titlefg        = menu_ink          : REMark Title foreground
8200   sp.titlebg        = menu_bar          : REMark Title background
8210   sp.litemhigh      = menu_ink          : REMark Loose item highlight
8220   sp.litemavabg     = menu_pap          : REMark Loose item available background
8230   sp.litemavafg     = menu_ink          : REMark Loex fifiose item available foreground
8240   sp.litemselbg     = menu_brd          : REMark Loose item selected background
8250   sp.litemselfg     = menu_ink          : REMark Loose item selected foreground
8260   sp.litemunabg     = menu_pap          : REMark Loose item unavailable background
8270   sp.litemunafg     = menu_brd          : REMark Loose item unavailable foreground
8280   sp.infwinbd       = menu_brd          : REMark Information window border
8290   sp.infwinbg       = menu_pap          : REMark Information window background
8300   sp.infwinfg       = menu_ink          : REMark Information window foreground
8310   sp.appbd          = menu_brd          : REMark Application window border
8320   sp.appbg          = menu_pap          : REMark Application window background
8330   sp.appfg          = menu_ink          : REMark Application window foreground
8340   sp.appmg          = menu_pap          : REMark Application window middleground
8350   sp.appihigh       = menu_ink          : REMark Application window item highlight
8360   sp.appiavabg      = menu_pap          : REMark Application window item available background
8370   sp.appiavafg      = menu_ink          : REMark Application window item available foreground
8380   sp.appiselbg      = menu_brd          : REMark Application window item selected background
8390   sp.appiselfg      = menu_ink          : REMark Application window item selected foreground
8400   sp.appiunabg      = menu_pap          : REMark Application window item unavailable background
8410   sp.appiunafg      = menu_brd          : REMark Application window item unavailable foreground
8420   sp.scrbar         = menu_ink          : REMark Pan/scroll bar
8430   sp.scrbararr      = menu_brd          : REMark Pan/scroll bar arrow
8440   sp.buthigh        = menu_ink          : REMark Button highlight
8450   sp.butbd          = menu_brd          : REMark Button border
8460   sp.butbg          = menu_pap          : REMark Button background
8470   sp.butfg          = menu_ink          : REMark Button foreground
8480 END DEFine mk_my_old_palette
8490 :
8500 DEFine FuNction getstr$(poste%,drap%,defn,a$)
8510 REMark ouvre une fen‘tre sur un poste de menu dƒliƒ et propose
8520 REMark une cha•ne  ƒditer
8530 REMark les paramtres sont
8540 REMark - le numƒro du poste
8550 REMark - un drapeau si le rƒsultat doit ‘tre un chiffre (si drap%<>0)
8560 REMark - la dƒfinition de travail dans laquelle se trouve le poste
8570 REMark - la cha•ne qu'on peut ƒditer
8580 REMark returns the string or ""
8590 REMark uses the global (!) vars menu_pap% & menu_brd%
8600 REMark
8610   LOCal chan%,b$,res
8620   b$=" ":b$=a$   : REMark c'est curieusement nƒcessaire
8630   chan%=FOPEN("con")
8640   PAPER#chan%,menu_brd
8650   INK#chan%,menu_ink
8660   DR_LWDF#chan%,defn,poste%             : REMark open channel over item
8670   CLS#chan%
8680   res=GET_STR(#chan%,drap%,b$)          : REMark get string
8690   CLOSE#chan%
8700   IF res<0:b$=""                        : REMark ooops
8710   CH_ITEM defn,-1,poste%,-1,"",b$
8720   RETurn b$
8730 END DEFine getstr$
8740 :
8750 DEFine FuNction showcont$(string$,length%)
8760 REMark this adds '...' at the end of a string, to show that it continues
8770 LOCal l%,lengt%
8780   l%=LEN(string$)
8790   IF NOT l%:RETurn ""                   : REMark empty string
8800   lengt%=length% DIV 6
8810   IF l%<=lengt%:RETurn string$          : REMark entire string fits wdw
8820   RETurn string$(1 TO lengt%-3)&"..."
8830 END DEFine showcont$
8840 :
8850 DEFine FuNction get_free_text$(defn,item%,prompt$,length%)
8860 REMark this opens wdw over loose-item item% of the working definition defn
8870 REMark allows free text to be entered
8880 REMark params: working defintion,item over which wdw is to be opened
8890 REMark         first value of string, length of max string in wdw, if this is...
8900 REMark            -1, the length will be determined here
8910 LOCal chan%,string$,tempsizes%(3)
8920   chan%=FOPEN('con')
8930   IF chan%<0:RETurn prompt$
8940   DR_LWDF#chan%,defn,item%              : REMark open wdw over item
8950   PAPER#chan%,menu_brd
8960   INK#chan%,menu_ink
8970   CLS#chan%
8980   string$=WEDIT$(#chan%,prompt$)
8990   IF length%=-1
9000      WSIZE#chan%,tempsizes%
9010      length%=tempsizes%(0)
9020   END IF
9030   CLOSE#chan%
9040   IF length%
9050     CH_ITEM defn,-1,item%,-1,"",showcont$(string$,length%)
9060   ELSE
9070     CH_ITEM defn,-1,item%,-1,"",string$
9080   END IF
9090   RETurn string$
9100 END DEFine get_free_text$
9110 :
9120 DEFine FuNction do_caps(a$,caps%)
9130 REMark set string to capitales etc,caps% determines whether the string....
9140 REMark ...must be lower case (=0), upper case(=1), all words capitalized (=2),
9150 REMark ... first word only capitalized (=3), or nothing(=-1)
9160   SELect ON caps%
9170    =0: MIN a$
9180    =1: MAJ a$
9190    =2: CAP2 a$
9200    =3: CAP a$
9210   END SELect
9220   RETurn a$
9230 END DEFine do_caps
9240 :
9250 DEFine FuNction centr$(ctr$,leng)
9260 REMark this "centres" the string ctr$ (i.e. left padds it with spaces) so that
9270 REMark it is centred in a window of leng width (leng is length in chars)
9280   LOCal l%,a$
9290   a$=ctr$
9300   l%=INT(leng-LEN(a$))/2
9310   IF l%<0:RETurn a$
9320   a$=FILL$(' ',l%)&a$&FILL$(' ',l%)
9330   RETurn a$
9340 END DEFine centr$
9350 :
9360 DEFine FuNction set_colours% (force_mode%)
9370 REMark this sets colour variables according to the colour mode wished, if possible.
9380 REMark force_mode%=
9390 REMark     0 if ql colours wished (mode 4 only)
9400 REMark     1 if pal colours wished
9410 REMark     2 if 24 bit colours wished
9420 REMark This function returns the true QL mode set (4,8,32,33 etc.).
9430 REMark This function also sets the GLOBAL variable mycolour_mode% to the
9440 REMark mode 0,1,2 as above - whatever is possible on the machine, i.e.
9450 REMark if colour_24 is wished on a 4 colour machine (which won't succeed)
9460 REMark this function will set set that variable to 0, i.e. QL mode.
9470 REMark
9480 REMark This function needs the outptr toolkit
9490 LOCal mode%
9500   mode%=RMODE                           : REMark get current mode now
9510   SELect ON force_mode%
9520       = 0                               : REMark mode 4 wished
9530        SELect ON mode%
9540           = 4                           : REMark already on, so do nothing
9550           = 0,1,8 : MODE 4              : REMark old modes, set to 4 colours
9560           = REMAINDER
9570             COLOUR_QL                   : REMark if higher colour mode, set to "mode 4"
9580             MODE 4
9590        END SELect
9600        mode%=0                          : REMark new mode we're in  - 4 colour mode
9610       = 1                               : REMark PALette wished
9620        SELect ON mode%
9630           = 16,32,33 : COLOUR_PAL:mode%=1: REMark we're in high colour mode, but PAL colours wished
9640           = REMAINDER
9650             IF mode%<>4:MODE 4
9660             mode%=0
9670        END SELect
9680       = 2
9690        SELect ON mode%
9700           = 32,33 : COLOUR_24 :mode%=2  : REMark we're in high col mode 24 bit colours
9710           = 16 : COLOUR_PAL : mode%=1
9720           = REMAINDER
9730             IF mode%<>4:MODE 4
9740             mode%=0
9750        END SELect
9760   END SELect
9770   mycolour_mode%=mode%                  : REMark set this global variable now:
9780   REMark now set some colour variables
9790   SELect ON mode%
9800        = 0                              : REMark Ql 4 mode colours
9810         black          = 0
9820         white          = 7
9830         red            = 2
9840         green          = 4
9850         grey           = 255
9860         dull_pink      = 19
9870         Blue           = 0
9880         Magenta        = 2
9890         Yellow         = 7
9900         Cyan           = 0
9910         Dark_slate     = 0
9920         Slate_grey     = 255
9930         Dark_grey      = 255
9940         Light_grey     = 255
9950         Ash_grey       = 255
9960         Dark_red       = 210
9970         light_green    = 220
9980         Mustard        = 242
9990         Dark_green     = 228
10000         Sea_blue       = 0
10010         Dark_blue      = 0
10020         Purple         = 0
10030         Shocking_pink  = 234
10040         Orange         = 234
10050         Lime_green     = 4
10060         Apple_green    = 4
10070         Bright_blue    = 0
10080         Mauve          = 0
10090         Peach          = 7
10100         Light_yellow   = 7
10110         Light_blue     = 0
10120         Sky_blue       = 0
10130         Rose_pink      = 234
10140         pink           = 234
10150         Beige          = 7
10160         Pastel_pink    = 234
10170         Pastel_yellow  = 7
10180         Pastel_green   = 4
10190         Pastel_cyan    = 0
10200         Pastel_blue    = 0
10210         Pastel_rose    = 234
10220         Brick          = 210
10230         Light_khaki    = 7
10240         Dull_green     = 228
10250         Dull_cyan      = 0
10260         Steel_blue     = 0
10270         dull_pink      = 234
10280         Brown          = 0
10290         Khaki          = 7
10300         Dusky_green    = 4
10310         Dusky_blue     = 0
10320         Midnight_blue  = 0
10330         Plum           = 0
10340         Dusky_pink     = 234
10350         Buff           = 0
10360         Avocado        = 4
10370         Dull_turquoise = 4
10380         Dull_blue      = 0
10390         Faded_purple   = 234
10400         Cerise         = 2
10410         Tan_normal     = 0
10420         Grass_green    = 4
10430         Sea_green      = 4
10440         Ultramarine    = 0
10450         Deep_purple    = 234
10460         Light_orange   = 7
10470         light_red      = 2
10480         wg_striped     = 92
10490         br_striped     = 82
10500         wr_striped     = 107
10510         bg_striped     = 100
10520        = 1                      : REMark Colour_pal
10530         black          = 0
10540         white          = 1
10550         red            = 2
10560         green          = 3
10570         Blue           = 4
10580         Magenta        = 5
10590         Yellow         = 6
10600         Cyan           = 7
10610         Dark_slate     = 8
10620         Slate_grey     = 9
10630         Dark_grey      = 10
10640         grey           = 11
10650         Light_grey     = 12
10660         Ash_grey       = 13
10670         Dark_red       = 14
10680         light_green    = 15
10690         Mustard        = 16
10700         Dark_green     = 17
10710         Sea_blue       = 18
10720         Dark_blue      = 19
10730         Purple         = 20
10740         Shocking_pink  = 21
10750         Orange         = 22
10760         Lime_green     = 23
10770         Apple_green    = 24
10780         Bright_blue    = 25
10790         Mauve          = 26
10800         Peach          = 27
10810         Light_yellow   = 28
10820         Light_blue     = 29
10830         Sky_blue       = 30
10840         Rose_pink      = 31
10850         pink           = 32
10860         Beige          = 33
10870         Pastel_pink    = 34
10880         Pastel_yellow  = 35
10890         Pastel_green   = 36
10900         Pastel_cyan    = 37
10910         Pastel_blue    = 38
10920         Pastel_rose    = 39
10930         Brick          = 40
10940         Light_khaki    = 41
10950         Dull_green     = 42
10960         Dull_cyan      = 43
10970         Steel_blue     = 44
10980         dull_pink      = 45
10990         Brown          = 46
11000         Khaki          = 47
11010         Dusky_green    = 48
11020         Dusky_blue     = 49
11030         Midnight_blue  = 50
11040         Plum           = 51
11050         Dusky_pink     = 52
11060         Buff           = 53
11070         Avocado        = 54
11080         Dull_turquoise = 55
11090         Dull_blue      = 56
11100         Faded_purple   = 57
11110         Cerise         = 58
11120         Tan_normal     = 59
11130         Grass_green    = 60
11140         Sea_green      = 61
11150         Ultramarine    = 62
11160         Deep_purple    = 63
11170         pinkish        = 77
11180         light_red      = 109
11190         Light_orange   = 208
11200         wg_striped     = light_green
11210         br_striped     = Dark_red
11220         wr_striped     = light_red
11230         bg_striped     = Dark_green
11240        = 2                      : REMark colour_24
11250         black          = HEX('000000')
11260         white          = HEX('FFFFFF')
11270         red            = HEX('FF0000')
11280         green          = HEX('00FF00')
11290         Blue           = HEX('0000FF')
11300         Magenta        = HEX('FF00FF')
11310         Yellow         = HEX('FFFF00')
11320         Cyan           = HEX('00FFFF')
11330         Dark_slate     = HEX('242424')
11340         Slate_grey     = HEX('494949')
11350         Dark_grey      = HEX('6D6D6D')
11360         grey           = HEX('929292')
11370         Light_grey     = HEX('B6B6B6')
11380         Ash_grey       = HEX('DBDBDB')
11390         Dark_red       = HEX('920000')
11400         light_green    = HEX('B6FFB6')
11410         Mustard        = HEX('929200')
11420         Dark_green     = HEX('009200')
11430         Sea_blue       = HEX('009292')
11440         Dark_blue      = HEX('000092')
11450         Purple         = HEX('920092')
11460         Shocking_pink  = HEX('FF0092')
11470         Orange         = HEX('FF9200')
11480         Lime_green     = HEX('92FF00')
11490         Apple_green    = HEX('00FF92')
11500         Bright_blue    = HEX('0092FF')
11510         Mauve          = HEX('9200FF')
11520         Peach          = HEX('FFB6B6')
11530         Light_yellow   = HEX('FFFFB6')
11540         Light_blue     = HEX('B6FFFF')
11550         Sky_blue       = HEX('B6B6FF')
11560         Rose_pink      = HEX('FFB6FF')
11570         pink           = HEX('FFB6DB')
11580         Beige          = HEX('FFDBB6')
11590         Pastel_pink    = HEX('FFDBDB')
11600         Pastel_yellow  = HEX('FFFFDB')
11610         Pastel_green   = HEX('DBFFDB')
11620         Pastel_cyan    = HEX('DBFFFF')
11630         Pastel_blue    = HEX('DBDBFF')
11640         Pastel_rose    = HEX('FFDBFF')
11650         Brick          = HEX('B66D6D')
11660         Light_khaki    = HEX('B6B66D')
11670         Dull_green     = HEX('6DB66D')
11680         Dull_cyan      = HEX('6DB6B6')
11690         Steel_blue     = HEX('6D6DB6')
11700         dull_pink      = HEX('B66DB6')
11710         Brown          = HEX('6D2424')
11720         Khaki          = HEX('6D6D24')
11730         Dusky_green    = HEX('246D24')
11740         Dusky_blue     = HEX('246D6D')
11750         Midnight_blue  = HEX('24246D')
11760         Plum           = HEX('6D246D')
11770         Dusky_pink     = HEX('B64992')
11780         Buff           = HEX('B69249')
11790         Avocado        = HEX('92B649')
11800         Dull_turquoise = HEX('49B692')
11810         Dull_blue      = HEX('4992B6')
11820         Faded_purple   = HEX('9249B6')
11830         Cerise         = HEX('920049')
11840         Tan_normal     = HEX('924900')
11850         Grass_green    = HEX('499200')
11860         Sea_green      = HEX('009249')
11870         Ultramarine    = HEX('004992')
11880         Deep_purple    = HEX('490092')
11890         Light_orange   = HEX('ffb624')
11900         pinkish        = HEX('C0037B')
11910         light_red      = HEX('FC1729')
11920         wg_striped     = light_green
11930         br_striped     = Dark_red
11940         wr_striped     = light_red
11950         bg_striped     = Dark_green
11960   END SELect
11970   mode%=RMODE
11980   RETurn mode%                     : REMark return current screen mode now
11990 END DEFine set_colours%
12000 :
12010 DEFine FuNction fen_app2 (adresse,tableau$,nr_sects%,selk%,phys%)
12020 REMark les paramtres sont: l'adresse (0 la premire fois)
12030 REMark le tableau de cha•ne, le nombre de sections,
12040 REMark le drapeau si on veut des touches de sƒlection et si sƒlection=action,
12050 REMark un tableau d'entiers contenant les tailles x,y et les origines
12060 REMark et les touches  exclure des touches de sƒlection
12070 LOCal app_attributs%(3,1),temp%,bord%,pap%,ori_x%,ori_y%
12080 LOCal tail_x%,tail_y%,fleches%,barrre%
12090 :
12100   IF adresse:RECHP adresse              : REMark si c'est un 2nd appel, on enlve l'ancienne
12110   tail_x%=phys%(0)                      : REMark (largeur)    Ceci est pour la clartƒ
12120   tail_y%=phys%(1)                      : REMark (hauteur)    seulement, on pourrait
12130   ori_x%=phys%(2)                       : REMark            utiliser directement le
12140   ori_y%=phys%(3)                       : REMark origs x,y    tableau phys% dans AP_MAKE
12150 :
12160   adresse=AP_MAKE2(selk%,tail_x%,tail_y%,ori_x%,ori_y%,nr_sects%,tableau$)
12170   IF adresse<0: RETurn 0                : REMark si erreur, on retourne 0
12180                                         : REMark Maintenant on dƒtermine les attributs
12190   bord%=sp.appbd                        : REMark couleur bordure
12200   pap%=sp.appbg                         : REMark couleur "papier"
12210   touche_sel%=9                         : REMark touche de sƒlection - TAB
12220   fleches%=sp.scrbararr                 : REMark couleur des flches
12230   barre%=sp.scrbar                      : REMark couleur de la barre d'ascenseur
12240 :
12250   app_attributs%(0,0)=1
12260   app_attributs%(0,1)=sp.appihigh       : REMark larg. & coul. trait poste courant
12270 :
12280   app_attributs%(1,0)=sp.appiunabg
12290   app_attributs%(1,1)=sp.appiunafg      : REMark unavailable
12300 :
12310   app_attributs%(2,0)=sp.appiavabg
12320   app_attributs%(2,1)=sp.appiavafg      : REMark disponible
12330 :
12340   app_attributs%(3,0)=sp.appiselbg
12350   app_attributs%(3,1)=sp.appiselfg      : REMark sƒlectionnƒ
12360 :
12370   er=AP_PARS(adresse,0,bord%,pap%,touche_sel%,fleches%,barre%,app_attributs%)
12380 :
12390   IF er:RETurn 0
12400 :
12410   RETurn adresse  : REMark on retourne l'adresse
12420 END DEFine fen_app
12430 :
12440 DEFine PROCedure scroll_arrows(rangees%,nbr_objets%,appfen,cty%)
12450 REMark Cette procƒdure dƒcide s'il faut laisser les flches de dƒfilement
12460 REMark params: le nombre de rangƒes, le nombre total d'objets
12470 REMark le pointer vers la sous-fen d'app et la dƒfinition de controle
12480   IF nbr_objets%<=rangees% +1
12490     POKE_W appfen+150,0
12500     cty%(1,2)=nbr_objets%
12510   ELSE
12520     POKE_W appfen+150,6
12530 REMark    cty%(1,2)=rangees%-1             : rem if scroll arrows are there, there is one line less in the window
12540     cty%(1,2)=rangees%
12550  END IF
12560  IF DIMN(cty%,1)=2:cty%(2,2)=cty%(1,2)
12570  cty%(0,1)=1                            : REMark show that definitionhas changed
12580 END DEFine scroll_arrows
12590 :
12600 DEFine PROCedure init
12610 LOCal temp%
12620   text=0:sprite=2:blob=4:pattern=6
12630   retr=256:retn=-256:sous1=254:sous2=252
12640   REMark normal: hit = selects/deselects, do = returns & leaves selected
12650   REMark retr = return even on hit, reset item to available after,
12660   REMark retn = return even on hit, hit selects/deselects, do selects
12670 :
12680   hit$=CHR$(1):do$=CHR$(2):cancel$=CHR$(3):sleep$=CHR$(7):wake$=CHR$(8)
12690   help$=CHR$(4):move$=CHR$(5):size$=CHR$(6):cr$=CHR$(10)
12700 :
12710   appwinoffset%=16                      : REMark appsub win additional y offset
12720   xsize%=400                            : REMark x size divisible by 4!
12730   ysize%=200                            : REMark provisional y size%
12740   lxs=24:lys=12                         : REMark standard item x,y, sizes
12750   inf1y=lys+4                           : REMark y orig of inner border
12760   xesp=lxs+2                            : REMark loose items x spacing
12770   nbr_post%=3                           : REMark nbr of item right & left in title bar
12780   main_coul%=SMSCFG(2)                  : REMark colourways
12790   app_coul%=main_coul%                  : REMark preset colourways
12800   temp%=set_colours% (2)                : REMark always try to set highest colours
12810   IF temp%<2 AND main_coul%>3:main_coul%=0 : REMark old wman
12820   make_colours main_coul%               : REMark set all colours now
12830 :
12840   rep$=SMSCFG(1)                        : REMark dir with files
12850   titre$="Dir: "&rep$&" "
12860 :
12870 RESTORE 12860
12880 :
12890 : REMark make loose item attributes
12900 :
12910   DIM std_iattr(3,3)
12920   RD_IATT std_iattr
12930     DATA 1,sp.litemhigh : REMark current item border size & colour
12940     DATA sp.litemunabg,sp.litemunafg,0,0 : REMark unavailable item paper & ink
12950     DATA sp.litemavabg,sp.litemavafg,0,0 : REMark   available         "    "       "
12960     DATA sp.litemselbg,sp.litemselfg,0,0 : REMark   selected
12970 :
12980 : REMark make data for sprites - uses the modified RD_SPRT function
12990 : REMark
13000 : REMark
13010 :
13020   espace_vide=ALCHP(100)                : REMark GLOBAL var for 0 mask sprites!
13030 :
13040   main_sprite=RD_SPRT(0)                : REMark 0 = normal mask
13050     DATA 11,13,6,5,4
13060     DATA '     www     '
13070     DATA '     waw     '
13080     DATA '     waw     '
13090     DATA '     waw     '
13100     DATA 'wwwwwa awwwww'
13110     DATA 'waaaa   aaaaw'
13120     DATA 'wwwwwa awwwww'
13130     DATA '     waw     '
13140     DATA '     waw     '
13150     DATA '     waw     '
13160     DATA '     www     '
13170 :
13180   move_sprite=RD_SPRT(1)                : REMark 1 = 0 mask
13190   IF temp%>1:move_sprite=6              : REMark or system sprite!
13200     DATA 10,14,5,4,4
13210     DATA 'aaaaaaaaaa    '
13220     DATA 'awwwwwwwwa    '
13230     DATA 'awwrrrrwwa    '
13240     DATA 'awwrrrrwwaaaaa'
13250     DATA 'awwrrwwwwwwwwa'
13260     DATA 'awwwwwwwwrrwwa'
13270     DATA 'aaaaawwrrrrwwa'
13280     DATA '    awwrrrrwwa'
13290     DATA '    awwwwwwwwa'
13300     DATA '    aaaaaaaaaa'
13310 :
13320   zzz_sprite= RD_SPRT(1)
13330   IF temp%>1:zzz_sprite=10
13340     DATA 7,15,0,0,4
13350     DATA 'wwwww          '
13360     DATA '   w           '
13370     DATA '  w  wwww      '
13380     DATA ' w     w       '
13390     DATA 'wwwww w  wwww  '
13400     DATA '     wwww  w   '
13410     DATA '          wwww '
13420 :
13430  size_sprite=RD_SPRT(1)
13440  IF temp%>1:size_sprite=7
13450     DATA 9,14,5,4,4
13460     DATA 'aaaaaaaaaaaaaa'
13470     DATA 'awwwwwwwwwwwwa'
13480     DATA 'awwrrrrrrrrwwa'
13490     DATA 'awwrrrrrrrrwwa'
13500     DATA 'awwrrwwwwwwwwa'
13510     DATA 'awwrrwwrrrrwwa'
13520     DATA 'awwrrwwrrrrwwa'
13530     DATA 'awwwwwwwwwwwwa'
13540     DATA 'aaaaaaaaaaaaaa'
13550 :
13560   make_my_data 1
13570   make_main_wdw
13580   main_lfl%(target_its%)=128
13590 :
13600 END DEFine init
13610 :
13620 DEFine PROCedure make_main_wdw
13630 REMark make the main window
13640 LOCal main_lot,main_iwt,main_iot1
13650 :
13660   REMark first make sure of the wdw sizes (we also come here from change_size!)
13670   xsize%=xsize%-(xsize% MOD 4)                    : REMark make into nbr divisible by 4
13680   IF xsize%<360:xsize%=360
13690 :
13700   REMark calculate the y size of the wdw so that it always looks nice, in that
13710   REMark the rows in the appsub always fill the appsub entirely
13720   ysize%=ysize%+(ysize%&&1)             : REMark make into even number
13730   IF ysize%<100:ysize%=100              : REMark predetermioned min ysize% before calculations
13740   ysize%=ysize%-inf1y-14-appwinoffset%
13750   rangees=ysize% DIV 12                 : REMark GLOBAL var: nbr of rows in appsub
13760   IF rangees>data_count%-2:rangees=data_count%-2
13770   IF rangees<3:rangees=3
13780   ysize%=rangees*12+inf1y+14 +appwinoffset%
13790 :
13800   REMark first calc info obj size - this also influences one loose item, used
13810   REMark in the title bar
13820   longueur_titre=LEN(titre$)*6          : REMark length in pixels
13830   titre_x=INT((xsize%-longueur_titre)/2): REMark x orig
13840 :
13850   REMark first the loose items
13860 :
13870   yinf1y=inf1y+4:
13880   esc%=0:move_it%=esc%+1:size_it%=move_it%+1
13890   sleep_it%=size_it%+1:all_it%=sleep_it%+1:go_it%=all_it%+1
13900   dir_it%=go_it%+1:target_its%=dir_it%+1:make_it%=target_its%+6
13910 :
13920 RESTORE 13910
13930   REMark reset data pointer to here (The Basic Linker does that automatically)
13940 :
13950   temp%=xsize% DIV 7
13960   main_lit%=13:DIM main_lfl%(main_lit%)   : REMark there are 14 items!
13970   main_lot=RD_LOT(std_iattr,main_lit%)
13980     DATA lxs,lys,4+xesp*0,2,0,0,cancel$,text+retr,'ESC'
13990     DATA lxs,lys,4+xesp*1,2,0,0,move$,sprite+retr,move_sprite
14000     DATA lxs,lys,4+xesp*2,2,0,0,size$,sprite+retn,size_sprite
14010     DATA lxs,lys,xsize%-2-xesp*1,2,0,0,sleep$,sprite+retr,zzz_sprite
14020     DATA lxs,lys,xsize%-2-xesp*2,2,0,0,'A',sous1+retn,"All"
14030     DATA lxs,lys,xsize%-2-xesp*3,2,0,0,'O',sous1+retn,"OK"
14040     DATA 6*6,lys-2,titre_x+30,3,0,0   ,'D',text+retn,rep$
14050     DATA 8*6,lys,8,inf1y+2,0,0,        'G',sous1+retn,"Generic"
14060     DATA 8*6,lys,8+temp%,inf1y+2,0,0,  'T',sous2+retn,"Atari"
14070     DATA 8*6,lys,8+temp%*2,inf1y+2,0,0,'L',sous2-2+retn,"GoldCard"
14080     DATA 8*6,lys,8+temp%*3,inf1y+2,0,0,'Q',sous1+retn,"Q40/Q60"
14090     DATA 8*6,lys,8+temp%*4,inf1y+2,0,0,'X',sous2+retn,"QXL"
14100     DATA 8*6,lys,8+temp%*5,inf1y+2,0,0,'R',sous2+retn-2,"Aurora"
14110     DATA 8*6,lys,8+temp%*6,inf1y+2,0,0,'M',sous1+retn,"Make"
14120 :
14130   REMark now info wdw objects - here only one, the title
14140   REMark make the title data: calculate size and x position of this object
14150 :
14160   main_iot1=RD_IOT(0)
14170     DATA longueur_titre,10,4,INT((lys-10)/2),sous1,sp.titlefg,0,0,titre$
14180 :
14190   REMark now the info wdws
14200 :
14210   main_iwt=RD_IWT(2)                              : REMark there are 3
14220 :
14230 REMark first the title bar
14240 :
14250     DATA xsize%-8-(2*nbr_post%*xesp),lys+2,xesp*nbr_post%+4,0         : REMark x,y sizes & origs
14260     DATA 0,0,sp.infwinbd,sp.titlebg     : REMark shadow, border size & col,paper col
14270     DATA 0                              : REMark object pointer (none)
14280 :
14290 REMark now wdw containing title itself
14300 :
14310     DATA longueur_titre+8,lys,titre_x-4,2
14320     DATA 0,0,sp.infwindbd,sp.infwinbg
14330     DATA main_iot1                      : REMark one object
14340 :
14350 REMark inner border
14360 :
14370     DATA xsize%-8,ysize%-inf1y-2,4,inf1y
14380     DATA 0,1,sp.infwinbd,sp.infwinbg
14390     DATA 0
14400 :
14410 REMark now the appsub wdw - use the OUTPTR functions for this
14420 :
14430   app_addr=0                            : REMark there is none yet
14440   mk_main_app 1                         : REMark (this destroys my DATA pointer)
14450   main_app_list = app_addr+180          : REMark list built by OUTPTR
14460 :
14470 : REMark  working defn
14480 :
14490 RESTORE 14480
14500                                         : REMark reset DATA pointer (due to Basic Linker, restore must be ALONE on the line)
14510   main_defn=RD_WDEF
14520     DATA 2,1,sp.winbd,sp.winbg          : REMark shad,border size & col,pap col
14530     DATA xsize%,ysize%,0,0    : REMark x,y size & initial ptr pos
14540     DATA main_sprite,main_lot,main_iwt,main_app_list : REMark sprite,loose items, info wins, appsubs
14550 END DEFine make_main_wdw
14560 :
14570 DEFine PROCedure warn_make
14580 REMark makes warning & info window
14590 REMark uses global vars green etc...
14600 REMark SETS the global var warning_width% (nbr of chars in one line in wdw)
14610 LOCal lys%,warn_lot,warn_iot1,warn_iwt
14620 LOCal x%,y%,warning_paper,warning_brd
14630   RESTORE 14620
14640   x%=188:y%=82
14650   warning_paper=menu_pap
14660   warning_brd=pink                      : REMark pink
14670 REMark  if menu_brd=4:warning_brd=light_green      : rem light green
14680   lys%=10
14690   DIM warn_lfl%(1)
14700   warn_lot=RD_LOT(std_iattr,1)
14710     DATA 30,lys%,4,4,0,0,cancel$,text+retr,'ESC'
14720     DATA 30,lys%,x%-34,4,0,0,'O',text+retr,"OK"
14730 :
14740   warn_iot1=RD_IOT (0)
14750     DATA 12*9,10,0,1,text,red,2,0,'Attention'
14760 :
14770   warn_iwt=RD_IWT(1)
14780     DATA x%-16,y%-32,8,24
14790     DATA 0,1,red,warning_paper
14800     DATA 0
14810 :
14820     DATA 120,12,(x%-112) DIV 2,4
14830     DATA 0,1,menu_brd,warning_paper
14840     DATA warn_iot1
14850 :
14860   warn_defn=RD_WDEF
14870     DATA 2,1,menu_brd,warning_brd
14880     DATA x%,y%,0,0
14890     DATA main_sprite,warn_lot,warn_iwt,0
14900 :
14910   warning_width%=(x%-16) DIV 6                    : REMark width of warning winfow in chars
14920 :
14930 END DEFine warn_make
14940 :
14950 DEFine FuNction RD_SPRT(drap%)
14960 REMark drap% determine si masque 0
14970   LOCal tmp_patt$(32,32)
14980   LOCal xs,ys,xo,yo, md, l
14990   READ ys,xs,xo,yo,md
15000   DIM tmp_patt$(ys,xs)
15010   FOR l=0 TO ys-1:READ tmp_patt$(l)
15020   l=ALCHP(SPRSP(xs,ys))
15030   SPSET l,xo,yo,md,tmp_patt$(0 TO ys-1,1 TO xs)
15040   IF drap%
15050     POKE_L l+16,espace_vide-(l+16)
15060   END IF
15070   RETurn l
15080 END DEFine RD_SPRT
15090 :
15100 DEFine FuNction RD_WDEF
15110 REMark read window defn
15120   LOCal ldef%(3), lspr, lloose, linfo, lappl, lattr%(3)
15130   RD_WATT lattr%
15140   READ ldef%(0), ldef%(1), ldef%(2), ldef%(3)
15150   READ lspr, lloose, linf, lappl
15160   RETurn MK_WDEF (ldef%, lattr%, lspr, lloose, linf, lappl)
15170 END DEFine RD_WDEF
15180 :
15190 DEFine FuNction RD_LOT (lattr,nitem)
15200   LOCal count(3)
15210   LOCal item, ltyp, a$, lsk$
15220   LOCal ldef%(nitem,6), lptr(3,nitem), lstr$(nitem,85)
15230   lsk$=''
15240   FOR item = 0 TO nitem
15250     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
15260     READ ldef%(item,4), ldef%(item,5)
15270     READ a$: lsk$=lsk$ & a$
15280     READ ltyp
15290     ldef%(item,6)=ltyp: ltyp=(ltyp MOD 256)/2
15300     IF ltyp>10 OR ltyp <0::ltyp=0
15310     IF ltyp
15320       READ lptr(ltyp,count(ltyp))
15330     ELSE
15340       READ lstr$(count(0))
15350     END IF
15360     count(ltyp)=count(ltyp)+1
15370   END FOR item
15380   RETurn MK_LIL (lattr, ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), ldef%(TO, 4 TO 5), lsk$, ldef%(TO, 6), lstr$, lptr(1), lptr(2), lptr(3))
15390 END DEFine RD_LOT
15400 :
15410 DEFine FuNction RD_IWT(nitem)
15420   LOCal item,mc1
15430   LOCal ldef%(nitem,3), latt%(nitem,3), lptr(nitem)
15440   FOR item = 0 TO nitem
15450     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
15460     READ latt%(item,0), latt%(item,1), latt%(item,2), latt%(item,3)
15470     READ lptr(item)
15480   END FOR item
15490   RETurn MK_IWL (ldef%, latt%, lptr)
15500 END DEFine RD_IWT
15510 :
15520 DEFine FuNction RD_IOT(nitem)
15530   LOCal count(3)
15540   LOCal item, ltyp, work1, work2
15550   LOCal ldef%(nitem,4), lptr(3,nitem), lstr$(nitem,85)
15560   FOR item = 0 TO nitem
15570     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
15580     READ ltyp
15590     ldef%(item,4)=ltyp:  ltyp=(ltyp MOD 256)/2
15600     IF ltyp >10:ltyp=0
15610     IF ltyp
15620       READ lptr(0,item),lptr(ltyp,count(ltyp))
15630     ELSE
15640       IF mycolour_mode%=2
15650           READ work1,work2,work3
15660           lptr(0,item)=WL_4_IOL(work1,work2,work3)
15670       ELSE
15680           READ work1
15690           READ work2: work1=work1*256+work2
15700           READ work2: lptr(0,item)=work1*256+work2
15710       END IF
15720       READ lstr$(count(0))
15730     END IF
15740     count(ltyp) = count(ltyp) + 1
15750   END FOR item
15760   RETurn MK_IOL (ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), lptr(0), ldef%(TO, 4), lstr$, lptr(1), lptr(2), lptr(3))
15770 END DEFine RD_IOT
15780 :
15790 DEFine PROCedure RD_IATT (lattr)
15800   LOCal i
15810   READ lattr(0,0), lattr(0,1)
15820   FOR i=1 TO 3: READ lattr(i,0), lattr(i,1), lattr(i,2), lattr(i,3)
15830 END DEFine RD_IATT
15840 :
15850 DEFine PROCedure RD_WATT (lattr%)
15860   READ lattr%(0), lattr%(1),lattr%(2),lattr%(3)
15870 END DEFine RD_WATT
15880 :
15890 DEFine PROCedure sa
15900   SAVE_O dev8_extras_exe_source_SMSQEMake_bas
15910 END DEFine sa
