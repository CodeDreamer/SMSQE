100 REMark $$asmb=dev1_booty_outptr_bin,0,10
110 REMark $$asmb=dev1_booty_qptr_bin,50,172
120 REMark $$asmb=dev1_basic_make_bin,0,10
130 REMark $$stak=20000
140 compiled=NOT IS_OPEN(#0)
150 IF NOT compiled
160   SCR_SIZE#0, xsz%,ysz%
170   OUTLN#0,xsz%,ysz%-28,0,28
180   WSET -1
190 END IF
200 init
210 main
220 STOP
230 DEFine PROCedure make_new_dir (poste%)
240 LOCal quel_rep$
250   IF MENU_OK
260     quel_rep$= DIR_SELECT$("The Files are where?",,10,10,main_coul%,app_coul%)
270   ELSE
280     quel_rep$=getstr$(poste%,0,main_defn,a_lire$)
290   END IF
300   IF LEN (quel_rep$) >= 5
310      rep$=quel_rep$(1 TO 5)
320   END IF
330   CH_ITEM main_defn,-1,poste%,-1,"",rep$
340   main_lfl%(poste%)=main_lfl%(poste%)+1
350 END DEFine make_new_dir
360 :
370 :
380 DEFine PROCedure main
390 LOCal lp%,item%,event%,swnum%,xrel%,yrel%,item%,lp2%,tot%
400   IF compiled
410      OPEN#1,'con'
420      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
430      SCR_SIZE#1,xsz%,ysz%
440   ELSE
450      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
460      SCR_SIZE#0,xsz%,ysz%
470   END IF
480   POS_PTR main_defn,40*65536+20,1
490   REPeat lp%
500     RD_PTR main_defn,item%,swnum%,event%,xrel%,yrel%,main_lfl%,main_mfl%,main_cty%
510     IF swnum%= -1
520       SELect ON item%
530               =esc%     : EXIT lp%
540               =move_it% : CH_WIN main_defn
550               =size_it% : change_size xrel%,yrel%
560               =sleep_it%: button main_defn,main_lfl%,main_mfl%,main_cty%,"SMSQ/E Make"
570               =all_it%
580                  set_statusses
590                  main_cty%(0,1)=1
600               =go_it%   : do_the_work
610               =dir_it%: make_new_dir item%
620               =target_its% TO target_its%+4
630                           start%=1:machines%=0
640                           FOR lp2%=target_its% TO target_its%+4
650                               IF main_lfl%(lp2%):machines%=machines%+start%
660                               start%=start%*2
670                           END FOR lp2%
680                           make_my_data machines%
690                           mk_main_app
700                           AP_IN main_defn,main_app_list
710                           IF main_app_list:DR_ADRW main_defn,0,main_mfl%,main_cty%
720                           DR_WDRW main_defn
730       END SELect
740     END IF
750   END REPeat lp%
760   DR_UNST main_defn
770   IF compiled:CLOSE#1
780   RECHP espace_vide
790 END DEFine main
800 :
810 DEFine PROCedure button (def_trav,menu_drap%,app_drap%,control%,chaine$)
820   DR_UNST def_trav
830   IF compiled: CLOSE#1
840   BUTTON_WAIT chaine$
850   IF compiled
860     OPEN#1,"CON_"
870     DR_PPOS def_trav,-1,-1,menu_drap%,app_drap%,control%
880   ELSE
890     DR_PULD def_trav,-1,-1,menu_drap%,app_drap%,control%
900   END IF
910 END DEFine button
920 :
930 DEFine PROCedure mk_main_app
940 LOCal t2%,temp%,sections%,att_phys%(3),lp%
950   DIM main_cty%(0):DIM main_mfl%(0,0)
960   sections%=2
970   temp%=sections% :t2%=rangees
980   att_phys%(0)=xsize%-16      :REMark taille x se calcule sur la fen. prim.
990   att_phys%(1)=t2%            :REMark taille y = nombre de rangÉes
1000   att_phys%(2)=4              :REMark origine x
1010   att_phys%(3)=inf1y +appwinoffset%        :REMark origine y
1020   app_addr=fen_app2(app_addr,mdata$,temp%,1,att_phys%,"DO ")
1030   IF app_addr
1040       DIM main_cty%(sections%,2)           : REMark dÉfinition de controle
1050       main_cty%(0,0)=1                               : REMark on montre 1 section au dÉbut
1060       main_cty%(1,2)=t2%
1070       IF sections%=2:main_cty%(2,2)=t2%
1080       IF temp%
1090            DIM main_mfl%(temp%-1,0)       : REMark le tableau de drapeaux
1100            set_statusses
1110       ELSE
1120            POKE_W app_addr,PEEK_W(app_addr)+8
1130       END IF
1140       scroll_arrows t2%,temp%,app_addr,main_cty%
1150   END IF
1160 END DEFine mk_main_app
1170 :
1180 DEFine PROCedure set_statusses
1190 LOCal temp%,lp%,stat%
1200   temp%=DIMN(main_mfl%,1)
1210   stat%=main_lfl%(all_it%)
1220   FOR lp%=0 TO temp%
1230     IF mdata$(lp%+1,1)='*'
1240       main_mfl%(lp%,0)=16
1250     ELSE
1260       main_mfl%(lp%,0)=stat%
1270     END IF
1280   END FOR lp%
1290 END DEFine set_statusses
1300 :
1310 DEFine PROCedure make_my_data (what_machine%)
1320 REMark what_machine% = 1 -> generic
1330 REMark what_machine% = 2 -> atari
1340 REMark what_machine% = 4 -> Gold Card
1350 REMark what_machine% = 8 -> Qx0
1360 REMark what_machine% = 16 -> QXL
1370   DIM mdata$(100,50)
1380   REMark first make generic data
1390   data_count%=1                              : REMark general data_count%
1400   IF what_machine%&&1
1410     generic_data                        : REMark set up generic data
1420     read_my_datas                       : REMark and read it
1430   END IF
1440   IF what_machine%&&2
1450      atari_data                         : REMark set up machine specific data....
1460      read_my_datas                      : REMark and read it
1470    END IF
1480   IF what_machine%&&4
1490      gc_data                                      : REMark set up machine specific data....
1500      read_my_datas                      : REMark and read it
1510    END IF
1520   IF what_machine%&&8
1530      q40_data                                     : REMark set up machine specific data....
1540      read_my_datas                      : REMark and read it
1550    END IF
1560   IF what_machine%&&16
1570      qxl_data                                     : REMark set up machine specific data....
1580      read_my_datas                      : REMark and read it
1590    END IF
1600 END DEFine make_my_data
1610 :
1620 DEFine PROCedure read_my_datas
1630 REMark reads datas into the global var mdata$
1640 REMark uses data_count% from make_my_data
1650 LOCal lp%
1660   REPeat lp%
1670     READ mdata$(data_count%)
1680     IF mdata$(data_count%)="":EXIT lp%
1690     data_count%=data_count%+1
1700   END REPeat lp%
1710 END DEFine read_my_datas
1720 :
1730 DEFine PROCedure generic_data
1740 RESTORE 1740
1750   DATA '**** -- SMSQ Generic -- ****'
1760   DATA 'smsq_smsq_loader_link'
1770   DATA 'smsq_smsq_fh_link'
1780   DATA 'smsq_smsq_link'
1790   DATA 'smsq_smsq_cache_link'
1800   DATA 'smsq_sbas_link'
1810   DATA 'smsq_smsq_lang_link'
1820   DATA 'smsq_sbas_lang_link'
1830   DATA 'smsq_sbas_procs_link'
1840   DATA 'smsq_smsq_wman_link'
1850   DATA 'smsq_smsq_hotkey_link'
1860   DATA 'smsq_smsq_vers_link'
1870   DATA 'smsq_smsq_1mb_link',''
1880 END DEFine generic_data
1890 DEFine PROCedure q40_data
1900 RESTORE 1900
1910   DATA '**** -- Q40/Q60 -- ****'
1920   DATA 'smsq_q40_hwinit_link'
1930   DATA 'smsq_q40_nasty_link'
1940   DATA 'smsq_q40_cache_link'
1950   DATA 'smsq_smsq_cache40c_link'
1960   DATA 'smsq_q40_driver_ser_link'
1970   DATA 'smsq_q40_driver_dv3_link'
1980   DATA 'smsq_q40_driver_ql_link'
1990   DATA 'smsq_q40_driver_16_link'
2000   DATA 'smsq_q40_kbd_lang_link'
2010   DATA 'smsq_sbas_procs_q60_link'
2020   DATA 'smsq_q40_sysspr_link',''
2030 END DEFine q40_data
2040 :
2050 DEFine PROCedure atari_data
2060 RESTORE 2060
2070   DATA '**** -- Atari -- ****'
2080   DATA 'smsq_atari_hwinit_link'
2090   DATA 'smsq_atari_nasty_link'
2100   DATA 'smsq_atari_driver_ql_link'
2110   DATA 'smsq_atari_driver_mo_link'
2120   DATA 'smsq_atari_driver_ser_link'
2130   DATA 'smsq_atari_driver_dv3_link'
2140   DATA 'smsq_atari_kbd_lang_link'
2150   DATA 'smsq_atari_sysspr_link'
2160   DATA 'sys_boot_st_host_link',''
2170 END DEFine atari_data
2180 :
2190 DEFine PROCedure gc_data
2200 RESTORE 2200
2210   DATA '**** -- (Super)GoldCard -- ****'
2220   DATA 'smsq_gold_host_link'
2230   DATA 'smsq_gold_hwinit_link'
2240   DATA 'smsq_gold_nasty_link'
2250   DATA 'smsq_gold_nasty_s_link'
2260   DATA 'smsq_gold_driver_ql_link'
2270   DATA 'smsq_gold_driver_most_link'
2280   DATA 'smsq_gold_driver_nd_link'
2290   DATA 'smsq_gold_driver_nds_link'
2300   DATA 'smsq_gold_driver_dv3_link'
2310   DATA 'smsq_gold_kbd_lang_link'
2320   DATA 'smsq_gold_kbd_abc_lng_link'
2330   DATA 'smsq_gold_kbd_abc_link'
2340   DATA 'smsq_gold_roms_link'
2350   DATA 'smsq_gold_qimi_link'
2360   DATA 'smsq_gold_sysspr_link',''
2370 END DEFine gc_data
2380 :
2390 DEFine PROCedure qxl_data
2400 RESTORE 2400
2410   DATA '**** -- QXL -- ****'
2420   DATA 'smsq_qxl_host_link'
2430   DATA 'smsq_qxl_hwinit_link'
2440   DATA 'smsq_qxl_nasty_e_link'
2450   DATA 'smsq_qxl_driver_ql_link'
2460   DATA 'smsq_qxl_driver_16_link'
2470   DATA 'smsq_qxl_driver_most_link'
2480   DATA 'smsq_qxl_driver_nd_link'
2490   DATA 'smsq_qxl_procs_link'
2500   DATA 'smsq_qxl_driver_dv3e_link'
2510   DATA 'smsq_qxl_kbd_lang_link'
2520   DATA 'smsq_qxl_ecache_link'
2530   DATA 'smsq_qxl_sysspr_link',''
2540 END DEFine qxl_data
2550 :
2560 DEFine PROCedure do_the_work
2570 REMark this tries to compile all of the selected files
2580 REMark if the file was compiled OK, setus set to available
2590 REMark if the file was not compiled ok, status left selected
2600 LOCal lp%,bound%,a$,my_error%,tot_err%
2610   bound%=DIMN(main_mfl%,1)              : REMark number of items to check
2620   tot_err%=0
2630   FOR lp%=0 TO bound%
2640     a$=mdata$(lp%+1)                              : REMark potential file to compile
2650     IF a$(1)<>"*" AND main_mfl%(lp%,0)
2660        a$=rep$&a$                       : REMark add dir
2670        my_error%=compile_and_check% (a$): REMark try to compile
2680        IF NOT my_error%
2690            main_mfl%(lp%,0)=1
2700            DR_ADRW main_defn,0,main_mfl%,main_cty%
2710        END IF
2720        tot_err%=tot_err%+my_error%
2730     END IF
2740   END FOR lp%
2750   main_cty%(0,1)=1
2760   main_lfl%(go_it%)=1
2770   IF main_lfl%(make_it%)=128 AND NOT tot_err%:make_targets
2780 END DEFine do_the_work
2790 :
2800 DEFine FuNction compile_and_check% (a$)
2810 REMark this tries to compile a file, using the "make" prog
2820 REMark this returns 1 if errors, else 0
2830 LOCal chan%,is_ok$,ch_len
2840   EXEP_W "make";a$&" -w -l"             : REMark exec make & wait
2850   is_ok$=a$(1 TO LEN(a$)-4)             : REMark file name w/o extension
2860   chan%=FOP_IN(is_ok$&'log')            : REMark open log file
2870   IF chan%<0:RETurn 1                             : REMark can't even open it!
2880   ch_len=FLEN(#chan%)                             : REMark file length
2890   GET#chan%\ch_len-10                             : REMark this is where error report is
2900   INPUT#chan%,is_ok$                              : REMark get it
2910   CLOSE#chan%
2920   IF NOT is_ok$=="No errors"
2930     RETurn 1                                      : REMark anything other than "no errors" signals errors
2940   ELSE
2950     RETurn 0
2960   END IF
2970 END DEFine compile_and_check%
2980 :
2990 DEFine PROCedure make_targets
3000 REMark targets are only made if there was no error during compilation
3010 REMark to achieve this, a primitive approach is used:
3020 REMark a check is made whether an appsub menu item status is still selected
3030 REMark if it is, then the
3040   LOCal lp%,current_target%,tot_targets%
3050   tot_targets%=0                        : REMark no targets to make yet
3060   current_target%=0
3070   FOR lp%=0 TO data_count%-2
3080     IF main_mfl%(lp%,0)=16
3090       REMark we're covering a new target
3100       tot_targets%=tot_targets%+current_target% : REMark show old target
3110       current_target%=find_target%(lp%+1) : REMark get new target we're covering
3120     ELSE
3130       IF main_mfl%(lp%,0)<>0:current_target%=0
3140     END IF
3150   END FOR lp%
3160   tot_targets%=tot_targets%+current_target%
3170   REMark at the end, here, tot_targets holds a bitmap
3180   REMark of targets for which compilation was ok
3190   IF (machines%&&1) AND NOT (tot_targets% && 1):RETurn : REMark generic was asked for but not compiled correctly!
3200   IF tot_targets%&&2:atari_target
3210   IF tot_targets%&&4:gc_target
3220   IF tot_targets%&&8:qx0_target
3230   IF tot_targets%&&16:qxl_target
3240 END DEFine make_targets
3250 :
3260 DEFine PROCedure atari_target
3270   cmd$='m'
3280   p$=rep$&'sys_boot_st_flp'
3290   f$=rep$&'sys_boot_file'
3300   ah$=rep$&'sys_boot_st_host'
3310   ld$=rep$&'smsq_smsq_loader'
3320   hi$=rep$&'smsq_atari_hwinit'
3330   os1f$=rep$&'smsq_smsq_fh_os'
3340   os1$=rep$&'smsq_smsq_os'
3350   ca$=rep$&'smsq_smsq_cache'
3360   os2$=rep$&'smsq_sbas_control'
3370   ns1$=rep$&'smsq_atari_nasty'
3380   ln1$=rep$&'smsq_smsq_lang'
3390   ln2$=rep$&'smsq_atari_kbd_lang'
3400   ln3$=rep$&'smsq_sbas_lang'
3410   ex1$=rep$&'smsq_sbas_procs_x'
3420   ex2$=rep$&'smsq_atari_driver_dv3'
3430   ex3$=rep$&'smsq_atari_driver_ser'
3440   ex4$=rep$&'smsq_atari_driver_mono'
3450   ex5$=rep$&'smsq_atari_driver_ql'
3460   ex6$=rep$&'smsq_smsq_wman'
3470   ex7$=rep$&'smsq_smsq_hotkey'
3480   ex8$=rep$&'smsq_atari_sysspr'
3490   nl$=rep$&'sys_boot_null'
3500   IF 'm' INSTR cmd$ = 0: ex4$ = nl$
3510   EW f$, ah$,ld$,hi$,os1f$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4$,ex5$,ex6$,ex7$,ex8$, rep$&'smsq_atari_SMSQ.PRG'
3520 END DEFine atari_target
3530 :
3540 DEFine PROCedure gc_target
3550   f$=rep$&'sys_boot_file'
3560   v$=rep$&'smsq_smsq_vers'
3570   h$=rep$&'smsq_gold_host'
3580   ld$=rep$&'smsq_smsq_loader'
3590   hi$=rep$&'smsq_gold_hwinit'
3600   os1$=rep$&'smsq_smsq_os'
3610   ca$=rep$&'smsq_smsq_cache'
3620   os2$=rep$&'smsq_sbas_control'
3630   ns1$=rep$&'smsq_gold_nasty'
3640   ns2$=rep$&'smsq_gold_nasty_s'
3650   ln1$=rep$&'smsq_smsq_lang'
3660   ln2$=rep$&'smsq_gold_kbd_lang'
3670   ln2a$=rep$&'smsq_gold_kbd_abc_lang'
3680   ln3$=rep$&'smsq_sbas_lang'
3690   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
3700   ex2a$=rep$&'smsq_gold_kbd_abc'
3710   ex2$=rep$&'smsq_gold_driver_most'
3720   ex3a$=rep$&'smsq_gold_driver_ql'
3730   ex3q$=rep$&'smsq_gold_qimi'
3740   ex3w$=rep$&'smsq_smsq_wman'
3750   ex4$=rep$&'smsq_gold_driver_dv3'
3760   ex5$=rep$&'smsq_gold_driver_nd'
3770   ex6$=rep$&'smsq_gold_driver_nds'
3780   ex7$=rep$&'smsq_smsq_hotkey'
3790   ex8$=rep$&'smsq_gold_sysspr'
3800   nl$=rep$&'sys_boot_null'
3810   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex3a$,ex3q$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,ex1$, rep$&'SMSQ_GOLD_gold'
3820 END DEFine gc_target
3830 :
3840 DEFine PROCedure qx0_target
3850   nl$=rep$&'sys_boot_null'
3860   f$=rep$&'sys_boot_file'
3870   qr$=rep$&'sys_boot_q40_rom'
3880   ld$=rep$&'smsq_smsq_loader'
3890   hi$=rep$&'smsq_q40_hwinit'
3900   os1$=rep$&'smsq_smsq_1mb_os' : REMark change for fast memory
3910   ca$=rep$&'smsq_smsq_cache40c'
3920   caq$=rep$&'smsq_q40_cache'
3930   os2$=rep$&'smsq_sbas_control'
3940   ns2$=rep$&'smsq_q40_nasty'
3950   ln1$=rep$&'smsq_smsq_lang'
3960   ln2$=rep$&'smsq_q40_kbd_lang'
3970   ln3$=rep$&'smsq_sbas_lang'
3980   ex1$=rep$&'smsq_sbas_procs_x_q60'
3990   ex2$=rep$&'smsq_q40_driver_dv3'
4000   ex3$=rep$&'smsq_q40_driver_ser'
4010   ex4a$=rep$&'smsq_q40_driver_ql'
4020   ex4b$=rep$&'smsq_q40_driver_16'
4030   ex4w$=rep$&'smsq_smsq_wman'
4040   ex5$=rep$&'smsq_q40_sysspr'
4050   ex9$=rep$&'smsq_smsq_hotkey'
4060   EW f$, qr$,ld$,hi$,os1$,ca$,caq$,os2$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4a$,ex4b$,ex4w$,ex5$,ex9$,ns2$, rep$&'smsq_q40_rom'; p$
4070 END DEFine qx0_target
4080 :
4090 DEFine PROCedure qxl_target
4100   IF FOP_IN (rep$&'smsq_qxl_qxlh.exe') <> -7: INPUT 'QXLH.EXE exists'; cmd$: QUIT
4110   p$   = rep$&'sys_boot_file'
4120   bl$  = rep$&'smsq_qxl_qxl2_exe'
4130   hst$ = rep$&'smsq_qxl_host'
4140   hi$  = rep$&'smsq_qxl_hwinit'
4150   ld$  = rep$&'smsq_smsq_loader'
4160   os1$ = rep$&'smsq_smsq_os'
4170   ca$  = rep$&'smsq_smsq_cache'
4180   os2$ = rep$&'smsq_sbas_control'
4190   ns1$ = rep$&'smsq_qxl_nasty_e'
4200   ln1$ = rep$&'smsq_smsq_lang'
4210   ln2$ = rep$&'smsq_qxl_kbd_lang'
4220   ln3$ = rep$&'smsq_sbas_lang'
4230   ex1$ = rep$&'smsq_qxl_procs_x'
4240   ex2$ = rep$&'smsq_qxl_driver_most'  : REMark Keyboard before CON
4250   ex3$ = rep$&'smsq_qxl_driver_ql'
4260   ex3b$ = rep$&'smsq_qxl_driver_16'
4270   ex3w$ = rep$&'smsq_smsq_wman'
4280   ex4$ = rep$&'smsq_qxl_driver_nd'
4290   ex5$ = rep$&'smsq_qxl_driver_dv3e'
4300   ex6$= rep$&'smsq_smsq_hotkey'
4310   ex7$ = rep$&'smsq_qxl_sysspr'
4320   last$ = rep$&'smsq_qxl_ecache'
4330   dml$ = rep$&'smsq_qxl_dummy'
4340   qm$  = rep$&'qmon_qxl_bin'
4350   r$='ram1_smsqe.exe' : f$=rep$&'smsq_qxl_smsqe.exe'
4360   DELETE f$
4370   EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3w$,ex4$,ex5$,ex6$,ex7$,last$, r$
4380   EW cct, bl$,r$, f$
4390   DELETE r$
4400   OPEN #4,f$
4410   lenf=FLEN(#4)
4420   lens% = INT ((lenf+511)/512)
4430   lenb% = lenf - lens% * 512
4440   IF lenb%: lenb% = lenb% + 512
4450   BPUT #4\2, lenb% MOD 256, lenb% DIV 256, lens% MOD 256, lens% DIV 256
4460   CLOSE #4
4470 END DEFine qxl_target
4480 :
4490 DEFine FuNction find_target% (current%)
4500   IF "SMSQ Generic" INSTR mdata$(current%):RETurn 1
4510   IF "Atari" INSTR mdata$(current%):RETurn 2
4520   IF "GoldCard" INSTR mdata$(current%):RETurn 4
4530   IF "Q40" INSTR mdata$(current%):RETurn 8
4540   IF "QXL" INSTR mdata$(current%):RETurn 16
4550   RETurn 0                                        : REMark huh???
4560 END DEFine find_target%
4570 :
4580 DEFine PROCedure change_size   (x%,y%)
4590 LOCal xrel%,yrel%,temp,a$
4600   CH_WIN main_defn,xrel%,yrel%                    : REMark ptr displacement
4610   POS_PTR main_defn,2*65536+1,1
4620   xsize%=xsize%-xrel%
4630   ysize%=ysize%-yrel%                             : REMark new sizes
4640   IF xsize%>xsz%-16:xsize%=xsz%-16
4650   IF ysize%>ysz%-16:ysize%=ysize%-16    : REMark max values
4660   DR_UNST main_defn                               : REMark no more wdw defn...
4670   DR_REMV main_defn                               : REMark .... now!
4680   app_addr=0
4690   make_main_wdw
4700   IF compiled
4710      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
4720   ELSE
4730      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
4740   END IF
4750   POS_PTR main_defn,x%*65536+y%,1
4760 END DEFine change_size
4770 :
4780 DEFine FuNction warning_pos% (mess$,channel%,title$,pos)
4790 REMark warning wdw with positioning
4800   POS_PTR main_defn,pos,-1
4810   RETurn warning%(mess$,channel%,title$)
4820 END DEFine warning_pos%
4830 :
4840 DEFine FuNction warning%(mess$,channel%,title$)
4850 REMark shows a warning message (mess$) in a small window. Hit ESC or OK
4860 REMark to return; returns 0 if ESC, 1 if OK  hit
4870 LOCal item%,lp%,chan%,y%,swnum%
4880   IF channel%
4890     chan%=channel%
4900   ELSE
4910     chan%=FOPEN("con")
4920   END IF
4930   CH_ITEM warn_defn,-3,0,-1,'','Attention'
4940   CH_ITEM warn_defn,-1,0,-1,cancel$,'ESC'
4950   CH_ITEM warn_defn,-1,1,-1,'O','OK'
4960   warn_lfl%(0)=1:warn_lfl%(1)=1
4970   POKE_W warn_defn+46,pink                        : REMark paper colour of wdw
4980   POKE_W warn_defn+44,red                         : REMark border around wdw
4990   DR_PULD warn_defn,-1,-1
5000   DR_IWDF #chan%,warn_defn,0
5010   PAPER#chan%,white :INK#chan%,red
5020   IF title$<>"":PRINT#chan%,title$;" :"
5030   PRINT #chan%,mess$
5040   POS_PTR warn_defn,160*65536+10,-1
5050   REPeat lp%
5060     RD_PTR warn_defn,item%,swnum%,y%,y%,y%,warn_lfl%
5070     IF swnum%=-1:EXIT lp%
5080   END REPeat lp%
5090   IF NOT channel%:CLOSE#chan%
5100   DR_UNST warn_defn
5110   RETurn item%
5120 END DEFine warning%
5130 :
5140 DEFine PROCedure info_pos (mess$,channel%,pos)
5150 REMark warning wdw with positioning
5160   POS_PTR main_defn,pos,-1
5170   info mess$,channel%
5180 END DEFine info_pos
5190 :
5200 DEFine PROCedure info(mess$,channel%)
5210 REMark shows window containing mess$
5220   LOCal chan%
5230   IF channel%
5240     chan%=channel%
5250   ELSE
5260     chan%=FOPEN("con")
5270   END IF
5280   CH_ITEM warn_defn,-3,0,-1,"",'  Info.  '
5290   CH_ITEM warn_defn,-1,0,-1,"",'Info'
5300   CH_ITEM warn_defn,-1,1,-1,"",'Info'
5310   POKE_W warn_defn+44,green                       : REMark border
5320   POKE_W warn_defn+46,light_green                  : REMark paper col
5330   DR_PULD warn_defn,-1,-1
5340   POS_PTR warn_defn,90*65536+70,-1
5350   DR_IWDF #chan%,warn_defn,0
5360   PAPER#chan%,white:INK#chan%,black
5370   PRINT #chan%,mess$
5380   IF NOT channel%:CLOSE#chan%
5390 END DEFine info
5400 :
5410 DEFine PROCedure button (def_trav,menu_drap%,app_drap%,control%,chaine$,type_col%)
5420 REMark les parametres sont, dans l'ordre:
5430 REMark la dÉfinition de la fenëtre, suivie des tableaux pour
5440 REMark les postes du menu dÉliÉ, les postes de la sous-fenëtre d'appl.
5450 REMark et la dÉfinition de contròle, suivi du nom du bouton et
5460 REMark du type couleurs.
5470 REMark Il est supposÉ que vous avez ouvert une fenëtre #1 si
5480 REMark le programme est compilÉ
5490   DR_UNST def_trav
5500   IF compiled: CLOSE#1
5510   BTN chaine$,type_col%
5520   IF compiled
5530     OPEN#1,"CON_"
5540     DR_PPOS def_trav,-1,-1,menu_drap%,app_drap%,control%
5550   ELSE
5560     DR_PULD def_trav,-1,-1,menu_drap%,app_drap%,control%
5570   END IF
5580 END DEFine button
5590 :
5600 DEFine PROCedure make_colours (colourway%)
5610 REMark this initialises the GLOBAL variables menu_pap, menu_brd, menu_inv,
5620 REMark menu_ink and menu_bar according to the colourway passed as parameter.
5630 REMark The GLOBAl variable mycolour_mode% must have been set prior to calling
5640 REMark this function, preferrably via the function set_colours%.
5650 REMark Moreover the colours black, white,red, green,wg_stripes, etc...
5660 REMark must have been set prior to calling this procedure:
5670 REMark Preferrably, they were also set via the function set_colours%
5680 :
5690 REMark this function needs the extension WL_MK16 (outptr_bin)
5700 :
5710 REMark the colourways% are:
5720 REMark 0= white/green
5730 REMark 1= black/red
5740 REMark 2= white/red
5750 REMark 3= black/green
5760 REMark 4= use system palette (only available in modes 32 and 33)
5770 :
5780 REMark the Global war mycolour_mode% is:
5790 REMark 0 = we are in QL mode 4
5800 REMark 1 = we are in PAL mode (256 colours)
5810 REMark 2 - we are in 24 bit colour mode
5820 REMark preferrably, this is set via the function set_colours%
5830 :
5840   IF colourway%>7 OR colourway%<0: colourway%=0: REMark must be between 0 & 7
5850 :
5860 REMark first make sure that what we request is possible.
5870 REMark colourway 4 is only possible if we are in 24 bit colour mode
5880 REMark if it is requested in another mode, it is set to 0
5890 :
5900   IF colourway%>3
5910     IF mycolour_mode%< 2
5920        colourway%=0
5930      ELSE
5940        make_my_palette colourway%-4:RETurn   : REMark set colours according to system palette
5950     END IF
5960   END IF
5970 :
5980   SELect ON mycolour_mode%
5990     =0                                  : REMark QL 4 colours
6000       SELect ON colourway%
6010           =0:                           : REMark white/green
6020                 menu_pap=white
6030                 menu_brd=green
6040                 menu_ink=black
6050                 menu_bar=wg_striped
6060                 menu_inv=red
6070           =1:                           : REMark black/red
6080                 menu_pap=black
6090                 menu_brd=red
6100                 menu_ink=white
6110                 menu_bar=br_striped
6120                 menu_inv=green
6130           =2:                           : REMark white/red
6140                 menu_pap=white
6150                 menu_brd=red
6160                 menu_ink=black
6170                 menu_bar=wr_striped
6180                 menu_inv=green
6190           =3:                           : REMark black/green
6200                 menu_pap=black
6210                 menu_brd=green
6220                 menu_ink=white
6230                 menu_bar=bg_striped
6240                 menu_inv=red
6250       END SELect
6260     =1                                  : REMark pal colours
6270       SELect ON colourway%
6280           =0:                           : REMark white/green
6290                 menu_pap=white+256
6300                 menu_brd=green+256
6310                 menu_ink=black+256
6320                 menu_bar=wg_striped+256
6330                 menu_inv=red +256
6340           =1:                           : REMark black/red
6350                 menu_pap=black+256
6360                 menu_brd=red  +256
6370                 menu_ink=white +256
6380                 menu_bar=br_striped +256
6390                 menu_inv=green +256
6400                 omenu_pap=black+256
6410           =2:                           : REMark white/red
6420                 menu_pap=white +256
6430                 menu_brd=red   +256
6440                 menu_ink=black +256
6450                 menu_bar=wr_striped +256
6460                 menu_inv=green +256
6470           =3:                           : REMark black/green
6480                 menu_pap=black +256
6490                 menu_brd=green +256
6500                 menu_ink=white +256
6510                 menu_bar=bg_striped +256
6520                 menu_inv=red   +256
6530       END SELect
6540     =2                                  : REMark 24 bit colours
6550       SELect ON colourway%
6560           =0:                           : REMark white/green
6570                 menu_pap=WL_MK16(white)
6580                 menu_brd=WL_MK16(green)
6590                 menu_ink=WL_MK16(black)
6600                 menu_bar=WL_MK16(wg_striped)
6610                 menu_inv=WL_MK16(red)
6620           =1:                           : REMark black/red
6630                 menu_pap=WL_MK16(black)
6640                 menu_brd=WL_MK16(red)
6650                 menu_ink=WL_MK16(white)
6660                 menu_bar=WL_MK16(br_striped)
6670                 menu_inv=WL_MK16(green)
6680           =2:                           : REMark white/red
6690                 menu_pap=WL_MK16(white)
6700                 menu_brd=WL_MK16(red)
6710                 menu_ink=WL_MK16(black)
6720                 menu_bar=WL_MK16(wr_striped)
6730                 menu_inv=WL_MK16(green)
6740           =3:                           : REMark black/green
6750                 menu_pap=WL_MK16(black)
6760                 menu_brd=WL_MK16(green)
6770                 menu_ink=WL_MK16(white)
6780                 menu_bar=WL_MK16(bg_striped)
6790                 menu_inv=WL_MK16(red)
6800       END SELect
6810   END SELect
6820 :
6830   make_my_old_palette
6840 END DEFine make_colours
6850 :
6860 DEFine PROCedure make_my_palette (cw%)
6870   sp.winbd            = HEX('0200') : REMark Window border
6880   sp.winbg            = HEX('0201') : REMark Window background
6890   sp.winfg            = HEX('0202') : REMark Window foreground
6900   sp.winmg            = HEX('0203') : REMark Window middleground
6910   sp.titlebg          = HEX('0204') : REMark Title background
6920   sp.titletextbg  = HEX('0205') : REMark Title text background
6930   sp.titlefg          = HEX('0206') : REMark Title foreground
6940   sp.litemhigh        = HEX('0207') : REMark Loose item highlight
6950   sp.litemavabg   = HEX('0208') : REMark Loose item available background
6960   sp.litemavafg   = HEX('0209') : REMark Loose item available foreground
6970   sp.litemselbg   = HEX('020a') : REMark Loose item selected background
6980   sp.litemselfg   = HEX('020b') : REMark Loose item selected foreground
6990   sp.litemunabg   = HEX('020c') : REMark Loose item unavailable background
7000   sp.litemunafg   = HEX('020d') : REMark Loose item unavailable foreground
7010   sp.infwinbd         = HEX('020e') : REMark Information window border
7020   sp.infwinbg         = HEX('020f') : REMark Information window background
7030   sp.infwinfg         = HEX('0210') : REMark Information window foreground
7040   sp.infwinmg         = HEX('0211') : REMark Information window middleground
7050   sp.subinfbd         = HEX('0212') : REMark Subsidiary information window border
7060   sp.subinfbg         = HEX('0213') : REMark Subsidiary information window background
7070   sp.subinffg         = HEX('0214') : REMark Subsidiary information window foreground
7080   sp.subinfmg         = HEX('0215') : REMark Subsidiary information window middleground
7090   sp.appbd            = HEX('0216') : REMark Application window border
7100   sp.appbg            = HEX('0217') : REMark Application window background
7110   sp.appfg            = HEX('0218') : REMark Application window foreground
7120   sp.appmg            = HEX('0219') : REMark Application window middleground
7130   sp.appihigh         = HEX('021a') : REMark Application window item highlight
7140   sp.appiavabg        = HEX('021b') : REMark Application window item available background
7150   sp.appiavafg        = HEX('021c') : REMark Application window item available foreground
7160   sp.appiselbg        = HEX('021d') : REMark Application window item selected background
7170   sp.appiselfg        = HEX('021e') : REMark Application window item selected foreground
7180   sp.appiunabg        = HEX('021f') : REMark Application window item unavailable background
7190   sp.appiunafg        = HEX('0220') : REMark Application window item unavailable foreground
7200   sp.scrbar           = HEX('0221') : REMark Pan/scroll bar
7210   sp.scrbarsec        = HEX('0222') : REMark Pan/scroll bar section
7220   sp.scrbararr        = HEX('0223') : REMark Pan/scroll bar arrow
7230   sp.buthigh          = HEX('0224') : REMark Button highlight
7240   sp.butbd            = HEX('0225') : REMark Button border
7250   sp.butbg            = HEX('0226') : REMark Button background
7260   sp.butfg            = HEX('0227') : REMark Button foreground
7270   sp.hintbd           = HEX('0228') : REMark Hint border
7280   sp.hintbg           = HEX('0229') : REMark Hint background
7290   sp.hintfg           = HEX('022a') : REMark Hint foreground
7300   sp.hintmg           = HEX('022b') : REMark Hint middleground
7310   sp.errbg            = HEX('022c') : REMark Error message background
7320   sp.errfg            = HEX('022d') : REMark Error message foreground
7330   sp.errmg            = HEX('022e') : REMark Error message middleground
7340   sp.shaded           = HEX('022f') : REMark sp.shaded area
7350   sp.3ddark           = HEX('0230') : REMark Dark 3D border shade
7360   sp.3dlight          = HEX('0231') : REMark Light 3D border shade
7370   sp.vertfill         = HEX('0232') : REMark Vertical area fill
7380   IF NOT (IS_OPEN(#0) OR IS_OPEN(#1))
7390     OPEN#1,'con'
7400     SP_JOBPAL -1,cw%
7410     CLOSE#1
7420   ELSE
7430     SP_JOBPAL -1,cw%
7440   END IF
7450 END DEFine make_my_palette
7460 :
7470 DEFine PROCedure make_my_old_palette
7480 REMark this translates the colours used in the wdws into 4 colour mode...
7490   sp.winbd          =         menu_brd   : REMark Window border
7500   sp.winbg          =         menu_pap   : REMark Window background
7510   sp.winfg          =         menu_pap   : REMark Window foreground
7520   sp.titletextbg=   menu_pap   : REMark Title text background
7530   sp.titlefg        =         menu_ink   : REMark Title foreground
7540   sp.titlebg        =         menu_bar   : REMark Title background
7550   sp.litemhigh      =         menu_ink   : REMark Loose item highlight
7560   sp.litemavabg =   menu_pap   : REMark Loose item available background
7570   sp.litemavafg =   menu_ink   : REMark Loex fifiose item available foreground
7580   sp.litemselbg =   menu_brd   : REMark Loose item selected background
7590   sp.litemselfg =   menu_ink   : REMark Loose item selected foreground
7600   sp.litemunabg =   menu_pap   : REMark Loose item unavailable background
7610   sp.litemunafg =   menu_brd   : REMark Loose item unavailable foreground
7620   sp.infwinbd       =         menu_brd   : REMark Information window border
7630   sp.infwinbg       =         menu_pap   : REMark Information window background
7640   sp.infwinfg       =         menu_ink   : REMark Information window foreground
7650   sp.appbd          =         menu_brd   : REMark Application window border
7660   sp.appbg          =         menu_pap   : REMark Application window background
7670   sp.appfg          =         menu_ink   : REMark Application window foreground
7680   sp.appmg          =         menu_pap   : REMark Application window middleground
7690   sp.appihigh       =         menu_ink   : REMark Application window item highlight
7700   sp.appiavabg      =         menu_pap   : REMark Application window item available background
7710   sp.appiavafg      =         menu_ink   : REMark Application window item available foreground
7720   sp.appiselbg      =         menu_brd   : REMark Application window item selected background
7730   sp.appiselfg      =         menu_ink   : REMark Application window item selected foreground
7740   sp.appiunabg      =         menu_pap   : REMark Application window item unavailable background
7750   sp.appiunafg      =         menu_brd   : REMark Application window item unavailable foreground
7760   sp.scrbar         =         menu_ink   : REMark Pan/scroll bar
7770   sp.scrbararr      =         menu_brd   : REMark Pan/scroll bar arrow
7780   sp.buthigh        =         menu_ink   : REMark Button highlight
7790   sp.butbd          =         menu_brd   : REMark Button border
7800   sp.butbg          =         menu_pap   : REMark Button background
7810   sp.butfg          =         menu_ink   : REMark Button foreground
7820 END DEFine mk_my_old_palette
7830 :
7840 DEFine FuNction getstr$(poste%,drap%,defn,a$)
7850 REMark ouvre une fenëtre sur un poste de menu dÉliÉ et propose
7860 REMark une chaïne ç Éditer
7870 REMark les paramêtres sont
7880 REMark - le numÉro du poste
7890 REMark - un drapeau si le rÉsultat doit ëtre un chiffre (si drap%<>0)
7900 REMark - la dÉfinition de travail dans laquelle se trouve le poste
7910 REMark - la chaïne qu'on peut Éditer
7920 REMark returns the string or ""
7930 REMark uses the global (!) vars menu_pap% & menu_brd%
7940 REMark
7950   LOCal chan%,b$,res
7960   b$=" ":b$=a$   : REMark c'est curieusement nÉcessaire
7970   chan%=FOPEN("con")
7980   PAPER#chan%,menu_brd
7990   INK#chan%,menu_ink
8000   DR_LWDF#chan%,defn,poste%             : REMark open channel over item
8010   CLS#chan%
8020   res=GET_STR(#chan%,drap%,b$)          : REMark get string
8030   CLOSE#chan%
8040   IF res<0:b$=""                        : REMark ooops
8050   CH_ITEM defn,-1,poste%,-1,"",b$
8060   RETurn b$
8070 END DEFine getstr$
8080 :
8090 DEFine FuNction showcont$(string$,length%)
8100 REMark this adds '...' at the end of a string, to show that it continues
8110 LOCal l%,lengt%
8120   l%=LEN(string$)
8130   IF NOT l%:RETurn ""                   : REMark empty string
8140   lengt%=length% DIV 6
8150   IF l%<=lengt%:RETurn string$          : REMark entire string fits wdw
8160   RETurn string$(1 TO lengt%-3)&"..."
8170 END DEFine showcont$
8180 :
8190 DEFine FuNction get_free_text$(defn,item%,prompt$,length%)
8200 REMark this opens wdw over loose-item item% of the working definition defn
8210 REMark allows free text to be entered
8220 REMark params: working defintion,item over which wdw is to be opened
8230 REMark         first value of string, length of max string in wdw, if this is...
8240 REMark            -1, the length will be determined here
8250 LOCal chan%,string$,tempsizes%(3)
8260   chan%=FOPEN('con')
8270   IF chan%<0:RETurn prompt$
8280   DR_LWDF#chan%,defn,item%              : REMark open wdw over item
8290   PAPER#chan%,menu_brd
8300   INK#chan%,menu_ink
8310   CLS#chan%
8320   string$=WEDIT$(#chan%,prompt$)
8330   IF length%=-1
8340      WSIZE#chan%,tempsizes%
8350      length%=tempsizes%(0)
8360   END IF
8370   CLOSE#chan%
8380   IF length%
8390     CH_ITEM defn,-1,item%,-1,"",showcont$(string$,length%)
8400   ELSE
8410     CH_ITEM defn,-1,item%,-1,"",string$
8420   END IF
8430   RETurn string$
8440 END DEFine get_free_text$
8450 :
8460 DEFine FuNction do_caps(a$,caps%)
8470 REMark set string to capitales etc,caps% determines whether the string....
8480 REMark ...must be lower case (=0), upper case(=1), all words capitalized (=2),
8490 REMark ... first word only capitalized (=3), or nothing(=-1)
8500   SELect ON caps%
8510    =0: MIN a$
8520    =1: MAJ a$
8530    =2: CAP2 a$
8540    =3: CAP a$
8550   END SELect
8560   RETurn a$
8570 END DEFine do_caps
8580 :
8590 DEFine FuNction centr$(ctr$,leng)
8600 REMark this "centres" the string ctr$ (i.e. left padds it with spaces) so that
8610 REMark it is centred in a window of leng width (leng is length in chars)
8620   LOCal l%,a$
8630   a$=ctr$
8640   l%=INT(leng-LEN(a$))/2
8650   IF l%<0:RETurn a$
8660   a$=FILL$(' ',l%)&a$&FILL$(' ',l%)
8670   RETurn a$
8680 END DEFine centr$
8690 :
8700 DEFine FuNction set_colours% (force_mode%)
8710 REMark this sets colour variables according to the colour mode wished, if possible.
8720 REMark force_mode%=
8730 REMark       0 if ql colours wished (mode 4 only)
8740 REMark       1 if pal colours wished
8750 REMark       2 if 24 bit colours wished
8760 REMark This function returns the true QL mode set (4,8,32,33 etc.).
8770 REMark This function also sets the GLOBAL variable mycolour_mode% to the
8780 REMark mode 0,1,2 as above - whatever is possible on the machine, i.e.
8790 REMark if colour_24 is wished on a 4 colour machine (which won't succeed)
8800 REMark this function will set set that variable to 0, i.e. QL mode.
8810 REMark
8820 REMark This function needs the outptr toolkit
8830 LOCal mode%
8840   mode%=RMODE                                     : REMark get current mode now
8850   SELect ON force_mode%
8860       = 0                                         : REMark mode 4 wished
8870        SELect ON mode%
8880             = 4                                   : REMark already on, so do nothing
8890             = 0,1,8 : MODE 4            : REMark old modes, set to 4 colours
8900             = REMAINDER
8910               COLOUR_QL                           : REMark if higher colour mode, set to "mode 4"
8920               MODE 4
8930        END SELect
8940        mode%=0                                    : REMark new mode we're in  - 4 colour mode
8950       = 1                                         : REMark PALette wished
8960        SELect ON mode%
8970             = 32,33 : COLOUR_PAL:mode%=1          : REMark we're in high colour mode, but PAL colours wished
8980             = REMAINDER
8990               IF mode%<>4:MODE 4
9000               mode%=0
9010        END SELect
9020       = 2
9030        SELect ON mode%
9040             = 32,33 : COLOUR_24 :mode%=2          : REMark we're in high col mode 24 bit colours
9050             = REMAINDER
9060               IF mode%<>4:MODE 4
9070               mode%=0
9080        END SELect
9090   END SELect
9100   mycolour_mode%=mode%                            : REMark set this global variable now:
9110   REMark now set some colour variables
9120   SELect ON mode%
9130        = 0                                        : REMark Ql 4 mode colours
9140           black            = 0
9150           white            = 7
9160           red              = 2
9170           green            = 4
9180           grey             = 255
9190           dull_pink      = 19
9200           Blue             = 0
9210           Magenta        = 2
9220           Yellow           = 7
9230           Cyan             = 0
9240           Dark_slate     = 0
9250           Slate_grey     = 255
9260           Dark_grey      = 255
9270           Light_grey     = 255
9280           Ash_grey       = 255
9290           Dark_red       = 210
9300           light_green    = 220
9310           Mustard        = 242
9320           Dark_green     = 228
9330           Sea_blue       = 0
9340           Dark_blue      = 0
9350           Purple           = 0
9360           Shocking_pink  = 234
9370           Orange           = 234
9380           Lime_green     = 4
9390           Apple_green    = 4
9400           Bright_blue    = 0
9410           Mauve            = 0
9420           Peach            = 7
9430           Light_yellow   = 7
9440           Light_blue     = 0
9450           Sky_blue       = 0
9460           Rose_pink      = 234
9470           pink             = 234
9480           Beige            = 7
9490           Pastel_pink    = 234
9500           Pastel_yellow  = 7
9510           Pastel_green   = 4
9520           Pastel_cyan    = 0
9530           Pastel_blue    = 0
9540           Pastel_rose    = 234
9550           Brick            = 210
9560           Light_khaki    = 7
9570           Dull_green     = 228
9580           Dull_cyan      = 0
9590           Steel_blue     = 0
9600           dull_pink      = 234
9610           Brown            = 0
9620           Khaki            = 7
9630           Dusky_green    = 4
9640           Dusky_blue     = 0
9650           Midnight_blue  = 0
9660           Plum             = 0
9670           Dusky_pink     = 234
9680           Buff             = 0
9690           Avocado        = 4
9700           Dull_turquoise = 4
9710           Dull_blue      = 0
9720           Faded_purple   = 234
9730           Cerise           = 2
9740           Tan_normal     = 0
9750           Grass_green    = 4
9760           Sea_green      = 4
9770           Ultramarine    = 0
9780           Deep_purple    = 234
9790           Light_orange   = 7
9800           light_red      = 2
9810           wg_striped     = 92
9820           br_striped     = 82
9830           wr_striped     = 107
9840           bg_striped     = 100
9850        = 1                              : REMark Colour_pal
9860           black            = 0
9870           white            = 1
9880           red              = 2
9890           green            = 3
9900           Blue             = 4
9910           Magenta        = 5
9920           Yellow           = 6
9930           Cyan             = 7
9940           Dark_slate     = 8
9950           Slate_grey     = 9
9960           Dark_grey      = 10
9970           grey             = 11
9980           Light_grey     = 12
9990           Ash_grey       = 13
10000           Dark_red       = 14
10010           light_green    = 15
10020           Mustard        = 16
10030           Dark_green     = 17
10040           Sea_blue       = 18
10050           Dark_blue      = 19
10060           Purple           = 20
10070           Shocking_pink  = 21
10080           Orange           = 22
10090           Lime_green     = 23
10100           Apple_green    = 24
10110           Bright_blue    = 25
10120           Mauve            = 26
10130           Peach            = 27
10140           Light_yellow   = 28
10150           Light_blue     = 29
10160           Sky_blue       = 30
10170           Rose_pink      = 31
10180           pink             = 32
10190           Beige            = 33
10200           Pastel_pink    = 34
10210           Pastel_yellow  = 35
10220           Pastel_green   = 36
10230           Pastel_cyan    = 37
10240           Pastel_blue    = 38
10250           Pastel_rose    = 39
10260           Brick            = 40
10270           Light_khaki    = 41
10280           Dull_green     = 42
10290           Dull_cyan      = 43
10300           Steel_blue     = 44
10310           dull_pink      = 45
10320           Brown            = 46
10330           Khaki            = 47
10340           Dusky_green    = 48
10350           Dusky_blue     = 49
10360           Midnight_blue  = 50
10370           Plum             = 51
10380           Dusky_pink     = 52
10390           Buff             = 53
10400           Avocado        = 54
10410           Dull_turquoise = 55
10420           Dull_blue      = 56
10430           Faded_purple   = 57
10440           Cerise           = 58
10450           Tan_normal     = 59
10460           Grass_green    = 60
10470           Sea_green      = 61
10480           Ultramarine    = 62
10490           Deep_purple    = 63
10500           pinkish        = 77
10510           light_red      = 109
10520           Light_orange   = 208
10530           wg_striped     = light_green
10540           br_striped     = Dark_red
10550           wr_striped     = light_red
10560           bg_striped     = Dark_green
10570        = 2                              : REMark colour_24
10580           black            = HEX('000000')
10590           white            = HEX('FFFFFF')
10600           red              = HEX('FF0000')
10610           green            = HEX('00FF00')
10620           Blue             = HEX('0000FF')
10630           Magenta        = HEX('FF00FF')
10640           Yellow           = HEX('FFFF00')
10650           Cyan             = HEX('00FFFF')
10660           Dark_slate     = HEX('242424')
10670           Slate_grey     = HEX('494949')
10680           Dark_grey      = HEX('6D6D6D')
10690           grey             = HEX('929292')
10700           Light_grey     = HEX('B6B6B6')
10710           Ash_grey       = HEX('DBDBDB')
10720           Dark_red       = HEX('920000')
10730           light_green    = HEX('B6FFB6')
10740           Mustard        = HEX('929200')
10750           Dark_green     = HEX('009200')
10760           Sea_blue       = HEX('009292')
10770           Dark_blue      = HEX('000092')
10780           Purple           = HEX('920092')
10790           Shocking_pink  = HEX('FF0092')
10800           Orange           = HEX('FF9200')
10810           Lime_green     = HEX('92FF00')
10820           Apple_green    = HEX('00FF92')
10830           Bright_blue    = HEX('0092FF')
10840           Mauve            = HEX('9200FF')
10850           Peach            = HEX('FFB6B6')
10860           Light_yellow   = HEX('FFFFB6')
10870           Light_blue     = HEX('B6FFFF')
10880           Sky_blue       = HEX('B6B6FF')
10890           Rose_pink      = HEX('FFB6FF')
10900           pink             = HEX('FFB6DB')
10910           Beige            = HEX('FFDBB6')
10920           Pastel_pink    = HEX('FFDBDB')
10930           Pastel_yellow  = HEX('FFFFDB')
10940           Pastel_green   = HEX('DBFFDB')
10950           Pastel_cyan    = HEX('DBFFFF')
10960           Pastel_blue    = HEX('DBDBFF')
10970           Pastel_rose    = HEX('FFDBFF')
10980           Brick            = HEX('B66D6D')
10990           Light_khaki    = HEX('B6B66D')
11000           Dull_green     = HEX('6DB66D')
11010           Dull_cyan      = HEX('6DB6B6')
11020           Steel_blue     = HEX('6D6DB6')
11030           dull_pink      = HEX('B66DB6')
11040           Brown            = HEX('6D2424')
11050           Khaki            = HEX('6D6D24')
11060           Dusky_green    = HEX('246D24')
11070           Dusky_blue     = HEX('246D6D')
11080           Midnight_blue  = HEX('24246D')
11090           Plum             = HEX('6D246D')
11100           Dusky_pink     = HEX('B64992')
11110           Buff             = HEX('B69249')
11120           Avocado        = HEX('92B649')
11130           Dull_turquoise = HEX('49B692')
11140           Dull_blue      = HEX('4992B6')
11150           Faded_purple   = HEX('9249B6')
11160           Cerise           = HEX('920049')
11170           Tan_normal     = HEX('924900')
11180           Grass_green    = HEX('499200')
11190           Sea_green      = HEX('009249')
11200           Ultramarine    = HEX('004992')
11210           Deep_purple    = HEX('490092')
11220           Light_orange   = HEX('ffb624')
11230           pinkish        = HEX('C0037B')
11240           light_red      = HEX('FC1729')
11250           wg_striped     = light_green
11260           br_striped     = Dark_red
11270           wr_striped     = light_red
11280           bg_striped     = Dark_green
11290   END SELect
11300   mode%=RMODE
11310   RETurn mode%                             : REMark return current screen mode now
11320 END DEFine set_colours%
11330 :
11340 DEFine FuNction fen_app2 (adresse,tableau$,nr_sects%,selk%,phys%)
11350 REMark les paramêtres sont: l'adresse (0 la premiêre fois)
11360 REMark le tableau de chaïne, le nombre de sections,
11370 REMark le drapeau si on veut des touches de sÉlection et si sÉlection=action,
11380 REMark un tableau d'entiers contenant les tailles x,y et les origines
11390 REMark et les touches ç exclure des touches de sÉlection
11400 LOCal app_attributs%(3,1),temp%,bord%,pap%,ori_x%,ori_y%
11410 LOCal tail_x%,tail_y%,fleches%,barrre%
11420 :
11430   IF adresse:RECHP adresse  : REMark si c'est un 2nd appel, on enlêve l'ancienne
11440   tail_x%=phys%(0)            : REMark (largeur)    Ceci est pour la clartÉ
11450   tail_y%=phys%(1)            : REMark (hauteur)    seulement, on pourrait
11460   ori_x%=phys%(2)             : REMark            utiliser directement le
11470   ori_y%=phys%(3)             : REMark origs x,y    tableau phys% dans AP_MAKE
11480 :
11490   adresse=APMAKE2(selk%,tail_x%,tail_y%,ori_x%,ori_y%,nr_sects%,tableau$)
11500   IF adresse<0: RETurn 0      : REMark si erreur, on retourne 0
11510                                         : REMark Maintenant on dÉtermine les attributs
11520   bord%=sp.appbd                        : REMark couleur bordure
11530   pap%=sp.appbg                         : REMark couleur "papier"
11540   touche_sel%=9               : REMark touche de sÉlection - TAB
11550   fleches%=sp.scrbararr                 : REMark couleur des flêches
11560   barre%=sp.scrbar                      : REMark couleur de la barre d'ascenseur
11570 :
11580   app_attributs%(0,0)=1
11590   app_attributs%(0,1)=sp.appihigh       : REMark larg. & coul. trait poste courant
11600 :
11610   app_attributs%(1,0)=sp.appiunabg
11620   app_attributs%(1,1)=sp.appiunafg  : REMark unavailable
11630 :
11640   app_attributs%(2,0)=sp.appiavabg
11650   app_attributs%(2,1)=sp.appiavafg  : REMark disponible
11660 :
11670   app_attributs%(3,0)=sp.appiselbg
11680   app_attributs%(3,1)=sp.appiselfg  : REMark sÉlectionnÉ
11690 :
11700   er=AP_PARS(adresse,0,bord%,pap%,touche_sel%,fleches%,barre%,app_attributs%)
11710 :
11720   IF er:RETurn 0
11730 :
11740   RETurn adresse  : REMark on retourne l'adresse
11750 END DEFine fen_app
11760 :
11770 DEFine PROCedure scroll_arrows(rangees%,nbr_objets%,appfen,cty%)
11780 REMark Cette procÉdure dÉcide s'il faut laisser les flêches de dÉfilement
11790 REMark params: le nombre de rangÉes, le nombre total d'objets
11800 REMark le pointer vers la sous-fen d'app et la dÉfinition de controle
11810   IF nbr_objets%<=rangees% +1
11820     POKE_W appfen+150,0
11830     cty%(1,2)=nbr_objets%
11840   ELSE
11850     POKE_W appfen+150,6
11860 REMark    cty%(1,2)=rangees%-1      : rem if scroll arrows are there, there is one line less in the window
11870     cty%(1,2)=rangees%
11880  END IF
11890  IF DIMN(cty%,1)=2:cty%(2,2)=cty%(1,2)
11900  cty%(0,1)=1                  : REMark show that definitionhas changed
11910 END DEFine scroll_arrows
11920 :
11930 DEFine PROCedure init
11940 LOCal temp%
11950   text=0:sprite=2:blob=4:pattern=6
11960   retr=256:retn=-256:sous1=254:sous2=252
11970   REMark normal: hit = selects/deselects, do = returns & leaves selected
11980   REMark retr = return even on hit, reset item to available after,
11990   REMark retn = return even on hit, hit selects/deselects, do selects
12000 :
12010   hit$=CHR$(1):do$=CHR$(2):cancel$=CHR$(3):sleep$=CHR$(7):wake$=CHR$(8)
12020   help$=CHR$(4):move$=CHR$(5):size$=CHR$(6):cr$=CHR$(10)
12030 :
12040   appwinoffset%=16                      : REMark appsub win additional y offset
12050   xsize%=400                                      : REMark x size divisible by 4!
12060   ysize%=200                                      : REMark provisional y size%
12070   lxs=24:lys=12                         : REMark standard item x,y, sizes
12080   inf1y=lys+4                                     : REMark y orig of inner border
12090   xesp=lxs+2                                      : REMark loose items x spacing
12100   nbr_post%=3                                     : REMark nbr of item right & left in title bar
12110   main_coul%=SMSCFG(2)                            : REMark colourways
12120   app_coul%=main_coul%                            : REMark preset colourways
12130   temp%=set_colours% (2)                : REMark always try to set highest colours
12140   IF temp%<2 AND main_coul%>3:main_coul%=0 : REMark old wman
12150   make_colours main_coul%               : REMark set all colours now
12160 :
12170   rep$=SMSCFG(1)                        : REMark dir with files
12180   titre$="Dir: "&rep$&" "
12190 :
12200 RESTORE 12200
12210 :
12220 : REMark make loose item attributes
12230 :
12240   DIM std_iattr(3,3)
12250   RD_IATT std_iattr
12260     DATA 1,sp.litemhigh : REMark current item border size & colour
12270     DATA sp.litemunabg,sp.litemunafg,0,0 : REMark unavailable item paper & ink
12280     DATA sp.litemavabg,sp.litemavafg,0,0 : REMark   available         "    "       "
12290     DATA sp.litemselbg,sp.litemselfg,0,0 : REMark   selected
12300 :
12310 : REMark make fdata for sprites - uses the modified RD_SPRT function
12320 : REMark
12330 : REMark
12340 :
12350   espace_vide=ALCHP(100)                : REMark GLOBAL var for 0 mask sprites!
12360 :
12370   main_sprite=RD_SPRT(0)                : REMark 0 = normal mask
12380     DATA 11,13,6,5,4
12390     DATA '     www     '
12400     DATA '     waw     '
12410     DATA '     waw     '
12420     DATA '     waw     '
12430     DATA 'wwwwwa awwwww'
12440     DATA 'waaaa   aaaaw'
12450     DATA 'wwwwwa awwwww'
12460     DATA '     waw     '
12470     DATA '     waw     '
12480     DATA '     waw     '
12490     DATA '     www     '
12500 :
12510   move_sprite=RD_SPRT(1)                : REMark 1 = 0 mask
12520   IF temp%>1:move_sprite=6              : REMark or system sprite!
12530     DATA 10,14,5,4,4
12540     DATA 'aaaaaaaaaa    '
12550     DATA 'awwwwwwwwa    '
12560     DATA 'awwrrrrwwa    '
12570     DATA 'awwrrrrwwaaaaa'
12580     DATA 'awwrrwwwwwwwwa'
12590     DATA 'awwwwwwwwrrwwa'
12600     DATA 'aaaaawwrrrrwwa'
12610     DATA '    awwrrrrwwa'
12620     DATA '    awwwwwwwwa'
12630     DATA '    aaaaaaaaaa'
12640 :
12650   zzz_sprite= RD_SPRT(1)
12660   IF temp%>1:zzz_sprite=10
12670     DATA 7,15,0,0,4
12680     DATA 'wwwww          '
12690     DATA '   w           '
12700     DATA '  w  wwww      '
12710     DATA ' w     w       '
12720     DATA 'wwwww w  wwww  '
12730     DATA '     wwww  w   '
12740     DATA '          wwww '
12750 :
12760  size_sprite=RD_SPRT(1)
12770  IF temp%>1:size_sprite=7
12780     DATA 9,14,5,4,4
12790     DATA 'aaaaaaaaaaaaaa'
12800     DATA 'awwwwwwwwwwwwa'
12810     DATA 'awwrrrrrrrrwwa'
12820     DATA 'awwrrrrrrrrwwa'
12830     DATA 'awwrrwwwwwwwwa'
12840     DATA 'awwrrwwrrrrwwa'
12850     DATA 'awwrrwwrrrrwwa'
12860     DATA 'awwwwwwwwwwwwa'
12870     DATA 'aaaaaaaaaaaaaa'
12880 :
12890   make_my_data 1
12900   make_main_wdw
12910 :
12920 END DEFine init
12930 :
12940 DEFine PROCedure make_main_wdw
12950 REMark make the main window
12960 :
12970   REMark first make sure of the wdw sizes (we also come here from change_size!)
12980   xsize%=xsize%-(xsize% MOD 4)                    : REMark make into nbr divisible by 4
12990   IF xsize%<360:xsize%=360
13000 :
13010   REMark calculate the y size of the wdw so that it always looks nice, in that
13020   REMark the rows in the appsub always fill the appsub entirely
13030   ysize%=ysize%+(ysize%&&1)             : REMark make into even number
13040   IF ysize%<100:ysize%=100              : REMark predetermioned min ysize% before calculations
13050   ysize%=ysize%-inf1y-14-appwinoffset%
13060   rangees=ysize% DIV 12                 : REMark GLOBAL var: nbr of rows in appsub
13070   IF rangees>data_count%-2:rangees=data_count%-2
13080   ysize%=rangees*12+inf1y+14 +appwinoffset%
13090 :
13100   REMark first calc info obj size - this also influences one llose item, used
13110   REMark in the title bar
13120   longueur_titre=LEN(titre$)*6                    : REMark length in pixels
13130   titre_x=INT((xsize%-longueur_titre)/2): REMark x orig
13140 :
13150   REMark first the loose items
13160 :
13170   yinf1y=inf1y+4:
13180   esc%=0:move_it%=esc%+1:size_it%=move_it%+1
13190   sleep_it%=size_it%+1:all_it%=sleep_it%+1:go_it%=all_it%+1
13200   dir_it%=go_it%+1:target_its%=dir_it%+1:make_it%=target_its%+5
13210 :
13220 RESTORE 13220
13230   REMark reset data pointer to here (The Basic Linker does that automatically)
13240 :
13250   temp%=xsize% DIV 6
13260   main_lit=12:DIM main_lfl%(main_lit)   : REMark there are 13 items!
13270   main_lot=RD_LOT(std_iattr,main_lit)
13280     DATA lxs,lys,4+xesp*0,2,0,0,cancel$,text+retr,'ESC'
13290     DATA lxs,lys,4+xesp*1,2,0,0,move$,sprite+retr,move_sprite
13300     DATA lxs,lys,4+xesp*2,2,0,0,size$,sprite+retn,size_sprite
13310     DATA lxs,lys,xsize%-2-xesp*1,2,0,0,sleep$,sprite+retr,zzz_sprite
13320     DATA lxs,lys,xsize%-2-xesp*2,2,0,0,'A',text+retn,"All"
13330     DATA lxs,lys,xsize%-2-xesp*3,2,0,0,'O',sous1+retn,"OK"
13340     DATA 6*6,lys-2,titre_x+30,3,0,0,'D',text+retn,rep$
13350     DATA 8*6,lys,8,inf1y+2,0,0,'G',sous1+retn,"Generic"
13360     DATA 8*6,lys,8+temp%,inf1y+2,0,0,'T',sous2+retn,"Atari"
13370     DATA 8*6,lys,8+temp%*2,inf1y+2,0,0,'G',sous1+retn,"GoldCard"
13380     DATA 8*6,lys,8+temp%*3,inf1y+2,0,0,'Q',sous1+retn,"Q40/Q60"
13390     DATA 8*6,lys,8+temp%*4,inf1y+2,0,0,'X',sous2+retn,"QXL"
13400     DATA 8*6,lys,8+temp%*5,inf1y+2,0,0,'M',sous2+retn,"Make"
13410 :
13420     main_lfl%(target_its%)=128
13430 :
13440   REMark now info wdw objects - here only one, the title
13450   REMark make the title data: calculate size and x position of this object
13460 :
13470   main_iot1=RD_IOT(0)
13480     DATA longueur_titre,10,4,INT((lys-10)/2),sous1,sp.titlefg,0,0,titre$
13490 :
13500   REMark now the info wdws
13510 :
13520   main_iwt=RD_IWT(2)                              : REMark there are 3
13530 :
13540 REMark first the title bar
13550 :
13560     DATA xsize%-8-(2*nbr_post%*xesp),lys+2,xesp*nbr_post%+4,0         : REMark x,y sizes & origs
13570     DATA 0,0,sp.infwinbd,sp.titlebg     : REMark shadow, border size & col,paper col
13580     DATA 0                                        : REMark object pointer (none)
13590 :
13600 REMark now wdw containing title itself
13610 :
13620     DATA longueur_titre+8,lys,titre_x-4,2
13630     DATA 0,0,sp.infwindbd,sp.infwinbg
13640     DATA main_iot1                      : REMark one object
13650 :
13660 REMark inner border
13670 :
13680     DATA xsize%-8,ysize%-inf1y-2,4,inf1y
13690     DATA 0,1,sp.infwinbd,sp.infwinbg
13700     DATA 0
13710 :
13720 REMark now the appsub wdw - use the OUTPTR functions for this
13730 :
13740   app_addr=0                                      : REMark there is none yet
13750   mk_main_app 1                         : REMark (this destroys my DATA pointer)
13760   main_app_list = app_addr+180                    : REMark list built by OUTPTR
13770 :
13780 : REMark  working defn
13790 :
13800 RESTORE 13800
13810                                                   : REMark reset DATA pointer (due to Basic Linker, restore must be ALONE on the line)
13820   main_defn=RD_WDEF
13830     DATA 2,1,sp.winbd,sp.winbg                    : REMark shad,border size & col,pap col
13840     DATA xsize%,ysize%,0,0    : REMark x,y size & initial ptr pos
13850     DATA main_sprite,main_lot,main_iwt,main_app_list : REMark sprite,loose items, info wins, appsubs
13860 END DEFine make_main_wdw
13870 :
13880 DEFine PROCedure warn_make
13890 REMark makes warn,ing & info window
13900 REMark uses global vars green etc...
13910 REMark SETS the global var warning_width% (nbr of chars in one line in wdw)
13920 LOCal lys%,warn_lot,warn_iot1,warn_iwt
13930 LOCal x%,y%,warning_paper,warning_brd
13940 RESTORE 13940
13950   x%=188:y%=82
13960   warning_paper=menu_pap
13970   warning_brd=pink                      : REMark pink
13980 REMark  if menu_brd=4:warning_brd=light_green      : rem light green
13990   lys%=10
14000   DIM warn_lfl%(1)
14010   warn_lot=RD_LOT(std_iattr,1)
14020     DATA 30,lys%,4,4,0,0,cancel$,text+retr,'ESC'
14030     DATA 30,lys%,x%-34,4,0,0,'O',text+retr,"OK"
14040 :
14050   warn_iot1=RD_IOT (0)
14060     DATA 12*9,10,0,1,text,red,2,0,'Attention'
14070 :
14080   warn_iwt=RD_IWT(1)
14090     DATA x%-16,y%-32,8,24
14100     DATA 0,1,red,warning_paper
14110     DATA 0
14120 :
14130     DATA 120,12,(x%-112) DIV 2,4
14140     DATA 0,1,menu_brd,warning_paper
14150     DATA warn_iot1
14160 :
14170   warn_defn=RD_WDEF
14180     DATA 2,1,menu_brd,warning_brd
14190     DATA x%,y%,0,0
14200     DATA main_sprite,warn_lot,warn_iwt,0
14210 :
14220   warning_width%=(x%-16) DIV 6                    : REMark width of warning winfow in chars
14230 :
14240 END DEFine warn_make
14250 :
14260 DEFine PROCedure about_make (titre$,version$)
14270 REMark this presupposes that wolfsoft_sprite is part of the sprites!
14280 LOCal lxs%,lys%,xesp%,inf1y%,abt_lot,longueur_titre%,titre_x%
14290 LOCal abt_iwt,abt_iot1,abt_iot2
14300   ab_xt%=200:ab_yt%=120          : REMark tailles x et y: la taille x devrait
14310                                    : REMark ëtre un chiffre divisible par 4
14320   lxs%=24:lys%=10                    : REMark tailles x et y pour postes standard
14330   inf1y%=lys%+4                      : REMark posi. y de la fen. d'inf. intÉrieure
14340   xesp%=lxs%+2
14350   nbr_post%=0                      : REMark nombre de postes dans un coin
14360 :
14370 RESTORE 14370
14380 :
14390   yinf1y%=inf1y%+4:
14400 :
14410   DIM abt_lfl%(0)
14420   abt_lot=RD_LOT(std_iattr,0)
14430     DATA ab_xt%-16,lys%,8,ab_yt%-lys%-8,0,0,cancel$,text+retr,'Super!'
14440 :
14450 : REMark  D'abord le titre qui est un objet d'une sous-fenëtre d'info.
14460 :
14470   longueur_titre%=LEN("Version "&version$)*6             : REMark on calcule la taille et la
14480   titre_x%=INT((ab_xt%-longueur_titre%)/2)  : REMark position x de cet objet
14490   abt_iot2=RD_IOT(1)
14500     DATA longueur_titre%,lys%,4,8,text,menu_ink,0,0,"Version "&version$
14510     DATA 52,24,ab_xt%-60,40,sprite,0,wolfsoft_sprite
14520   longueur_titre%=LEN(titre$)*6             : REMark on calcule la taille et la
14530   titre_x%=INT((ab_xt%-longueur_titre%)/2)  : REMark position x de cet objet
14540   abt_iot1=RD_IOT(0)
14550     DATA longueur_titre%,lys%,4,INT((lys%-10)/2),text,menu_ink,0,0,titre$
14560   abt_iwt=RD_IWT(2)               : REMark on veut 3 sous-fen. d'info
14570 REMark d'abord la fenëtre contenant la barre entourant le titre
14580     DATA ab_xt%-8-(2*nbr_post%*xesp%),lys%+2,xesp%*nbr_post%+4,0  : REMark taille x,y, origines x,y
14590     DATA 0,0,menu_brd,menu_bar     : REMark ombre, taille & couleur bordure
14600                                    : REMark puis couleur papier
14610     DATA 0                         : REMark pointeur vers objets
14620 REMark maintenant la fenëtre qui contient le titre
14630     DATA longueur_titre%+8,lys%,titre_x%-2,2   : REMark on utilise titre_x% etc
14640     DATA 0,0, menu_brd,menu_pap
14650     DATA abt_iot1                 : REMark un objet, le titre
14660 REMark maintenant la fenëtre de la bordure intÉrieure
14670     DATA ab_xt%-8,ab_yt%-inf1y%-2,4,inf1y%
14680     DATA 0,1, menu_brd,menu_pap
14690     DATA abt_iot2
14700   abt_defn=RD_WDEF
14710     DATA 2,1,menu_ink,menu_pap: REMark ombre, larg. & coul. bord., coul. papier
14720     DATA ab_xt%,ab_yt%,10,10 : REMark taille x,y et position pointeur
14730     DATA main_sprite,abt_lot,abt_iwt,0
14740 :
14750 END DEFine about_make
14760 :
14770 DEFine PROCedure about
14780   LOCal loop%,item%,dummy%,chan%
14790   DR_PULD abt_defn,-1,-1,abt_lfl%
14800   chan%=FOPEN('con')
14810   DR_IWDF#chan%,abt_defn,2
14820   INK#chan%,menu_ink
14830   PAPER#chan%,menu_pap
14840   AT#chan%,4,2
14850   PRINT#chan%,"Copyright (c) 1999"
14860   AT#chan%,5,2
14870   PRINT#chan%,"W. Lenerz"
14880   CLOSE#chan%
14890   REPeat loop%
14900     RD_PTR abt_defn,item%,dummy%,dummy%,dummy%,dummy%,abt_lfl%
14910     SELect ON item%
14920             =0     : EXIT loop%
14930     END SELect
14940   END REPeat loop%
14950   DR_UNST abt_defn
14960 END DEFine about
14970 :
14980 DEFine FuNction RD_SPRT(drap%)
14990 REMark drap% determine si masque 0
15000   LOCal tmp_patt$(32,32)
15010   LOCal xs,ys,xo,yo, md, l
15020   READ ys,xs,xo,yo,md
15030   DIM tmp_patt$(ys,xs)
15040   FOR l=0 TO ys-1:READ tmp_patt$(l)
15050   l=ALCHP(SPRSP(xs,ys))
15060   SPSET l,xo,yo,md,tmp_patt$(0 TO ys-1,1 TO xs)
15070   IF drap%
15080     POKE_L l+16,espace_vide-(l+16)
15090   END IF
15100   RETurn l
15110 END DEFine RD_SPRT
15120 :
15130 DEFine FuNction RD_WDEF
15140 REMark read window defn
15150   LOCal ldef%(3), lspr, lloose, linfo, lappl, lattr%(3)
15160   RD_WATT lattr%
15170   READ ldef%(0), ldef%(1), ldef%(2), ldef%(3)
15180   READ lspr, lloose, linf, lappl
15190   RETurn MK_WDEF (ldef%, lattr%, lspr, lloose, linf, lappl)
15200 END DEFine RD_WDEF
15210 :
15220 DEFine FuNction RD_LOT (lattr,nitem)
15230   LOCal count(3)
15240   LOCal item, ltyp, a$, lsk$
15250   LOCal ldef%(nitem,6), lptr(3,nitem), lstr$(nitem,85)
15260   lsk$=''
15270   FOR item = 0 TO nitem
15280     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
15290     READ ldef%(item,4), ldef%(item,5)
15300     READ a$: lsk$=lsk$ & a$
15310     READ ltyp
15320     ldef%(item,6)=ltyp: ltyp=(ltyp MOD 256)/2
15330     IF ltyp>10 OR ltyp <0::ltyp=0
15340     IF ltyp
15350       READ lptr(ltyp,count(ltyp))
15360     ELSE
15370       READ lstr$(count(0))
15380     END IF
15390     count(ltyp)=count(ltyp)+1
15400   END FOR item
15410   RETurn MK_LIL (lattr, ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), ldef%(TO, 4 TO 5), lsk$, ldef%(TO, 6), lstr$, lptr(1), lptr(2), lptr(3))
15420 END DEFine RD_LOT
15430 :
15440 DEFine FuNction RD_IWT(nitem)
15450   LOCal item,mc1
15460   LOCal ldef%(nitem,3), latt%(nitem,3), lptr(nitem)
15470   FOR item = 0 TO nitem
15480     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
15490     READ latt%(item,0), latt%(item,1), latt%(item,2), latt%(item,3)
15500     READ lptr(item)
15510   END FOR item
15520   RETurn MK_IWL (ldef%, latt%, lptr)
15530 END DEFine RD_IWT
15540 :
15550 DEFine FuNction RD_IOT(nitem)
15560   LOCal count(3)
15570   LOCal item, ltyp, work1, work2
15580   LOCal ldef%(nitem,4), lptr(3,nitem), lstr$(nitem,85)
15590   FOR item = 0 TO nitem
15600     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
15610     READ ltyp
15620     ldef%(item,4)=ltyp:  ltyp=(ltyp MOD 256)/2
15630     IF ltyp >10:ltyp=0
15640     IF ltyp
15650       READ lptr(0,item),lptr(ltyp,count(ltyp))
15660     ELSE
15670       IF mycolour_mode%=2
15680           READ work1,work2,work3
15690           lptr(0,item)=WL_4_IOL(work1,work2,work3)
15700       ELSE
15710           READ work1
15720           READ work2: work1=work1*256+work2
15730           READ work2: lptr(0,item)=work1*256+work2
15740       END IF
15750       READ lstr$(count(0))
15760     END IF
15770     count(ltyp) = count(ltyp) + 1
15780   END FOR item
15790   RETurn MK_IOL (ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), lptr(0), ldef%(TO, 4), lstr$, lptr(1), lptr(2), lptr(3))
15800 END DEFine RD_IOT
15810 :
15820 DEFine PROCedure RD_IATT (lattr)
15830   LOCal i
15840   READ lattr(0,0), lattr(0,1)
15850   FOR i=1 TO 3: READ lattr(i,0), lattr(i,1), lattr(i,2), lattr(i,3)
15860 END DEFine RD_IATT
15870 :
15880 DEFine PROCedure RD_WATT (lattr%)
15890   READ lattr%(0), lattr%(1),lattr%(2),lattr%(3)
15900 END DEFine RD_WATT
15910 :
