100 REMark $$asmb=dev1_booty_outptr_bin,0,10
110 REMark $$asmb=dev1_booty_qptr_bin,50,172
120 REMark $$asmb=dev1_basic_make_bin,0,10
130 REMark $$stak=20000
140 compiled=NOT IS_OPEN(#0)
150 IF NOT compiled
160   SCR_SIZE#0, xsz%,ysz%
170   OUTLN#0,xsz%,ysz%-28,0,28
180   WSET -1
190 END IF
200 init
210 main
220 STOP
230 DEFine PROCedure make_new_dir (poste%)
240 LOCal quel_rep$
250   IF MENU_OK
260     quel_rep$= DIR_SELECT$("The Files are where?",,10,10,main_coul%,app_coul%)
270   ELSE
280     quel_rep$=getstr$(poste%,0,main_defn,a_lire$)
290   END IF
300   IF LEN (quel_rep$) >= 5
310      rep$=quel_rep$(1 TO 5)
320   END IF
330   CH_ITEM main_defn,-1,poste%,-1,"",rep$
340   main_lfl%(poste%)=main_lfl%(poste%)+1
350 END DEFine make_new_dir
360 :
370 :
380 DEFine PROCedure main
390 LOCal lp%,item%,event%,swnum%,xrel%,yrel%,item%,lp2%,tot%
400   IF compiled
410      OPEN#1,'con'
420      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
430      SCR_SIZE#1,xsz%,ysz%
440   ELSE
450      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
460      SCR_SIZE#0,xsz%,ysz%
470   END IF
480   POS_PTR main_defn,40*65536+20,1
490   REPeat lp%
500     RD_PTR main_defn,item%,swnum%,event%,xrel%,yrel%,main_lfl%,main_mfl%,main_cty%
510     IF swnum%= -1
520       SELect ON item%
530               =esc%     : EXIT lp%
540               =move_it% : CH_WIN main_defn
550               =size_it% : change_size xrel%,yrel%
560               =sleep_it%: button main_defn,main_lfl%,main_mfl%,main_cty%,"SMSQ/E Make"
570               =all_it%
580                  set_statusses
590                  main_cty%(0,1)=1
600               =go_it%   : do_the_work
610               =dir_it%: make_new_dir item%
620               =target_its% TO target_its%+4
630                           start%=1:machines%=0
640                           FOR lp2%=target_its% TO target_its%+4
650                               IF main_lfl%(lp2%):machines%=machines%+start%
660                               start%=start%*2
670                           END FOR lp2%
680                           make_my_data machines%
690                           mk_main_app
700                           AP_IN main_defn,main_app_list
710                           IF main_app_list:DR_ADRW main_defn,0,main_mfl%,main_cty%
720                           DR_WDRW main_defn
730       END SELect
740     END IF
750   END REPeat lp%
760   DR_UNST main_defn
770   IF compiled:CLOSE#1
780   RECHP espace_vide
790 END DEFine main
800 :
810 DEFine PROCedure button (def_trav,menu_drap%,app_drap%,control%,chaine$)
820   DR_UNST def_trav
830   IF compiled: CLOSE#1
840   BUTTON_WAIT chaine$
850   IF compiled
860     OPEN#1,"CON_"
870     DR_PPOS def_trav,-1,-1,menu_drap%,app_drap%,control%
880   ELSE
890     DR_PULD def_trav,-1,-1,menu_drap%,app_drap%,control%
900   END IF
910 END DEFine button
920 :
930 DEFine PROCedure mk_main_app
940 LOCal t2%,temp%,sections%,att_phys%(3),lp%
950   DIM main_cty%(0):DIM main_mfl%(0,0)
960   sections%=2
970   temp%=sections% :t2%=rangees
980   att_phys%(0)=xsize%-16      :REMark taille x se calcule sur la fen. prim.
990   att_phys%(1)=t2%            :REMark taille y = nombre de rangÉes
1000   att_phys%(2)=4              :REMark origine x
1010   att_phys%(3)=inf1y +appwinoffset%        :REMark origine y
1020   app_addr=fen_app2(app_addr,mdata$,temp%,1,att_phys%,"DO ")
1030   IF app_addr
1040       DIM main_cty%(sections%,2)           : REMark dÉfinition de controle
1050       main_cty%(0,0)=1                               : REMark on montre 1 section au dÉbut
1060       main_cty%(1,2)=t2%
1070       IF sections%=2:main_cty%(2,2)=t2%
1080       IF temp%
1090            DIM main_mfl%(temp%-1,0)       : REMark le tableau de drapeaux
1100            set_statusses
1110       ELSE
1120            POKE_W app_addr,PEEK_W(app_addr)+8
1130       END IF
1140       scroll_arrows t2%,temp%,app_addr,main_cty%
1150   END IF
1160 END DEFine mk_main_app
1170 :
1180 DEFine PROCedure set_statusses
1190 LOCal temp%,lp%,stat%
1200   temp%=DIMN(main_mfl%,1)
1210   stat%=main_lfl%(all_it%)
1220   FOR lp%=0 TO temp%
1230     IF mdata$(lp%+1,1)='*'
1240       main_mfl%(lp%,0)=16
1250     ELSE
1260       main_mfl%(lp%,0)=stat%
1270     END IF
1280   END FOR lp%
1290 END DEFine set_statusses
1300 :
1310 DEFine PROCedure make_my_data (what_machine%)
1320 REMark what_machine% = 1 -> generic
1330 REMark what_machine% = 2 -> atari
1340 REMark what_machine% = 4 -> Gold Card
1350 REMark what_machine% = 8 -> Qx0
1360 REMark what_machine% = 16 -> QXL
1370   DIM mdata$(100,50)
1380   REMark first make generic data
1390   data_count%=1                              : REMark general data_count%
1400   IF what_machine%&&1
1410     generic_data                        : REMark set up generic data
1420     read_my_datas                       : REMark and read it
1430   END IF
1440   IF what_machine%&&2
1450      atari_data                         : REMark set up machine specific data....
1460      read_my_datas                      : REMark and read it
1470    END IF
1480   IF what_machine%&&4
1490      gc_data                                      : REMark set up machine specific data....
1500      read_my_datas                      : REMark and read it
1510    END IF
1520   IF what_machine%&&8
1530      q40_data                                     : REMark set up machine specific data....
1540      read_my_datas                      : REMark and read it
1550    END IF
1560   IF what_machine%&&16
1570      qxl_data                                     : REMark set up machine specific data....
1580      read_my_datas                      : REMark and read it
1590    END IF
1600 END DEFine make_my_data
1610 :
1620 DEFine PROCedure read_my_datas
1630 REMark reads datas into the global var mdata$
1640 REMark uses data_count% from make_my_data
1650 LOCal lp%
1660   REPeat lp%
1670     READ mdata$(data_count%)
1680     IF mdata$(data_count%)="":EXIT lp%
1690     data_count%=data_count%+1
1700   END REPeat lp%
1710 END DEFine read_my_datas
1720 :
1730 DEFine PROCedure generic_data
1740 RESTORE 1740
1750   DATA '**** -- SMSQ Generic -- ****'
1760   DATA 'smsq_smsq_loader_link'
1770   DATA 'smsq_smsq_fh_link'
1780   DATA 'smsq_smsq_link'
1790   DATA 'smsq_smsq_cache_link'
1800   DATA 'smsq_sbas_link'
1810   DATA 'smsq_smsq_lang_link'
1820   DATA 'smsq_sbas_lang_link'
1830   DATA 'smsq_sbas_procs_link'
1840   DATA 'smsq_smsq_wman_link'
1850   DATA 'smsq_smsq_hotkey_link'
1860   DATA 'smsq_smsq_vers_link'
1870   DATA 'smsq_smsq_1mb_link',''
1880 END DEFine generic_data
1890 DEFine PROCedure q40_data
1900 RESTORE 1900
1910   DATA '**** -- Q40/Q60 -- ****'
1920   DATA 'smsq_q40_hwinit_link'
1930   DATA 'smsq_q40_nasty_link'
1940   DATA 'smsq_q40_cache_link'
1950   DATA 'smsq_q40_cachemode_link'
1960   DATA 'smsq_smsq_cache40c_link'
1970   DATA 'smsq_q40_driver_ser_link'
1980   DATA 'smsq_q40_driver_dv3_link'
1990   DATA 'smsq_q40_driver_ql_link'
2000   DATA 'smsq_q40_driver_16_link'
2010   DATA 'smsq_q40_kbd_lang_link'
2020   DATA 'smsq_sbas_procs_q60_link'
2030   DATA 'smsq_q40_sysspr_link',''
2040 END DEFine q40_data
2050 :
2060 DEFine PROCedure atari_data
2070 RESTORE 2070
2080   DATA '**** -- Atari -- ****'
2090   DATA 'smsq_atari_hwinit_link'
2100   DATA 'smsq_atari_nasty_link'
2110   DATA 'smsq_atari_driver_ql_link'
2120   DATA 'smsq_atari_driver_mo_link'
2130   DATA 'smsq_atari_driver_ser_link'
2140   DATA 'smsq_atari_driver_dv3_link'
2150   DATA 'smsq_atari_kbd_lang_link'
2160   DATA 'smsq_atari_sysspr_link'
2170   DATA 'sys_boot_st_host_link',''
2180 END DEFine atari_data
2190 :
2200 DEFine PROCedure gc_data
2210 RESTORE 2210
2220   DATA '**** -- (Super)GoldCard -- ****'
2230   DATA 'smsq_gold_host_link'
2240   DATA 'smsq_gold_hwinit_link'
2250   DATA 'smsq_gold_nasty_link'
2260   DATA 'smsq_gold_nasty_s_link'
2270   DATA 'smsq_gold_driver_ql_link'
2280   DATA 'smsq_gold_driver_most_link'
2290   DATA 'smsq_gold_driver_nd_link'
2300   DATA 'smsq_gold_driver_nds_link'
2310   DATA 'smsq_gold_driver_dv3_link'
2320   DATA 'smsq_gold_kbd_lang_link'
2330   DATA 'smsq_gold_kbd_abc_lng_link'
2340   DATA 'smsq_gold_kbd_abc_link'
2350   DATA 'smsq_gold_roms_link'
2360   DATA 'smsq_gold_qimi_link'
2370   DATA 'smsq_gold_sysspr_link',''
2380 END DEFine gc_data
2390 :
2400 DEFine PROCedure qxl_data
2410 RESTORE 2410
2420   DATA '**** -- QXL -- ****'
2430   DATA 'smsq_qxl_host_link'
2440   DATA 'smsq_qxl_hwinit_link'
2450   DATA 'smsq_qxl_nasty_e_link'
2460   DATA 'smsq_qxl_driver_ql_link'
2470   DATA 'smsq_qxl_driver_16_link'
2480   DATA 'smsq_qxl_driver_most_link'
2490   DATA 'smsq_qxl_driver_nd_link'
2500   DATA 'smsq_qxl_procs_link'
2510   DATA 'smsq_qxl_driver_dv3e_link'
2520   DATA 'smsq_qxl_kbd_lang_link'
2530   DATA 'smsq_qxl_ecache_link'
2540   DATA 'smsq_qxl_sysspr_link',''
2550 END DEFine qxl_data
2560 :
2570 DEFine PROCedure do_the_work
2580 REMark this tries to compile all of the selected files
2590 REMark if the file was compiled OK, setus set to available
2600 REMark if the file was not compiled ok, status left selected
2610 LOCal lp%,bound%,a$,my_error%,tot_err%
2620   bound%=DIMN(main_mfl%,1)              : REMark number of items to check
2630   tot_err%=0
2640   FOR lp%=0 TO bound%
2650     a$=mdata$(lp%+1)                              : REMark potential file to compile
2660     IF a$(1)<>"*" AND main_mfl%(lp%,0)
2670        a$=rep$&a$                       : REMark add dir
2680        my_error%=compile_and_check% (a$): REMark try to compile
2690        IF NOT my_error%
2700            main_mfl%(lp%,0)=1
2710            DR_ADRW main_defn,0,main_mfl%,main_cty%
2720        END IF
2730        tot_err%=tot_err%+my_error%
2740     END IF
2750   END FOR lp%
2760   main_cty%(0,1)=1
2770   main_lfl%(go_it%)=1
2780   IF main_lfl%(make_it%)=128 AND NOT tot_err%:make_targets
2790 END DEFine do_the_work
2800 :
2810 DEFine FuNction compile_and_check% (a$)
2820 REMark this tries to compile a file, using the "make" prog
2830 REMark this returns 1 if errors, else 0
2840 LOCal chan%,is_ok$,ch_len
2850   EXEP_W "make";a$&" -w -l"             : REMark exec make & wait
2860   is_ok$=a$(1 TO LEN(a$)-4)             : REMark file name w/o extension
2870   chan%=FOP_IN(is_ok$&'log')            : REMark open log file
2880   IF chan%<0:RETurn 1                             : REMark can't even open it!
2890   ch_len=FLEN(#chan%)                             : REMark file length
2900   GET#chan%\ch_len-10                             : REMark this is where error report is
2910   INPUT#chan%,is_ok$                              : REMark get it
2920   CLOSE#chan%
2930   IF NOT is_ok$=="No errors"
2940     RETurn 1                                      : REMark anything other than "no errors" signals errors
2950   ELSE
2960     RETurn 0
2970   END IF
2980 END DEFine compile_and_check%
2990 :
3000 DEFine PROCedure make_targets
3010 REMark targets are only made if there was no error during compilation
3020 REMark to achieve this, a primitive approach is used:
3030 REMark a check is made whether an appsub menu item status is still selected
3040 REMark if it is, then the
3050   LOCal lp%,current_target%,tot_targets%
3060   tot_targets%=0                        : REMark no targets to make yet
3070   current_target%=0
3080   FOR lp%=0 TO data_count%-2
3090     IF main_mfl%(lp%,0)=16
3100       REMark we're covering a new target
3110       tot_targets%=tot_targets%+current_target% : REMark show old target
3120       current_target%=find_target%(lp%+1) : REMark get new target we're covering
3130     ELSE
3140       IF main_mfl%(lp%,0)<>0:current_target%=0
3150     END IF
3160   END FOR lp%
3170   tot_targets%=tot_targets%+current_target%
3180   REMark at the end, here, tot_targets holds a bitmap
3190   REMark of targets for which compilation was ok
3200   IF (machines%&&1) AND NOT (tot_targets% && 1):RETurn : REMark generic was asked for but not compiled correctly!
3210   IF tot_targets%&&2:atari_target
3220   IF tot_targets%&&4:gc_target
3230   IF tot_targets%&&8:qx0_target
3240   IF tot_targets%&&16:qxl_target
3250 END DEFine make_targets
3260 :
3270 DEFine PROCedure atari_target
3280   cmd$='m'
3290   p$=rep$&'sys_boot_st_flp'
3300   f$=rep$&'sys_boot_file'
3310   ah$=rep$&'sys_boot_st_host'
3320   ld$=rep$&'smsq_smsq_loader'
3330   hi$=rep$&'smsq_atari_hwinit'
3340   os1f$=rep$&'smsq_smsq_fh_os'
3350   os1$=rep$&'smsq_smsq_os'
3360   ca$=rep$&'smsq_smsq_cache'
3370   os2$=rep$&'smsq_sbas_control'
3380   ns1$=rep$&'smsq_atari_nasty'
3390   ln1$=rep$&'smsq_smsq_lang'
3400   ln2$=rep$&'smsq_atari_kbd_lang'
3410   ln3$=rep$&'smsq_sbas_lang'
3420   ex1$=rep$&'smsq_sbas_procs_x'
3430   ex2$=rep$&'smsq_atari_driver_dv3'
3440   ex3$=rep$&'smsq_atari_driver_ser'
3450   ex4$=rep$&'smsq_atari_driver_mono'
3460   ex5$=rep$&'smsq_atari_driver_ql'
3470   ex6$=rep$&'smsq_smsq_wman'
3480   ex7$=rep$&'smsq_smsq_hotkey'
3490   ex8$=rep$&'smsq_atari_sysspr'
3500   nl$=rep$&'sys_boot_null'
3510   IF 'm' INSTR cmd$ = 0: ex4$ = nl$
3520   EW f$, ah$,ld$,hi$,os1f$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4$,ex5$,ex6$,ex7$,ex8$, rep$&'smsq_atari_SMSQ.PRG'
3530 END DEFine atari_target
3540 :
3550 DEFine PROCedure gc_target
3560   f$=rep$&'sys_boot_file'
3570   v$=rep$&'smsq_smsq_vers'
3580   h$=rep$&'smsq_gold_host'
3590   ld$=rep$&'smsq_smsq_loader'
3600   hi$=rep$&'smsq_gold_hwinit'
3610   os1$=rep$&'smsq_smsq_os'
3620   ca$=rep$&'smsq_smsq_cache'
3630   os2$=rep$&'smsq_sbas_control'
3640   ns1$=rep$&'smsq_gold_nasty'
3650   ns2$=rep$&'smsq_gold_nasty_s'
3660   ln1$=rep$&'smsq_smsq_lang'
3670   ln2$=rep$&'smsq_gold_kbd_lang'
3680   ln2a$=rep$&'smsq_gold_kbd_abc_lang'
3690   ln3$=rep$&'smsq_sbas_lang'
3700   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
3710   ex2a$=rep$&'smsq_gold_kbd_abc'
3720   ex2$=rep$&'smsq_gold_driver_most'
3730   ex3a$=rep$&'smsq_gold_driver_ql'
3740   ex3q$=rep$&'smsq_gold_qimi'
3750   ex3w$=rep$&'smsq_smsq_wman'
3760   ex4$=rep$&'smsq_gold_driver_dv3'
3770   ex5$=rep$&'smsq_gold_driver_nd'
3780   ex6$=rep$&'smsq_gold_driver_nds'
3790   ex7$=rep$&'smsq_smsq_hotkey'
3800   ex8$=rep$&'smsq_gold_sysspr'
3810   nl$=rep$&'sys_boot_null'
3820   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex3a$,ex3q$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,ex1$, rep$&'SMSQ_GOLD_gold'
3830 END DEFine gc_target
3840 :
3850 DEFine PROCedure qx0_target
3860   nl$=rep$&'sys_boot_null'
3870   f$=rep$&'sys_boot_file'
3880   qr$=rep$&'sys_boot_q40_rom'
3890   ld$=rep$&'smsq_smsq_loader'
3900   hi$=rep$&'smsq_q40_hwinit'
3910   os1$=rep$&'smsq_smsq_1mb_os' : REMark change for fast memory
3920   ca$=rep$&'smsq_smsq_cache40c'
3930   caq$=rep$&'smsq_q40_cache'
3940   caq4$=rep$&'smsq_q40_cachemode'
3950   os2$=rep$&'smsq_sbas_control'
3960   ns2$=rep$&'smsq_q40_nasty'
3970   ln1$=rep$&'smsq_smsq_lang'
3980   ln2$=rep$&'smsq_q40_kbd_lang'
3990   ln3$=rep$&'smsq_sbas_lang'
4000   ex1$=rep$&'smsq_sbas_procs_x_q60'
4010   ex2$=rep$&'smsq_q40_driver_dv3'
4020   ex3$=rep$&'smsq_q40_driver_ser'
4030   ex4a$=rep$&'smsq_q40_driver_ql'
4040   ex4b$=rep$&'smsq_q40_driver_16'
4050   ex4w$=rep$&'smsq_smsq_wman'
4060   ex5$=rep$&'smsq_q40_sysspr'
4070   ex9$=rep$&'smsq_smsq_hotkey'
4080   EW f$, qr$,ld$,hi$,os1$,ca$,caq$,os2$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4a$,ex4b$,ex4w$,ex5$,ex9$,caq4$,ns2$, rep$&'smsq_q40_rom'; p$
4090 END DEFine qx0_target
4100 :
4110 DEFine PROCedure qxl_target
4120   IF FOP_IN (rep$&'smsq_qxl_qxlh.exe') <> -7: INPUT 'QXLH.EXE exists'; cmd$: QUIT
4130   p$   = rep$&'sys_boot_file'
4140   bl$  = rep$&'smsq_qxl_qxl2_exe'
4150   hst$ = rep$&'smsq_qxl_host'
4160   hi$  = rep$&'smsq_qxl_hwinit'
4170   ld$  = rep$&'smsq_smsq_loader'
4180   os1$ = rep$&'smsq_smsq_os'
4190   ca$  = rep$&'smsq_smsq_cache'
4200   os2$ = rep$&'smsq_sbas_control'
4210   ns1$ = rep$&'smsq_qxl_nasty_e'
4220   ln1$ = rep$&'smsq_smsq_lang'
4230   ln2$ = rep$&'smsq_qxl_kbd_lang'
4240   ln3$ = rep$&'smsq_sbas_lang'
4250   ex1$ = rep$&'smsq_qxl_procs_x'
4260   ex2$ = rep$&'smsq_qxl_driver_most'  : REMark Keyboard before CON
4270   ex3$ = rep$&'smsq_qxl_driver_ql'
4280   ex3b$ = rep$&'smsq_qxl_driver_16'
4290   ex3w$ = rep$&'smsq_smsq_wman'
4300   ex4$ = rep$&'smsq_qxl_driver_nd'
4310   ex5$ = rep$&'smsq_qxl_driver_dv3e'
4320   ex6$= rep$&'smsq_smsq_hotkey'
4330   ex7$ = rep$&'smsq_qxl_sysspr'
4340   last$ = rep$&'smsq_qxl_ecache'
4350   dml$ = rep$&'smsq_qxl_dummy'
4360   qm$  = rep$&'qmon_qxl_bin'
4370   r$='ram1_smsqe.exe' : f$=rep$&'smsq_qxl_smsqe.exe'
4380   DELETE f$
4390   EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3w$,ex4$,ex5$,ex6$,ex7$,last$, r$
4400   EW cct, bl$,r$, f$
4410   DELETE r$
4420   OPEN #4,f$
4430   lenf=FLEN(#4)
4440   lens% = INT ((lenf+511)/512)
4450   lenb% = lenf - lens% * 512
4460   IF lenb%: lenb% = lenb% + 512
4470   BPUT #4\2, lenb% MOD 256, lenb% DIV 256, lens% MOD 256, lens% DIV 256
4480   CLOSE #4
4490 END DEFine qxl_target
4500 :
4510 DEFine FuNction find_target% (current%)
4520   IF "SMSQ Generic" INSTR mdata$(current%):RETurn 1
4530   IF "Atari" INSTR mdata$(current%):RETurn 2
4540   IF "GoldCard" INSTR mdata$(current%):RETurn 4
4550   IF "Q40" INSTR mdata$(current%):RETurn 8
4560   IF "QXL" INSTR mdata$(current%):RETurn 16
4570   RETurn 0                                        : REMark huh???
4580 END DEFine find_target%
4590 :
4600 DEFine PROCedure change_size   (x%,y%)
4610 LOCal xrel%,yrel%,temp,a$
4620   CH_WIN main_defn,xrel%,yrel%                    : REMark ptr displacement
4630   POS_PTR main_defn,2*65536+1,1
4640   xsize%=xsize%-xrel%
4650   ysize%=ysize%-yrel%                             : REMark new sizes
4660   IF xsize%>xsz%-16:xsize%=xsz%-16
4670   IF ysize%>ysz%-16:ysize%=ysize%-16    : REMark max values
4680   DR_UNST main_defn                               : REMark no more wdw defn...
4690   DR_REMV main_defn                               : REMark .... now!
4700   app_addr=0
4710   make_main_wdw
4720   IF compiled
4730      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
4740   ELSE
4750      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
4760   END IF
4770   POS_PTR main_defn,x%*65536+y%,1
4780 END DEFine change_size
4790 :
4800 DEFine FuNction warning_pos% (mess$,channel%,title$,pos)
4810 REMark warning wdw with positioning
4820   POS_PTR main_defn,pos,-1
4830   RETurn warning%(mess$,channel%,title$)
4840 END DEFine warning_pos%
4850 :
4860 DEFine FuNction warning%(mess$,channel%,title$)
4870 REMark shows a warning message (mess$) in a small window. Hit ESC or OK
4880 REMark to return; returns 0 if ESC, 1 if OK  hit
4890 LOCal item%,lp%,chan%,y%,swnum%
4900   IF channel%
4910     chan%=channel%
4920   ELSE
4930     chan%=FOPEN("con")
4940   END IF
4950   CH_ITEM warn_defn,-3,0,-1,'','Attention'
4960   CH_ITEM warn_defn,-1,0,-1,cancel$,'ESC'
4970   CH_ITEM warn_defn,-1,1,-1,'O','OK'
4980   warn_lfl%(0)=1:warn_lfl%(1)=1
4990   POKE_W warn_defn+46,pink                        : REMark paper colour of wdw
5000   POKE_W warn_defn+44,red                         : REMark border around wdw
5010   DR_PULD warn_defn,-1,-1
5020   DR_IWDF #chan%,warn_defn,0
5030   PAPER#chan%,white :INK#chan%,red
5040   IF title$<>"":PRINT#chan%,title$;" :"
5050   PRINT #chan%,mess$
5060   POS_PTR warn_defn,160*65536+10,-1
5070   REPeat lp%
5080     RD_PTR warn_defn,item%,swnum%,y%,y%,y%,warn_lfl%
5090     IF swnum%=-1:EXIT lp%
5100   END REPeat lp%
5110   IF NOT channel%:CLOSE#chan%
5120   DR_UNST warn_defn
5130   RETurn item%
5140 END DEFine warning%
5150 :
5160 DEFine PROCedure info_pos (mess$,channel%,pos)
5170 REMark warning wdw with positioning
5180   POS_PTR main_defn,pos,-1
5190   info mess$,channel%
5200 END DEFine info_pos
5210 :
5220 DEFine PROCedure info(mess$,channel%)
5230 REMark shows window containing mess$
5240   LOCal chan%
5250   IF channel%
5260     chan%=channel%
5270   ELSE
5280     chan%=FOPEN("con")
5290   END IF
5300   CH_ITEM warn_defn,-3,0,-1,"",'  Info.  '
5310   CH_ITEM warn_defn,-1,0,-1,"",'Info'
5320   CH_ITEM warn_defn,-1,1,-1,"",'Info'
5330   POKE_W warn_defn+44,green                       : REMark border
5340   POKE_W warn_defn+46,light_green                  : REMark paper col
5350   DR_PULD warn_defn,-1,-1
5360   POS_PTR warn_defn,90*65536+70,-1
5370   DR_IWDF #chan%,warn_defn,0
5380   PAPER#chan%,white:INK#chan%,black
5390   PRINT #chan%,mess$
5400   IF NOT channel%:CLOSE#chan%
5410 END DEFine info
5420 :
5430 DEFine PROCedure button (def_trav,menu_drap%,app_drap%,control%,chaine$,type_col%)
5440 REMark les parametres sont, dans l'ordre:
5450 REMark la dÉfinition de la fenëtre, suivie des tableaux pour
5460 REMark les postes du menu dÉliÉ, les postes de la sous-fenëtre d'appl.
5470 REMark et la dÉfinition de contròle, suivi du nom du bouton et
5480 REMark du type couleurs.
5490 REMark Il est supposÉ que vous avez ouvert une fenëtre #1 si
5500 REMark le programme est compilÉ
5510   DR_UNST def_trav
5520   IF compiled: CLOSE#1
5530   BTN chaine$,type_col%
5540   IF compiled
5550     OPEN#1,"CON_"
5560     DR_PPOS def_trav,-1,-1,menu_drap%,app_drap%,control%
5570   ELSE
5580     DR_PULD def_trav,-1,-1,menu_drap%,app_drap%,control%
5590   END IF
5600 END DEFine button
5610 :
5620 DEFine PROCedure make_colours (colourway%)
5630 REMark this initialises the GLOBAL variables menu_pap, menu_brd, menu_inv,
5640 REMark menu_ink and menu_bar according to the colourway passed as parameter.
5650 REMark The GLOBAl variable mycolour_mode% must have been set prior to calling
5660 REMark this function, preferrably via the function set_colours%.
5670 REMark Moreover the colours black, white,red, green,wg_stripes, etc...
5680 REMark must have been set prior to calling this procedure:
5690 REMark Preferrably, they were also set via the function set_colours%
5700 :
5710 REMark this function needs the extension WL_MK16 (outptr_bin)
5720 :
5730 REMark the colourways% are:
5740 REMark 0= white/green
5750 REMark 1= black/red
5760 REMark 2= white/red
5770 REMark 3= black/green
5780 REMark 4= use system palette (only available in modes 32 and 33)
5790 :
5800 REMark the Global war mycolour_mode% is:
5810 REMark 0 = we are in QL mode 4
5820 REMark 1 = we are in PAL mode (256 colours)
5830 REMark 2 - we are in 24 bit colour mode
5840 REMark preferrably, this is set via the function set_colours%
5850 :
5860   IF colourway%>7 OR colourway%<0: colourway%=0: REMark must be between 0 & 7
5870 :
5880 REMark first make sure that what we request is possible.
5890 REMark colourway 4 is only possible if we are in 24 bit colour mode
5900 REMark if it is requested in another mode, it is set to 0
5910 :
5920   IF colourway%>3
5930     IF mycolour_mode%< 2
5940        colourway%=0
5950      ELSE
5960        make_my_palette colourway%-4:RETurn   : REMark set colours according to system palette
5970     END IF
5980   END IF
5990 :
6000   SELect ON mycolour_mode%
6010     =0                                  : REMark QL 4 colours
6020       SELect ON colourway%
6030           =0:                           : REMark white/green
6040                 menu_pap=white
6050                 menu_brd=green
6060                 menu_ink=black
6070                 menu_bar=wg_striped
6080                 menu_inv=red
6090           =1:                           : REMark black/red
6100                 menu_pap=black
6110                 menu_brd=red
6120                 menu_ink=white
6130                 menu_bar=br_striped
6140                 menu_inv=green
6150           =2:                           : REMark white/red
6160                 menu_pap=white
6170                 menu_brd=red
6180                 menu_ink=black
6190                 menu_bar=wr_striped
6200                 menu_inv=green
6210           =3:                           : REMark black/green
6220                 menu_pap=black
6230                 menu_brd=green
6240                 menu_ink=white
6250                 menu_bar=bg_striped
6260                 menu_inv=red
6270       END SELect
6280     =1                                  : REMark pal colours
6290       SELect ON colourway%
6300           =0:                           : REMark white/green
6310                 menu_pap=white+256
6320                 menu_brd=green+256
6330                 menu_ink=black+256
6340                 menu_bar=wg_striped+256
6350                 menu_inv=red +256
6360           =1:                           : REMark black/red
6370                 menu_pap=black+256
6380                 menu_brd=red  +256
6390                 menu_ink=white +256
6400                 menu_bar=br_striped +256
6410                 menu_inv=green +256
6420                 omenu_pap=black+256
6430           =2:                           : REMark white/red
6440                 menu_pap=white +256
6450                 menu_brd=red   +256
6460                 menu_ink=black +256
6470                 menu_bar=wr_striped +256
6480                 menu_inv=green +256
6490           =3:                           : REMark black/green
6500                 menu_pap=black +256
6510                 menu_brd=green +256
6520                 menu_ink=white +256
6530                 menu_bar=bg_striped +256
6540                 menu_inv=red   +256
6550       END SELect
6560     =2                                  : REMark 24 bit colours
6570       SELect ON colourway%
6580           =0:                           : REMark white/green
6590                 menu_pap=WL_MK16(white)
6600                 menu_brd=WL_MK16(green)
6610                 menu_ink=WL_MK16(black)
6620                 menu_bar=WL_MK16(wg_striped)
6630                 menu_inv=WL_MK16(red)
6640           =1:                           : REMark black/red
6650                 menu_pap=WL_MK16(black)
6660                 menu_brd=WL_MK16(red)
6670                 menu_ink=WL_MK16(white)
6680                 menu_bar=WL_MK16(br_striped)
6690                 menu_inv=WL_MK16(green)
6700           =2:                           : REMark white/red
6710                 menu_pap=WL_MK16(white)
6720                 menu_brd=WL_MK16(red)
6730                 menu_ink=WL_MK16(black)
6740                 menu_bar=WL_MK16(wr_striped)
6750                 menu_inv=WL_MK16(green)
6760           =3:                           : REMark black/green
6770                 menu_pap=WL_MK16(black)
6780                 menu_brd=WL_MK16(green)
6790                 menu_ink=WL_MK16(white)
6800                 menu_bar=WL_MK16(bg_striped)
6810                 menu_inv=WL_MK16(red)
6820       END SELect
6830   END SELect
6840 :
6850   make_my_old_palette
6860 END DEFine make_colours
6870 :
6880 DEFine PROCedure make_my_palette (cw%)
6890   sp.winbd            = HEX('0200') : REMark Window border
6900   sp.winbg            = HEX('0201') : REMark Window background
6910   sp.winfg            = HEX('0202') : REMark Window foreground
6920   sp.winmg            = HEX('0203') : REMark Window middleground
6930   sp.titlebg          = HEX('0204') : REMark Title background
6940   sp.titletextbg  = HEX('0205') : REMark Title text background
6950   sp.titlefg          = HEX('0206') : REMark Title foreground
6960   sp.litemhigh        = HEX('0207') : REMark Loose item highlight
6970   sp.litemavabg   = HEX('0208') : REMark Loose item available background
6980   sp.litemavafg   = HEX('0209') : REMark Loose item available foreground
6990   sp.litemselbg   = HEX('020a') : REMark Loose item selected background
7000   sp.litemselfg   = HEX('020b') : REMark Loose item selected foreground
7010   sp.litemunabg   = HEX('020c') : REMark Loose item unavailable background
7020   sp.litemunafg   = HEX('020d') : REMark Loose item unavailable foreground
7030   sp.infwinbd         = HEX('020e') : REMark Information window border
7040   sp.infwinbg         = HEX('020f') : REMark Information window background
7050   sp.infwinfg         = HEX('0210') : REMark Information window foreground
7060   sp.infwinmg         = HEX('0211') : REMark Information window middleground
7070   sp.subinfbd         = HEX('0212') : REMark Subsidiary information window border
7080   sp.subinfbg         = HEX('0213') : REMark Subsidiary information window background
7090   sp.subinffg         = HEX('0214') : REMark Subsidiary information window foreground
7100   sp.subinfmg         = HEX('0215') : REMark Subsidiary information window middleground
7110   sp.appbd            = HEX('0216') : REMark Application window border
7120   sp.appbg            = HEX('0217') : REMark Application window background
7130   sp.appfg            = HEX('0218') : REMark Application window foreground
7140   sp.appmg            = HEX('0219') : REMark Application window middleground
7150   sp.appihigh         = HEX('021a') : REMark Application window item highlight
7160   sp.appiavabg        = HEX('021b') : REMark Application window item available background
7170   sp.appiavafg        = HEX('021c') : REMark Application window item available foreground
7180   sp.appiselbg        = HEX('021d') : REMark Application window item selected background
7190   sp.appiselfg        = HEX('021e') : REMark Application window item selected foreground
7200   sp.appiunabg        = HEX('021f') : REMark Application window item unavailable background
7210   sp.appiunafg        = HEX('0220') : REMark Application window item unavailable foreground
7220   sp.scrbar           = HEX('0221') : REMark Pan/scroll bar
7230   sp.scrbarsec        = HEX('0222') : REMark Pan/scroll bar section
7240   sp.scrbararr        = HEX('0223') : REMark Pan/scroll bar arrow
7250   sp.buthigh          = HEX('0224') : REMark Button highlight
7260   sp.butbd            = HEX('0225') : REMark Button border
7270   sp.butbg            = HEX('0226') : REMark Button background
7280   sp.butfg            = HEX('0227') : REMark Button foreground
7290   sp.hintbd           = HEX('0228') : REMark Hint border
7300   sp.hintbg           = HEX('0229') : REMark Hint background
7310   sp.hintfg           = HEX('022a') : REMark Hint foreground
7320   sp.hintmg           = HEX('022b') : REMark Hint middleground
7330   sp.errbg            = HEX('022c') : REMark Error message background
7340   sp.errfg            = HEX('022d') : REMark Error message foreground
7350   sp.errmg            = HEX('022e') : REMark Error message middleground
7360   sp.shaded           = HEX('022f') : REMark sp.shaded area
7370   sp.3ddark           = HEX('0230') : REMark Dark 3D border shade
7380   sp.3dlight          = HEX('0231') : REMark Light 3D border shade
7390   sp.vertfill         = HEX('0232') : REMark Vertical area fill
7400   IF NOT (IS_OPEN(#0) OR IS_OPEN(#1))
7410     OPEN#1,'con'
7420     SP_JOBPAL -1,cw%
7430     CLOSE#1
7440   ELSE
7450     SP_JOBPAL -1,cw%
7460   END IF
7470 END DEFine make_my_palette
7480 :
7490 DEFine PROCedure make_my_old_palette
7500 REMark this translates the colours used in the wdws into 4 colour mode...
7510   sp.winbd          =         menu_brd   : REMark Window border
7520   sp.winbg          =         menu_pap   : REMark Window background
7530   sp.winfg          =         menu_pap   : REMark Window foreground
7540   sp.titletextbg=   menu_pap   : REMark Title text background
7550   sp.titlefg        =         menu_ink   : REMark Title foreground
7560   sp.titlebg        =         menu_bar   : REMark Title background
7570   sp.litemhigh      =         menu_ink   : REMark Loose item highlight
7580   sp.litemavabg =   menu_pap   : REMark Loose item available background
7590   sp.litemavafg =   menu_ink   : REMark Loex fifiose item available foreground
7600   sp.litemselbg =   menu_brd   : REMark Loose item selected background
7610   sp.litemselfg =   menu_ink   : REMark Loose item selected foreground
7620   sp.litemunabg =   menu_pap   : REMark Loose item unavailable background
7630   sp.litemunafg =   menu_brd   : REMark Loose item unavailable foreground
7640   sp.infwinbd       =         menu_brd   : REMark Information window border
7650   sp.infwinbg       =         menu_pap   : REMark Information window background
7660   sp.infwinfg       =         menu_ink   : REMark Information window foreground
7670   sp.appbd          =         menu_brd   : REMark Application window border
7680   sp.appbg          =         menu_pap   : REMark Application window background
7690   sp.appfg          =         menu_ink   : REMark Application window foreground
7700   sp.appmg          =         menu_pap   : REMark Application window middleground
7710   sp.appihigh       =         menu_ink   : REMark Application window item highlight
7720   sp.appiavabg      =         menu_pap   : REMark Application window item available background
7730   sp.appiavafg      =         menu_ink   : REMark Application window item available foreground
7740   sp.appiselbg      =         menu_brd   : REMark Application window item selected background
7750   sp.appiselfg      =         menu_ink   : REMark Application window item selected foreground
7760   sp.appiunabg      =         menu_pap   : REMark Application window item unavailable background
7770   sp.appiunafg      =         menu_brd   : REMark Application window item unavailable foreground
7780   sp.scrbar         =         menu_ink   : REMark Pan/scroll bar
7790   sp.scrbararr      =         menu_brd   : REMark Pan/scroll bar arrow
7800   sp.buthigh        =         menu_ink   : REMark Button highlight
7810   sp.butbd          =         menu_brd   : REMark Button border
7820   sp.butbg          =         menu_pap   : REMark Button background
7830   sp.butfg          =         menu_ink   : REMark Button foreground
7840 END DEFine mk_my_old_palette
7850 :
7860 DEFine FuNction getstr$(poste%,drap%,defn,a$)
7870 REMark ouvre une fenëtre sur un poste de menu dÉliÉ et propose
7880 REMark une chaïne ç Éditer
7890 REMark les paramêtres sont
7900 REMark - le numÉro du poste
7910 REMark - un drapeau si le rÉsultat doit ëtre un chiffre (si drap%<>0)
7920 REMark - la dÉfinition de travail dans laquelle se trouve le poste
7930 REMark - la chaïne qu'on peut Éditer
7940 REMark returns the string or ""
7950 REMark uses the global (!) vars menu_pap% & menu_brd%
7960 REMark
7970   LOCal chan%,b$,res
7980   b$=" ":b$=a$   : REMark c'est curieusement nÉcessaire
7990   chan%=FOPEN("con")
8000   PAPER#chan%,menu_brd
8010   INK#chan%,menu_ink
8020   DR_LWDF#chan%,defn,poste%             : REMark open channel over item
8030   CLS#chan%
8040   res=GET_STR(#chan%,drap%,b$)          : REMark get string
8050   CLOSE#chan%
8060   IF res<0:b$=""                        : REMark ooops
8070   CH_ITEM defn,-1,poste%,-1,"",b$
8080   RETurn b$
8090 END DEFine getstr$
8100 :
8110 DEFine FuNction showcont$(string$,length%)
8120 REMark this adds '...' at the end of a string, to show that it continues
8130 LOCal l%,lengt%
8140   l%=LEN(string$)
8150   IF NOT l%:RETurn ""                   : REMark empty string
8160   lengt%=length% DIV 6
8170   IF l%<=lengt%:RETurn string$          : REMark entire string fits wdw
8180   RETurn string$(1 TO lengt%-3)&"..."
8190 END DEFine showcont$
8200 :
8210 DEFine FuNction get_free_text$(defn,item%,prompt$,length%)
8220 REMark this opens wdw over loose-item item% of the working definition defn
8230 REMark allows free text to be entered
8240 REMark params: working defintion,item over which wdw is to be opened
8250 REMark         first value of string, length of max string in wdw, if this is...
8260 REMark            -1, the length will be determined here
8270 LOCal chan%,string$,tempsizes%(3)
8280   chan%=FOPEN('con')
8290   IF chan%<0:RETurn prompt$
8300   DR_LWDF#chan%,defn,item%              : REMark open wdw over item
8310   PAPER#chan%,menu_brd
8320   INK#chan%,menu_ink
8330   CLS#chan%
8340   string$=WEDIT$(#chan%,prompt$)
8350   IF length%=-1
8360      WSIZE#chan%,tempsizes%
8370      length%=tempsizes%(0)
8380   END IF
8390   CLOSE#chan%
8400   IF length%
8410     CH_ITEM defn,-1,item%,-1,"",showcont$(string$,length%)
8420   ELSE
8430     CH_ITEM defn,-1,item%,-1,"",string$
8440   END IF
8450   RETurn string$
8460 END DEFine get_free_text$
8470 :
8480 DEFine FuNction do_caps(a$,caps%)
8490 REMark set string to capitales etc,caps% determines whether the string....
8500 REMark ...must be lower case (=0), upper case(=1), all words capitalized (=2),
8510 REMark ... first word only capitalized (=3), or nothing(=-1)
8520   SELect ON caps%
8530    =0: MIN a$
8540    =1: MAJ a$
8550    =2: CAP2 a$
8560    =3: CAP a$
8570   END SELect
8580   RETurn a$
8590 END DEFine do_caps
8600 :
8610 DEFine FuNction centr$(ctr$,leng)
8620 REMark this "centres" the string ctr$ (i.e. left padds it with spaces) so that
8630 REMark it is centred in a window of leng width (leng is length in chars)
8640   LOCal l%,a$
8650   a$=ctr$
8660   l%=INT(leng-LEN(a$))/2
8670   IF l%<0:RETurn a$
8680   a$=FILL$(' ',l%)&a$&FILL$(' ',l%)
8690   RETurn a$
8700 END DEFine centr$
8710 :
8720 DEFine FuNction set_colours% (force_mode%)
8730 REMark this sets colour variables according to the colour mode wished, if possible.
8740 REMark force_mode%=
8750 REMark       0 if ql colours wished (mode 4 only)
8760 REMark       1 if pal colours wished
8770 REMark       2 if 24 bit colours wished
8780 REMark This function returns the true QL mode set (4,8,32,33 etc.).
8790 REMark This function also sets the GLOBAL variable mycolour_mode% to the
8800 REMark mode 0,1,2 as above - whatever is possible on the machine, i.e.
8810 REMark if colour_24 is wished on a 4 colour machine (which won't succeed)
8820 REMark this function will set set that variable to 0, i.e. QL mode.
8830 REMark
8840 REMark This function needs the outptr toolkit
8850 LOCal mode%
8860   mode%=RMODE                                     : REMark get current mode now
8870   SELect ON force_mode%
8880       = 0                                         : REMark mode 4 wished
8890        SELect ON mode%
8900             = 4                                   : REMark already on, so do nothing
8910             = 0,1,8 : MODE 4            : REMark old modes, set to 4 colours
8920             = REMAINDER
8930               COLOUR_QL                           : REMark if higher colour mode, set to "mode 4"
8940               MODE 4
8950        END SELect
8960        mode%=0                                    : REMark new mode we're in  - 4 colour mode
8970       = 1                                         : REMark PALette wished
8980        SELect ON mode%
8990             = 32,33 : COLOUR_PAL:mode%=1          : REMark we're in high colour mode, but PAL colours wished
9000             = REMAINDER
9010               IF mode%<>4:MODE 4
9020               mode%=0
9030        END SELect
9040       = 2
9050        SELect ON mode%
9060             = 32,33 : COLOUR_24 :mode%=2          : REMark we're in high col mode 24 bit colours
9070             = REMAINDER
9080               IF mode%<>4:MODE 4
9090               mode%=0
9100        END SELect
9110   END SELect
9120   mycolour_mode%=mode%                            : REMark set this global variable now:
9130   REMark now set some colour variables
9140   SELect ON mode%
9150        = 0                                        : REMark Ql 4 mode colours
9160           black            = 0
9170           white            = 7
9180           red              = 2
9190           green            = 4
9200           grey             = 255
9210           dull_pink      = 19
9220           Blue             = 0
9230           Magenta        = 2
9240           Yellow           = 7
9250           Cyan             = 0
9260           Dark_slate     = 0
9270           Slate_grey     = 255
9280           Dark_grey      = 255
9290           Light_grey     = 255
9300           Ash_grey       = 255
9310           Dark_red       = 210
9320           light_green    = 220
9330           Mustard        = 242
9340           Dark_green     = 228
9350           Sea_blue       = 0
9360           Dark_blue      = 0
9370           Purple           = 0
9380           Shocking_pink  = 234
9390           Orange           = 234
9400           Lime_green     = 4
9410           Apple_green    = 4
9420           Bright_blue    = 0
9430           Mauve            = 0
9440           Peach            = 7
9450           Light_yellow   = 7
9460           Light_blue     = 0
9470           Sky_blue       = 0
9480           Rose_pink      = 234
9490           pink             = 234
9500           Beige            = 7
9510           Pastel_pink    = 234
9520           Pastel_yellow  = 7
9530           Pastel_green   = 4
9540           Pastel_cyan    = 0
9550           Pastel_blue    = 0
9560           Pastel_rose    = 234
9570           Brick            = 210
9580           Light_khaki    = 7
9590           Dull_green     = 228
9600           Dull_cyan      = 0
9610           Steel_blue     = 0
9620           dull_pink      = 234
9630           Brown            = 0
9640           Khaki            = 7
9650           Dusky_green    = 4
9660           Dusky_blue     = 0
9670           Midnight_blue  = 0
9680           Plum             = 0
9690           Dusky_pink     = 234
9700           Buff             = 0
9710           Avocado        = 4
9720           Dull_turquoise = 4
9730           Dull_blue      = 0
9740           Faded_purple   = 234
9750           Cerise           = 2
9760           Tan_normal     = 0
9770           Grass_green    = 4
9780           Sea_green      = 4
9790           Ultramarine    = 0
9800           Deep_purple    = 234
9810           Light_orange   = 7
9820           light_red      = 2
9830           wg_striped     = 92
9840           br_striped     = 82
9850           wr_striped     = 107
9860           bg_striped     = 100
9870        = 1                              : REMark Colour_pal
9880           black            = 0
9890           white            = 1
9900           red              = 2
9910           green            = 3
9920           Blue             = 4
9930           Magenta        = 5
9940           Yellow           = 6
9950           Cyan             = 7
9960           Dark_slate     = 8
9970           Slate_grey     = 9
9980           Dark_grey      = 10
9990           grey             = 11
10000           Light_grey     = 12
10010           Ash_grey       = 13
10020           Dark_red       = 14
10030           light_green    = 15
10040           Mustard        = 16
10050           Dark_green     = 17
10060           Sea_blue       = 18
10070           Dark_blue      = 19
10080           Purple           = 20
10090           Shocking_pink  = 21
10100           Orange           = 22
10110           Lime_green     = 23
10120           Apple_green    = 24
10130           Bright_blue    = 25
10140           Mauve            = 26
10150           Peach            = 27
10160           Light_yellow   = 28
10170           Light_blue     = 29
10180           Sky_blue       = 30
10190           Rose_pink      = 31
10200           pink             = 32
10210           Beige            = 33
10220           Pastel_pink    = 34
10230           Pastel_yellow  = 35
10240           Pastel_green   = 36
10250           Pastel_cyan    = 37
10260           Pastel_blue    = 38
10270           Pastel_rose    = 39
10280           Brick            = 40
10290           Light_khaki    = 41
10300           Dull_green     = 42
10310           Dull_cyan      = 43
10320           Steel_blue     = 44
10330           dull_pink      = 45
10340           Brown            = 46
10350           Khaki            = 47
10360           Dusky_green    = 48
10370           Dusky_blue     = 49
10380           Midnight_blue  = 50
10390           Plum             = 51
10400           Dusky_pink     = 52
10410           Buff             = 53
10420           Avocado        = 54
10430           Dull_turquoise = 55
10440           Dull_blue      = 56
10450           Faded_purple   = 57
10460           Cerise           = 58
10470           Tan_normal     = 59
10480           Grass_green    = 60
10490           Sea_green      = 61
10500           Ultramarine    = 62
10510           Deep_purple    = 63
10520           pinkish        = 77
10530           light_red      = 109
10540           Light_orange   = 208
10550           wg_striped     = light_green
10560           br_striped     = Dark_red
10570           wr_striped     = light_red
10580           bg_striped     = Dark_green
10590        = 2                              : REMark colour_24
10600           black            = HEX('000000')
10610           white            = HEX('FFFFFF')
10620           red              = HEX('FF0000')
10630           green            = HEX('00FF00')
10640           Blue             = HEX('0000FF')
10650           Magenta        = HEX('FF00FF')
10660           Yellow           = HEX('FFFF00')
10670           Cyan             = HEX('00FFFF')
10680           Dark_slate     = HEX('242424')
10690           Slate_grey     = HEX('494949')
10700           Dark_grey      = HEX('6D6D6D')
10710           grey             = HEX('929292')
10720           Light_grey     = HEX('B6B6B6')
10730           Ash_grey       = HEX('DBDBDB')
10740           Dark_red       = HEX('920000')
10750           light_green    = HEX('B6FFB6')
10760           Mustard        = HEX('929200')
10770           Dark_green     = HEX('009200')
10780           Sea_blue       = HEX('009292')
10790           Dark_blue      = HEX('000092')
10800           Purple           = HEX('920092')
10810           Shocking_pink  = HEX('FF0092')
10820           Orange           = HEX('FF9200')
10830           Lime_green     = HEX('92FF00')
10840           Apple_green    = HEX('00FF92')
10850           Bright_blue    = HEX('0092FF')
10860           Mauve            = HEX('9200FF')
10870           Peach            = HEX('FFB6B6')
10880           Light_yellow   = HEX('FFFFB6')
10890           Light_blue     = HEX('B6FFFF')
10900           Sky_blue       = HEX('B6B6FF')
10910           Rose_pink      = HEX('FFB6FF')
10920           pink             = HEX('FFB6DB')
10930           Beige            = HEX('FFDBB6')
10940           Pastel_pink    = HEX('FFDBDB')
10950           Pastel_yellow  = HEX('FFFFDB')
10960           Pastel_green   = HEX('DBFFDB')
10970           Pastel_cyan    = HEX('DBFFFF')
10980           Pastel_blue    = HEX('DBDBFF')
10990           Pastel_rose    = HEX('FFDBFF')
11000           Brick            = HEX('B66D6D')
11010           Light_khaki    = HEX('B6B66D')
11020           Dull_green     = HEX('6DB66D')
11030           Dull_cyan      = HEX('6DB6B6')
11040           Steel_blue     = HEX('6D6DB6')
11050           dull_pink      = HEX('B66DB6')
11060           Brown            = HEX('6D2424')
11070           Khaki            = HEX('6D6D24')
11080           Dusky_green    = HEX('246D24')
11090           Dusky_blue     = HEX('246D6D')
11100           Midnight_blue  = HEX('24246D')
11110           Plum             = HEX('6D246D')
11120           Dusky_pink     = HEX('B64992')
11130           Buff             = HEX('B69249')
11140           Avocado        = HEX('92B649')
11150           Dull_turquoise = HEX('49B692')
11160           Dull_blue      = HEX('4992B6')
11170           Faded_purple   = HEX('9249B6')
11180           Cerise           = HEX('920049')
11190           Tan_normal     = HEX('924900')
11200           Grass_green    = HEX('499200')
11210           Sea_green      = HEX('009249')
11220           Ultramarine    = HEX('004992')
11230           Deep_purple    = HEX('490092')
11240           Light_orange   = HEX('ffb624')
11250           pinkish        = HEX('C0037B')
11260           light_red      = HEX('FC1729')
11270           wg_striped     = light_green
11280           br_striped     = Dark_red
11290           wr_striped     = light_red
11300           bg_striped     = Dark_green
11310   END SELect
11320   mode%=RMODE
11330   RETurn mode%                             : REMark return current screen mode now
11340 END DEFine set_colours%
11350 :
11360 DEFine FuNction fen_app2 (adresse,tableau$,nr_sects%,selk%,phys%)
11370 REMark les paramêtres sont: l'adresse (0 la premiêre fois)
11380 REMark le tableau de chaïne, le nombre de sections,
11390 REMark le drapeau si on veut des touches de sÉlection et si sÉlection=action,
11400 REMark un tableau d'entiers contenant les tailles x,y et les origines
11410 REMark et les touches ç exclure des touches de sÉlection
11420 LOCal app_attributs%(3,1),temp%,bord%,pap%,ori_x%,ori_y%
11430 LOCal tail_x%,tail_y%,fleches%,barrre%
11440 :
11450   IF adresse:RECHP adresse  : REMark si c'est un 2nd appel, on enlêve l'ancienne
11460   tail_x%=phys%(0)            : REMark (largeur)    Ceci est pour la clartÉ
11470   tail_y%=phys%(1)            : REMark (hauteur)    seulement, on pourrait
11480   ori_x%=phys%(2)             : REMark            utiliser directement le
11490   ori_y%=phys%(3)             : REMark origs x,y    tableau phys% dans AP_MAKE
11500 :
11510   adresse=AP_MAKE2(selk%,tail_x%,tail_y%,ori_x%,ori_y%,nr_sects%,tableau$)
11520   IF adresse<0: RETurn 0      : REMark si erreur, on retourne 0
11530                                         : REMark Maintenant on dÉtermine les attributs
11540   bord%=sp.appbd                        : REMark couleur bordure
11550   pap%=sp.appbg                         : REMark couleur "papier"
11560   touche_sel%=9               : REMark touche de sÉlection - TAB
11570   fleches%=sp.scrbararr                 : REMark couleur des flêches
11580   barre%=sp.scrbar                      : REMark couleur de la barre d'ascenseur
11590 :
11600   app_attributs%(0,0)=1
11610   app_attributs%(0,1)=sp.appihigh       : REMark larg. & coul. trait poste courant
11620 :
11630   app_attributs%(1,0)=sp.appiunabg
11640   app_attributs%(1,1)=sp.appiunafg  : REMark unavailable
11650 :
11660   app_attributs%(2,0)=sp.appiavabg
11670   app_attributs%(2,1)=sp.appiavafg  : REMark disponible
11680 :
11690   app_attributs%(3,0)=sp.appiselbg
11700   app_attributs%(3,1)=sp.appiselfg  : REMark sÉlectionnÉ
11710 :
11720   er=AP_PARS(adresse,0,bord%,pap%,touche_sel%,fleches%,barre%,app_attributs%)
11730 :
11740   IF er:RETurn 0
11750 :
11760   RETurn adresse  : REMark on retourne l'adresse
11770 END DEFine fen_app
11780 :
11790 DEFine PROCedure scroll_arrows(rangees%,nbr_objets%,appfen,cty%)
11800 REMark Cette procÉdure dÉcide s'il faut laisser les flêches de dÉfilement
11810 REMark params: le nombre de rangÉes, le nombre total d'objets
11820 REMark le pointer vers la sous-fen d'app et la dÉfinition de controle
11830   IF nbr_objets%<=rangees% +1
11840     POKE_W appfen+150,0
11850     cty%(1,2)=nbr_objets%
11860   ELSE
11870     POKE_W appfen+150,6
11880 REMark    cty%(1,2)=rangees%-1      : rem if scroll arrows are there, there is one line less in the window
11890     cty%(1,2)=rangees%
11900  END IF
11910  IF DIMN(cty%,1)=2:cty%(2,2)=cty%(1,2)
11920  cty%(0,1)=1                  : REMark show that definitionhas changed
11930 END DEFine scroll_arrows
11940 :
11950 DEFine PROCedure init
11960 LOCal temp%
11970   text=0:sprite=2:blob=4:pattern=6
11980   retr=256:retn=-256:sous1=254:sous2=252
11990   REMark normal: hit = selects/deselects, do = returns & leaves selected
12000   REMark retr = return even on hit, reset item to available after,
12010   REMark retn = return even on hit, hit selects/deselects, do selects
12020 :
12030   hit$=CHR$(1):do$=CHR$(2):cancel$=CHR$(3):sleep$=CHR$(7):wake$=CHR$(8)
12040   help$=CHR$(4):move$=CHR$(5):size$=CHR$(6):cr$=CHR$(10)
12050 :
12060   appwinoffset%=16                      : REMark appsub win additional y offset
12070   xsize%=400                                      : REMark x size divisible by 4!
12080   ysize%=200                                      : REMark provisional y size%
12090   lxs=24:lys=12                         : REMark standard item x,y, sizes
12100   inf1y=lys+4                                     : REMark y orig of inner border
12110   xesp=lxs+2                                      : REMark loose items x spacing
12120   nbr_post%=3                                     : REMark nbr of item right & left in title bar
12130   main_coul%=SMSCFG(2)                            : REMark colourways
12140   app_coul%=main_coul%                            : REMark preset colourways
12150   temp%=set_colours% (2)                : REMark always try to set highest colours
12160   IF temp%<2 AND main_coul%>3:main_coul%=0 : REMark old wman
12170   make_colours main_coul%               : REMark set all colours now
12180 :
12190   rep$=SMSCFG(1)                        : REMark dir with files
12200   titre$="Dir: "&rep$&" "
12210 :
12220 RESTORE 12220
12230 :
12240 : REMark make loose item attributes
12250 :
12260   DIM std_iattr(3,3)
12270   RD_IATT std_iattr
12280     DATA 1,sp.litemhigh : REMark current item border size & colour
12290     DATA sp.litemunabg,sp.litemunafg,0,0 : REMark unavailable item paper & ink
12300     DATA sp.litemavabg,sp.litemavafg,0,0 : REMark   available         "    "       "
12310     DATA sp.litemselbg,sp.litemselfg,0,0 : REMark   selected
12320 :
12330 : REMark make fdata for sprites - uses the modified RD_SPRT function
12340 : REMark
12350 : REMark
12360 :
12370   espace_vide=ALCHP(100)                : REMark GLOBAL var for 0 mask sprites!
12380 :
12390   main_sprite=RD_SPRT(0)                : REMark 0 = normal mask
12400     DATA 11,13,6,5,4
12410     DATA '     www     '
12420     DATA '     waw     '
12430     DATA '     waw     '
12440     DATA '     waw     '
12450     DATA 'wwwwwa awwwww'
12460     DATA 'waaaa   aaaaw'
12470     DATA 'wwwwwa awwwww'
12480     DATA '     waw     '
12490     DATA '     waw     '
12500     DATA '     waw     '
12510     DATA '     www     '
12520 :
12530   move_sprite=RD_SPRT(1)                : REMark 1 = 0 mask
12540   IF temp%>1:move_sprite=6              : REMark or system sprite!
12550     DATA 10,14,5,4,4
12560     DATA 'aaaaaaaaaa    '
12570     DATA 'awwwwwwwwa    '
12580     DATA 'awwrrrrwwa    '
12590     DATA 'awwrrrrwwaaaaa'
12600     DATA 'awwrrwwwwwwwwa'
12610     DATA 'awwwwwwwwrrwwa'
12620     DATA 'aaaaawwrrrrwwa'
12630     DATA '    awwrrrrwwa'
12640     DATA '    awwwwwwwwa'
12650     DATA '    aaaaaaaaaa'
12660 :
12670   zzz_sprite= RD_SPRT(1)
12680   IF temp%>1:zzz_sprite=10
12690     DATA 7,15,0,0,4
12700     DATA 'wwwww          '
12710     DATA '   w           '
12720     DATA '  w  wwww      '
12730     DATA ' w     w       '
12740     DATA 'wwwww w  wwww  '
12750     DATA '     wwww  w   '
12760     DATA '          wwww '
12770 :
12780  size_sprite=RD_SPRT(1)
12790  IF temp%>1:size_sprite=7
12800     DATA 9,14,5,4,4
12810     DATA 'aaaaaaaaaaaaaa'
12820     DATA 'awwwwwwwwwwwwa'
12830     DATA 'awwrrrrrrrrwwa'
12840     DATA 'awwrrrrrrrrwwa'
12850     DATA 'awwrrwwwwwwwwa'
12860     DATA 'awwrrwwrrrrwwa'
12870     DATA 'awwrrwwrrrrwwa'
12880     DATA 'awwwwwwwwwwwwa'
12890     DATA 'aaaaaaaaaaaaaa'
12900 :
12910   make_my_data 1
12920   make_main_wdw
12930 :
12940 END DEFine init
12950 :
12960 DEFine PROCedure make_main_wdw
12970 REMark make the main window
12980 :
12990   REMark first make sure of the wdw sizes (we also come here from change_size!)
13000   xsize%=xsize%-(xsize% MOD 4)                    : REMark make into nbr divisible by 4
13010   IF xsize%<360:xsize%=360
13020 :
13030   REMark calculate the y size of the wdw so that it always looks nice, in that
13040   REMark the rows in the appsub always fill the appsub entirely
13050   ysize%=ysize%+(ysize%&&1)             : REMark make into even number
13060   IF ysize%<100:ysize%=100              : REMark predetermioned min ysize% before calculations
13070   ysize%=ysize%-inf1y-14-appwinoffset%
13080   rangees=ysize% DIV 12                 : REMark GLOBAL var: nbr of rows in appsub
13090   IF rangees>data_count%-2:rangees=data_count%-2
13100   ysize%=rangees*12+inf1y+14 +appwinoffset%
13110 :
13120   REMark first calc info obj size - this also influences one llose item, used
13130   REMark in the title bar
13140   longueur_titre=LEN(titre$)*6                    : REMark length in pixels
13150   titre_x=INT((xsize%-longueur_titre)/2): REMark x orig
13160 :
13170   REMark first the loose items
13180 :
13190   yinf1y=inf1y+4:
13200   esc%=0:move_it%=esc%+1:size_it%=move_it%+1
13210   sleep_it%=size_it%+1:all_it%=sleep_it%+1:go_it%=all_it%+1
13220   dir_it%=go_it%+1:target_its%=dir_it%+1:make_it%=target_its%+5
13230 :
13240 RESTORE 13240
13250   REMark reset data pointer to here (The Basic Linker does that automatically)
13260 :
13270   temp%=xsize% DIV 6
13280   main_lit=12:DIM main_lfl%(main_lit)   : REMark there are 13 items!
13290   main_lot=RD_LOT(std_iattr,main_lit)
13300     DATA lxs,lys,4+xesp*0,2,0,0,cancel$,text+retr,'ESC'
13310     DATA lxs,lys,4+xesp*1,2,0,0,move$,sprite+retr,move_sprite
13320     DATA lxs,lys,4+xesp*2,2,0,0,size$,sprite+retn,size_sprite
13330     DATA lxs,lys,xsize%-2-xesp*1,2,0,0,sleep$,sprite+retr,zzz_sprite
13340     DATA lxs,lys,xsize%-2-xesp*2,2,0,0,'A',text+retn,"All"
13350     DATA lxs,lys,xsize%-2-xesp*3,2,0,0,'O',sous1+retn,"OK"
13360     DATA 6*6,lys-2,titre_x+30,3,0,0,'D',text+retn,rep$
13370     DATA 8*6,lys,8,inf1y+2,0,0,'G',sous1+retn,"Generic"
13380     DATA 8*6,lys,8+temp%,inf1y+2,0,0,'T',sous2+retn,"Atari"
13390     DATA 8*6,lys,8+temp%*2,inf1y+2,0,0,'G',sous1+retn,"GoldCard"
13400     DATA 8*6,lys,8+temp%*3,inf1y+2,0,0,'Q',sous1+retn,"Q40/Q60"
13410     DATA 8*6,lys,8+temp%*4,inf1y+2,0,0,'X',sous2+retn,"QXL"
13420     DATA 8*6,lys,8+temp%*5,inf1y+2,0,0,'M',sous2+retn,"Make"
13430 :
13440     main_lfl%(target_its%)=128
13450 :
13460   REMark now info wdw objects - here only one, the title
13470   REMark make the title data: calculate size and x position of this object
13480 :
13490   main_iot1=RD_IOT(0)
13500     DATA longueur_titre,10,4,INT((lys-10)/2),sous1,sp.titlefg,0,0,titre$
13510 :
13520   REMark now the info wdws
13530 :
13540   main_iwt=RD_IWT(2)                              : REMark there are 3
13550 :
13560 REMark first the title bar
13570 :
13580     DATA xsize%-8-(2*nbr_post%*xesp),lys+2,xesp*nbr_post%+4,0         : REMark x,y sizes & origs
13590     DATA 0,0,sp.infwinbd,sp.titlebg     : REMark shadow, border size & col,paper col
13600     DATA 0                                        : REMark object pointer (none)
13610 :
13620 REMark now wdw containing title itself
13630 :
13640     DATA longueur_titre+8,lys,titre_x-4,2
13650     DATA 0,0,sp.infwindbd,sp.infwinbg
13660     DATA main_iot1                      : REMark one object
13670 :
13680 REMark inner border
13690 :
13700     DATA xsize%-8,ysize%-inf1y-2,4,inf1y
13710     DATA 0,1,sp.infwinbd,sp.infwinbg
13720     DATA 0
13730 :
13740 REMark now the appsub wdw - use the OUTPTR functions for this
13750 :
13760   app_addr=0                                      : REMark there is none yet
13770   mk_main_app 1                         : REMark (this destroys my DATA pointer)
13780   main_app_list = app_addr+180                    : REMark list built by OUTPTR
13790 :
13800 : REMark  working defn
13810 :
13820 RESTORE 13820
13830                                                   : REMark reset DATA pointer (due to Basic Linker, restore must be ALONE on the line)
13840   main_defn=RD_WDEF
13850     DATA 2,1,sp.winbd,sp.winbg                    : REMark shad,border size & col,pap col
13860     DATA xsize%,ysize%,0,0    : REMark x,y size & initial ptr pos
13870     DATA main_sprite,main_lot,main_iwt,main_app_list : REMark sprite,loose items, info wins, appsubs
13880 END DEFine make_main_wdw
13890 :
13900 DEFine PROCedure warn_make
13910 REMark makes warn,ing & info window
13920 REMark uses global vars green etc...
13930 REMark SETS the global var warning_width% (nbr of chars in one line in wdw)
13940 LOCal lys%,warn_lot,warn_iot1,warn_iwt
13950 LOCal x%,y%,warning_paper,warning_brd
13960 RESTORE 13960
13970   x%=188:y%=82
13980   warning_paper=menu_pap
13990   warning_brd=pink                      : REMark pink
14000 REMark  if menu_brd=4:warning_brd=light_green      : rem light green
14010   lys%=10
14020   DIM warn_lfl%(1)
14030   warn_lot=RD_LOT(std_iattr,1)
14040     DATA 30,lys%,4,4,0,0,cancel$,text+retr,'ESC'
14050     DATA 30,lys%,x%-34,4,0,0,'O',text+retr,"OK"
14060 :
14070   warn_iot1=RD_IOT (0)
14080     DATA 12*9,10,0,1,text,red,2,0,'Attention'
14090 :
14100   warn_iwt=RD_IWT(1)
14110     DATA x%-16,y%-32,8,24
14120     DATA 0,1,red,warning_paper
14130     DATA 0
14140 :
14150     DATA 120,12,(x%-112) DIV 2,4
14160     DATA 0,1,menu_brd,warning_paper
14170     DATA warn_iot1
14180 :
14190   warn_defn=RD_WDEF
14200     DATA 2,1,menu_brd,warning_brd
14210     DATA x%,y%,0,0
14220     DATA main_sprite,warn_lot,warn_iwt,0
14230 :
14240   warning_width%=(x%-16) DIV 6                    : REMark width of warning winfow in chars
14250 :
14260 END DEFine warn_make
14270 :
14280 DEFine PROCedure about_make (titre$,version$)
14290 REMark this presupposes that wolfsoft_sprite is part of the sprites!
14300 LOCal lxs%,lys%,xesp%,inf1y%,abt_lot,longueur_titre%,titre_x%
14310 LOCal abt_iwt,abt_iot1,abt_iot2
14320   ab_xt%=200:ab_yt%=120          : REMark tailles x et y: la taille x devrait
14330                                    : REMark ëtre un chiffre divisible par 4
14340   lxs%=24:lys%=10                    : REMark tailles x et y pour postes standard
14350   inf1y%=lys%+4                      : REMark posi. y de la fen. d'inf. intÉrieure
14360   xesp%=lxs%+2
14370   nbr_post%=0                      : REMark nombre de postes dans un coin
14380 :
14390 RESTORE 14390
14400 :
14410   yinf1y%=inf1y%+4:
14420 :
14430   DIM abt_lfl%(0)
14440   abt_lot=RD_LOT(std_iattr,0)
14450     DATA ab_xt%-16,lys%,8,ab_yt%-lys%-8,0,0,cancel$,text+retr,'Super!'
14460 :
14470 : REMark  D'abord le titre qui est un objet d'une sous-fenëtre d'info.
14480 :
14490   longueur_titre%=LEN("Version "&version$)*6             : REMark on calcule la taille et la
14500   titre_x%=INT((ab_xt%-longueur_titre%)/2)  : REMark position x de cet objet
14510   abt_iot2=RD_IOT(1)
14520     DATA longueur_titre%,lys%,4,8,text,menu_ink,0,0,"Version "&version$
14530     DATA 52,24,ab_xt%-60,40,sprite,0,wolfsoft_sprite
14540   longueur_titre%=LEN(titre$)*6             : REMark on calcule la taille et la
14550   titre_x%=INT((ab_xt%-longueur_titre%)/2)  : REMark position x de cet objet
14560   abt_iot1=RD_IOT(0)
14570     DATA longueur_titre%,lys%,4,INT((lys%-10)/2),text,menu_ink,0,0,titre$
14580   abt_iwt=RD_IWT(2)               : REMark on veut 3 sous-fen. d'info
14590 REMark d'abord la fenëtre contenant la barre entourant le titre
14600     DATA ab_xt%-8-(2*nbr_post%*xesp%),lys%+2,xesp%*nbr_post%+4,0  : REMark taille x,y, origines x,y
14610     DATA 0,0,menu_brd,menu_bar     : REMark ombre, taille & couleur bordure
14620                                    : REMark puis couleur papier
14630     DATA 0                         : REMark pointeur vers objets
14640 REMark maintenant la fenëtre qui contient le titre
14650     DATA longueur_titre%+8,lys%,titre_x%-2,2   : REMark on utilise titre_x% etc
14660     DATA 0,0, menu_brd,menu_pap
14670     DATA abt_iot1                 : REMark un objet, le titre
14680 REMark maintenant la fenëtre de la bordure intÉrieure
14690     DATA ab_xt%-8,ab_yt%-inf1y%-2,4,inf1y%
14700     DATA 0,1, menu_brd,menu_pap
14710     DATA abt_iot2
14720   abt_defn=RD_WDEF
14730     DATA 2,1,menu_ink,menu_pap: REMark ombre, larg. & coul. bord., coul. papier
14740     DATA ab_xt%,ab_yt%,10,10 : REMark taille x,y et position pointeur
14750     DATA main_sprite,abt_lot,abt_iwt,0
14760 :
14770 END DEFine about_make
14780 :
14790 DEFine PROCedure about
14800   LOCal loop%,item%,dummy%,chan%
14810   DR_PULD abt_defn,-1,-1,abt_lfl%
14820   chan%=FOPEN('con')
14830   DR_IWDF#chan%,abt_defn,2
14840   INK#chan%,menu_ink
14850   PAPER#chan%,menu_pap
14860   AT#chan%,4,2
14870   PRINT#chan%,"Copyright (c) 1999"
14880   AT#chan%,5,2
14890   PRINT#chan%,"W. Lenerz"
14900   CLOSE#chan%
14910   REPeat loop%
14920     RD_PTR abt_defn,item%,dummy%,dummy%,dummy%,dummy%,abt_lfl%
14930     SELect ON item%
14940             =0     : EXIT loop%
14950     END SELect
14960   END REPeat loop%
14970   DR_UNST abt_defn
14980 END DEFine about
14990 :
15000 DEFine FuNction RD_SPRT(drap%)
15010 REMark drap% determine si masque 0
15020   LOCal tmp_patt$(32,32)
15030   LOCal xs,ys,xo,yo, md, l
15040   READ ys,xs,xo,yo,md
15050   DIM tmp_patt$(ys,xs)
15060   FOR l=0 TO ys-1:READ tmp_patt$(l)
15070   l=ALCHP(SPRSP(xs,ys))
15080   SPSET l,xo,yo,md,tmp_patt$(0 TO ys-1,1 TO xs)
15090   IF drap%
15100     POKE_L l+16,espace_vide-(l+16)
15110   END IF
15120   RETurn l
15130 END DEFine RD_SPRT
15140 :
15150 DEFine FuNction RD_WDEF
15160 REMark read window defn
15170   LOCal ldef%(3), lspr, lloose, linfo, lappl, lattr%(3)
15180   RD_WATT lattr%
15190   READ ldef%(0), ldef%(1), ldef%(2), ldef%(3)
15200   READ lspr, lloose, linf, lappl
15210   RETurn MK_WDEF (ldef%, lattr%, lspr, lloose, linf, lappl)
15220 END DEFine RD_WDEF
15230 :
15240 DEFine FuNction RD_LOT (lattr,nitem)
15250   LOCal count(3)
15260   LOCal item, ltyp, a$, lsk$
15270   LOCal ldef%(nitem,6), lptr(3,nitem), lstr$(nitem,85)
15280   lsk$=''
15290   FOR item = 0 TO nitem
15300     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
15310     READ ldef%(item,4), ldef%(item,5)
15320     READ a$: lsk$=lsk$ & a$
15330     READ ltyp
15340     ldef%(item,6)=ltyp: ltyp=(ltyp MOD 256)/2
15350     IF ltyp>10 OR ltyp <0::ltyp=0
15360     IF ltyp
15370       READ lptr(ltyp,count(ltyp))
15380     ELSE
15390       READ lstr$(count(0))
15400     END IF
15410     count(ltyp)=count(ltyp)+1
15420   END FOR item
15430   RETurn MK_LIL (lattr, ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), ldef%(TO, 4 TO 5), lsk$, ldef%(TO, 6), lstr$, lptr(1), lptr(2), lptr(3))
15440 END DEFine RD_LOT
15450 :
15460 DEFine FuNction RD_IWT(nitem)
15470   LOCal item,mc1
15480   LOCal ldef%(nitem,3), latt%(nitem,3), lptr(nitem)
15490   FOR item = 0 TO nitem
15500     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
15510     READ latt%(item,0), latt%(item,1), latt%(item,2), latt%(item,3)
15520     READ lptr(item)
15530   END FOR item
15540   RETurn MK_IWL (ldef%, latt%, lptr)
15550 END DEFine RD_IWT
15560 :
15570 DEFine FuNction RD_IOT(nitem)
15580   LOCal count(3)
15590   LOCal item, ltyp, work1, work2
15600   LOCal ldef%(nitem,4), lptr(3,nitem), lstr$(nitem,85)
15610   FOR item = 0 TO nitem
15620     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
15630     READ ltyp
15640     ldef%(item,4)=ltyp:  ltyp=(ltyp MOD 256)/2
15650     IF ltyp >10:ltyp=0
15660     IF ltyp
15670       READ lptr(0,item),lptr(ltyp,count(ltyp))
15680     ELSE
15690       IF mycolour_mode%=2
15700           READ work1,work2,work3
15710           lptr(0,item)=WL_4_IOL(work1,work2,work3)
15720       ELSE
15730           READ work1
15740           READ work2: work1=work1*256+work2
15750           READ work2: lptr(0,item)=work1*256+work2
15760       END IF
15770       READ lstr$(count(0))
15780     END IF
15790     count(ltyp) = count(ltyp) + 1
15800   END FOR item
15810   RETurn MK_IOL (ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), lptr(0), ldef%(TO, 4), lstr$, lptr(1), lptr(2), lptr(3))
15820 END DEFine RD_IOT
15830 :
15840 DEFine PROCedure RD_IATT (lattr)
15850   LOCal i
15860   READ lattr(0,0), lattr(0,1)
15870   FOR i=1 TO 3: READ lattr(i,0), lattr(i,1), lattr(i,2), lattr(i,3)
15880 END DEFine RD_IATT
15890 :
15900 DEFine PROCedure RD_WATT (lattr%)
15910   READ lattr%(0), lattr%(1),lattr%(2),lattr%(3)
15920 END DEFine RD_WATT
15930 :
