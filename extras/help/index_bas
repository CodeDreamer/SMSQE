100 REMark for the Documentation, see dev8_help_help_index_txt
110 REMark version 1.00 (c) W. Lenerz 1999-2003
120 :
130 REMark where to put the "help" file
140 DATA "ram1_"
150 :
160 REMark Insert the directories to search in for source files containind XDEFs
170 REMark Put an empty string at the end
180 DATA "dev8_"
190 DATA ""
200 :
210 main
220 :
230 DEFine PROCedure main
240 LOCal lp%
250   RESTORE :CLOSE
260   logchan%=2                            : REMark visual log channel
270   READ helpdir$
280   helpfile$=helpdir$&"help_index"
290   helpchan%=FOP_OVER(helpfile$)
300   IF helpchan%<0:REPORT#logchan%,helpchan%:STOP
310   sl$='ram1_scandir_list'
320   sr$='ram1_scandir_report'
330   repchan%=FOP_OVER(sr$)
340   log "Scanning now ...."
350   REPeat lp%
360     READ scandir$
370     IF scandir$="":EXIT lp%
380     do_scan scandir$
390   END REPeat lp%
400   log "... Done"
410   CLOSE
420 END DEFine main
430   :
440 DEFine PROCedure do_scan (scandir$)
450 LOCal f$,lp%,ok%,file$,infile%,leng%,line$,pos%,char%,dirchan%,searchfile%,filechan%
460   log "Scanning "&scandir$&" ..."
470   IF scandir$='':
480      log "Empty dir passed!"
490      RETurn                                : REMark what?
500   END IF
510   FOR lp%=1 TO 10
520     f$=sl$&RND(1 TO 100)&RND(1 TO 100) & RND (1 TO 100)
530     IF FTEST(f$)=-7:ok%=1:EXIT lp%
540   END FOR lp%
550   IF NOT ok%
560     log "Can't open file for dir after 10 attempts!"
570     RETurn                                 : REMark ooops
580   END IF
590   DIR\f$,scandir$                       : REMark make dir channel
600   dirchan%=FOP_IN(f$)                   : REMark now
610   IF dirchan%<0
620     log "cound't open "&f$
630     RETurn
640   END IF
650   INPUT#dirchan%,file$
660   INPUT#dirchan%,file$                  : REMark get rid of stuff put by Dir
670   REPeat infile%
680     IF EOF(#dirchan%):EXIT infile%      : REMark end of dir file reached
690     INPUT#dirchan%,file$                : REMark read filename
700     leng%=LEN(file$)
710     IF leng%<3:NEXT infile%             : REMark can't be _asm nor subdir
720     IF file$(leng%-2 TO)==" ->"
730       file$=scandir$(1 TO 5)&file$(TO leng%-3)&"_"
740       do_scan file$                     : REMark if subdir,scan it
750       NEXT infile%                      : REMark and tale next file
760     END IF
770     IF leng%<4:NEXT infile%              : REMark can't be _asm file
780     IF file$(leng%-3 TO)=="_asm"
790       file$=scandir$(1 TO 5)&file$
800       filechan%=FOP_IN(file$)           : REMark open _asm file
810       PRINT#repchan%,file$              : REMark report it
820       IF filechan%<0
830          PRINT#logchan%,file$;' ';      : REMark ooops
840          log "Couldn't open "&file$
850          log filechan%
860          NEXT infile%
870       END IF
880       REPeat searchfile%
890         IF EOF(#filechan%):EXIT searchfile%
900         INPUT#filechan%,line$           : REMark get one line from file
910         pos%='xdef ' INSTR line$        : REMark do we have an xdef in line?
920         IF NOT pos%
930            pos%='xdef'&CHR$(9) INSTR line$ : REMark no, perhaps with tab at end?
940         END IF
950         IF pos%
960           line$=line$(pos%+5 TO)        : REMark this is what is xdefed
970           line$=del_space$(line$)       : REMark no spaces
980           IF line$(1)='[' OR line$(1)='{':NEXT searchfile% : REMark no macros
990           FOR char%=1 TO LEN(line$)
1000             IF line$(char%)=' ' OR line$(char%)=',' OR line$(char%)=';'OR line$(char%)=CHR$(9):char%=char%-1:EXIT char%
1010           END FOR char%                 : REMark no comment characters
1020           line$=line$(1 TO char%)       : REMark this is exactly what is XDEF'd
1030           leng%=LEN(line$)
1040           IF leng%<20: line$=line$&FILL$(' ',20-leng%) : REMark nice format
1050           PRINT#helpchan%,line$;file$   : REMark xdef & filename into helpfile
1060           log line$&file$
1070         END IF
1080       END REPeat searchfile%
1090       CLOSE#filechan%
1100     END IF
1110   END REPeat infile%
1120   CLOSE#dirchan%
1130   DELETE f$
1140 END DEFine do_scan
1150 :
1160 DEFine FuNction del_space$(str$)
1170 LOCal del_loop
1180 REPeat del_loop
1190  IF str$='':RETurn str$
1200  IF str$(1)<>' ' AND str$(1)<>CHR$(9):RETurn str$
1210  str$=str$(2 TO)
1220 END REPeat del_loop
1230 END DEFine
1240 :
1250 DEFine PROCedure sa
1260   SAVE_O dev8_extras_help_index_bas
1270 END DEFine sa
1280 :
1290 DEFine PROCedure log (string$)
1300   PRINT #logchan%,string$
1310   PRINT #repchan%,string$
1320 END DEFine log
1330 :
