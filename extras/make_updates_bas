100 REMark $$asmb=dev8_extras_blinker_bin,0,10
110 compiled=NOT IS_OPEN(#0)
120 IF NOT compiled
130   SCR_SIZE#0, xsz%,ysz%
140   OUTLN#0,xsz%,ysz%-28,0,28
150   WINDOW#0,256,30,xsz%-256,ysz%-30
160   WINDOW#1,256,ysz%-58,xsz%-256,28
170   WINDOW#2,xsz%-256,ysz%-28,0,28
180   BORDER#0,1,4:PAPER#0,0:INK#0,7
190   BORDER#1,1,4:PAPER#1,0: INK 7
200   BORDER#2,1,4:PAPER#2,0:INK#2,7
210   CLS#0:CLS#1:CLS#2
220 END IF
230 init
240 main
250 STOP
260 :
270 DEFine PROCedure main
280   LOCal boucle,item%,event%,swnum%,xrel%,yrel%,poste
290   IF compiled
300      OPEN#1,'con'
310      DR_PPOS main_defn,-1,-1,main_lfl%
320   ELSE
330      DR_PULD main_defn,-1,-1,main_lfl%
340   END IF
350   infochan%=FOPEN("con")
360   PAPER#infochan%,menu_pap
370   INK#infochan%,menu_ink
380   DR_IWDF#infochan%,main_defn,2
390   REPeat boucle
400     RD_PTR main_defn,item%,swnum%,event%,xrel%,yrel%,main_lfl%
410     SELect ON item%
420             =esc%     : EXIT boucle
430             =deplace% : CH_WIN main_defn:DR_IWDF#infochan%,main_defn,2
440             =oldd%    : contents_file$=FILE_SELECT$(,contents_file$)
450                         CH_ITEM main_defn,-1,oldd%,-1,'',contents_file$
460             =newd%    : new_drive$=DIR_SELECT$
470                         CH_ITEM main_defn,-1,newd%,-1,'',new_drive$
480             =make%    : make_content_file
490             =start%   : start_update
500     END SELect
510     IF item%>0:main_lfl%(item%)=main_lfl%(item)+1
520   END REPeat boucle
530   CLOSE#infochan%
540   DR_UNST main_defn
550   IF compiled:CLOSE#1
560   RECHP espace_vide
570 END DEFine main
580 :
590 DEFine PROCedure sa
600   SAVE_O dev8_extras_make_updates_bas
610 END DEFine sa
620 :
630 DEFine PROCedure make_content_file
640 LOCal totchan%
650   IF new_drive$="":RETurn
660   CLS#infochan%
670   totchan%=FOP_OVER(contents_file$)
680   IF totchan%<0:PRINT infochan%,"Couldn't open ";contents_file$:RETurn
690   get_dir_structure new_drive$,totchan%
700   CLOSE#totchan%
710 END DEFine make_content_file
720 :
730 DEFine PROCedure get_dir_structure (dirr$,what_chan%)
740 REMark this gets the entire content of a drive (or dir) into a file
750 LOCal a$,lp%,chan%,device$,file$
760 LOCal k%
770   REPeat lp%
780     file$="ram1_ddir_"&RND(0 TO 999)&RND(0 TO 999) & RND(0 TO 999)&"_pkk" : REMark temp file
790     IF FTEST (file$)=-7:EXIT lp%        : REMark file doesn't exist yet - super!
800   END REPeat lp%
810   chan%=FOP_OVER(file$)
820   IF chan%<0:RETurn                     : REMark oops how is this possible?
830   PRINT#infochan%,"Parsing : "&dirr$&CHR$(10)    : REMark keep me informed
840   WSTAT#chan%,dirr$                     : REMark
850   PRINT#chan%
860   GET#chan%\0
870   device$=dirr$(1 TO 4)&'_'
880   REPeat lp%
890     IF EOF(#chan%):EXIT lp%
900     INPUT#chan%,a$
910     IF a$="":NEXT lp%
920     k%=LEN(a$)
930     IF k%<4:NEXT lp%
940     IF a$(k%-2 TO k%)=' ->'
950       PRINT#what_chan%,a$
960       a$=device$&a$(1 TO k%-3)&"_"
970       get_dir_structure a$,what_chan%
980     ELSE
990       PRINT#what_chan%,a$;waterm$       : REMark print name of file
1000       INPUT#chan%,a$                    : REMark length + date
1010       a$=make_date_n_length$(a$)
1020       PRINT#what_chan%,a$               : REMark print length + date
1030     END IF
1040   END REPeat lp%
1050   CLOSE#chan%
1060   DELETE file$
1070 END DEFine get_dir_structure
1080 :
1090 DEFine FuNction make_date_n_length$(a$)
1100 REMark return a string:length_watermark_date (as long int) for the formatted
1110 REMark a$ input
1120 REMark local b$,y$,m$,d$,h$,mi$,s$,timestamp$
1130   b$=STRIP_SPACES$(a$ (1 TO 10))        : REMark file length
1140   timestamp$=make_long_date_from_strings$(a$ (13 TO))
1150   RETurn b$&waterm$&timestamp$
1160 END DEFine make_date_n_length$
1170 :
1180 DEFine FuNction make_long_date_from_strings$(a$)
1190 LOCal y,m,d,h,mi,s
1200   y=a$(1 TO 4)
1210   m=month_in_numbers%(a$(6 TO 8))
1220   d=a$(10 TO 11)
1230   h=a$(13 TO 14)
1240   mi=a$(16 TO 17)
1250   s=a$(19 TO 20)
1260   RETurn LONG$(MKDATE(y,m,d,h,mi,s))
1270 END DEFine make_long_date_from_strings$
1280 :
1290 DEFine FuNction month_in_numbers%(month$)
1300 REMark   this returns the month number from a 3 char month string
1310 REMark   if not found (wrong month string) returns 0
1320 REMark this presules the correct existence of allmonths$
1330 LOCal er
1340   er=month$ INSTR allmonths$
1350   RETurn INT((er+2)/3)
1360 END DEFine month_in_numbers%
1370 :
1380 DEFine PROCedure start_update
1390   CLS#infochan%
1400   to_zip$="-j9 -r ram1_SMSQEUPD.zip "
1410   allstop%=0
1420   read_contents_file
1430   check_dir_structure new_drive$
1440   do_zip
1450 END DEFine start_update
1460 :
1470 DEFine PROCedure check_dir_structure (dirr$)
1480 REMark this checks whether the device mdir$ contains new or updated files
1490 REMark as compared to the content of a content$ file
1500 REMark note that this does not remove files that were in the old version butno longer
1510 REMark are in the new version on the device
1520 LOCal a$,lp%,chan%,device$,file$
1530 LOCal k%
1540   REPeat lp%
1550     file$="ram1_ddir_"&RND(0 TO 999)&RND(0 TO 999) & RND(0 TO 999)&"_pkk" : REMark temp file
1560     IF FTEST (file$)=-7:EXIT lp%        : REMark file doesn't exist yet - super!
1570   END REPeat lp%
1580   chan%=FOP_OVER(file$)
1590   IF chan%<0:RETurn                     : REMark oops how is this possible?
1600   PE_PRINT#infochan%,"Parsing : "&dirr$&CHR$(10)    : REMark keep me informed
1610   DIR#chan%,dirr$                     : REMark do this dir now
1620   PRINT#chan%
1630   GET#chan%\0                           : REMark start at beginning of file
1640   INPUT#chan%,a$:INPUT#chan%,a$
1650   device$=dirr$(1 TO 4)&'_'
1660   REPeat lp%
1670     IF EOF(#chan%):EXIT lp%
1680     IF CODE(INKEY$(#1,0))=27:allstop%=1:EXIT lp%
1690     IF allstop%:EXIT lp%
1700     INPUT#chan%,a$                      : REMark get a filename
1710     IF a$="":NEXT lp%                   : REMark oops?
1720     k%=LEN(a$)
1730     IF k%<3:check_file a$,device$:NEXT lp%:EXIT lp% : REMark this can't be a directory
1740     IF a$(k%-2 TO k%)=' ->'
1750       a$=device$&a$(1 TO k%-3)&"_"
1760       check_dir_structure a$
1770     ELSE
1780      check_file a$,device$
1790     END IF
1800   END REPeat lp%
1810   CLOSE#chan%
1820   DELETE file$
1830 END DEFine check_dir_structure
1840 :
1850 DEFine PROCedure read_contents_file
1860 REMark this reads the contents file into a large array
1870 LOCal lp%,chan%,counter%
1880   chan%=FOP_IN(contents_file$)
1890   IF chan%<0:RETurn                     : REMark ooops?
1900   DIM contents$(10000,200)
1910   FOR counter%=1 TO 10000
1920      IF EOF(#chan%):EXIT counter%
1930      INPUT#chan%,contents$(counter%)
1940   END FOR counter%                      : REMark DO NOT sort ! ! !
1950 END DEFine read_contents_file
1960 :
1970 DEFine PROCedure check_file (fle$,device$)
1980 REMark this checks whether a file on the device device$ is newer or
1990 REMark of a different size etc as the one contained in the contents file
2000 LOCal chan%,new_date,old_date,new_len,old_len,res%,a$,k%
2010   res%=ASEARCH(contents$,fle$&waterm$,0,0)   : REMark try to find this file
2020   IF res%=-1
2030      zip_this_file fle$        : REMark file not found in contents$ - new file
2040      RETurn                     : REMark finished
2050   END IF
2060   chan%=FOP_IN(device$&fle$)
2070   IF chan%<0:RETurn             : REMark what?
2080   new_date=FUPDT(#chan%)
2090   new_len=FLEN(#chan%)          : REMark length & date of new file
2100   CLOSE#chan%
2110   a$=contents$(res%+1)          : REMark length & date of old file
2120   k%=waterm$ INSTR a$           : REMark find end of old length
2130   IF NOT k%:RETurn                 : REMark what?
2140   old_len=a$(1 TO k%-1)
2150   old_date=a$(k%+wml% TO)     : REMark old length
2160   IF (old_len<>new_len) OR (old_date<>new_date)
2170     zip_this_file fle$
2180   END IF
2190 END DEFine check_file
2200 :
2210 DEFine PROCedure do_zip
2220 LOCal dat$,prg$,destt$
2230   dat$=DATAD$
2240   prg$=PROGD$
2250   destt$=DESTD$
2260   DEST_USE new_drive$  :PROG_USE newˆdrive$ :DATA_USE new_drive$
2270   EW dev1_progs_zip;to_zip$
2280   DATA_USE dat$
2290   PROG_USE prg$
2300   DEST_USE destt$
2310 END DEFine do_zip
2320 :
2330 DEFine PROCedure zip_this_file (file$)
2340 REMark zips the file, or rather adds it to the zip command line
2350    to_zip$=to_zip$&" "&file$
2360    PE_PRINT#infochan%,"Adding :" & file$&CHR$(10)
2370 END DEFine zip_this_file
2380 :
2390 DEFine FuNction getallmonths$
2400 REMark   this returns the months as a string from jan..dec in current language
2410 REMark this, thus should be lthe language used by WSTAT...
2420 LOCal mymonth$,n%,local$
2430   mymonth$=""
2440   FOR n%=0 TO 11
2450     local$=DATE$(31*24*60*60*n%)
2460     mymonth$=mymonth$&local$(6 TO 8)
2470   END FOR n%
2480   RETurn mymonth$
2490 END DEFine getallmonths$
2500 :
2510 : REMark --------------------------------------------------------------------
2520 : REMark a partir d'ici, des fonctions standard
2530 : REMark --------------------------------------------------------------------
2540 :
2550 DEFine PROCedure make_colours (colourway%)
2560 REMark Ceci intitialise les variables menu_pap, menu_brd, menu_ink
2570 REMark et menu_bar selon le type-couleurs choisi
2580 :
2590 REMark 0= blanc/vert
2600 REMark 1= noir/rouge
2610 REMark 2= blanc/rouge
2620 REMark 3= noir/vert
2630 :
2640   colway=colourway%&&3: REMark doit ‘tre entre 0 et 3
2650   SELect ON colway
2660     =0:                     : REMark blanc/vert
2670             menu_pap=7
2680             menu_brd=4
2690             menu_ink=0
2700             menu_bar=84
2710     =1:                     : REMark noir/rouge
2720             menu_pap=0
2730             menu_brd=2
2740             menu_ink=7
2750             menu_bar=82
2760     =2:                     : REMark blanc/rouge
2770             menu_pap=7
2780             menu_brd=2
2790             menu_ink=0
2800             menu_bar=98
2810     =3:                     : REMark noir/vert
2820             menu_pap=0
2830             menu_brd=4
2840             menu_ink=7
2850             menu_bar=100
2860   END SELect
2870 END DEFine make_colours
2880 :
2890 DEFine PROCedure button (def_trav,menu_drap%,app_drap%,ctrl%,chaine$,type_col%)
2900   DR_UNST main_defn
2910   IF compiled: CLOSE#1
2920   BTN chaine$,type_col%
2930   IF compiled
2940     OPEN#1,"CON_"
2950     DR_PPOS def_trav,-1,-1,menu_drap%,app_drap%,ctrl%
2960   ELSE
2970     DR_PULD def_trav,-1,-1,menu_drap%,app_drap%,control%
2980   END IF
2990 END DEFine button
3000 :
3010 DEFine PROCedure init
3020 : REMark quelques couleurs utiles
3030   black=0:red=2:green=4:white=7:grey=255:pink=234:dark_red=210
3040   dark_green=228:light_green=220:red_green=242:striped=19
3050 :
3060   text=0:sprite=2:blob=4:pattern=6
3070   retr=256:retn=-256:sous1=text-2:sous2=text-4
3080 :
3090 : REMark les codes-type pour les ƒvnements
3100   hit$=CHR$(1):do$=CHR$(2):cancel$=CHR$(3):sleep$=CHR$(7):wake$=CHR$(8)
3110   help$=CHR$(4):move$=CHR$(5):size$=CHR$(6):cr$=CHR$(10)
3120 :
3130   xtaille=460:ytaille=220          : REMark tailles x et y: la taille x devrait
3140                                    : REMark ‘tre un chiffre divisible par 4
3150   lxs=24:lys=10                    : REMark tailles x et y pour postes standard
3160   inf1y=lys+4                      : REMark posi. y de la fen. d'inf. intƒrieure
3170   xesp=lxs+2
3180   nbr_post%=5                      : REMark nombre de postes dans un coin
3190   colourways%=0
3200   make_colours colourways%
3210 :
3220   titre$="SMSQ/E Update Maker"
3230   contents_file$="ram1_content_2y99"
3240   new_drive$="dev8_"
3250   waterm$="¼"&CHR$(255)&"WL"&CHR$(254)&"½"   : REMark my watermark - the chances of that being in a filename are paractically inexistent
3260   wml%=LEN(waterm$)
3270   allmonths$=getallmonths$              : REMark make a string with all months
3280                                         : REMark abbreviations
3290 :
3300   RESTORE
3310 :
3320 : REMark  Les attributes des postes dƒliƒs
3330 :
3340   DIM std_iattr(3,3)
3350   RD_IATT std_iattr
3360     DATA 1,menu_ink : REMark taille et couleur de bordure du poste courant
3370     DATA menu_pap,menu_brd,0,0 : REMark papier et encre d'un poste indisponible
3380     DATA menu_pap,menu_ink,0,0 : REMark disponible
3390     DATA menu_brd,menu_ink,0,0 : REMark selectionnƒ
3400 :
3410   espace_vide=ALCHP(100)
3420 :
3430   main_sprite=RD_SPRT(0)           : REMark ici un masque normal
3440     DATA 11,13,6,5,4
3450     DATA '     www     '
3460     DATA '     waw     '
3470     DATA '     waw     '
3480     DATA '     waw     '
3490     DATA 'wwwwwa awwwww'
3500     DATA 'waaaa   aaaaw'
3510     DATA 'wwwwwa awwwww'
3520     DATA '     waw     '
3530     DATA '     waw     '
3540     DATA '     waw     '
3550     DATA '     www     '
3560 :
3570   move_sprite=RD_SPRT(1)           : REMark ici un masque 0
3580     DATA 10,14,5,4,4
3590     DATA 'aaaaaaaaaa    '
3600     DATA 'awwwwwwwwa    '
3610     DATA 'awwrrrrwwa    '
3620     DATA 'awwrrrrwwaaaaa'
3630     DATA 'awwrrwwwwwwwwa'
3640     DATA 'awwwwwwwwrrwwa'
3650     DATA 'aaaaawwrrrrwwa'
3660     DATA '    awwrrrrwwa'
3670     DATA '    awwwwwwwwa'
3680     DATA '    aaaaaaaaaa'
3690 :
3700 zzz_sprite= RD_SPRT(1)             : REMark ici un masque 0
3710     DATA 7,15,0,0,4
3720     DATA 'wwwww          '
3730     DATA '   w           '
3740     DATA '  w  wwww      '
3750     DATA ' w     w       '
3760     DATA 'wwwww w  wwww  '
3770     DATA '     wwww  w   '
3780     DATA '          wwww '
3790 :
3800  wake_sprite=RD_SPRT(1)            : REMark ici un masque 0
3810     DATA 9,14,0,0,4
3820     DATA "             w"
3830     DATA "            w "
3840     DATA "      w   ww  "
3850     DATA "     ww www   "
3860     DATA "    wwwwww    "
3870     DATA "   www ww     "
3880     DATA "  w   w      "
3890     DATA " w            "
3900     DATA "w             "
3910 :
3920   yinf1y=inf1y+4
3930   esc%=0:move%=esc%+1
3940   make%=move%+1:oldd%=make%+1:newd%=oldd%+1
3950   start%=newd%+1
3960 :
3970   main_lit=5:DIM main_lfl%(main_lit)       : REMark on veut 4 postes
3980   main_lot=RD_LOT(std_iattr,main_lit)
3990     DATA lxs,lys,4+xesp*0,2,0,0,cancel$,text+retr,'ESC'
4000     DATA lxs,lys,4+xesp*1,2,0,0,move$,sprite+retr,move_sprite
4010     DATA lxs*5,lys,xtaille-2-xesp*5,2,0,0,"M",text-2+retr,'Make Content file'
4020     DATA 50*6,lys,112,inf1y+8,1,0,'O',text+retr,contents_file$
4030     DATA 50*6,lys,112,inf1y+24,1,0,'N',text+retr,new_drive$
4040     DATA lxs*3,lys,4+xesp*2,2,0,0,"S",text+retr-2,"Start Upd."
4050 :
4060   longueur_titre=LEN(titre$)*6             : REMark on calcule la taille et la
4070   titre_x=INT((xtaille-longueur_titre)/2)  : REMark position x de cet objet
4080   main_iot1=RD_IOT(0)
4090     DATA longueur_titre,lys,4,INT((lys-10)/2),text,menu_ink,0,0,titre$
4100   sub_iot=RD_IOT(1)
4110     DATA 70,lys,0,8, text-2,menu_ink,0,0,"Old content file:"
4120     DATA 70,lys,0,24,text-2,menu_ink,0,0,"New SMSQ/E is on:"
4130 :
4140   main_iwt=RD_IWT(3)               : REMark on veut 3 sous-fen. d'info
4150 :
4160     DATA xtaille-8-(2*nbr_post%*xesp),lys+2,xesp*nbr_post%+4,0  : REMark taille x,y, origines x,y
4170     DATA 0,0,menu_brd,menu_bar     : REMark ombre, taille & couleur bordure
4180                                    : REMark puis couleur papier
4190     DATA 0                         : REMark pointeur vers objets
4200 :
4210 :
4220     DATA longueur_titre+8,lys,titre_x-2,2   : REMark on utilise titre_x etc
4230     DATA 0,0, menu_brd,menu_pap
4240     DATA main_iot1                 : REMark un objet, le titre
4250 :
4260     DATA xtaille-8,ytaille-inf1y-54,4,inf1y+52
4270     DATA 0,1, menu_brd,menu_pap
4280     DATA 0
4290 :
4300     DATA xtaille-8,50,4,inf1y
4310     DATA 0,1, menu_brd,menu_pap
4320     DATA sub_iot
4330 :
4340   main_defn=RD_WDEF
4350     DATA 2,1,menu_ink,menu_pap: REMark ombre, larg. & coul. bord., coul. papier
4360     DATA xtaille,ytaille,10,10 : REMark taille x,y et position pointeur
4370     DATA main_sprite,main_lot,main_iwt,0
4380 :
4390 END DEFine init
4400 :
4410 DEFine FuNction RD_SPRT(drap%)
4420 REMark drap% determne si masque 0
4430   LOCal tmp_patt$(32,32)
4440   LOCal xs,ys,xo,yo, md, l
4450   READ ys,xs,xo,yo,md
4460   DIM tmp_patt$(ys,xs)
4470   FOR l=0 TO ys-1:READ tmp_patt$(l)
4480   l=ALCHP(SPRSP(xs,ys))
4490   SPSET l,xo,yo,md,tmp_patt$(0 TO ys-1,1 TO xs)
4500   IF drap%
4510     POKE_L l+16,espace_vide-(l+16)
4520   END IF
4530   RETurn l
4540 END DEFine RD_SPRT
4550 :
4560 DEFine FuNction RD_WDEF
4570   LOCal ldef%(3), lspr, lloose, linfo, lappl, lattr%(3)
4580   RD_WATT lattr%
4590   READ ldef%(0), ldef%(1), ldef%(2), ldef%(3)
4600   READ lspr, lloose, linf, lappl
4610   RETurn MK_WDEF (ldef%, lattr%, lspr, lloose, linf, lappl)
4620 END DEFine RD_WDEF
4630 :
4640 DEFine FuNction RD_LOT (lattr,nitem)
4650   LOCal count(3)
4660   LOCal item, ltyp, a$, lsk$
4670   LOCal ldef%(nitem,6), lptr(3,nitem), lstr$(nitem,85)
4680   lsk$=''
4690   FOR item = 0 TO nitem
4700     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
4710     READ ldef%(item,4), ldef%(item,5)
4720     READ a$: lsk$=lsk$ & a$
4730     READ ltyp
4740     ldef%(item,6)=ltyp: ltyp=(ltyp MOD 256)/2
4750     IF ltyp>10:ltyp=0
4760     IF ltyp
4770       READ lptr(ltyp,count(ltyp))
4780     ELSE
4790       READ lstr$(count(0))
4800     END IF
4810     count(ltyp)=count(ltyp)+1
4820   END FOR item
4830   RETurn MK_LIL (lattr, ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), ldef%(TO, 4 TO 5), lsk$, ldef%(TO, 6), lstr$, lptr(1), lptr(2), lptr(3))
4840 END DEFine RD_LOT
4850 :
4860 DEFine FuNction RD_IWT(nitem)
4870   LOCal item
4880   LOCal ldef%(nitem,3), latt%(nitem,3), lptr(nitem)
4890   FOR item = 0 TO nitem
4900     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
4910     READ latt%(item,0), latt%(item,1), latt%(item,2), latt%(item,3)
4920     READ lptr(item)
4930   END FOR item
4940   RETurn MK_IWL (ldef%, latt%, lptr)
4950 END DEFine RD_IWT
4960 :
4970 DEFine FuNction RD_IOT(nitem)
4980   LOCal count(3)
4990   LOCal item, ltyp, work1, work2
5000   LOCal ldef%(nitem,4), lptr(3,nitem), lstr$(nitem,85)
5010   FOR item = 0 TO nitem
5020     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
5030     READ ltyp
5040     ldef%(item,4)=ltyp: ltyp=(ltyp MOD 256)/2
5050     IF ltyp >10:ltyp=0
5060     IF ltyp
5070       READ lptr(0,item),lptr(ltyp,count(ltyp))
5080     ELSE
5090       READ work1
5100       READ work2: work1=work1*256+work2
5110       READ work2: lptr(0,item)=work1*256+work2
5120       READ lstr$(count(0))
5130     END IF
5140     count(ltyp) = count(ltyp) + 1
5150   END FOR item
5160   RETurn MK_IOL (ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), lptr(0), ldef%(TO, 4), lstr$, lptr(1), lptr(2), lptr(3))
5170 END DEFine RD_IOT
5180 :
5190 DEFine PROCedure RD_IATT (lattr)
5200   LOCal i
5210   READ lattr(0,0), lattr(0,1)
5220   FOR i=1 TO 3: READ lattr(i,0), lattr(i,1), lattr(i,2), lattr(i,3)
5230 END DEFine RD_IATT
5240 :
5250 DEFine PROCedure RD_WATT (lattr%)
5260   READ lattr%(0), lattr%(1), lattr%(2), lattr%(3)
5270 END DEFine RD_WATT
