100   get_dev
110  PRINT "This program needs the 'ouptr_bin', 'is_extn' and 'current_line' extensions found on "&mdev$
120  PRINT "Type 'l' to load them. Type 'p' to proceed with the deletion"
130  REMark "current_line"  is in "cline_bin"
140 REMark read the documentation in the help subdirectory
150 REMark also for the 'current_line' keyword
160 REMark lrespr cline_bin before using this prog.
170 REMark if the "restoring" message always refers to 0, then you haven't loaded cline_bin
180 :
190 auto%=0:stop%=0:keep%=0
200 a$=cmd$
210 IF a$<>""
220   IF "auto" INSTR a$:auto%=1
230   IF "stop" INSTR a$:stop%=1
240   IF "keep" INSTR a$:keep%=1: REMark keep binaries
250 END IF
260 IF auto%:p
270 IF stop%:QUIT
280 :
290 DEFine PROCedure p
300   IF NOT IS_EXTN ('CURRENT_LINE')
310      PRINT "cline_bin not loaded"
320   ELSE
330      get_dev
340      delete_all_targets
350   END IF
360 END DEFine p
370 :
380 DEFine PROCedure get_dev
390   mdev$="DEV8_"
400 END DEFine get_dev
410 :
420 DEFine PROCedure delete_all_targets
430 REMark this deletes all targets and all intermediary targets, thus forcing
440 REMark a complete rebuild. Caution, this also deletes all lib and rel files
450   PRINT "Deleting all files with the extension rel"
460   delete_files_with_extension "rel",mdev$,1
470   PRINT "Deleting all files with the extension err"
480   delete_files_with_extension "err",mdev$,1
490   COPY mdev$&"minerva_inc_err_copy",mdev$&"minerva_inc_err"
500   COPY mdev$&"keys_err_copy",mdev$&"keys_err"
510   PRINT "Deleting all files with the extension map"
520   delete_files_with_extension "map",mdev$,1
530   PRINT "Deleting all files with the extension log"
540   delete_files_with_extension "log",mdev$,1
550   PRINT "Deleting all files with the extension lib"
560   delete_files_with_extension "lib",mdev$,1
570   generic
580   atari
590   gold
600   q40
610   qxl
620   aurora
630   java
640   ptrgen
650   qpc
660   q68
670 END DEFine delete_all_targets
680 :
690 DEFine PROCedure generic
700   read_datas CURRENT_LINE
710   delete_files
720   DATA "dd_qlnd_new" ,"dd_nd_new","dv3_test",'smsq_sbas_lang','smsq_sbas_control'
730   DATA "smsq_sbas_procs_x","smsq_smsq_cache","smsq_smsq_cache40c","smsq_smsq_fh_os"
740   DATA "smsq_smsq_hotkey","smsq_smsq_lang","smsq_smsq_loader","smsq_sbas_lang"
750   DATA "smsq_smsq_os","smsq_smsq_1mb_os","smsq_smsq_vers","smsq_smsq_wman"
760   DATA "util_thg_thing",'smsq_home_home','smsq_recent_recent',"smsq_sbas_lang"
770   DATA "",""
780 END DEFine generic
790 :
800 DEFine PROCedure atari
810   read_datas CURRENT_LINE
820   delete_files
830   DATA "smsq_atari_driver_dv3","smsq_atari_driver_ser","smsq_atari_driver_ql"
840   DATA "smsq_atari_driver_mono","smsq_atari_hwinit","smsq_atari_nasty"
850   DATA "smsq_atari_kbd_lang"
860   DATA "sys_boot_st_host","smsq_atari_sysspr"
870   DATA "","smsq_atari_SMSQ.PRG",""
880 END DEFine atari_targets
890 :
900 DEFine PROCedure gold
910   read_datas CURRENT_LINE
920   delete_files
930   DATA "smsq_gold_driver_dv3","smsq_gold_driver_most","smsq_gold_driver_nd","smsq_gold_driver_8"
940   DATA "smsq_gold_driver_nds","smsq_gold_driver_ql","smsq_gold_host"
950   DATA "smsq_gold_hwinit","smsq_gold_kbd_abc","smsq_gold_kbd_abc_lang"
960   DATA "smsq_gold_kbd_lang","smsq_gold_nasty","smsq_gold_nasty_s"
970   DATA "smsq_gold_qimi","smsq_gold_roms","sys_gold_boot"
980   DATA "smsq_gold_sysspr"
990   data  ""
1000   data  "smsq_gold_gold8","smsq_gold_gold"
1010   data  ""
1020 END DEFine gold
1030 :
1040 DEFine PROCedure java
1050   read_datas CURRENT_LINE
1060   delete_files
1070   DATA "smsq_java_driver_most","smsq_java_driver_ql","smsq_java_host","smsq_java_smsq_os"
1080   DATA "smsq_java_driver_16"
1090   DATA "smsq_java_driver_8","smsq_java_smsq_1mb_os"
1100   DATA "smsq_java_driver_nfa_nfa","smsq_java_smsq_os"
1110   DATA "smsq_java_hwinit","smsq_java_host","smsq_java_sysspr"
1120   DATA "smsq_java_driver_dv3e","smsq_java_ip_x"
1130   DATA ""
1140   DATA "SMSQ_java_java"
1150   DATA ""
1160 END DEFine java
1170 :
1180 DEFine PROCedure qpc
1190   read_datas CURRENT_LINE
1200   delete_files
1210   DATA 'smsq_qpc_host'
1220   DATA 'smsq_smsq_qpc_os'
1230   DATA 'smsq_qpc_hwinit'
1240   DATA 'smsq_qpc_nasty_e'
1250   DATA 'smsq_qpc_kbd_lang'
1260   DATA 'smsq_qpc_driver_most'
1270   DATA 'smsq_qpc_driver_ql'
1280   DATA 'smsq_qpc_driver_16'
1290   DATA 'smsq_qpc_driver_8'
1300   DATA 'smsq_qpc_sysspr'
1310   DATA 'smsq_qpc_driver_dv3e'
1320   DATA 'smsq_qpc_procs_x'
1330   DATA 'smsq_qpc_cdaudio_x'
1340   DATA 'smsq_qpc_dos_x'
1350   DATA 'smsq_qpc_ip_x'
1360   DATA ""
1370   DATA "smsq_qpc_smsqe.bin"
1380   DATA ""
1390 END DEFine qpc
1400 :
1410 DEFine PROCedure aurora
1420   read_datas CURRENT_LINE
1430   delete_files
1440   DATA "smsq_gold_driver_dv3","smsq_gold_driver_most","smsq_gold_driver_nd"
1450   DATA "smsq_gold_driver_nds","smsq_gold_driver_ql","smsq_gold_host"
1460   DATA "smsq_gold_hwinit","smsq_gold_kbd_abc","smsq_gold_kbd_abc_lang"
1470   DATA "smsq_gold_kbd_lang","smsq_gold_nasty","smsq_gold_nasty_s"
1480   DATA "smsq_gold_qimi","smsq_gold_roms","sys_gold_boot"
1490   DATA "smsq_gold_sysspr","smsq_aurora_driver_8","smsq_aurora_sysspr"
1500   data ""
1510   DATA "smsq_aurora_smsqe","smsq_gold_gold","smsq_gold_gold8"
1520   data ""
1530 END DEFine aurora
1540 :
1550 DEFine PROCedure q40
1560   read_datas CURRENT_LINE
1570   delete_files
1580   DATA "smsq_q40_driver_dv3","smsq_q40_driver_16","smsq_q40_cache"
1590   DATA "smsq_q40_driver_ser","smsq_q40_driver_ser","smsq_q40_hwinit"
1600   DATA "smsq_q40_kbd_lang","smsq_q40_nasty"
1610   DATA "smsq_q40_driver_ql","smsq_sbas_procs_x_q60"
1620   DATA "smsq_q40_sysspr","smsq_q40_cachemode"
1630   DATA ""
1640   data "smsq_q40_rom"
1650   DATA "smsq_q40_rom_lo","smsq_q40_rom_hi"
1660   data ""
1670 END DEFine q40
1680 :
1690 DEFine PROCedure qxl
1700   read_datas CURRENT_LINE
1710   delete_files
1720   DATA "smsq_qxl_driver_16","smsq_qxl_driver_nd","smsq_qxl_driver_most"
1730   DATA "smsq_qxl_ecache","smsq_qxl_driver_ql","smsq_qxl_host","smsq_qxl_hwinit"
1740   DATA "smsq_qxl_kbd_lang","smsq_qxl_nasty_e","smsq_qxl_procs_x"
1750   DATA "smsq_qxl_test","smsq_qxl_driver_dv3e","smsq_qxl_sysspr"
1760   DATA ""
1770   data "smsq_qxl_smsqe.exe"
1780   data ""
1790 END DEFine qxl
1800 :
1810 DEFine PROCedure q68
1820   read_datas CURRENT_LINE
1830   delete_files
1840   DATA "smsq_q68_driver_16","smsq_q68_driver_nd","smsq_q68_driver_most"
1850   DATA "smsq_q68_driver_ql","smsq_q68_hwinit" ,"smsq_smsq_q68_os"
1860   DATA "smsq_q68_kbd_lang","smsq_q68_nasty","smsq_q68_sbas_procs_x"
1870   DATA "smsq_q68_driver_dv3","smsq_q40_sysspr","smsq_q68_driver_8"
1880   DATA ""
1890   data "smsq_q68_QL_RAM.BIN","smsq_q68_QL_RAM.BIN3"
1900   data ""
1910 END DEFine q68
1920 :
1930 DEFine PROCedure ptrgen
1940   read_datas CURRENT_LINE
1950   delete_files
1960   DATA ""
1970   DATA "ee_wman_wman","ee_ptr_gen","ee_hot_rext_german","ee_hot_rext_french","ee_hot_rext_english",""
1980 END DEFine ptrgen
1990 :
2000 DEFine PROCedure read_datas (line_number)
2010 REMark this reads the datas as of the line number given as params
2020 REMark and puts them in the global arrays delete$ and keep$
2030 LOCal lp%,a$
2040   DIM delete$(100,60)
2050   RESTORE line_number                   : REMark start reading from here
2060   PRINT "restoring : ";line_number
2070   FOR lp% = 0 TO 100
2080     READ a$                             : REMark get filename
2090     IF a$ ="":EXIT lp%                  : REMark finished
2100     delete$(lp%)=mdev$&a$
2110   END FOR lp%
2120   DIM keep$(100,60)
2130   FOR lp% = 0 TO 100
2140     READ a$                             : REMark get filename
2150     IF a$ ="":EXIT lp%                  : REMark finished
2160     keep$(lp%)=mdev$&a$
2170   END FOR lp%
2180 END DEFine read_datas
2190 :
2200 DEFine PROCedure delete_files
2210 REMark delete all files held in delete$
2220 LOCal stop_here%,lp%
2230   stop_here%=DIMN(delete$,1)
2240   FOR lp%=0 TO stop_here%
2250     IF delete$(lp%)="":EXIT lp%
2260     DELETE delete$(lp%)
2270   END FOR lp%
2280   IF NOT keep%
2290     stop_here%=DIMN(keep$,1)
2300     FOR lp%=0 TO stop_here%
2310       IF keep$(lp%)="":EXIT lp%
2320       DELETE keep$(lp%)
2330     END FOR lp%
2340   END IF
2350 END DEFine delete_files
2360 :
2370 DEFine PROCedure sa
2380   SAVE_O dev8_extras_del_all_bas
2390 END DEFine sa
2400 :
2410 DEFine PROCedure delete_files_with_extension(ext$,mdir$,ddown%)
2420 REMark this deletes all files in the dir mdir$ with the extension ext$
2430 REMark possibly drilling down to all subirs(ddown%<>0) to do the same there
2440 REMark by calling itself recursively
2450 LOCal chan%,lp%,myfile$,a$,device$,t%,exlen%
2460   FOR lp%=1 TO 50
2470     myfile$="ram1_dele_extns"&RND(1 TO 80)&RND (1 TO 80) & RND (1 TO 50)
2480     chan%=FOP_OVER (myfile$)            : REMark try to open unique file
2490     IF chan%>0:EXIT lp%
2500   END FOR lp%
2510   IF chan%<0:RETurn                     : REMark ooops!!!!
2520   exlen%=LEN(ext$)
2530   device$=mdir$(1 TO 5)
2540   WDIR#chan%,mdir$                      : REMark dir of this rep in file
2550   GET#chan%\0                           : REMark reset file pointer to start
2560   REPeat lp%
2570     IF EOF(#chan%):EXIT lp%
2580     INPUT#chan%,a$                      : REMark get filename
2590     IF a$="":NEXT lp%
2600     a$=device$&a$
2610     t%= ' ->' INSTR a$                  : REMark is it a subdir?
2620     IF t% AND ddown%
2630       delete_files_with_extension ext$,a$(1 TO t%-1),ddown% : REMark yes, possibly drill down
2640       NEXT lp%
2650     END IF
2660     t%=LEN(a$)
2670     IF t%<exlen%:NEXT lp%               : REMark filename is shorter than extension
2680     IF a$(t%-exlen%+1 TO) ==ext$ : DELETE a$
2690   END REPeat lp%
2700   CLOSE#chan%
2710   DELETE myfile$
2720 END DEFine delete_files_with_extension
2730 :
2740 DEFine PROCedure l
2750   LRESPR "dev8_extras_source_outptr_bin"
2760   LRESPR "dev8_extras_blinker_bin"
2770   LRESPR "dev8_extras_cline_bin"
2780 END DEFine l
