100 REMark read the documentation in the help subdirectory
110 REMark also for the 'current_line' keyword
120 REMark lrespr cline_bin before using this prog.
130 REMark if the "restoring" message always refers to 0, then you haven't loaded cline_bin
140 :
150 DEFine PROCedure p
160   IF NOT IS_EXTN ('CURRENT_LINE')
170      PRINT "cline_bin not loaded"
180   ELSE
190      delete_all_targets
200   END IF
210 END DEFine p
220 :
230 DEFine PROCedure delete_all_targets
240 REMark this deletes all targets and all intermediary targets, thus forcing
250 REMark a complete rebuild. Caution, this also deletes all lib and rel files
260   delete_files_with_extension "rel","dev8_",1
270   delete_files_with_extension "err","dev8_",1
280   delete_files_with_extension "map","dev8_",1
290   delete_files_with_extension "log","dev8_",1
300   COPY_O dev8_minerva_inc_ERR_copy,  dev8_minerva_inc_ERR
310   COPY_O dev8_keys_err_copy,  dev8_keys_err
320   generic
330   atari
340   gold
350   q40
360   qxl
370 END DEFine delete_all_targets
380 :
390 DEFine PROCedure generic
400   generic_libs
410   generic_targets
420 END DEFine generic
430 :
440 DEFine PROCedure generic_libs
450   read_datas CURRENT_LINE
460   delete_files
470   DATA "dd_dev_lib","dd_nd_lib","dd_qlnd_lib","dd_rd_lib","dd_rd_fm_lib"
480   DATA "dv3_bbx_lib","dv3_dv3_lib","dv3_fd_cct","dv3_msd_lib"
490   DATA "dv3_qlf_lib","ee_button_lib","ee_hk_lib","ee_hot_lib","ee_hxt_lib"
500   DATA "ee_procs_lib","ee_procs_rext","ee_wman_lib","ee_wman_wman"
510   DATA "iod_history_lib","iod_iob_lib","iod_iou_lib","iod_iox_lib"
520   DATA "iod_nul_lib","iod_par_lib","iod_pipe_lib","iod_ser_lib"
530   DATA "lang_english_lib","lang_french_lib","lang_german_lib"
540   DATA "sbsext_ed_lib","sbsext_ext_lib","sbsext_ut_lib","smsq_cr_lib"
550   DATA "smsq_hwt_lib","smsq_init_lib","smsq_io_lib","smsq_ioa_lib","smsq_ioq_lib"
560   DATA "smsq_kbd_ibm_lib","smsq_mem_lib","smsq_mem_1mb_lib","smsq_qa_lib"
570   DATA "smsq_qd_lib","smsq_sbas_lib","smsq_sbas_procs_lib","smsq_shd_lib"
580   DATA "smsq_sms_lib","smsq_spp_lib","smsq_uq_lib","sys_sdp_lib","util_cv_lib"
590   DATA "util_dummy_lib","util_gut_lib","util_pars_lib","util_thg_lib"
600   DATA "sys_boot_lib","smsq_mem_fh_lib"
610   DATA ""
620 END DEFine generic_libs
630 :
640 DEFine PROCedure generic_targets
650   read_datas CURRENT_LINE
660   delete_files
670   DATA "dd_qlnd_new" ,"dd_nd_new","dv3_test",'smsq_sbas_lang','smsq_sbas_control'
680   DATA "smsq_sbas_procs_x","smsq_smsq_cache","smsq_smsq_cache40c","smsq_smsq_fh_os"
690   DATA "smsq_smsq_hotkey","smsq_smsq_lang","smsq_smsq_loader"
700   DATA "smsq_smsq_os","smsq_smsq_1mb_os","smsq_smsq_vers","smsq_smsq_wman"
710   DATA "util_thg_thing"
720   DATA ""
730 END DEFine generic_targets
740 :
750 DEFine PROCedure atari
760  atari_libs
770  atari_targets
780 END DEFine atari
790 :
800 DEFine PROCedure atari_libs
810   read_datas CURRENT_LINE
820   delete_files
830   DATA "dv3_atari_lib","iod_con2_atmono_lib","iod_con2_atql_lib"
840   DATA "smsq_atari_hdop_lib","smsq_atari_kbd_lib","smsq_atari_par_lib"
850   DATA "smsq_atari_ser_lib","sys_boot_st_lib"
860   DATA ""
870 END DEFine atari_libs
880 :
890 DEFine PROCedure atari_targets
900   read_datas CURRENT_LINE
910   delete_files
920   DATA "smsq_atari_driver_dv3","smsq_atari_driver_ser","smsq_atari_driver_ql"
930   DATA "smsq_atari_driver_mono","smsq_atari_hwinit","smsq_atari_nasty"
940   DATA "smsq_atari_kbd_lang","smsq_atari_SMSQ.PRG"
950   DATA "sys_boot_st_host"
960   DATA ""
970 END DEFine atari_targets
980 :
990 DEFine PROCedure gold
1000  gold_libs
1010  gold_targets
1020 END DEFine gold
1030 :
1040 DEFine PROCedure gold_libs
1050   read_datas CURRENT_LINE
1060   delete_files
1070   DATA "dv3_gold_lib","iod_con2_goldql_lib","smsq_gold_par_lib","smsq_gold_ser_lib"
1080   DATA ""
1090 END DEFine gold_libs
1100 :
1110 DEFine PROCedure gold_targets
1120   read_datas CURRENT_LINE
1130   delete_files
1140   DATA "smsq_gold_driver_dv3","smsq_gold_driver_most","smsq_gold_driver_nd"
1150   DATA "smsq_gold_driver_nds","smsq_gold_driver_ql","smsq_gold_host"
1160   DATA "smsq_gold_hwinit","smsq_gold_kbd_abc","smsq_gold_kbd_abc_lang"
1170   DATA "smsq_gold_kbd_lang","smsq_gold_nasty","smsq_gold_nasty_s"
1180   DATA "smsq_gold_qimi","smsq_gold_roms","sys_gold_boot","smsq_gold_gold"
1190   DATA ""
1200 END DEFine gold_targets
1210 :
1220 DEFine PROCedure q40
1230   q40_libs
1240   q40_targets
1250 END DEFine q40
1260 :
1270 DEFine PROCedure q40_libs
1280   read_datas CURRENT_LINE
1290   delete_files
1300   DATA "dv3_q40_lib","iod_con2_q40ql_lib","iod_con2_q4016_lib","sbsext_ext_q60_lib"
1310   DATA ""
1320 END DEFine q40_libs
1330 :
1340 DEFine PROCedure q40_targets
1350   read_datas CURRENT_LINE
1360   delete_files
1370   DATA "smsq_q40_driver_dv3","smsq_q40_driver_16","smsq_q40_cache"
1380   DATA "smsq_q40_driver_ser","smsq_q40_driver_ser","smsq_q40_hwinit"
1390   DATA "smsq_q40_kbd_lang","smsq_q40_nasty","smsq_q40_rom"
1400   DATA "smsq_q40_rom_lo","smsq_q40_rom_hi"
1410   DATA "smsq_q40_driver_ql","smsq_sbas_procs_x_q60"
1420   DATA ""
1430 END DEFine q40_targets
1440 :
1450 DEFine PROCedure qxl
1460   qxl_libs
1470   qxl_targets
1480 END DEFine qxl
1490 :
1500 DEFine PROCedure qxl_libs
1510   read_datas CURRENT_LINE
1520   delete_files
1530   DATA "dv3_qxl_lib","iod_con2_qxl16_lib","iod_con2_qxlql_lib","smsq_qxl_hdop_lib"
1540   DATA ""
1550 END DEFine qxl_libs
1560 :
1570 DEFine PROCedure qxl_targets
1580   read_datas CURRENT_LINE
1590   delete_files
1600   DATA "smsq_qxl_driver_16","smsq_qxl_driver_nd","smsq_qxl_driver_most"
1610   DATA "smsq_qxl_ecache","smsq_qxl_driver_ql","smsq_qxl_host","smsq_qxl_hwinit"
1620   DATA "smsq_qxl_kbd_lang","smsq_qxl_nasty_e","smsq_qxl_procs_x","smsq_qxl_smsqe.exe"
1630   DATA "smsq_qxl_test","smsq_qxl_driver_dv3e"
1640   DATA ""
1650 END DEFine qxl_targets
1660 :
1670 DEFine PROCedure read_datas (line_number)
1680 REMark this reads the datas as of the line number given as params
1690 REMark and puts them in the global array delete$
1700 LOCal lp%,a$
1710   DIM delete$(100,100)
1720   RESTORE line_number                   : REMark start reading from here
1730   PRINT "restoring : ";line_number
1740   FOR lp% = 0 TO 100
1750     READ a$                             : REMark get filename
1760     IF a$ ="":EXIT lp%                  : REMark finished
1770     delete$(lp%)="dev8_"&a$
1780   END FOR lp%
1790 END DEFine read_datas
1800 :
1810 DEFine PROCedure delete_files
1820 REMark delete all files held in delete$
1830 LOCal stop_here%,lp%
1840   stop_here%=DIMN(delete$,1)
1850   FOR lp%=0 TO stop_here%
1860     IF delete$(lp%)="":EXIT lp%
1870     DELETE delete$(lp%)
1880   END FOR lp%
1890 END DEFine delete_files
1900 :
1910 DEFine PROCedure sa
1920   SAVE_O dev8_extras_del_all_bas
1930 END DEFine sa
1940 :
1950 DEFine PROCedure delete_files_with_extension(ext$,mdir$,ddown%)
1960 REMark this deletes all files in the dir mdir$ with the extension ext$
1970 REMark possibly drilling down to all subirs(ddown%<>0) to do the same there
1980 REMark by calling itself recursively
1990 LOCal chan%,lp%,myfile$,a$,device$,t%,exlen%
2000   FOR lp%=1 TO 50
2010     myfile$="ram1_dele_extns"&RND(1 TO 80)&RND (1 TO 80) & RND (1 TO 50)
2020     chan%=FOP_OVER (myfile$)            : REMark try to open unique file
2030     IF chan%>0:EXIT lp%
2040   END FOR lp%
2050   IF chan%<0:RETurn                     : REMark ooops!!!!
2060   exlen%=LEN(ext$)
2070   device$=mdir$(1 TO 5)
2080   WDIR#chan%,mdir$                      : REMark dir of this rep in file
2090   GET#chan%\0                           : REMark reset file pointer to start
2100   REPeat lp%
2110     IF EOF(#chan%):EXIT lp%
2120     INPUT#chan%,a$                      : REMark get filename
2130     IF a$="":NEXT lp%
2140     a$=device$&a$
2150     t%= ' ->' INSTR a$                  : REMark is it a subdir?
2160     IF t% AND ddown%
2170       delete_files_with_extension ext$,a$(1 TO t%-1),ddown% : REMark yes, possibly drill down
2180       NEXT lp%
2190     END IF
2200     t%=LEN(a$)
2210     IF t%<exlen%:NEXT lp%               : REMark filename is shorter than extension
2220     IF a$(t%-exlen%+1 TO) ==ext$ : DELETE a$
2230   END REPeat lp%
2240   CLOSE#chan%
2250   DELETE myfile$
2260 END DEFine delete_files_with_extension
2270 :
