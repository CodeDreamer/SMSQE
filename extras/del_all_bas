100   get_dev
110  PRINT "This program needs the 'ouptr_bin', 'is_extn' and 'current_line' extensions found on "&mdev$
120  PRINT "Type 'l' to load them. Type 'p' to proceed with the deletion"
130  REMark "current_line"  is in "cline_bin"
140 REMark read the documentation in the help subdirectory
150 REMark also for the 'current_line' keyword
160 REMark lrespr cline_bin before using this prog.
170 REMark if the "restoring" message always refers to 0, then you haven't loaded cline_bin
180 :
190 auto%=0:stop%=0:keep%=0
200 a$=cmd$
210 IF a$<>""
220   IF "auto" INSTR a$:auto%=1
230   IF "stop" INSTR a$:stop%=1
240   IF "keep" INSTR a$:keep%=1: REMark keep binaries
250 END IF
260 IF auto%:p
270 IF stop%:QUIT
280 :
290 DEFine PROCedure p
300   IF NOT IS_EXTN ('CURRENT_LINE')
310      PRINT "cline_bin not loaded"
320   ELSE
330      get_dev
340      delete_all_targets
350   END IF
360 END DEFine p
370 :
380 DEFine PROCedure get_dev
390   mdev$="DEV8_"
400 END DEFine get_dev
410 :
420 DEFine PROCedure delete_all_targets
430 REMark this deletes all targets and all intermediary targets, thus forcing
440 REMark a complete rebuild. Caution, this also deletes all lib and rel files
450   PRINT "Deleting all files with the extension rel"
460   delete_files_with_extension "rel",mdev$,1
470   PRINT "Deleting all files with the extension err"
480   delete_files_with_extension "err",mdev$,1
490   COPY mdev$&"minerva_inc_err_copy",mdev$&"minerva_inc_err"
500   COPY mdev$&"keys_err_copy",mdev$&"keys_err"
510   PRINT "Deleting all files with the extension map"
520   delete_files_with_extension "map",mdev$,1
530   PRINT "Deleting all files with the extension log"
540   delete_files_with_extension "log",mdev$,1
550   PRINT "Deleting all files with the extension lib"
560   delete_files_with_extension "lib",mdev$,1
570   generic
580   atari
590   gold
600   q40
610   qxl
620   aurora
630   java
640   ptrgen
650   qpc
660 END DEFine delete_all_targets
670 :
680 DEFine PROCedure generic
690   read_datas CURRENT_LINE
700   delete_files
710   DATA "dd_qlnd_new" ,"dd_nd_new","dv3_test",'smsq_sbas_lang','smsq_sbas_control'
720   DATA "smsq_sbas_procs_x","smsq_smsq_cache","smsq_smsq_cache40c","smsq_smsq_fh_os"
730   DATA "smsq_smsq_hotkey","smsq_smsq_lang","smsq_smsq_loader"
740   DATA "smsq_smsq_os","smsq_smsq_1mb_os","smsq_smsq_vers","smsq_smsq_wman"
750   DATA "util_thg_thing",""
760   DATA ""
770 END DEFine generic
780 :
790 DEFine PROCedure atari
800   read_datas CURRENT_LINE
810   delete_files
820   DATA "smsq_atari_driver_dv3","smsq_atari_driver_ser","smsq_atari_driver_ql"
830   DATA "smsq_atari_driver_mono","smsq_atari_hwinit","smsq_atari_nasty"
840   DATA "smsq_atari_kbd_lang"
850   DATA "sys_boot_st_host","smsq_atari_sysspr"
860   DATA "","smsq_atari_SMSQ.PRG",""
870 END DEFine atari_targets
880 :
890 DEFine PROCedure gold
900   read_datas CURRENT_LINE
910   delete_files
920   DATA "smsq_gold_driver_dv3","smsq_gold_driver_most","smsq_gold_driver_nd","smsq_gold_driver_8"
930   DATA "smsq_gold_driver_nds","smsq_gold_driver_ql","smsq_gold_host"
940   DATA "smsq_gold_hwinit","smsq_gold_kbd_abc","smsq_gold_kbd_abc_lang"
950   DATA "smsq_gold_kbd_lang","smsq_gold_nasty","smsq_gold_nasty_s"
960   DATA "smsq_gold_qimi","smsq_gold_roms","sys_gold_boot"
970   DATA "smsq_gold_sysspr","","smsq_gold_gold8","smsq_gold_gold",""
980 END DEFine gold
990 :
1000 DEFine PROCedure java
1010   read_datas CURRENT_LINE
1020   delete_files
1030   DATA "smsq_java_driver_most","smsq_java_driver_ql","smsq_java_host","smsq_java_smsq_os"
1040   DATA "smsq_java_driver_16"
1050   DATA "smsq_java_driver_8","smsq_java_smsq_1mb_os"
1060   DATA "smsq_java_driver_nfa_nfa","smsq_java_rtc","smsq_java_smsq_os"
1070   DATA "smsq_java_hwinit","smsq_java_host",""
1080   DATA "SMSQ_java_java","smsq_java_driver_dv3e"
1090   DATA ""
1100 END DEFine java
1110 :
1120 DEFine PROCedure qpc
1130   read_datas CURRENT_LINE
1140   delete_files
1150   DATA 'smsq_qpc_host'
1160   DATA 'smsq_qpc_hwinit'
1170   DATA 'smsq_smsq_loader'
1180   DATA 'smsq_smsq_qpc_os'
1190   DATA 'smsq_smsq_cache'
1200   DATA 'smsq_sbas_control'
1210   DATA 'smsq_qpc_nasty_e'
1220   DATA 'smsq_smsq_lang'
1230   DATA 'smsq_qpc_kbd_lang'
1240   DATA 'smsq_sbas_lang'
1250   DATA 'smsq_sbas_procs_x'
1260   DATA 'smsq_qpc_driver_most'
1270   DATA 'smsq_qpc_driver_ql'
1280   DATA 'smsq_qpc_driver_16'
1290   DATA 'smsq_qpc_driver_8'
1300   DATA 'smsq_smsq_wman'
1310   DATA 'smsq_qpc_sysspr'
1320   DATA 'smsq_qpc_driver_dv3e'
1330   DATA 'smsq_smsq_hotkey'
1340   DATA 'smsq_qpc_procs_x'
1350   DATA 'smsq_qpc_cdaudio_x'
1360   DATA 'smsq_qpc_dos_x'
1370   DATA 'smsq_qpc_ip_x'
1380   DATA 'smsq_home_home'
1390   DATA "qmon_qpc_smsq"
1400   DATA ""
1410   DATA "smsq_qpc_smsqe_bin"
1420   DATA ""
1430 END DEFine qpc
1440 :
1450 DEFine PROCedure aurora
1460   read_datas CURRENT_LINE
1470   delete_files
1480   DATA "smsq_gold_driver_dv3","smsq_gold_driver_most","smsq_gold_driver_nd"
1490   DATA "smsq_gold_driver_nds","smsq_gold_driver_ql","smsq_gold_host"
1500   DATA "smsq_gold_hwinit","smsq_gold_kbd_abc","smsq_gold_kbd_abc_lang"
1510   DATA "smsq_gold_kbd_lang","smsq_gold_nasty","smsq_gold_nasty_s"
1520   DATA "smsq_gold_qimi","smsq_gold_roms","sys_gold_boot"
1530   DATA "smsq_gold_sysspr","smsq_aurora_driver_8","smsq_aurora_sysspr",""
1540   DATA "smsq_aurora_smsqe","smsq_gold_gold","smsq_gold_gold8", ""
1550 END DEFine aurora
1560 :
1570 DEFine PROCedure q40
1580   read_datas CURRENT_LINE
1590   delete_files
1600   DATA "smsq_q40_driver_dv3","smsq_q40_driver_16","smsq_q40_cache"
1610   DATA "smsq_q40_driver_ser","smsq_q40_driver_ser","smsq_q40_hwinit"
1620   DATA "smsq_q40_kbd_lang","smsq_q40_nasty"
1630   DATA "smsq_q40_driver_ql","smsq_sbas_procs_x_q60"
1640   DATA "smsq_q40_sysspr","smsq_q40_cachemode"
1650   DATA "","smsq_q40_rom"
1660   DATA "smsq_q40_rom_lo","smsq_q40_rom_hi",""
1670 END DEFine q40
1680 :
1690 DEFine PROCedure qxl
1700   read_datas CURRENT_LINE
1710   delete_files
1720   DATA "smsq_qxl_driver_16","smsq_qxl_driver_nd","smsq_qxl_driver_most"
1730   DATA "smsq_qxl_ecache","smsq_qxl_driver_ql","smsq_qxl_host","smsq_qxl_hwinit"
1740   DATA "smsq_qxl_kbd_lang","smsq_qxl_nasty_e","smsq_qxl_procs_x"
1750   DATA "smsq_qxl_test","smsq_qxl_driver_dv3e","smsq_qxl_sysspr"
1760   DATA "","smsq_qxl_smsqe.exe",""
1770 END DEFine qxl
1780 :
1790 DEFine PROCedure ptrgen
1800   read_datas CURRENT_LINE
1810   delete_files
1820   DATA ""
1830   DATA "ee_wman_wman","ee_ptr_gen","ee_hot_rext_german","ee_hot_rext_french","ee_hot_rext_english",""
1840 END DEFine ptrgen
1850 :
1860 DEFine PROCedure read_datas (line_number)
1870 REMark this reads the datas as of the line number given as params
1880 REMark and puts them in the global arrays delete$ and keep$
1890 LOCal lp%,a$
1900   DIM delete$(100,60)
1910   RESTORE line_number                   : REMark start reading from here
1920   PRINT "restoring : ";line_number
1930   FOR lp% = 0 TO 100
1940     READ a$                             : REMark get filename
1950     IF a$ ="":EXIT lp%                  : REMark finished
1960     delete$(lp%)=mdev$&a$
1970   END FOR lp%
1980   DIM keep$(100,60)
1990   FOR lp% = 0 TO 100
2000     READ a$                             : REMark get filename
2010     IF a$ ="":EXIT lp%                  : REMark finished
2020     keep$(lp%)=mdev$&a$
2030   END FOR lp%
2040 END DEFine read_datas
2050 :
2060 DEFine PROCedure delete_files
2070 REMark delete all files held in delete$
2080 LOCal stop_here%,lp%
2090   stop_here%=DIMN(delete$,1)
2100   FOR lp%=0 TO stop_here%
2110     IF delete$(lp%)="":EXIT lp%
2120     DELETE delete$(lp%)
2130   END FOR lp%
2140   IF NOT keep%
2150     stop_here%=DIMN(keep$,1)
2160     FOR lp%=0 TO stop_here%
2170       IF keep$(lp%)="":EXIT lp%
2180       DELETE keep$(lp%)
2190     END FOR lp%
2200   END IF
2210 END DEFine delete_files
2220 :
2230 DEFine PROCedure sa
2240   SAVE_O dev8_extras_del_all_bas
2250 END DEFine sa
2260 :
2270 DEFine PROCedure delete_files_with_extension(ext$,mdir$,ddown%)
2280 REMark this deletes all files in the dir mdir$ with the extension ext$
2290 REMark possibly drilling down to all subirs(ddown%<>0) to do the same there
2300 REMark by calling itself recursively
2310 LOCal chan%,lp%,myfile$,a$,device$,t%,exlen%
2320   FOR lp%=1 TO 50
2330     myfile$="ram1_dele_extns"&RND(1 TO 80)&RND (1 TO 80) & RND (1 TO 50)
2340     chan%=FOP_OVER (myfile$)            : REMark try to open unique file
2350     IF chan%>0:EXIT lp%
2360   END FOR lp%
2370   IF chan%<0:RETurn                     : REMark ooops!!!!
2380   exlen%=LEN(ext$)
2390   device$=mdir$(1 TO 5)
2400   WDIR#chan%,mdir$                      : REMark dir of this rep in file
2410   GET#chan%\0                           : REMark reset file pointer to start
2420   REPeat lp%
2430     IF EOF(#chan%):EXIT lp%
2440     INPUT#chan%,a$                      : REMark get filename
2450     IF a$="":NEXT lp%
2460     a$=device$&a$
2470     t%= ' ->' INSTR a$                  : REMark is it a subdir?
2480     IF t% AND ddown%
2490       delete_files_with_extension ext$,a$(1 TO t%-1),ddown% : REMark yes, possibly drill down
2500       NEXT lp%
2510     END IF
2520     t%=LEN(a$)
2530     IF t%<exlen%:NEXT lp%               : REMark filename is shorter than extension
2540     IF a$(t%-exlen%+1 TO) ==ext$ : DELETE a$
2550   END REPeat lp%
2560   CLOSE#chan%
2570   DELETE myfile$
2580 END DEFine delete_files_with_extension
2590 :
2600 DEFine PROCedure l
2610   LRESPR "dev8_extras_source_outptr_bin"
2620   LRESPR "dev8_extras_blinker_bin"
2630   LRESPR "dev8_extras_cline_bin"
2640 END DEFine l
