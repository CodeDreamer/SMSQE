100 REMark read the documentation in the help subdirectory
110 REMark also for the 'current_line' keyword
120 REMark lrespr cline_bin before using this prog.
130 REMark if the "restoring" message always refers to 0, then you haven't loaded cline_bin
140 :
150 DEFine PROCedure p
160   delete_all_targets
170 END DEFine p
180 :
190 DEFine PROCedure delete_all_targets
200 REMark this deletes all targets and all intermediary targets, thus forcing
210 REMark a complete rebuild. Caution, this also deletes all lib and rel files
220   delete_files_with_extension "rel","dev8_",1
230   delete_files_with_extension "err","dev8_",1
240   delete_files_with_extension "map","dev8_",1
250   delete_files_with_extension "log","dev8_",1
260   COPY_O dev8_minerva_inc_ERR_copy,  dev8_minerva_inc_ERR
270   COPY_O dev8_keys_err_copy,  dev8_keys_err
280   generic
290   atari
300   gold
310   q40
320   qxl
330 END DEFine delete_all_targets
340 :
350 DEFine PROCedure generic
360   generic_libs
370   generic_targets
380 END DEFine generic
390 :
400 DEFine PROCedure generic_libs
410   read_datas CURRENT_LINE
420   delete_files
430   DATA "dd_dev_lib","dd_nd_lib","dd_qlnd_lib","dd_rd_lib","dd_rd_fm_lib"
440   DATA "dv3_bbx_lib","dv3_dv3_lib","dv3_fd_cct","dv3_msd_lib"
450   DATA "dv3_qlf_lib","ee_button_lib","ee_hk_lib","ee_hot_lib","ee_hxt_lib"
460   DATA "ee_procs_lib","ee_procs_rext","ee_wman_lib","ee_wman_wman"
470   DATA "iod_history_lib","iod_iob_lib","iod_iou_lib","iod_iox_lib"
480   DATA "iod_nul_lib","iod_par_lib","iod_pipe_lib","iod_ser_lib"
490   DATA "lang_english_lib","lang_french_lib","lang_german_lib"
500   DATA "sbsext_ed_lib","sbsext_ext_lib","sbsext_ut_lib","smsq_cr_lib"
510   DATA "smsq_hwt_lib","smsq_init_lib","smsq_io_lib","smsq_ioa_lib","smsq_ioq_lib"
520   DATA "smsq_kbd_ibm_lib","smsq_mem_lib","smsq_mem_1mb_lib","smsq_qa_lib"
530   DATA "smsq_qd_lib","smsq_sbas_lib","smsq_sbas_procs_lib","smsq_shd_lib"
540   DATA "smsq_sms_lib","smsq_spp_lib","smsq_uq_lib","sys_sdp_lib","util_cv_lib"
550   DATA "util_dummy_lib","util_gut_lib","util_pars_lib","util_thg_lib"
560   DATA "sys_boot_lib","smsq_mem_fh_lib"
570   DATA ""
580 END DEFine generic_libs
590 :
600 DEFine PROCedure generic_targets
610   read_datas CURRENT_LINE
620   delete_files
630   DATA "dd_qlnd_new" ,"dd_nd_new","dv3_test",'smsq_sbas_lang','smsq_sbas_control'
640   DATA "smsq_sbas_procs_x","smsq_smsq_cache","smsq_smsq_cache40c","smsq_smsq_fh_os"
650   DATA "smsq_smsq_hotkey","smsq_smsq_lang","smsq_smsq_loader"
660   DATA "smsq_smsq_os","smsq_smsq_1mb_os","smsq_smsq_vers","smsq_smsq_wman"
670   DATA "util_thg_thing"
680   DATA ""
690 END DEFine generic_targets
700 :
710 DEFine PROCedure atari
720  atari_libs
730  atari_targets
740 END DEFine atari
750 :
760 DEFine PROCedure atari_libs
770   read_datas CURRENT_LINE
780   delete_files
790   DATA "dv3_atari_lib","iod_con2_atmono_lib","iod_con2_atql_lib"
800   DATA "smsq_atari_hdop_lib","smsq_atari_kbd_lib","smsq_atari_par_lib"
810   DATA "smsq_atari_ser_lib","sys_boot_st_lib"
820   DATA ""
830 END DEFine atari_libs
840 :
850 DEFine PROCedure atari_targets
860   read_datas CURRENT_LINE
870   delete_files
880   DATA "smsq_atari_driver_dv3","smsq_atari_driver_ser","smsq_atari_driver_ql"
890   DATA "smsq_atari_driver_mono","smsq_atari_hwinit","smsq_atari_nasty"
900   DATA "smsq_atari_kbd_lang","smsq_atari_SMSQ.PRG"
910   DATA "sys_boot_st_host"
920   DATA ""
930 END DEFine atari_targets
940 :
950 DEFine PROCedure gold
960  gold_libs
970  gold_targets
980 END DEFine gold
990 :
1000 DEFine PROCedure gold_libs
1010   read_datas CURRENT_LINE
1020   delete_files
1030   DATA "dv3_gold_lib","iod_con2_goldql_lib","smsq_gold_par_lib","smsq_gold_ser_lib"
1040   DATA ""
1050 END DEFine gold_libs
1060 :
1070 DEFine PROCedure gold_targets
1080   read_datas CURRENT_LINE
1090   delete_files
1100   DATA "smsq_gold_driver_dv3","smsq_gold_driver_most","smsq_gold_driver_nd"
1110   DATA "smsq_gold_driver_nds","smsq_gold_driver_ql","smsq_gold_host"
1120   DATA "smsq_gold_hwinit","smsq_gold_kbd_abc","smsq_gold_kbd_abc_lang"
1130   DATA "smsq_gold_kbd_lang","smsq_gold_nasty","smsq_gold_nasty_s"
1140   DATA "smsq_gold_qimi","smsq_gold_roms","sys_gold_boot","smsq_gold_gold"
1150   DATA ""
1160 END DEFine gold_targets
1170 :
1180 DEFine PROCedure q40
1190   q40_libs
1200   q40_targets
1210 END DEFine q40
1220 :
1230 DEFine PROCedure q40_libs
1240   read_datas CURRENT_LINE
1250   delete_files
1260   DATA "dv3_q40_lib","iod_con2_q40ql_lib","iod_con2_q4016_lib"
1270   DATA ""
1280 END DEFine q40_libs
1290 :
1300 DEFine PROCedure q40_targets
1310   read_datas CURRENT_LINE
1320   delete_files
1330   DATA "smsq_q40_driver_dv3","smsq_q40_driver_16","smsq_q40_cache"
1340   DATA "smsq_q40_driver_ser","smsq_q40_driver_ser","smsq_q40_hwinit"
1350   DATA "smsq_q40_kbd_lang","smsq_q40_nasty","smsq_q40_rom"
1360   DATA "smsq_q40_driver_ql","smsq_sbas_procs_x_q60"
1370   DATA ""
1380 END DEFine q40_targets
1390 :
1400 DEFine PROCedure qxl
1410   qxl_libs
1420   qxl_targets
1430 END DEFine qxl
1440 :
1450 DEFine PROCedure qxl_libs
1460   read_datas CURRENT_LINE
1470   delete_files
1480   DATA "dv3_qxl_lib","iod_con2_qxl16_lib","iod_con2_qxlql_lib","smsq_qxl_hdop_lib"
1490   DATA ""
1500 END DEFine qxl_libs
1510 :
1520 DEFine PROCedure qxl_targets
1530   read_datas CURRENT_LINE
1540   delete_files
1550   DATA "smsq_qxl_driver_16","smsq_qxl_driver_nd","smsq_qxl_driver_most"
1560   DATA "smsq_qxl_ecache","smsq_qxl_driver_ql","smsq_qxl_host","smsq_qxl_hwinit"
1570   DATA "smsq_qxl_kbd_lang","smsq_qxl_nasty_e","smsq_qxl_procs_x","smsq_qxl_smsqe.exe"
1580   DATA "smsq_qxl_test","smsq_qxl_driver_dv3e"
1590   DATA ""
1600 END DEFine qxl_targets
1610 :
1620 DEFine PROCedure read_datas (line_number)
1630 REMark this reads the datas as of the line number given as params
1640 REMark and puts them in the global array delete$
1650 LOCal lp%,a$
1660   DIM delete$(100,100)
1670   RESTORE line_number                   : REMark start reading from here
1680   PRINT "restoring : ";line_number
1690   FOR lp% = 0 TO 100
1700     READ a$                             : REMark get filename
1710     IF a$ ="":EXIT lp%                  : REMark finished
1720     delete$(lp%)="dev8_"&a$
1730   END FOR lp%
1740 END DEFine read_datas
1750 :
1760 DEFine PROCedure delete_files
1770 REMark delete all files held in delete$
1780 LOCal stop_here%,lp%
1790   stop_here%=DIMN(delete$,1)
1800   FOR lp%=0 TO stop_here%
1810     IF delete$(lp%)="":EXIT lp%
1820     DELETE delete$(lp%)
1830   END FOR lp%
1840 END DEFine delete_files
1850 :
1860 DEFine PROCedure sa
1870   SAVE_O dev8_extras_del_all_bas
1880 END DEFine sa
1890 :
1900 DEFine PROCedure delete_files_with_extension(ext$,mdir$,ddown%)
1910 REMark this deletes all files in the dir mdir$ with the extension ext$
1920 REMark possibly drilling down to all subirs(ddown%<>0) to do the same there
1930 REMark by calling itself recursively
1940 LOCal chan%,lp%,myfile$,a$,device$,t%,exlen%
1950   FOR lp%=1 TO 50
1960     myfile$="ram1_dele_extns"&RND(1 TO 80)&RND (1 TO 80) & RND (1 TO 50)
1970     chan%=FOP_OVER (myfile$)            : REMark try to open unique file
1980     IF chan%>0:EXIT lp%
1990   END FOR lp%
2000   IF chan%<0:RETurn                     : REMark ooops!!!!
2010   exlen%=LEN(ext$)
2020   device$=mdir$(1 TO 5)
2030   WDIR#chan%,mdir$                      : REMark dir of this rep in file
2040   GET#chan%\0                           : REMark reset file pointer to start
2050   REPeat lp%
2060     IF EOF(#chan%):EXIT lp%
2070     INPUT#chan%,a$                      : REMark get filename
2080     IF a$="":NEXT lp%
2090     a$=device$&a$
2100     t%= ' ->' INSTR a$                  : REMark is it a subdir?
2110     IF t% AND ddown%
2120       delete_files_with_extension ext$,a$(1 TO t%-1),ddown% : REMark yes, possibly drill down
2130       NEXT lp%
2140     END IF
2150     t%=LEN(a$)
2160     IF t%<exlen%:NEXT lp%               : REMark filename is shorter than extension
2170     IF a$(t%-exlen%+1 TO) ==ext$ : DELETE a$
2180   END REPeat lp%
2190   CLOSE#chan%
2200   DELETE myfile$
2210 END DEFine delete_files_with_extension
2220 :
