100   get_dev
110  PRINT "This program needs the 'ouptr_bin', 'is_extn' and 'current_line' extensions found on "&mdev$
120  PRINT "Type 'l' to load them. Type 'p' to proceed with the deletion"
130  REMark "current_line"  is in "cline_bin"
140 REMark read the documentation in the help subdirectory
150 REMark also for the 'current_line' keyword
160 REMark lrespr cline_bin before using this prog.
170 REMark if the "restoring" message always refers to 0, then you haven't loaded cline_bin
180 :
190 auto%=0:stop%=0:keep%=0
200 a$=cmd$
210 IF a$<>""
220   IF "auto" INSTR a$:auto%=1
230   IF "stop" INSTR a$:stop%=1
240   IF "keep" INSTR a$:keep%=1: REMark keep binaries
250 END IF
260 IF auto%:p
270 IF stop%:QUIT
280 :
290 DEFine PROCedure p
300   IF NOT IS_EXTN ('CURRENT_LINE')
310      PRINT "cline_bin not loaded"
320   ELSE
330      get_dev
340      delete_all_targets
350   END IF
360 END DEFine p
370 :
380 DEFine PROCedure get_dev
390   mdev$="DEV8_"
400 END DEFine get_dev
410 :
420 DEFine PROCedure delete_all_targets
430 REMark this deletes all targets and all intermediary targets, thus forcing
440 REMark a complete rebuild. Caution, this also deletes all lib and rel files
450   PRINT "Deleting all files with the extension rel"
460   delete_files_with_extension "rel",mdev$,1
470   PRINT "Deleting all files with the extension err"
480   delete_files_with_extension "err",mdev$,1
490   COPY mdev$&"minerva_inc_err_copy",mdev$&"minerva_inc_err"
500   COPY mdev$&"keys_err_copy",mdev$&"keys_err"
510   PRINT "Deleting all files with the extension map"
520   delete_files_with_extension "map",mdev$,1
530   PRINT "Deleting all files with the extension log"
540   delete_files_with_extension "log",mdev$,1
550   PRINT "Deleting all files with the extension lib"
560   delete_files_with_extension "lib",mdev$,1
570   generic
580   atari
590   gold
600   q40
610   qxl
620   aurora
630   java
640   ptrgen
650   qpc
660 END DEFine delete_all_targets
670 :
680 DEFine PROCedure generic
690   read_datas CURRENT_LINE
700   delete_files
710   DATA "dd_qlnd_new" ,"dd_nd_new","dv3_test",'smsq_sbas_lang','smsq_sbas_control'
720   DATA "smsq_sbas_procs_x","smsq_smsq_cache","smsq_smsq_cache40c","smsq_smsq_fh_os"
730   DATA "smsq_smsq_hotkey","smsq_smsq_lang","smsq_smsq_loader"
740   DATA "smsq_smsq_os","smsq_smsq_1mb_os","smsq_smsq_vers","smsq_smsq_wman"
750   DATA "util_thg_thing",""
760   DATA ""
770 END DEFine generic
780 :
790 DEFine PROCedure atari
800   read_datas CURRENT_LINE
810   delete_files
820   DATA "smsq_atari_driver_dv3","smsq_atari_driver_ser","smsq_atari_driver_ql"
830   DATA "smsq_atari_driver_mono","smsq_atari_hwinit","smsq_atari_nasty"
840   DATA "smsq_atari_kbd_lang"
850   DATA "sys_boot_st_host","smsq_atari_sysspr"
860   DATA "","smsq_atari_SMSQ.PRG",""
870 END DEFine atari_targets
880 :
890 DEFine PROCedure gold
900   read_datas CURRENT_LINE
910   delete_files
920   DATA "smsq_gold_driver_dv3","smsq_gold_driver_most","smsq_gold_driver_nd","smsq_gold_driver_8"
930   DATA "smsq_gold_driver_nds","smsq_gold_driver_ql","smsq_gold_host"
940   DATA "smsq_gold_hwinit","smsq_gold_kbd_abc","smsq_gold_kbd_abc_lang"
950   DATA "smsq_gold_kbd_lang","smsq_gold_nasty","smsq_gold_nasty_s"
960   DATA "smsq_gold_qimi","smsq_gold_roms","sys_gold_boot"
970   DATA "smsq_gold_sysspr","","smsq_gold_gold8","smsq_gold_gold",""
980 END DEFine gold
990 :
1000 DEFine PROCedure java
1010   read_datas CURRENT_LINE
1020   delete_files
1030   DATA "smsq_java_driver_most","smsq_java_driver_ql","smsq_java_host","smsq_java_smsq_os"
1040   DATA "smsq_java_driver_16"
1050   DATA "smsq_java_driver_8","smsq_java_smsq_1mb_os"
1060   DATA "smsq_java_driver_nfa_nfa","smsq_java_smsq_os"
1070   DATA "smsq_java_hwinit","smsq_java_host"
1080   DATA "smsq_java_driver_dv3e","smsq_java_ip_x"
1090   DATA ""
1100   DATA "SMSQ_java_java"
1110   DATA ""
1120 END DEFine java
1130 :
1140 DEFine PROCedure qpc
1150   read_datas CURRENT_LINE
1160   delete_files
1170   DATA 'smsq_qpc_host'
1180   DATA 'smsq_qpc_hwinit'
1190   DATA 'smsq_smsq_loader'
1200   DATA 'smsq_smsq_qpc_os'
1210   DATA 'smsq_smsq_cache'
1220   DATA 'smsq_sbas_control'
1230   DATA 'smsq_qpc_nasty_e'
1240   DATA 'smsq_smsq_lang'
1250   DATA 'smsq_qpc_kbd_lang'
1260   DATA 'smsq_sbas_lang'
1270   DATA 'smsq_sbas_procs_x'
1280   DATA 'smsq_qpc_driver_most'
1290   DATA 'smsq_qpc_driver_ql'
1300   DATA 'smsq_qpc_driver_16'
1310   DATA 'smsq_qpc_driver_8'
1320   DATA 'smsq_smsq_wman'
1330   DATA 'smsq_qpc_sysspr'
1340   DATA 'smsq_qpc_driver_dv3e'
1350   DATA 'smsq_smsq_hotkey'
1360   DATA 'smsq_qpc_procs_x'
1370   DATA 'smsq_qpc_cdaudio_x'
1380   DATA 'smsq_qpc_dos_x'
1390   DATA 'smsq_qpc_ip_x'
1400   DATA 'smsq_home_home'
1410   DATA "qmon_qpc_smsq"
1420   DATA ""
1430   DATA "smsq_qpc_smsqe.bin"
1440   DATA ""
1450 END DEFine qpc
1460 :
1470 DEFine PROCedure aurora
1480   read_datas CURRENT_LINE
1490   delete_files
1500   DATA "smsq_gold_driver_dv3","smsq_gold_driver_most","smsq_gold_driver_nd"
1510   DATA "smsq_gold_driver_nds","smsq_gold_driver_ql","smsq_gold_host"
1520   DATA "smsq_gold_hwinit","smsq_gold_kbd_abc","smsq_gold_kbd_abc_lang"
1530   DATA "smsq_gold_kbd_lang","smsq_gold_nasty","smsq_gold_nasty_s"
1540   DATA "smsq_gold_qimi","smsq_gold_roms","sys_gold_boot"
1550   DATA "smsq_gold_sysspr","smsq_aurora_driver_8","smsq_aurora_sysspr",""
1560   DATA "smsq_aurora_smsqe","smsq_gold_gold","smsq_gold_gold8", ""
1570 END DEFine aurora
1580 :
1590 DEFine PROCedure q40
1600   read_datas CURRENT_LINE
1610   delete_files
1620   DATA "smsq_q40_driver_dv3","smsq_q40_driver_16","smsq_q40_cache"
1630   DATA "smsq_q40_driver_ser","smsq_q40_driver_ser","smsq_q40_hwinit"
1640   DATA "smsq_q40_kbd_lang","smsq_q40_nasty"
1650   DATA "smsq_q40_driver_ql","smsq_sbas_procs_x_q60"
1660   DATA "smsq_q40_sysspr","smsq_q40_cachemode"
1670   DATA "","smsq_q40_rom"
1680   DATA "smsq_q40_rom_lo","smsq_q40_rom_hi",""
1690 END DEFine q40
1700 :
1710 DEFine PROCedure qxl
1720   read_datas CURRENT_LINE
1730   delete_files
1740   DATA "smsq_qxl_driver_16","smsq_qxl_driver_nd","smsq_qxl_driver_most"
1750   DATA "smsq_qxl_ecache","smsq_qxl_driver_ql","smsq_qxl_host","smsq_qxl_hwinit"
1760   DATA "smsq_qxl_kbd_lang","smsq_qxl_nasty_e","smsq_qxl_procs_x"
1770   DATA "smsq_qxl_test","smsq_qxl_driver_dv3e","smsq_qxl_sysspr"
1780   DATA "","smsq_qxl_smsqe.exe",""
1790 END DEFine qxl
1800 :
1810 DEFine PROCedure ptrgen
1820   read_datas CURRENT_LINE
1830   delete_files
1840   DATA ""
1850   DATA "ee_wman_wman","ee_ptr_gen","ee_hot_rext_german","ee_hot_rext_french","ee_hot_rext_english",""
1860 END DEFine ptrgen
1870 :
1880 DEFine PROCedure read_datas (line_number)
1890 REMark this reads the datas as of the line number given as params
1900 REMark and puts them in the global arrays delete$ and keep$
1910 LOCal lp%,a$
1920   DIM delete$(100,60)
1930   RESTORE line_number                   : REMark start reading from here
1940   PRINT "restoring : ";line_number
1950   FOR lp% = 0 TO 100
1960     READ a$                             : REMark get filename
1970     IF a$ ="":EXIT lp%                  : REMark finished
1980     delete$(lp%)=mdev$&a$
1990   END FOR lp%
2000   DIM keep$(100,60)
2010   FOR lp% = 0 TO 100
2020     READ a$                             : REMark get filename
2030     IF a$ ="":EXIT lp%                  : REMark finished
2040     keep$(lp%)=mdev$&a$
2050   END FOR lp%
2060 END DEFine read_datas
2070 :
2080 DEFine PROCedure delete_files
2090 REMark delete all files held in delete$
2100 LOCal stop_here%,lp%
2110   stop_here%=DIMN(delete$,1)
2120   FOR lp%=0 TO stop_here%
2130     IF delete$(lp%)="":EXIT lp%
2140     DELETE delete$(lp%)
2150   END FOR lp%
2160   IF NOT keep%
2170     stop_here%=DIMN(keep$,1)
2180     FOR lp%=0 TO stop_here%
2190       IF keep$(lp%)="":EXIT lp%
2200       DELETE keep$(lp%)
2210     END FOR lp%
2220   END IF
2230 END DEFine delete_files
2240 :
2250 DEFine PROCedure sa
2260   SAVE_O dev8_extras_del_all_bas
2270 END DEFine sa
2280 :
2290 DEFine PROCedure delete_files_with_extension(ext$,mdir$,ddown%)
2300 REMark this deletes all files in the dir mdir$ with the extension ext$
2310 REMark possibly drilling down to all subirs(ddown%<>0) to do the same there
2320 REMark by calling itself recursively
2330 LOCal chan%,lp%,myfile$,a$,device$,t%,exlen%
2340   FOR lp%=1 TO 50
2350     myfile$="ram1_dele_extns"&RND(1 TO 80)&RND (1 TO 80) & RND (1 TO 50)
2360     chan%=FOP_OVER (myfile$)            : REMark try to open unique file
2370     IF chan%>0:EXIT lp%
2380   END FOR lp%
2390   IF chan%<0:RETurn                     : REMark ooops!!!!
2400   exlen%=LEN(ext$)
2410   device$=mdir$(1 TO 5)
2420   WDIR#chan%,mdir$                      : REMark dir of this rep in file
2430   GET#chan%\0                           : REMark reset file pointer to start
2440   REPeat lp%
2450     IF EOF(#chan%):EXIT lp%
2460     INPUT#chan%,a$                      : REMark get filename
2470     IF a$="":NEXT lp%
2480     a$=device$&a$
2490     t%= ' ->' INSTR a$                  : REMark is it a subdir?
2500     IF t% AND ddown%
2510       delete_files_with_extension ext$,a$(1 TO t%-1),ddown% : REMark yes, possibly drill down
2520       NEXT lp%
2530     END IF
2540     t%=LEN(a$)
2550     IF t%<exlen%:NEXT lp%               : REMark filename is shorter than extension
2560     IF a$(t%-exlen%+1 TO) ==ext$ : DELETE a$
2570   END REPeat lp%
2580   CLOSE#chan%
2590   DELETE myfile$
2600 END DEFine delete_files_with_extension
2610 :
2620 DEFine PROCedure l
2630   LRESPR "dev8_extras_source_outptr_bin"
2640   LRESPR "dev8_extras_blinker_bin"
2650   LRESPR "dev8_extras_cline_bin"
2660 END DEFine l
2670   DATA "SMSQ_java_java","smsq_java_driver_dv3e"
