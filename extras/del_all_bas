100 REMark read the documentation in the help subdirectory
110 REMark also for the 'current_line' keyword
120 REMark lrespr cline_bin before using this prog.
130 REMark if the "restoring" message always refers to 0, then you haven't loaded cline_bin
140 :
150 auto%=0:stop%=0:keep%=0
160 a$=cmd$
170 IF a$<>""
180   IF "auto" INSTR a$:auto%=1
190   IF "stop" INSTR a$:stop%=1
200   IF "keep" INSTR a$:keep%=1: REMark keep binaries
210 END IF
220 IF auto%:p
230 IF stop%:QUIT
240 :
250 DEFine PROCedure p
260   IF NOT IS_EXTN ('CURRENT_LINE')
270      PRINT "cline_bin not loaded"
280   ELSE
290      delete_all_targets
300   END IF
310 END DEFine p
320 :
330 DEFine PROCedure delete_all_targets
340 REMark this deletes all targets and all intermediary targets, thus forcing
350 REMark a complete rebuild. Caution, this also deletes all lib and rel files
360   PRINT "Deleting all files with the extension rel"
370   delete_files_with_extension "rel","dev8_",1
380   PRINT "Deleting all files with the extension err"
390   delete_files_with_extension "err","dev8_",1
400      COPY DEV8_minerva_inc_err_copy,DEV8_minerva_inc_err
410      COPY dev8_keys_err_copy,dev8_keys_err
420   PRINT "Deleting all files with the extension map"
430   delete_files_with_extension "map","dev8_",1
440   PRINT "Deleting all files with the extension log"
450   delete_files_with_extension "log","dev8_",1
460   PRINT "Deleting all files with the extension lib"
470   delete_files_with_extension "lib","dev8_",1
480   generic
490   atari
500   gold
510   q40
520   qxl
530   aurora
540   java
550   ptrgen
560 END DEFine delete_all_targets
570 :
580 DEFine PROCedure generic
590   read_datas CURRENT_LINE
600   delete_files
610   DATA "dd_qlnd_new" ,"dd_nd_new","dv3_test",'smsq_sbas_lang','smsq_sbas_control'
620   DATA "smsq_sbas_procs_x","smsq_smsq_cache","smsq_smsq_cache40c","smsq_smsq_fh_os"
630   DATA "smsq_smsq_hotkey","smsq_smsq_lang","smsq_smsq_loader"
640   DATA "smsq_smsq_os","smsq_smsq_1mb_os","smsq_smsq_vers","smsq_smsq_wman"
650   DATA "util_thg_thing",""
660   DATA ""
670 END DEFine generic
680 :
690 DEFine PROCedure atari
700   read_datas CURRENT_LINE
710   delete_files
720   DATA "smsq_atari_driver_dv3","smsq_atari_driver_ser","smsq_atari_driver_ql"
730   DATA "smsq_atari_driver_mono","smsq_atari_hwinit","smsq_atari_nasty"
740   DATA "smsq_atari_kbd_lang"
750   DATA "sys_boot_st_host","smsq_atari_sysspr"
760   DATA "","smsq_atari_SMSQ.PRG",""
770 END DEFine atari_targets
780 :
790 DEFine PROCedure gold
800   read_datas CURRENT_LINE
810   delete_files
820   DATA "smsq_gold_driver_dv3","smsq_gold_driver_most","smsq_gold_driver_nd"
830   DATA "smsq_gold_driver_nds","smsq_gold_driver_ql","smsq_gold_host"
840   DATA "smsq_gold_hwinit","smsq_gold_kbd_abc","smsq_gold_kbd_abc_lang"
850   DATA "smsq_gold_kbd_lang","smsq_gold_nasty","smsq_gold_nasty_s"
860   DATA "smsq_gold_qimi","smsq_gold_roms","sys_gold_boot"
870   DATA "smsq_gold_sysspr","","smsq_gold_gold8","smsq_gold_gold",""
880 END DEFine gold
890 :
900 DEFine PROCedure java
910   read_datas CURRENT_LINE
920   delete_files
930   DATA "smsq_java_driver_most","smsq_java_driver_ql","smsq_java_host","smsq_java_smsq_os"
940   DATA "smsq_java_driver_16"
950   DATA "smsq_java_driver_nfa_nfa","smsq_java_rtc","dev8_smsq_java_smsq_os"
960   DATA "dev8_smsq_java_hwinit","dev8_smsq_java_host",""
970   DATA "dev8_SMSQ_java_java"
980   DATA ""
990 END DEFine java
1000 :
1010 DEFine PROCedure aurora
1020   read_datas CURRENT_LINE
1030   delete_files
1040   DATA "smsq_gold_driver_dv3","smsq_gold_driver_most","smsq_gold_driver_nd"
1050   DATA "smsq_gold_driver_nds","smsq_gold_driver_ql","smsq_gold_host"
1060   DATA "smsq_gold_hwinit","smsq_gold_kbd_abc","smsq_gold_kbd_abc_lang"
1070   DATA "smsq_gold_kbd_lang","smsq_gold_nasty","smsq_gold_nasty_s"
1080   DATA "smsq_gold_qimi","smsq_gold_roms","sys_gold_boot"
1090   DATA "smsq_gold_sysspr","smsq_aurora_driver_8","smsq_aurora_sysspr",""
1100   DATA "smsq_aurora_smsqe","smsq_gold_gold","smsq_gold_gold8", ""
1110 END DEFine aurora
1120 :
1130 DEFine PROCedure q40
1140   read_datas CURRENT_LINE
1150   delete_files
1160   DATA "smsq_q40_driver_dv3","smsq_q40_driver_16","smsq_q40_cache"
1170   DATA "smsq_q40_driver_ser","smsq_q40_driver_ser","smsq_q40_hwinit"
1180   DATA "smsq_q40_kbd_lang","smsq_q40_nasty"
1190   DATA "smsq_q40_driver_ql","smsq_sbas_procs_x_q60"
1200   DATA "smsq_q40_sysspr","smsq_q40_cachemode"
1210   DATA "","smsq_q40_rom"
1220   DATA "smsq_q40_rom_lo","smsq_q40_rom_hi",""
1230 END DEFine q40
1240 :
1250 DEFine PROCedure qxl
1260   read_datas CURRENT_LINE
1270   delete_files
1280   DATA "smsq_qxl_driver_16","smsq_qxl_driver_nd","smsq_qxl_driver_most"
1290   DATA "smsq_qxl_ecache","smsq_qxl_driver_ql","smsq_qxl_host","smsq_qxl_hwinit"
1300   DATA "smsq_qxl_kbd_lang","smsq_qxl_nasty_e","smsq_qxl_procs_x"
1310   DATA "smsq_qxl_test","smsq_qxl_driver_dv3e","smsq_qxl_sysspr"
1320   DATA "","smsq_qxl_smsqe.exe",""
1330 END DEFine qxl
1340 :
1350 DEFine PROCedure ptrgen
1360   read_datas CURRENT_LINE
1370   delete_files
1380   DATA ""
1390   DATA "ee_wman_wman","ee_ptr_gen",""
1400 END DEFine ptrgen
1410 :
1420 DEFine PROCedure read_datas (line_number)
1430 REMark this reads the datas as of the line number given as params
1440 REMark and puts them in the global arrays delete$ and keep$
1450 LOCal lp%,a$
1460   DIM delete$(100,60)
1470   RESTORE line_number                   : REMark start reading from here
1480   PRINT "restoring : ";line_number
1490   FOR lp% = 0 TO 100
1500     READ a$                             : REMark get filename
1510     IF a$ ="":EXIT lp%                  : REMark finished
1520     delete$(lp%)="dev8_"&a$
1530   END FOR lp%
1540   DIM keep$(100,60)
1550   FOR lp% = 0 TO 100
1560     READ a$                             : REMark get filename
1570     IF a$ ="":EXIT lp%                  : REMark finished
1580     keep$(lp%)="dev8_"&a$
1590   END FOR lp%
1600 END DEFine read_datas
1610 :
1620 DEFine PROCedure delete_files
1630 REMark delete all files held in delete$
1640 LOCal stop_here%,lp%
1650   stop_here%=DIMN(delete$,1)
1660   FOR lp%=0 TO stop_here%
1670     IF delete$(lp%)="":EXIT lp%
1680     DELETE delete$(lp%)
1690   END FOR lp%
1700   IF NOT keep%
1710     stop_here%=DIMN(keep$,1)
1720     FOR lp%=0 TO stop_here%
1730       IF keep$(lp%)="":EXIT lp%
1740       DELETE keep$(lp%)
1750     END FOR lp%
1760   END IF
1770 END DEFine delete_files
1780 :
1790 DEFine PROCedure sa
1800   SAVE_O dev8_extras_del_all_bas
1810 END DEFine sa
1820 :
1830 DEFine PROCedure delete_files_with_extension(ext$,mdir$,ddown%)
1840 REMark this deletes all files in the dir mdir$ with the extension ext$
1850 REMark possibly drilling down to all subirs(ddown%<>0) to do the same there
1860 REMark by calling itself recursively
1870 LOCal chan%,lp%,myfile$,a$,device$,t%,exlen%
1880   FOR lp%=1 TO 50
1890     myfile$="ram1_dele_extns"&RND(1 TO 80)&RND (1 TO 80) & RND (1 TO 50)
1900     chan%=FOP_OVER (myfile$)            : REMark try to open unique file
1910     IF chan%>0:EXIT lp%
1920   END FOR lp%
1930   IF chan%<0:RETurn                     : REMark ooops!!!!
1940   exlen%=LEN(ext$)
1950   device$=mdir$(1 TO 5)
1960   WDIR#chan%,mdir$                      : REMark dir of this rep in file
1970   GET#chan%\0                           : REMark reset file pointer to start
1980   REPeat lp%
1990     IF EOF(#chan%):EXIT lp%
2000     INPUT#chan%,a$                      : REMark get filename
2010     IF a$="":NEXT lp%
2020     a$=device$&a$
2030     t%= ' ->' INSTR a$                  : REMark is it a subdir?
2040     IF t% AND ddown%
2050       delete_files_with_extension ext$,a$(1 TO t%-1),ddown% : REMark yes, possibly drill down
2060       NEXT lp%
2070     END IF
2080     t%=LEN(a$)
2090     IF t%<exlen%:NEXT lp%               : REMark filename is shorter than extension
2100     IF a$(t%-exlen%+1 TO) ==ext$ : DELETE a$
2110   END REPeat lp%
2120   CLOSE#chan%
2130   DELETE myfile$
2140 END DEFine delete_files_with_extension
2150 :
