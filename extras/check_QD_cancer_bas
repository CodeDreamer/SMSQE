100 REMark Can be used to find files where QD has added unwanted Linefeeds towards the end.
110 REMark Copyright (C) W. Lenerz 2020.
120 REMark Needs the qd_bas extension.
130 ED:
140 DEFine PROCedure check_files (report_chan%,mdir$,extn$,do_change%,max_lfs%)
150 REMark device$ is global
160 LOCal chan%,lp%,myfile$,a$,t%,res,high%,low%,mxnb%
170   FOR lp%=1 TO 50
180     myfile$="ram1_check_QD_cancer"&RND(1 TO 80)&RND (1 TO 80) & RND (1 TO 50)
190     chan%=FOP_OVER (myfile$)            : REMark try to open unique file
200     IF chan%>0:EXIT lp%
210   END FOR lp%
220   IF chan%<0:RETurn chan%               : REMark ooops!!!!
230   IF do_change%
240     mxnb%= max_lfs%
250   ELSE
260     mxnb%=0
270   END IF
280   WDIR#chan%,mdir$                      : REMark dir of this rep in file
290   GET#chan%\0                           : REMark reset file pointer to start
300   REPeat lp%
310     IF EOF(#chan%):EXIT lp%
320     INPUT#chan%,a$                      : REMark get filename
330     IF a$="":NEXT lp%
340     a$=device$&a$                       : REMark device $ if from caller
350     global_count = global_count+1
360     t%= ' ->' INSTR a$                  : REMark is it a subdir?
370     IF t%
380       check_files report_chan%,a$(1 TO t%-1),extn$,do_chane%,max_lfs%: REMark yes, treat it
390       NEXT lp%
400     END IF
410     IF extn$<>""
420       IF NOT endswith%(a$,extn$):NEXT lp%
430     END IF
440 PRINT a$
450     matching_count=matching_count+1
460     IF (do_change%)
470       IF QD_BAD(a$,max_lfs%) : PRINT#report_chan%,a$
480     ELSE
490       res=QD_BAD(a$)
500       IF res
510         high%=INT(res/65536)
520         low%=res-(high%*65536)
530       ELSE
540         high%=0:low%=0
550       END IF
560       IF high%>max_lfs% OR low%>max_lfs%
570         PRINT#report_chan%, high%,low%,a$
580       END IF
590     END IF
600   END REPeat lp%
610   CLOSE#chan%
620   DELETE myfile$
630 END DEFine check_files
640 :
650 DEFine PROCedure do_dev (device$,do_change%,maxn%)
660 LOCal lp%,chan%
670   global_count=0
680   matching_count=1
690   CLS
700   CLOSE
710   chan%=FOP_OVER ("ram1_check_QD_cancer")       : REMark try to open unique file
720   IF chan%<0:REPORT#chan%:RETurn                : REMark ooops!!!!
730   IF do_change%
740     PRINT#chan%,"Files changed"
750   ELSE
760     PRINT#chan%,"Files to be changed"
770     PRINT#chan%,"last","inter","name"
780   END IF
790   check_files chan%,device$,"_asm",do_change%,maxn%
800   CLOSE#chan%
810   RETurn 1
820 END DEFine do_dev
830 :
840 DEFine FuNction endswith% (a$,extn$)
850 REMark checks whether a$ ends with extn$. returns 1 if so, else 0
860 LOCal t%,exlen%
870   t%=LEN(a$)
880   exlen%=LEN(extn$)
890   IF exlen%=0:RETurn 1
900   IF t%<exlen%:RETurn 0           : REMark filename is shorter than extension
910   RETurn a$(t%-exlen%+1 TO)==extn$
920 END DEFine endswith%
930 :
940 DEFine PROCedure check
950   do_dev "dev8_",0,2
960 END DEFine check
970 :
980 DEFine PROCedure treat
990   do_dev "dev8_",1,2
1000 END DEFine treat
1010 :
1020 DEFine PROCedure p
1030   check
1040 END DEFine p
1050 :
1060 DEFine PROCedure sa
1070   SAVE_O dev8_extras_check_QD_cancer_bas
1080 END DEFine sa
1090 :
