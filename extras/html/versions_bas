100 REMark   THIS PROGRAMS MAKES THE HTML FILE ""NFA2_versions.html" "
110 REMark   USED ON THE SMSQE WEBSITE.
120 :
130 REMark   THE RESULTING FILES ARE COPIED TO NFA2_
140 REMark   WHICH SHOULD POINT TO THE "website" subdir.
150 :
160 REMark   THIS  PROGRAM NEEDS THE FILES "VERSION_HEADER" AND "VERSIONS_FOOTER" WWHICH SUOUULD
170 REMark   LIE IN THE EXTRAS8HTML SUBDIRECTORY.
180 :
190 REMark   THE INFO IS TAKEN FROM THE "dev8_smsq_smsq_version_asm" FILE.
200 :
210 REMark   THIS PROGRAM IS COPYRIGHT (C) W. LENERZ 2004-2013
220 REMark   ALL RIGHTS RESERVED.
230 :
260 p
270 :
280 DEFine PROCedure init
290   lg$='class="lightTD"'
300   dg$='class="darkTD"'
310 END DEFine init
320 :
330 DEFine PROCedure make_into_html(mfile$,resfile$)
340 LOCal inc%,outc%,lp%,a$,lp2%,lp3%,t%,flag%
350   inc%=FOP_IN(mfile$)
360   IF inc%<0:RETurn
370   outc%=FOP_OVER(resfile$)
380   IF outc%<0
390     CLOSE#inc%
400     RETurn
410   END IF
420   init
430   copy_file "dev8_extras_html_versions_header",outc%
440   flag%=0
450   REPeat lp%
460     INPUT#inc%,a$
470     IF "xdef" INSTR a$:EXIT lp%   : REMark get to first separator
480   END REPeat lp%
490   REPeat lp%
500     IF EOF(#inc%):EXIT lp%
510     a$=ginput$                              : REMark get one line
520     IF "#&#" INSTR a$:NEXT lp%:EXIT lp%     : REMark ignore all those lines
530     IF '-------------' INSTR a$:EXIT lp%    : REMark this is the end
540     IF LEN(a$)<3:NEXT lp%                   : REMark line not long enough
550     IF a$(1)<>";":NEXT lp%                  : REMark only treat commented lines
560     a$=strip_tab$(a$(2 TO ))                : REMark strip off ";", tab & space
570     IF a$(1) INSTR "123456789" AND (a$(2)='.' OR a$(2) INSTR "123456789" )
580       IF flag%
590         PRINT#outc%;"            </TBODY>"
600         PRINT#outc%;"          </TABLE>"
610       END IF
620       t%="." INSTR a$
630       b$=a$(1 TO t%+2)                      : REMark this is the version number
640       PRINT#outc%;"  <BR><p><p>"
650       PRINT#outc%;"  <H2><U>SMSQ/E Version "&b$&"</U></H2>"
660       PRINT#outc%;"  <p> "
670       PRINT#outc%;'          <TABLE ';lg$;' border=0 cellPadding=5 cellSpacing=1 Width="100%">'
680       PRINT#outc%;"            <TBODY>"
690       flag%=1
700       a$=strip_tab$(a$(t%+3 TO))
710       IF a$="":NEXT lp%
720     END IF
730     b$=""
740     REPeat lp2%
750       b$=a$(LEN(a$))
760       IF b$ INSTR ".!":EXIT lp2%
770       b$=ginput$
780       if b$=";":exit lp2%
790       IF b$<>"":b$=strip_tab$(b$(2 TO))
800       IF b$="":EXIT lp2%
810       a$=a$&" "&b$
820     END REPeat lp2%
830     PRINT#outc%;"                <TR><TD ";dg$;" > "&a$&"</TD></TR>"
840   END REPeat lp%
850   PRINT#outc%;"            </TBODY>"
860   PRINT#outc%;"          </TABLE>"
870   copy_file "dev8_extras_html_versions_footer",outc%
880   CLOSE#outc%
890   CLOSE#inc%
900 END DEFine make_into_html
910 :
920 DEFine PROCedure copy_file (mfile$,chan%)
930 REMark copy content of text file mfile$ into channel chan%
940 LOCal inc%,lp%,a$
950   inc%=FOP_IN(mfile$)
960   IF inc%<0:RETurn                         : REMark open file failed
970   REPeat lp%
980     IF EOF(#inc%):EXIT lp%
990     INPUT#inc%,a$                       : REMark get one line
1000     PRINT#chan%,a$                      : REMark and print it
1010   END REPeat lp%
1020   CLOSE#inc%
1030 END DEFine copy_file
1040 :
1050 DEFine FuNction ginput$
1060 LOCal lp%,a$
1070   REPeat lp%
1080     IF EOF(#inc%):RETurn ""
1090     INPUT#inc%,a$
1100     a$=STRIP_SPACES$(a$)
1110     IF "-------" INSTR a$:RETurn ""
1120     IF a$<>"":RETurn a$
1130   END REPeat lp%
1140 END DEFine ginput$
1150 :
1160 DEFine PROCedure p
1170   make_into_html  "dev8_smsq_smsq_version_asm";"NFA2_versions.html"
1180   PRINT "done"\\"copied to NFA2_versions.html"
1190   IF cmd$<>"":QUIT
1200 END DEFine p
1210 :
1220 DEFine PROCedure sa
1230   SAVE_O dev8_extras_html_versions_bas
1240 END DEFine sa
1250 :
1260 DEFine FuNction strip_tab$(a$)
1270 REMark returns string stripped of tab and spaces
1280 LOCal b$,t%,lp%
1290   b$=STRIP_SPACES$(a$)
1300   IF b$="":RETurn ""
1310   REPeat lp%
1320     IF b$(1)<> CHR$(9):RETurn STRIP_SPACES$(b$)
1330     IF LEN(b$)=1:RETurn ""
1340     b$=b$(2 TO)
1350   END REPeat lp%
1360   RETurn ""
1370 END DEFine strip_tab$
