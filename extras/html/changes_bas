100 REMark   THIS PROGRAMS MAKES THE HTML FILE "CHANGES_HTML"
110 REMark   USED ON THE SMSQE WEBSITE.
120 :
130 REMark   THE RESULTING FILES ARE COPIED TO NFA2_
140 REMark   WHICH SHOULD POINT TO THE "website" subdir
150 :
160 REMark   THIS  PROGRAM NEEDS THE FILES "CHANGES_HEADER" AND "CHANGES_FOOTER" WWHICH SUOUULD
170 REMark   LIE IN THE EXTRAS8HTML SUBDIRECTORY.
180 :
190 REMark   THE INFO IS TAKEN FROM THE "CHNAGES_TXT" FILE IN DEV8_
200 :
210 REMark   THIS PROGRAM IS COPYRIGHT (C) W. LENERZ 2004-2013
220 REMark   ALL RIGHTS RESERVED.
230 :
240 p
250 :
260 DEFine PROCedure init
270   lg$='class="lightTD"'
280   dg$='class="darkTD"'
290 END DEFine init
300 :
310 DEFine PROCedure make_into_html(mfile$,resfile$)
320 LOCal inc%,outc%,lp%,a$,lp2%,lp3%,t%,t1%
330   inc%=FOP_IN(mfile$)
340   IF inc%<0:RETurn
350   outc%=FOP_OVER(resfile$)
360   IF outc%<0
370     CLOSE#inc%
380     RETurn
390   END IF
400   init
410   copy_file "dev8_extras_html_changes_header",outc%
420   REPeat lp%
430     INPUT#inc%,a$
440     IF "----------" INSTR a$:EXIT lp%   : REMark get to first separator
450   END REPeat lp%
460   REPeat lp%
470     IF EOF(#inc%):EXIT lp%
480     a$=ginput$                          : REMark this is the version
490     IF a$="":NEXT lp%                   : REMark what? premature end!
500     PRINT#outc%;"  <BR><p><p>"
510     PRINT#outc%;"  <H2>SMSQ/E Version "&a$&"</H2>"
520     PRINT#outc%;"  <p> "
530     PRINT#outc%;'          <TABLE ';lg$;' border=0 cellPadding=5 cellSpacing=1 Width="100%">'
540     PRINT#outc%;"            <TBODY>"
550 REMark now the headers & data within this version
560     REPeat lp2%
570       a$=ginput$
580       IF a$="":EXIT lp2%
590       t%=CODE (a$)
600       IF t%>64 AND t%<90
610         PRINT#outc%;"              <TR>"
620         PRINT#outc%;"                <TD ";lg$;" ><B>"&a$&"</B></TD>"
630         PRINT#outc%;"              </TR>"
640          ELSE
650         PRINT#outc%;"              <TR>"
660         t%=" "INSTR a$                  : REMark end of filename
670         t1%=CHR$(9) INSTR a$
680         IF NOT t%:t%=t1%
690         IF (t1%) AND (t1%<t%):t%=t1%
700         IF t%
710           b$=a$(1 TO t%-1)
720           PRINT#outc%;"                <TD ";dg$;" > "&b$&"</TD>"
730         ELSE
740           PRINT#outc%;"                <TD ";dg$;" > "&a$&"</TD>"
750           PRINT#outc%;"                <TD ";dg$;" > </TD>"
760           PRINT#outc%;"                <TD ";dg$;" > </TD>"
770           PRINT#outc%;"              </TR>"
780           NEXT lp2%
790         END IF
800         a$=strip_tab$(a$(t%+1 TO))     : REMark strip off filename
810         t%=" " INSTR a$
820         t1%=CHR$(9) INSTR a$
830         IF NOT t%:t%=t1%
840         IF (t1%) AND (t1%<t%):t%=t1%
850         b$=a$(1 TO t%-1)               : REMark the version
860         IF b$<>""
870           PRINT#outc%;"                <TD align=middle "&dg$&">"&b$&"</TD>"
880           a$=strip_tab$(a$(t%+1 TO))   : REMark strip off version, get comments
890           IF a$<>""
900             PRINT#outc%;"                <TD "&dg$&">"&a$&"</TD>"
910           END IF
920         END IF
930         PRINT#outc%;"              </TR>"
940       END IF
950     END REPeat lp2%
960     PRINT#outc%;"            </TBODY>"
970     PRINT#outc%;"          </TABLE>"
980   END REPeat lp%
990   copy_file "dev8_extras_html_changes_footer",outc%
1000   CLOSE#outc%
1010   CLOSE#inc%
1020 END DEFine make_into_html
1030 :
1040 DEFine PROCedure copy_file (mfile$,chan%)
1050 REMark copy content of text file mfile$ into channel chan%
1060 LOCal inc%,lp%,a$
1070   inc%=FOP_IN(mfile$)
1080   IF inc%<0:RETurn                         : REMark open file failed
1090   REPeat lp%
1100     IF EOF(#inc%):EXIT lp%
1110     INPUT#inc%,a$                       : REMark get one line
1120     PRINT#chan%,a$                      : REMark and print it
1130   END REPeat lp%
1140   CLOSE#inc%
1150 END DEFine copy_file
1160 :
1170 DEFine FuNction ginput$
1180 LOCal lp%,a$
1190   REPeat lp%
1200     IF EOF(#inc%):RETurn ""
1210     INPUT#inc%,a$
1220     a$=strip_tab$(a$)
1230     IF "-------" INSTR a$:RETurn ""
1240     IF a$<>"":RETurn a$
1250   END REPeat lp%
1260 END DEFine ginput$
1270 :
1280 DEFine PROCedure p
1290   PRINT 'Remember, each version must be separated by "----------------"'
1300   make_into_html  "dev8_changes_txt";"NFA2_chngelog.html"
1310   PRINT "done"
1320   PRINT "copied to NFA2_chngelog.html"
1330   IF cmd$<>"":QUIT
1340 END DEFine p
1350 :
1360 DEFine PROCedure sa
1370   SAVE_O dev8_extras_html_changes_bas
1380 END DEFine sa
1390 :
1400 DEFine FuNction strip_tab$(a$)
1410 REMark returns string stripped of tab and spaces
1420 LOCal b$,t%,lp%
1430   b$=STRIP_SPACES$(a$)
1440   IF b$="":RETurn ""
1450   REPeat lp%
1460     IF b$(1)<> CHR$(9):RETurn STRIP_SPACES$(b$)
1470     IF LEN(b$)=1:RETurn ""
1480     b$=b$(2 TO)
1490   END REPeat lp%
1500   RETurn ""
1510 END DEFine strip_tab$
