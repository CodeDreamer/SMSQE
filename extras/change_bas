100 DEFine PROCedure p
110   change 'win1_','dev8_','ram1_'
120 END DEFine p
130 :
140 DEFine PROCedure change (old$,new$,d$)
150 REMark change new for ol  in all files held on d$
160 REMark this does NOT recursed into directories
170 LOCal lp%,c%,a$
180   c%=FOP_OVER('ram1_px')
190   DIR#c%,d$
200   CLOSE#c%
210   c%=FOP_IN('ram1_px')
220   INPUT#c%,a$
230   INPUT#c%,a$
240   REPeat lp%
250     IF EOF(#c%):EXIT lp%
260     INPUT#c%,a$
270     IF ' ->' INSTR a$:NEXT lp%
280     do_xchange d$&a$,old$,new$
290   END REPeat lp%
300   CLOSE#c%
310 END DEFine change
320 :
330 DEFine PROCedure sa
340   SAVE_O dev8_extras_change_bas
350 END DEFine sa
360 :
370 DEFine PROCedure do_xchange (file$,old$,new$)
380 REMark exchange new$ for old$ in file, provided it is no executable file
390 LOCal ch%,f$,fdte,o%,lp%,hit%,line$,repl%
400   ch%=FOP_IN(file$)
410   IF ch%<0 :PRINT "coudn't open "&file$,:REPORT#1,ch%:RETurn
420   t%=FTYP(#ch%)
430   IF t%<> 0:CLOSE#ch%:RETurn
440   fdte=FUPDT(#ch%)
450   o%=FOP_OVER(file$&'2')
460   repl%=LEN(old$)
470   WHEN ERRor
480     line$=""        : REMark for files with no proper CR at end
490   END WHEN
500   REPeat lp%
510     IF EOF(#ch%):EXIT lp%
520     INPUT#ch%,line$
530     hit%=old$ INSTR line$: REMark possibly replace
540     IF hit%
550         IF hit%>1
560           line$=line$(1 TO hit%-1)&new$&line$(hit%+repl% TO)
570         ELSE
580           line$=new$ & line$(repl%+1 TO)
590         END IF
600     END IF
610     PRINT#o%,line$
620   END REPeat lp%
630   CLOSE#ch%
640   SET_FUPDT#o%,fdte
650   CLOSE#o%
660   DELETE file$
670   RENAME file$&'2',file$
680 END DEFine do_xchange
690 :
