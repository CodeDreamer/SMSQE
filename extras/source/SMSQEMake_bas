100 REMark $$asmb=dev8_extras_exe_source_outptr_bin,0,10
110 REMark $$asmb=dev1_booty_qptr_bin,50,172
120 REMark $$asmb=dev8_extras_source_SMSQEMake_bin,0,10
130 REMark rem $$asmb=dev8_extras_cline_bin,0,10      <---- NOOOOOOOOOOOOOOOOOOOOOOOOOOOOO!
140 REMark $$stak=20000
150 REMark Make sure the compiled version does not contain the CURRENT_LINE keyword
160 REMark use the ccrestore procedure herein to elmimenate it!!!!!!!!!!!!!!!!
170 compiled=NOT IS_OPEN(#0)
180 IF NOT compiled
190   SCR_SIZE#0, xsz%,ysz%
200   OUTLN#0,xsz%,ysz%-28,0,28
210   WSET -1
220 END IF
230 init
240 main
250 STOP
260 DEFine PROCedure make_new_dir (poste%)
270 REMark get athe new dir where the files are located
280 LOCal quel_rep$
290   IF MENU_OK
300     quel_rep$= DIR_SELECT$("The Files are where?",,10,10,main_coul%,app_coul%)
310   ELSE
320     quel_rep$=getstr$(poste%,0,main_defn,a_lire$)       : REMark use menu extns if possible
330   END IF
340   IF LEN (quel_rep$) >= 5
350      rep$=quel_rep$(1 TO 5)
360   END IF
370   CH_ITEM main_defn,-1,poste%,-1,"",rep$
380   main_lfl%(poste%)=main_lfl%(poste%)+1
390 END DEFine make_new_dir
400 :
410 DEFine PROCedure main
420 LOCal lp%,item%,event%,swnum%,xrel%,yrel%,item%,lp2%,tot%
430   IF compiled
440      OPEN#1,'con'
450      SCR_SIZE#1,xsz%,ysz%
460      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
470   ELSE
480      SCR_SIZE#1,xsz%,ysz%
490      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
500   END IF
510   POS_PTR main_defn,40*65536+20,1
520   REPeat lp%
530     RD_PTR main_defn,item%,swnum%,event%,xrel%,yrel%,main_lfl%,main_mfl%,main_cty%
540     IF swnum%= -1
550       SELect ON item%
560               =esc%     : EXIT lp%      : REMark quit prog
570               =move_it% : CH_WIN main_defn: REMark move
580               =size_it% : change_size xrel%,yrel% : REMark change size
590               =sleep_it%: button main_defn,main_lfl%,main_mfl%,main_cty%,"SMSQ/E Make"
600               =all_it%
610                  set_statusses          : REMark set all appsub item statusses
620                  main_cty%(0,1)=1       : REMark and redraw
630               =go_it%   : do_the_work
640               =dir_it%: make_new_dir item%
650               =target_its% TO target_its%+7
660                           start%=1:machines%=0
670                           FOR lp2%=target_its% TO target_its%+7
680                               IF main_lfl%(lp2%):machines%=machines%+start%
690                               start%=start%*2
700                           END FOR lp2%
710                           make_my_data machines%        : REMark make data arrays
720                           mk_main_app                   : REMark make appsub wdw
730                           main_app_list=app_addr+180
740                           AP_IN main_defn,main_app_list : REMark put it in
750                           DR_WDRW main_defn:main_cty%(0,1)=1 : REMark and redraw entire wdw
760       END SELect
770     END IF
780   END REPeat lp%
790   DR_UNST main_defn
800   IF compiled:CLOSE#1
810   RECHP espace_vide
820 END DEFine main
830 :
840 DEFine PROCedure button (def_trav,menu_drap%,app_drap%,control%,chaine$)
850 REMark set up as button
860   DR_UNST def_trav                      : REMark no more working defn
870   IF compiled: CLOSE#1                  : REMark more wdw
880   BUTTON_WAIT chaine$                   : REMark use menu extensions
890   IF compiled
900     OPEN#1,"CON"
910     DR_PPOS def_trav,-1,-1,menu_drap%,app_drap%,control%
920   ELSE
930     DR_PULD def_trav,-1,-1,menu_drap%,app_drap%,control%
940   END IF
950 END DEFine button
960 :
970 DEFine PROCedure mk_main_app
980 REMark make main application sub window
990 LOCal t2%,temp%,sections%,att_phys%(3)
1000   DIM main_cty%(0):DIM main_mfl%(0,0)
1010   sections%=2
1020   temp%=sections% :t2%=rangees
1030   att_phys%(0)=xsize%-16                : REMark xsize calculated on main wdw x size
1040   att_phys%(1)=t2%                      : REMark y size,nominal,in rows
1050   att_phys%(2)=4                        : REMark x origin x
1060   att_phys%(3)=inf1y +appwinoffset%     : REMark y origin
1070   app_addr=fen_app2(app_addr,mdata$,temp%,1,att_phys%,"DO ")
1080   IF app_addr
1090       DIM main_cty%(sections%,2)        : REMark ctrl defn
1100       main_cty%(0,0)=1                  : REMark show 1 section
1110       main_cty%(1,2)=t2%
1120       IF sections%=2:main_cty%(2,2)=t2% : REMark but allow 2
1130       IF temp%
1140            DIM main_mfl%(temp%-1,0)     : REMark flag array
1150            set_statusses                : REMark and set all statusses
1160       ELSE
1170            POKE_W app_addr,PEEK_W(app_addr)+8
1180       END IF
1190    REMark   scroll_arrows t2%,temp%,app_addr,main_cty%
1200   END IF
1210 END DEFine mk_main_app
1220 :
1230 DEFine PROCedure set_statusses
1240 REMark set all statusses of menu appsub objects
1250 LOCal temp%,lp%,stat%
1260   temp%=DIMN(main_mfl%,1)               : REMark how many are there?
1270   stat%=main_lfl%(all_it%)              : REMark status of "all" item
1280   FOR lp%=0 TO temp%
1290     IF mdata$(lp%+1,1)='*'
1300       main_mfl%(lp%,0)=16               : REMark comments set to unavailable
1310     ELSE
1320       main_mfl%(lp%,0)=stat%            : REMark all other to status
1330     END IF
1340   END FOR lp%
1350 END DEFine set_statusses
1360 :
1370 DEFine PROCedure make_my_data (what_machine%)
1380 REMark what_machine% = 1 -> generic
1390 REMark what_machine% = 2 -> atari
1400 REMark what_machine% = 4 -> Gold Card
1410 REMark what_machine% = 8 -> Qx0
1420 REMark what_machine% = 16 -> QXL
1430 REMark what_machine% = 32 -> aurora
1440 REMark what_machine% = 64 -> java
1450 REMark what_machine% = 128 -> ptrgen
1460 REMark these may be combined
1470   DIM mdata$(150,50)
1480   REMark first make generic data
1490   data_count%=1                         : REMark general data count (global var)
1500   IF what_machine%&&1
1510     generic_data                        : REMark set up generic data
1520     read_my_datas                       : REMark and read it
1530   END IF
1540   IF what_machine%&&2
1550      atari_data                         : REMark set up machine specific data....
1560      read_my_datas                      : REMark and read it
1570   END IF
1580   IF what_machine%&&4
1590      gc_data                            : REMark set up machine specific data....
1600      read_my_datas                      : REMark and read it
1610   END IF
1620   IF what_machine%&&8
1630      q40_data                           : REMark set up machine specific data....
1640      read_my_datas                      : REMark and read it
1650   END IF
1660   IF what_machine%&&16
1670      qxl_data                           : REMark set up machine specific data....
1680      read_my_datas                      : REMark and read it
1690   END IF
1700   IF what_machine%&&32
1710      aurora_data                        : REMark set up machine specific data....
1720      read_my_datas                      : REMark and read it
1730   END IF
1740   IF what_machine%&&64
1750      java_data                          : REMark set up machine specific data....
1760      read_my_datas                      : REMark and read it
1770   END IF
1780   IF what_machine%&&128
1790      ptrgen_data                        : REMark set up machine specific data....
1800      read_my_datas                      : REMark and read it
1810   END IF
1820 END DEFine make_my_data
1830 :
1840 DEFine PROCedure read_my_datas
1850 REMark reads datas into the global var mdata$
1860 REMark uses data_count% from make_my_data
1870 LOCal lp%
1880   REPeat lp%
1890     READ mdata$(data_count%)
1900     IF mdata$(data_count%)="":EXIT lp%
1910     data_count%=data_count%+1
1920   END REPeat lp%
1930 END DEFine read_my_datas
1940 :
1950 DEFine PROCedure generic_data
1960 RESTORE 1960
1970   DATA '**** -- SMSQ Generic -- ****'
1980   DATA 'smsq_smsq_loader_link'
1990   DATA 'smsq_smsq_fh_link'
2000   DATA 'smsq_smsq_link'
2010   DATA 'smsq_smsq_cache_link'
2020   DATA 'smsq_sbas_link'
2030   DATA 'smsq_smsq_lang_link'
2040   DATA 'smsq_sbas_lang_link'
2050   DATA 'smsq_sbas_procs_link'
2060   DATA 'smsq_smsq_wman_link'
2070   DATA 'smsq_smsq_hotkey_link'
2080   DATA 'smsq_smsq_vers_link'
2090   DATA 'smsq_home_link'
2100   DATA 'smsq_smsq_1mb_link',''
2110 END DEFine generic_data
2120 :
2130 DEFine PROCedure q40_data
2140 RESTORE 2140
2150   DATA '**** -- Q40/Q60 -- ****'
2160   DATA 'smsq_q40_hwinit_link'
2170   DATA 'smsq_q40_nasty_link'
2180   DATA 'smsq_q40_cache_link'
2190   DATA 'smsq_q40_cachemode_link'
2200   DATA 'smsq_smsq_cache40c_link'
2210   DATA 'smsq_q40_driver_ser_link'
2220   DATA 'smsq_q40_driver_dv3_link'
2230   DATA 'smsq_q40_driver_ql_link'
2240   DATA 'smsq_q40_driver_16_link'
2250   DATA 'smsq_q40_kbd_lang_link'
2260   DATA 'smsq_sbas_procs_link'
2270   DATA 'smsq_q40_sysspr_link',''
2280 END DEFine q40_data
2290 :
2300 DEFine PROCedure atari_data
2310 RESTORE 2310
2320   DATA '**** -- Atari -- ****'
2330   DATA 'smsq_atari_hwinit_link'
2340   DATA 'smsq_atari_nasty_link'
2350   DATA 'smsq_atari_driver_ql_link'
2360   DATA 'smsq_atari_driver_mo_link'
2370   DATA 'smsq_atari_driver_ser_link'
2380   DATA 'smsq_atari_driver_dv3_link'
2390   DATA 'smsq_atari_kbd_lang_link'
2400   DATA 'smsq_atari_sysspr_link'
2410   DATA 'sys_boot_st_host_link',''
2420 END DEFine atari_data
2430 :
2440 DEFine PROCedure gc_data
2450 RESTORE 2450
2460   DATA '**** -- (Super)GoldCard -- ****'
2470   DATA 'smsq_gold_host_link'
2480   DATA 'smsq_gold_hwinit_link'
2490   DATA 'smsq_gold_nasty_link'
2500   DATA 'smsq_gold_nasty_s_link'
2510   DATA 'smsq_gold_driver_ql_link'
2520   DATA 'smsq_gold_driver_8_link'
2530   DATA 'smsq_gold_driver_most_link'
2540   DATA 'smsq_gold_driver_nd_link'
2550   DATA 'smsq_gold_driver_nds_link'
2560   DATA 'smsq_gold_driver_dv3_link'
2570   DATA 'smsq_gold_kbd_lang_link'
2580   DATA 'smsq_gold_kbd_abc_lng_link'
2590   DATA 'smsq_gold_kbd_abc_link'
2600   DATA 'smsq_gold_roms_link'
2610   DATA 'smsq_gold_qimi_link'
2620   DATA 'smsq_gold_sysspr_link',''
2630 END DEFine gc_data
2640 :
2650 DEFine PROCedure aurora_data
2660 RESTORE 2660
2670   DATA '**** -- Aurora -- ****'
2680   DATA 'smsq_gold_host_link'
2690   DATA 'smsq_gold_hwinit_link'
2700   DATA 'smsq_gold_nasty_link'
2710   DATA 'smsq_gold_nasty_s_link'
2720   DATA 'smsq_aurora_driver_8_link'
2730   DATA 'smsq_gold_driver_ql_link'
2740   DATA 'smsq_gold_driver_most_link'
2750   DATA 'smsq_gold_driver_nd_link'
2760   DATA 'smsq_gold_driver_nds_link'
2770   DATA 'smsq_gold_driver_dv3_link'
2780   DATA 'smsq_gold_kbd_lang_link'
2790   DATA 'smsq_gold_kbd_abc_lng_link'
2800   DATA 'smsq_gold_kbd_abc_link'
2810   DATA 'smsq_gold_roms_link'
2820   DATA 'smsq_gold_qimi_link'
2830   DATA 'smsq_aurora_sysspr_link'
2840   DATA 'smsq_gold_sysspr_link',''
2850 END DEFine aurora_data
2860 :
2870 DEFine PROCedure qxl_data
2880 RESTORE 2880
2890   DATA '**** -- QXL -- ****'
2900   DATA 'smsq_qxl_host_link'
2910   DATA 'smsq_qxl_hwinit_link'
2920   DATA 'smsq_qxl_nasty_e_link'
2930   DATA 'smsq_qxl_driver_ql_link'
2940   DATA 'smsq_qxl_driver_16_link'
2950   DATA 'smsq_qxl_driver_most_link'
2960   DATA 'smsq_qxl_driver_nd_link'
2970   DATA 'smsq_qxl_procs_link'
2980   DATA 'smsq_qxl_driver_dv3e_link'
2990   DATA 'smsq_qxl_kbd_lang_link'
3000   DATA 'smsq_qxl_ecache_link'
3010   DATA 'smsq_qxl_sysspr_link',''
3020 END DEFine qxl_data
3030 :
3040 DEFine PROCedure java_data
3050 RESTORE 3050
3060   DATA '**** -- Java -- ****'
3070   DATA 'smsq_java_host_link'
3080   DATA 'smsq_java_smsq_link'
3090   DATA 'smsq_java_rtc_link'
3100   DATA 'smsq_java_hwinit_link'
3110   DATA 'smsq_java_driver_ql_link'
3120   DATA 'smsq_java_driver_16_link'
3130   DATA 'smsq_java_driver_most_link'
3140   DATA 'smsq_gold_kbd_lang_link'
3150   DATA 'smsq_gold_sysspr_link',''
3160 END DEFine java_data
3170 :
3180 DEFine PROCedure ptrgen_data
3190 RESTORE 3190
3200   DATA "**** -- PtrGen -- ****"
3210   DATA "ee_wman_link","ee_ptr_link",""
3220 END DEFine ptrgen_data
3230 :
3240 DEFine PROCedure do_the_work
3250 REMark this tries to compile all of the selected files
3260 REMark if the file was compiled OK, status (ogf the link file) set to available
3270 REMark if the file was not compiled ok, status left selected
3280 LOCal lp%,bound%,a$,my_error%,tot_err%
3290   bound%=DIMN(main_mfl%,1)              : REMark number of items to check
3300   tot_err%=0
3310   FOR lp%=0 TO bound%
3320     a$=mdata$(lp%+1)                    : REMark potential file to compile
3330     IF a$(1)<>"*" AND main_mfl%(lp%,0)
3340        a$=rep$&a$                       : REMark add dir
3350        my_error%=compile_and_check% (a$): REMark try to compile
3360        IF NOT my_error%
3370            main_mfl%(lp%,0)=1
3380            DR_ADRW main_defn,0,main_mfl%,main_cty%
3390        END IF
3400        tot_err%=tot_err%+my_error%
3410     END IF
3420   END FOR lp%
3430   main_cty%(0,1)=1
3440   main_lfl%(go_it%)=1
3450   IF main_lfl%(make_it%)=128 AND NOT tot_err%:make_targets
3460 END DEFine do_the_work
3470 :
3480 DEFine FuNction compile_and_check% (a$)
3490 REMark this tries to compile a file, using the "make" prog
3500 REMark this returns 1 if errors, else 0
3510 LOCal chan%,is_ok$,ch_len
3520   EXEP_W "make";a$&" -w -l"             : REMark exec make & wait
3530   is_ok$=a$(1 TO LEN(a$)-4)             : REMark file name w/o extension
3540   chan%=FOP_IN(is_ok$&'log')            : REMark open log file
3550   IF chan%<0:RETurn 1                   : REMark can't even open it!
3560   ch_len=FLEN(#chan%)                   : REMark file length
3570   GET#chan%\ch_len-10                   : REMark this is where error report is
3580   INPUT#chan%,is_ok$                    : REMark get it
3590   CLOSE#chan%
3600   IF NOT is_ok$=="No errors"
3610     RETurn 1                            : REMark anything other than "no errors" signals errors
3620   ELSE
3630     RETurn 0
3640   END IF
3650 END DEFine compile_and_check%
3660 :
3670 DEFine PROCedure make_targets
3680 REMark targets are only made if there was no error during compilation
3690 REMark to achieve this, a primitive approach is used:
3700 REMark a check is made whether an appsub menu item status is still selected
3710 REMark if it is, then the
3720   LOCal lp%,current_target%,tot_targets%
3730   tot_targets%=0                        : REMark no targets to make yet
3740   current_target%=0                     : REMark and no target yet
3750   FOR lp%=0 TO data_count%-2
3760     IF main_mfl%(lp%,0)=16
3770       REMark we're covering a new target
3780       tot_targets%=tot_targets%+current_target% : REMark show old target
3790       current_target%=find_target%(lp%+1) : REMark get new target we're covering
3800     ELSE
3810       IF main_mfl%(lp%,0)<>0:current_target%=0
3820     END IF
3830   END FOR lp%
3840   tot_targets%=tot_targets%+current_target%
3850   REMark at the end, here, tot_targets holds a bitmap
3860   REMark of targets for which compilation was ok
3870   IF (machines%&&1) AND NOT (tot_targets% && 1):RETurn : REMark generic was asked for but not compiled correctly!
3880   IF tot_targets%&&2:atari_target
3890   IF tot_targets%&&4:gc_target
3900   IF tot_targets%&&8:qx0_target
3910   IF tot_targets%&&16:qxl_target
3920   IF tot_targets%&&32:aurora_target
3930   IF tot_targets%&&64:java_target
3940 END DEFine make_targets
3950 :
3960 DEFine PROCedure atari_target
3970   cmd$='m'
3980   p$=rep$&'sys_boot_st_flp'
3990   f$=rep$&'sys_boot_file'
4000   ah$=rep$&'sys_boot_st_host'
4010   ld$=rep$&'smsq_smsq_loader'
4020   hi$=rep$&'smsq_atari_hwinit'
4030   os1f$=rep$&'smsq_smsq_fh_os'
4040   os1$=rep$&'smsq_smsq_os'
4050   ca$=rep$&'smsq_smsq_cache'
4060   os2$=rep$&'smsq_sbas_control'
4070   ns1$=rep$&'smsq_atari_nasty'
4080   ln1$=rep$&'smsq_smsq_lang'
4090   ln2$=rep$&'smsq_atari_kbd_lang'
4100   ln3$=rep$&'smsq_sbas_lang'
4110   ex1$=rep$&'smsq_sbas_procs_x'
4120   ex2$=rep$&'smsq_atari_driver_dv3'
4130   ex3$=rep$&'smsq_atari_driver_ser'
4140   ex4$=rep$&'smsq_atari_driver_mono'
4150   ex5$=rep$&'smsq_atari_driver_ql'
4160   ex6$=rep$&'smsq_smsq_wman'
4170   ex7$=rep$&'smsq_smsq_hotkey'
4180   ex8$=rep$&'smsq_atari_sysspr'
4190   ex9$=rep$&'smsq_home_home'
4200   nl$=rep$&'sys_boot_null'
4210   IF 'm' INSTR cmd$ = 0: ex4$ = nl$
4220   EW f$, ah$,ld$,hi$,os1f$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$, rep$&'smsq_atari_SMSQ.PRG'
4230 END DEFine atari_target
4240 :
4250 DEFine PROCedure gc_target
4260 REMark there are  2 targets for the Goldcard!
4270   f$=rep$&'sys_boot_file'
4280   v$=rep$&'smsq_smsq_vers'
4290   h$=rep$&'smsq_gold_host'
4300   ld$=rep$&'smsq_smsq_loader'
4310   hi$=rep$&'smsq_gold_hwinit'
4320   os1$=rep$&'smsq_smsq_os'
4330   ca$=rep$&'smsq_smsq_cache'
4340   os2$=rep$&'smsq_sbas_control'
4350   ns1$=rep$&'smsq_gold_nasty'
4360   ns2$=rep$&'smsq_gold_nasty_s'
4370   ln1$=rep$&'smsq_smsq_lang'
4380   ln2$=rep$&'smsq_gold_kbd_lang'
4390   ln2a$=rep$&'smsq_gold_kbd_abc_lang'
4400   ln3$=rep$&'smsq_sbas_lang'
4410   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
4420   ex2a$=rep$&'smsq_gold_kbd_abc'
4430   ex2$=rep$&'smsq_gold_driver_most'
4440   ex3a$=rep$&'smsq_gold_driver_ql'
4450   ex38$=rep$&'smsq_gold_driver_8'
4460   ex3q$=rep$&'smsq_gold_qimi'
4470   ex3w$=rep$&'smsq_smsq_wman'
4480   ex4$=rep$&'smsq_gold_driver_dv3'
4490   ex5$=rep$&'smsq_gold_driver_nd'
4500   ex6$=rep$&'smsq_gold_driver_nds'
4510   ex7$=rep$&'smsq_smsq_hotkey'
4520   ex8$=rep$&'smsq_gold_sysspr'
4530   ex9$=rep$&'smsq_home_home'
4540   nl$=rep$&'sys_boot_null'
4550   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex3a$,ex3q$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,ex1$,ex9$, rep$&'SMSQ_GOLD_gold'
4560   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex38$,ex3q$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,ex1$,ex9$, rep$&'SMSQ_GOLD_gold8'
4570 END DEFine gc_target
4580 :
4590 DEFine PROCedure aurora_target
4600   f$='dev8_sys_boot_file'
4610   v$='dev8_smsq_smsq_vers'
4620   h$='dev8_smsq_gold_host'
4630   ld$='dev8_smsq_smsq_loader'
4640   hi$='dev8_smsq_gold_hwinit'
4650   os1$='dev8_smsq_smsq_os'
4660   ca$='dev8_smsq_smsq_cache'
4670   os2$='dev8_smsq_sbas_control'
4680   ns1$='dev8_smsq_gold_nasty'
4690   ns2$='dev8_smsq_gold_nasty_s'
4700   ln1$='dev8_smsq_smsq_lang'
4710   ln2$='dev8_smsq_gold_kbd_lang'
4720   ln2a$='dev8_smsq_gold_kbd_abc_lang'
4730   ln3$='dev8_smsq_sbas_lang'
4740   ex1$='dev8_smsq_sbas_procs_x'   : REMark this must be after "roms"
4750   ex2a$='dev8_smsq_gold_kbd_abc'
4760   ex2$='dev8_smsq_gold_driver_most'
4770   ex3a$='dev8_smsq_gold_driver_ql'
4780   ex3b$='dev8_smsq_aurora_driver_8'
4790   ex3q$='dev8_smsq_gold_qimi'
4800   ex3w$='dev8_smsq_smsq_wman'
4810   ex3sq$='dev8_smsq_aurora_sysspr'
4820   ex3sh$='dev8_smsq_gold_sysspr'
4830   ex4$='dev8_smsq_gold_driver_dv3'
4840   ex5$='dev8_smsq_gold_driver_nd'
4850   ex6$='dev8_smsq_gold_driver_nds'
4860   ex8$="dev8_smsq_home_home"
4870   ex9$='dev8_smsq_smsq_hotkey'
4880   ex10$='dev8_smsq_gold_roms'
4890   nl$='dev8_sys_boot_null'
4900   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex3a$,ex3b$,ex3q$,ex3w$,ex3sh$,ex4$,ex5$,ex6$,ex9$,ex10$,ex8$,ex1$, 'dev8_SMSQ_Aurora_SMSQE'
4910 END DEFine aurora_target
4920 :
4930 DEFine PROCedure qx0_target
4940   nl$=rep$&'sys_boot_null'
4950   f$=rep$&'sys_boot_file'
4960   qr$=rep$&'sys_boot_q40_rom'
4970   ld$=rep$&'smsq_smsq_loader'
4980   hi$=rep$&'smsq_q40_hwinit'
4990   os1$=rep$&'smsq_smsq_1mb_os' : REMark change for fast memory
5000   ca$=rep$&'smsq_smsq_cache40c'
5010   caq$=rep$&'smsq_q40_cache'
5020   caq4$=rep$&'smsq_q40_cachemode'
5030   os2$=rep$&'smsq_sbas_control'
5040   ns2$=rep$&'smsq_q40_nasty'
5050   ln1$=rep$&'smsq_smsq_lang'
5060   ln2$=rep$&'smsq_q40_kbd_lang'
5070   ln3$=rep$&'smsq_sbas_lang'
5080   ex1$=rep$&'smsq_sbas_procs_x'
5090   ex2$=rep$&'smsq_q40_driver_dv3'
5100   ex3$=rep$&'smsq_q40_driver_ser'
5110   ex4a$=rep$&'smsq_q40_driver_ql'
5120   ex4b$=rep$&'smsq_q40_driver_16'
5130   ex4w$=rep$&'smsq_smsq_wman'
5140   ex5$=rep$&'smsq_q40_sysspr'
5150   ex9$=rep$&'smsq_smsq_hotkey'
5160   ex10$=rep$&'smsq_home_home'
5170   EW f$, qr$,ld$,hi$,os1$,ca$,caq$,os2$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4a$,ex4b$,ex4w$,ex5$,ex9$,ex10$,ns2$,caq4$, rep$&'smsq_q40_rom'
5180 END DEFine qx0_target
5190 :
5200 DEFine PROCedure qxl_target
5210   IF FOP_IN (rep$&'smsq_qxl_qxlh.exe') <> -7: INPUT 'QXLH.EXE exists'; cmd$: QUIT
5220   p$   = rep$&'sys_boot_file'
5230   bl$  = rep$&'smsq_qxl_qxl2_exe'
5240   hst$ = rep$&'smsq_qxl_host'
5250   hi$  = rep$&'smsq_qxl_hwinit'
5260   ld$  = rep$&'smsq_smsq_loader'
5270   os1$ = rep$&'smsq_smsq_os'
5280   ca$  = rep$&'smsq_smsq_cache'
5290   os2$ = rep$&'smsq_sbas_control'
5300   ns1$ = rep$&'smsq_qxl_nasty_e'
5310   ln1$ = rep$&'smsq_smsq_lang'
5320   ln2$ = rep$&'smsq_qxl_kbd_lang'
5330   ln3$ = rep$&'smsq_sbas_lang'
5340   ex1$ = rep$&'smsq_qxl_procs_x'
5350   ex2$ = rep$&'smsq_qxl_driver_most'  : REMark Keyboard before CON
5360   ex3$ = rep$&'smsq_qxl_driver_ql'
5370   ex3b$ = rep$&'smsq_qxl_driver_16'
5380   ex3w$ = rep$&'smsq_smsq_wman'
5390   ex4$ = rep$&'smsq_qxl_driver_nd'
5400   ex5$ = rep$&'smsq_qxl_driver_dv3e'
5410   ex6$= rep$&'smsq_smsq_hotkey'
5420   ex7$ = rep$&'smsq_qxl_sysspr'
5430   ex8$=rep$&'smsq_home_home'
5440   last$ = rep$&'smsq_qxl_ecache'
5450   dml$ = rep$&'smsq_qxl_dummy'
5460   qm$  = rep$&'qmon_qxl_bin'
5470   r$='ram1_smsqe.exe' : f$=rep$&'smsq_qxl_smsqe.exe'
5480   DELETE f$
5490   EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,last$, r$
5500   EW cct, bl$,r$, f$
5510   DELETE r$
5520   OPEN #4,f$
5530   lenf=FLEN(#4)
5540   lens% = INT ((lenf+511)/512)
5550   lenb% = lenf - lens% * 512
5560   IF lenb%: lenb% = lenb% + 512
5570   BPUT #4\2, lenb% MOD 256, lenb% DIV 256, lens% MOD 256, lens% DIV 256
5580   CLOSE #4
5590 END DEFine qxl_target
5600 :
5610 DEFine PROCedure java_target
5620   f$=rep$&'sys_boot_file'
5630   v$=rep$&'smsq_smsq_vers'
5640   h$=rep$&'smsq_java_host'
5650   rtc$=rep$&'smsq_java_rtc'
5660   ld$=rep$&'smsq_smsq_loader'
5670   hi$=rep$&'smsq_java_hwinit'
5680   os1$=rep$&'smsq_java_smsq_os'
5690   ca$=rep$&'smsq_smsq_cache'
5700   os2$=rep$&'smsq_sbas_control'
5710   ln1$=rep$&'smsq_smsq_lang'
5720   ln2$=rep$&'smsq_gold_kbd_lang'
5730   ln3$=rep$&'smsq_sbas_lang'
5740   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
5750   ex2$=rep$&'smsq_java_driver_most'
5760   ex3a$=rep$&'smsq_java_driver_ql'
5770   ex3b$=rep$&'smsq_java_driver_16'
5780   ex3w$=rep$&'smsq_smsq_wman'
5790   ex7$=rep$&'smsq_smsq_hotkey'
5800   ex8$=rep$&'smsq_gold_sysspr'
5810   ex9$=rep$&'smsq_home_home'
5820   nl$=rep$&'sys_boot_null'
5830   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,rtc$,ln1$,ln2$,ln3$,ex2$,ex3a$,ex3b$,ex3w$,ex1$,ex7$,ex8$,ex9$, rep$&'SMSQ_java_java'
5840 END DEFine java_target
5850 :
5860 DEFine FuNction find_target% (current%)
5870   IF "SMSQ Generic" INSTR mdata$(current%):RETurn 1
5880   IF "Atari" INSTR mdata$(current%):RETurn 2
5890   IF "GoldCard" INSTR mdata$(current%):RETurn 4
5900   IF "Q40" INSTR mdata$(current%):RETurn 8
5910   IF "QXL" INSTR mdata$(current%):RETurn 16
5920   IF "Aurora" INSTR mdata$(current%):RETurn 32
5930   IF "Java" INSTR mdata$(current%):RETurn 64
5940   RETurn 0                             : REMark huh???
5950 END DEFine find_target%
5960 :
5970 DEFine PROCedure change_size (x%,y%)
5980 LOCal xrel%,yrel%,temp,a$,ttemp%(main_lit%),abs%,yabs%,t%,bt$
5990   main_lfl%(size_it%)=1                 : REMark reset to available
6000   ARRCOPY main_lfl%,ttemp%              : REMark keep statusses
6010   CH_WIN main_defn,xrel%,yrel%          : REMark ptr displacement
6020   xsize%=xsize%-xrel%+3
6030   ysize%=ysize%-yrel%                   : REMark new sizes
6040   t%=2^4+2^5
6050   RPTR xabs%,yabs%,t%,t%,xrel%,yrel%,bt$: REMark get absolute coords
6060   IF xsize%>xsz%-16:xsize%=xsz%-16
6070   IF ysize%>ysz%-16:ysize%=ysz%-16      : REMark max values
6080   DR_UNST main_defn                     : REMark no more wdw defn...
6090   DR_REMV main_defn                     : REMark .... now!
6100   app_addr=0                            : REMark nor any appsub wdw
6110   make_main_wdw                         : REMark make window anew
6120   ARRCOPY ttemp%,main_lfl%
6130   t%=lxs*3
6140   xabs%=xabs%-t%:IF xabs%<0:xabs%=0     : REMark position ptr nicely
6150   yabs%=yabs%-8:IF yabs%<0:yabs%=0
6160   SPTR xabs%,yabs%,0                    : REMark now
6170   IF compiled
6180      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
6190   ELSE
6200      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
6210   END IF
6220    POS_PTR main_defn,t%*65536+8,1       : REMark position pointer in wdw
6230 END DEFine change_size
6240 :
6250 REMark as of here, standard functions/procs (except init)
6260 :
6270 DEFine FuNction warning_pos% (mess$,channel%,title$,pos)
6280 REMark warning wdw with positioning
6290   POS_PTR main_defn,pos,-1
6300   RETurn warning%(mess$,channel%,title$)
6310 END DEFine warning_pos%
6320 :
6330 DEFine FuNction warning%(mess$,channel%,title$)
6340 REMark shows a warning message (mess$) in a small window. Hit ESC or OK
6350 REMark to return; returns 0 if ESC, 1 if OK  hit
6360 LOCal item%,lp%,chan%,y%,swnum%
6370   IF channel%
6380     chan%=channel%
6390   ELSE
6400     chan%=FOPEN("con")
6410   END IF
6420   CH_ITEM warn_defn,-3,0,-1,'','Attention'
6430   CH_ITEM warn_defn,-1,0,-1,cancel$,'ESC'
6440   CH_ITEM warn_defn,-1,1,-1,'O','OK'
6450   warn_lfl%(0)=1:warn_lfl%(1)=1
6460   POKE_W warn_defn+46,pink                        : REMark paper colour of wdw
6470   POKE_W warn_defn+44,red                         : REMark border around wdw
6480   DR_PULD warn_defn,-1,-1
6490   DR_IWDF #chan%,warn_defn,0
6500   PAPER#chan%,white :INK#chan%,red
6510   IF title$<>"":PRINT#chan%,title$;" :"
6520   PRINT #chan%,mess$
6530   POS_PTR warn_defn,160*65536+10,-1
6540   REPeat lp%
6550     RD_PTR warn_defn,item%,swnum%,y%,y%,y%,warn_lfl%
6560     IF swnum%=-1:EXIT lp%
6570   END REPeat lp%
6580   IF NOT channel%:CLOSE#chan%
6590   DR_UNST warn_defn
6600   RETurn item%
6610 END DEFine warning%
6620 :
6630 DEFine PROCedure info_pos (mess$,channel%,pos)
6640 REMark warning wdw with positioning
6650   POS_PTR main_defn,pos,-1
6660   info mess$,channel%
6670 END DEFine info_pos
6680 :
6690 DEFine PROCedure info(mess$,channel%)
6700 REMark shows window containing mess$
6710   LOCal chan%
6720   IF channel%
6730     chan%=channel%
6740   ELSE
6750     chan%=FOPEN("con")
6760   END IF
6770   CH_ITEM warn_defn,-3,0,-1,"",'  Info.  '
6780   CH_ITEM warn_defn,-1,0,-1,"",'Info'
6790   CH_ITEM warn_defn,-1,1,-1,"",'Info'
6800   POKE_W warn_defn+44,green                       : REMark border
6810   POKE_W warn_defn+46,light_green                  : REMark paper col
6820   DR_PULD warn_defn,-1,-1
6830   POS_PTR warn_defn,90*65536+70,-1
6840   DR_IWDF #chan%,warn_defn,0
6850   PAPER#chan%,white:INK#chan%,black
6860   PRINT #chan%,mess$
6870   IF NOT channel%:CLOSE#chan%
6880 END DEFine info
6890 :
6900 DEFine PROCedure make_colours (colourway%)
6910 REMark this initialises the GLOBAL variables menu_pap, menu_brd, menu_inv,
6920 REMark menu_ink and menu_bar according to the colourway passed as parameter.
6930 REMark The GLOBAl variable mycolour_mode% must have been set prior to calling
6940 REMark this function, preferrably via the function set_colours%.
6950 REMark Moreover the colours black, white,red, green,wg_stripes, etc...
6960 REMark must have been set prior to calling this procedure:
6970 REMark Preferrably, they were also set via the function set_colours%
6980 :
6990 REMark this function needs the extension WL_MK16 (outptr_bin)
7000 :
7010 REMark the colourways% are:
7020 REMark 0= white/green
7030 REMark 1= black/red
7040 REMark 2= white/red
7050 REMark 3= black/green
7060 REMark 4= use system palette (only available in modes 32 and 33)
7070 :
7080 REMark the Global war mycolour_mode% is:
7090 REMark 0 = we are in QL mode 4
7100 REMark 1 = we are in PAL mode (256 colours)
7110 REMark 2 - we are in 24 bit colour mode
7120 REMark preferrably, this is set via the function set_colours%
7130 :
7140   IF colourway%>7 OR colourway%<0: colourway%=0: REMark must be between 0 & 7
7150 :
7160 REMark first make sure that what we request is possible.
7170 REMark colourway 4 is only possible if we are in 24 bit colour mode
7180 REMark if it is requested in another mode, it is set to 0
7190 :
7200   IF colourway%>3
7210     IF mycolour_mode%< 2
7220        colourway%=0
7230      ELSE
7240        make_my_palette colourway%-4:RETurn   : REMark set colours according to system palette
7250     END IF
7260   END IF
7270 :
7280   SELect ON mycolour_mode%
7290     =0                                  : REMark QL 4 colours
7300       SELect ON colourway%
7310           =0:                           : REMark white/green
7320                 menu_pap=white
7330                 menu_brd=green
7340                 menu_ink=black
7350                 menu_bar=wg_striped
7360                 menu_inv=red
7370           =1:                           : REMark black/red
7380                 menu_pap=black
7390                 menu_brd=red
7400                 menu_ink=white
7410                 menu_bar=br_striped
7420                 menu_inv=green
7430           =2:                           : REMark white/red
7440                 menu_pap=white
7450                 menu_brd=red
7460                 menu_ink=black
7470                 menu_bar=wr_striped
7480                 menu_inv=green
7490           =3:                           : REMark black/green
7500                 menu_pap=black
7510                 menu_brd=green
7520                 menu_ink=white
7530                 menu_bar=bg_striped
7540                 menu_inv=red
7550       END SELect
7560     =1                                  : REMark pal colours
7570       SELect ON colourway%
7580           =0:                           : REMark white/green
7590                 menu_pap=white+256
7600                 menu_brd=green+256
7610                 menu_ink=black+256
7620                 menu_bar=wg_striped+256
7630                 menu_inv=red +256
7640           =1:                           : REMark black/red
7650                 menu_pap=black+256
7660                 menu_brd=red  +256
7670                 menu_ink=white +256
7680                 menu_bar=br_striped +256
7690                 menu_inv=green +256
7700                 omenu_pap=black+256
7710           =2:                           : REMark white/red
7720                 menu_pap=white +256
7730                 menu_brd=red   +256
7740                 menu_ink=black +256
7750                 menu_bar=wr_striped +256
7760                 menu_inv=green +256
7770           =3:                           : REMark black/green
7780                 menu_pap=black +256
7790                 menu_brd=green +256
7800                 menu_ink=white +256
7810                 menu_bar=bg_striped +256
7820                 menu_inv=red   +256
7830       END SELect
7840     =2                                  : REMark 24 bit colours
7850       SELect ON colourway%
7860           =0:                           : REMark white/green
7870                 menu_pap=WL_MK16(white)
7880                 menu_brd=WL_MK16(green)
7890                 menu_ink=WL_MK16(black)
7900                 menu_bar=WL_MK16(wg_striped)
7910                 menu_inv=WL_MK16(red)
7920           =1:                           : REMark black/red
7930                 menu_pap=WL_MK16(black)
7940                 menu_brd=WL_MK16(red)
7950                 menu_ink=WL_MK16(white)
7960                 menu_bar=WL_MK16(br_striped)
7970                 menu_inv=WL_MK16(green)
7980           =2:                           : REMark white/red
7990                 menu_pap=WL_MK16(white)
8000                 menu_brd=WL_MK16(red)
8010                 menu_ink=WL_MK16(black)
8020                 menu_bar=WL_MK16(wr_striped)
8030                 menu_inv=WL_MK16(green)
8040           =3:                           : REMark black/green
8050                 menu_pap=WL_MK16(black)
8060                 menu_brd=WL_MK16(green)
8070                 menu_ink=WL_MK16(white)
8080                 menu_bar=WL_MK16(bg_striped)
8090                 menu_inv=WL_MK16(red)
8100       END SELect
8110   END SELect
8120 :
8130   make_my_old_palette
8140 END DEFine make_colours
8150 :
8160 DEFine PROCedure make_my_palette (cw%)
8170   sp.winbd            = HEX('0200') : REMark Window border
8180   sp.winbg            = HEX('0201') : REMark Window background
8190   sp.winfg            = HEX('0202') : REMark Window foreground
8200   sp.winmg            = HEX('0203') : REMark Window middleground
8210   sp.titlebg          = HEX('0204') : REMark Title background
8220   sp.titletextbg      = HEX('0205') : REMark Title text background
8230   sp.titlefg          = HEX('0206') : REMark Title foreground
8240   sp.litemhigh        = HEX('0207') : REMark Loose item highlight
8250   sp.litemavabg       = HEX('0208') : REMark Loose item available background
8260   sp.litemavafg       = HEX('0209') : REMark Loose item available foreground
8270   sp.litemselbg       = HEX('020a') : REMark Loose item selected background
8280   sp.litemselfg       = HEX('020b') : REMark Loose item  elected foreground
8290   sp.litemunabg       = HEX('020c') : REMark Loose item unavailable background
8300   sp.litemunafg       = HEX('020d') : REMark Loose item unavailable foreground
8310   sp.infwinbd         = HEX('020e') : REMark Information window border
8320   sp.infwinbg         = HEX('020f') : REMark Information window background
8330   sp.infwinfg         = HEX('0210') : REMark Information window foreground
8340   sp.infwinmg         = HEX('0211') : REMark Information window middleground
8350   sp.subinfbd         = HEX('0212') : REMark Subsidiary information window border
8360   sp.subinfbg         = HEX('0213') : REMark Subsidiary information window background
8370   sp.subinffg         = HEX('0214') : REMark Subsidiary information window foreground
8380   sp.subinfmg         = HEX('0215') : REMark Subsidiary information window middleground
8390   sp.appbd            = HEX('0216') : REMark Application window border
8400   sp.appbg            = HEX('0217') : REMark Application window background
8410   sp.appfg            = HEX('0218') : REMark Application window foreground
8420   sp.appmg            = HEX('0219') : REMark Application window middleground
8430   sp.appihigh         = HEX('021a') : REMark Application window item highlight
8440   sp.appiavabg        = HEX('021b') : REMark Application window item available background
8450   sp.appiavafg        = HEX('021c') : REMark Application window item available foreground
8460   sp.appiselbg        = HEX('021d') : REMark Application window item selected background
8470   sp.appiselfg        = HEX('021e') : REMark Application window item selected foreground
8480   sp.appiunabg        = HEX('021f') : REMark Application window item unavailable background
8490   sp.appiunafg        = HEX('0220') : REMark Application window item unavailable foreground
8500   sp.scrbar           = HEX('0221') : REMark Pan/scroll bar
8510   sp.scrbarsec        = HEX('0222') : REMark Pan/scroll bar section
8520   sp.scrbararr        = HEX('0223') : REMark Pan/scroll bar arrow
8530   sp.buthigh          = HEX('0224') : REMark Button highlight
8540   sp.butbd            = HEX('0225') : REMark Button border
8550   sp.butbg            = HEX('0226') : REMark Button background
8560   sp.butfg            = HEX('0227') : REMark Button foreground
8570   sp.hintbd           = HEX('0228') : REMark Hint border
8580   sp.hintbg           = HEX('0229') : REMark Hint background
8590   sp.hintfg           = HEX('022a') : REMark Hint foreground
8600   sp.hintmg           = HEX('022b') : REMark Hint middleground
8610   sp.errbg            = HEX('022c') : REMark Error message background
8620   sp.errfg            = HEX('022d') : REMark Error message foreground
8630   sp.errmg            = HEX('022e') : REMark Error message middleground
8640   sp.shaded           = HEX('022f') : REMark sp.shaded area
8650   sp.3ddark           = HEX('0230') : REMark Dark 3D border shade
8660   sp.3dlight          = HEX('0231') : REMark Light 3D border shade
8670   sp.vertfill         = HEX('0232') : REMark Vertical area fill
8680   IF NOT (IS_OPEN(#0) OR IS_OPEN(#1))
8690     OPEN#1,'con'
8700     SP_JOBPAL -1,cw%
8710     CLOSE#1
8720   ELSE
8730     SP_JOBPAL -1,cw%
8740   END IF
8750 END DEFine make_my_palette
8760 :
8770 DEFine PROCedure make_my_old_palette
8780 REMark this translates the colours used in the wdws into 4 colour mode...
8790   sp.winbd          = menu_brd          : REMark Window border
8800   sp.winbg          = menu_pap          : REMark Window background
8810   sp.winfg          = menu_pap          : REMark Window foreground
8820   sp.titletextbg    = menu_pap          : REMark Title text background
8830   sp.titlefg        = menu_ink          : REMark Title foreground
8840   sp.titlebg        = menu_bar          : REMark Title background
8850   sp.litemhigh      = menu_ink          : REMark Loose item highlight
8860   sp.litemavabg     = menu_pap          : REMark Loose item available background
8870   sp.litemavafg     = menu_ink          : REMark Loex fifiose item available foreground
8880   sp.litemselbg     = menu_brd          : REMark Loose item selected background
8890   sp.litemselfg     = menu_ink          : REMark Loose item selected foreground
8900   sp.litemunabg     = menu_pap          : REMark Loose item unavailable background
8910   sp.litemunafg     = menu_brd          : REMark Loose item unavailable foreground
8920   sp.infwinbd       = menu_brd          : REMark Information window border
8930   sp.infwinbg       = menu_pap          : REMark Information window background
8940   sp.infwinfg       = menu_ink          : REMark Information window foreground
8950   sp.appbd          = menu_brd          : REMark Application window border
8960   sp.appbg          = menu_pap          : REMark Application window background
8970   sp.appfg          = menu_ink          : REMark Application window foreground
8980   sp.appmg          = menu_pap          : REMark Application window middleground
8990   sp.appihigh       = menu_ink          : REMark Application window item highlight
9000   sp.appiavabg      = menu_pap          : REMark Application window item available background
9010   sp.appiavafg      = menu_ink          : REMark Application window item available foreground
9020   sp.appiselbg      = menu_brd          : REMark Application window item selected background
9030   sp.appiselfg      = menu_ink          : REMark Application window item selected foreground
9040   sp.appiunabg      = menu_pap          : REMark Application window item unavailable background
9050   sp.appiunafg      = menu_brd          : REMark Application window item unavailable foreground
9060   sp.scrbar         = menu_ink          : REMark Pan/scroll bar
9070   sp.scrbararr      = menu_brd          : REMark Pan/scroll bar arrow
9080   sp.buthigh        = menu_ink          : REMark Button highlight
9090   sp.butbd          = menu_brd          : REMark Button border
9100   sp.butbg          = menu_pap          : REMark Button background
9110   sp.butfg          = menu_ink          : REMark Button foreground
9120 END DEFine mk_my_old_palette
9130 :
9140 DEFine FuNction getstr$(poste%,drap%,defn,a$)
9150 REMark ouvre une fen‘tre sur un poste de menu dƒliƒ et propose
9160 REMark une cha•ne  ƒditer
9170 REMark les paramtres sont
9180 REMark - le numƒro du poste
9190 REMark - un drapeau si le rƒsultat doit ‘tre un chiffre (si drap%<>0)
9200 REMark - la dƒfinition de travail dans laquelle se trouve le poste
9210 REMark - la cha•ne qu'on peut ƒditer
9220 REMark returns the string or ""
9230 REMark uses the global (!) vars menu_pap% & menu_brd%
9240 REMark
9250   LOCal chan%,b$,res
9260   b$=" ":b$=a$   : REMark c'est curieusement nƒcessaire
9270   chan%=FOPEN("con")
9280   PAPER#chan%,menu_brd
9290   INK#chan%,menu_ink
9300   DR_LWDF#chan%,defn,poste%             : REMark open channel over item
9310   CLS#chan%
9320   res=GET_STR(#chan%,drap%,b$)          : REMark get string
9330   CLOSE#chan%
9340   IF res<0:b$=""                        : REMark ooops
9350   CH_ITEM defn,-1,poste%,-1,"",b$
9360   RETurn b$
9370 END DEFine getstr$
9380 :
9390 DEFine FuNction showcont$(string$,length%)
9400 REMark this adds '...' at the end of a string, to show that it continues
9410 LOCal l%,lengt%
9420   l%=LEN(string$)
9430   IF NOT l%:RETurn ""                   : REMark empty string
9440   lengt%=length% DIV 6
9450   IF l%<=lengt%:RETurn string$          : REMark entire string fits wdw
9460   RETurn string$(1 TO lengt%-3)&"..."
9470 END DEFine showcont$
9480 :
9490 DEFine FuNction get_free_text$(defn,item%,prompt$,length%)
9500 REMark this opens wdw over loose-item item% of the working definition defn
9510 REMark allows free text to be entered
9520 REMark params: working defintion,item over which wdw is to be opened
9530 REMark         first value of string, length of max string in wdw, if this is...
9540 REMark            -1, the length will be determined here
9550 LOCal chan%,string$,tempsizes%(3)
9560   chan%=FOPEN('con')
9570   IF chan%<0:RETurn prompt$
9580   DR_LWDF#chan%,defn,item%              : REMark open wdw over item
9590   PAPER#chan%,menu_brd
9600   INK#chan%,menu_ink
9610   CLS#chan%
9620   string$=WEDIT$(#chan%,prompt$)
9630   IF length%=-1
9640      WSIZE#chan%,tempsizes%
9650      length%=tempsizes%(0)
9660   END IF
9670   CLOSE#chan%
9680   IF length%
9690     CH_ITEM defn,-1,item%,-1,"",showcont$(string$,length%)
9700   ELSE
9710     CH_ITEM defn,-1,item%,-1,"",string$
9720   END IF
9730   RETurn string$
9740 END DEFine get_free_text$
9750 :
9760 DEFine FuNction do_caps(a$,caps%)
9770 REMark set string to capitales etc,caps% determines whether the string....
9780 REMark ...must be lower case (=0), upper case(=1), all words capitalized (=2),
9790 REMark ... first word only capitalized (=3), or nothing(=-1)
9800   SELect ON caps%
9810    =0: MIN a$
9820    =1: MAJ a$
9830    =2: CAP2 a$
9840    =3: CAP a$
9850   END SELect
9860   RETurn a$
9870 END DEFine do_caps
9880 :
9890 DEFine FuNction centr$(ctr$,leng)
9900 REMark this "centres" the string ctr$ (i.e. left padds it with spaces) so that
9910 REMark it is centred in a window of leng width (leng is length in chars)
9920   LOCal l%,a$
9930   a$=ctr$
9940   l%=INT(leng-LEN(a$))/2
9950   IF l%<0:RETurn a$
9960   a$=FILL$(' ',l%)&a$&FILL$(' ',l%)
9970   RETurn a$
9980 END DEFine centr$
9990 :
10000 DEFine FuNction set_colours% (force_mode%)
10010 REMark this sets colour variables according to the colour mode wished, if possible.
10020 REMark force_mode%=
10030 REMark     0 if ql colours wished (mode 4 only)
10040 REMark     1 if pal colours wished
10050 REMark     2 if 24 bit colours wished
10060 REMark This function returns the true QL mode set (4,8,32,33 etc.).
10070 REMark This function also sets the GLOBAL variable mycolour_mode% to the
10080 REMark mode 0,1,2 as above - whatever is possible on the machine, i.e.
10090 REMark if colour_24 is wished on a 4 colour machine (which won't succeed)
10100 REMark this function will set set that variable to 0, i.e. QL mode.
10110 REMark
10120 REMark This function needs the outptr toolkit
10130 LOCal mode%
10140   mode%=RMODE                           : REMark get current mode now
10150   SELect ON force_mode%
10160       = 0                               : REMark mode 4 wished
10170        SELect ON mode%
10180           = 4                           : REMark already on, so do nothing
10190           = 0,1,8 : MODE 4              : REMark old modes, set to 4 colours
10200           = REMAINDER
10210             COLOUR_QL                   : REMark if higher colour mode, set to "mode 4"
10220             MODE 4
10230        END SELect
10240        mode%=0                          : REMark new mode we're in  - 4 colour mode
10250       = 1                               : REMark PALette wished
10260        SELect ON mode%
10270           = 16,32,33 : COLOUR_PAL:mode%=1: REMark we're in high colour mode, but PAL colours wished
10280           = REMAINDER
10290             IF mode%<>4:MODE 4
10300             mode%=0
10310        END SELect
10320       = 2
10330        SELect ON mode%
10340           = 32,33 : COLOUR_24 :mode%=2  : REMark we're in high col mode 24 bit colours
10350           = 16 : COLOUR_PAL : mode%=1
10360           = REMAINDER
10370             IF mode%<>4:MODE 4
10380             mode%=0
10390        END SELect
10400   END SELect
10410   mycolour_mode%=mode%                  : REMark set this global variable now:
10420   REMark now set some colour variables
10430   SELect ON mode%
10440        = 0                              : REMark Ql 4 mode colours
10450         black          = 0
10460         white          = 7
10470         red            = 2
10480         green          = 4
10490         grey           = 255
10500         dull_pink      = 19
10510         Blue           = 0
10520         Magenta        = 2
10530         Yellow         = 7
10540         Cyan           = 0
10550         Dark_slate     = 0
10560         Slate_grey     = 255
10570         Dark_grey      = 255
10580         Light_grey     = 255
10590         Ash_grey       = 255
10600         Dark_red       = 210
10610         light_green    = 220
10620         Mustard        = 242
10630         Dark_green     = 228
10640         Sea_blue       = 0
10650         Dark_blue      = 0
10660         Purple         = 0
10670         Shocking_pink  = 234
10680         Orange         = 234
10690         Lime_green     = 4
10700         Apple_green    = 4
10710         Bright_blue    = 0
10720         Mauve          = 0
10730         Peach          = 7
10740         Light_yellow   = 7
10750         Light_blue     = 0
10760         Sky_blue       = 0
10770         Rose_pink      = 234
10780         pink           = 234
10790         Beige          = 7
10800         Pastel_pink    = 234
10810         Pastel_yellow  = 7
10820         Pastel_green   = 4
10830         Pastel_cyan    = 0
10840         Pastel_blue    = 0
10850         Pastel_rose    = 234
10860         Brick          = 210
10870         Light_khaki    = 7
10880         Dull_green     = 228
10890         Dull_cyan      = 0
10900         Steel_blue     = 0
10910         dull_pink      = 234
10920         Brown          = 0
10930         Khaki          = 7
10940         Dusky_green    = 4
10950         Dusky_blue     = 0
10960         Midnight_blue  = 0
10970         Plum           = 0
10980         Dusky_pink     = 234
10990         Buff           = 0
11000         Avocado        = 4
11010         Dull_turquoise = 4
11020         Dull_blue      = 0
11030         Faded_purple   = 234
11040         Cerise         = 2
11050         Tan_normal     = 0
11060         Grass_green    = 4
11070         Sea_green      = 4
11080         Ultramarine    = 0
11090         Deep_purple    = 234
11100         Light_orange   = 7
11110         light_red      = 2
11120         wg_striped     = 92
11130         br_striped     = 82
11140         wr_striped     = 107
11150         bg_striped     = 100
11160        = 1                      : REMark Colour_pal
11170         black          = 0
11180         white          = 1
11190         red            = 2
11200         green          = 3
11210         Blue           = 4
11220         Magenta        = 5
11230         Yellow         = 6
11240         Cyan           = 7
11250         Dark_slate     = 8
11260         Slate_grey     = 9
11270         Dark_grey      = 10
11280         grey           = 11
11290         Light_grey     = 12
11300         Ash_grey       = 13
11310         Dark_red       = 14
11320         light_green    = 15
11330         Mustard        = 16
11340         Dark_green     = 17
11350         Sea_blue       = 18
11360         Dark_blue      = 19
11370         Purple         = 20
11380         Shocking_pink  = 21
11390         Orange         = 22
11400         Lime_green     = 23
11410         Apple_green    = 24
11420         Bright_blue    = 25
11430         Mauve          = 26
11440         Peach          = 27
11450         Light_yellow   = 28
11460         Light_blue     = 29
11470         Sky_blue       = 30
11480         Rose_pink      = 31
11490         pink           = 32
11500         Beige          = 33
11510         Pastel_pink    = 34
11520         Pastel_yellow  = 35
11530         Pastel_green   = 36
11540         Pastel_cyan    = 37
11550         Pastel_blue    = 38
11560         Pastel_rose    = 39
11570         Brick          = 40
11580         Light_khaki    = 41
11590         Dull_green     = 42
11600         Dull_cyan      = 43
11610         Steel_blue     = 44
11620         dull_pink      = 45
11630         Brown          = 46
11640         Khaki          = 47
11650         Dusky_green    = 48
11660         Dusky_blue     = 49
11670         Midnight_blue  = 50
11680         Plum           = 51
11690         Dusky_pink     = 52
11700         Buff           = 53
11710         Avocado        = 54
11720         Dull_turquoise = 55
11730         Dull_blue      = 56
11740         Faded_purple   = 57
11750         Cerise         = 58
11760         Tan_normal     = 59
11770         Grass_green    = 60
11780         Sea_green      = 61
11790         Ultramarine    = 62
11800         Deep_purple    = 63
11810         pinkish        = 77
11820         light_red      = 109
11830         Light_orange   = 208
11840         wg_striped     = light_green
11850         br_striped     = Dark_red
11860         wr_striped     = light_red
11870         bg_striped     = Dark_green
11880        = 2                      : REMark colour_24
11890         black          = HEX('000000')
11900         white          = HEX('FFFFFF')
11910         red            = HEX('FF0000')
11920         green          = HEX('00FF00')
11930         Blue           = HEX('0000FF')
11940         Magenta        = HEX('FF00FF')
11950         Yellow         = HEX('FFFF00')
11960         Cyan           = HEX('00FFFF')
11970         Dark_slate     = HEX('242424')
11980         Slate_grey     = HEX('494949')
11990         Dark_grey      = HEX('6D6D6D')
12000         grey           = HEX('929292')
12010         Light_grey     = HEX('B6B6B6')
12020         Ash_grey       = HEX('DBDBDB')
12030         Dark_red       = HEX('920000')
12040         light_green    = HEX('B6FFB6')
12050         Mustard        = HEX('929200')
12060         Dark_green     = HEX('009200')
12070         Sea_blue       = HEX('009292')
12080         Dark_blue      = HEX('000092')
12090         Purple         = HEX('920092')
12100         Shocking_pink  = HEX('FF0092')
12110         Orange         = HEX('FF9200')
12120         Lime_green     = HEX('92FF00')
12130         Apple_green    = HEX('00FF92')
12140         Bright_blue    = HEX('0092FF')
12150         Mauve          = HEX('9200FF')
12160         Peach          = HEX('FFB6B6')
12170         Light_yellow   = HEX('FFFFB6')
12180         Light_blue     = HEX('B6FFFF')
12190         Sky_blue       = HEX('B6B6FF')
12200         Rose_pink      = HEX('FFB6FF')
12210         pink           = HEX('FFB6DB')
12220         Beige          = HEX('FFDBB6')
12230         Pastel_pink    = HEX('FFDBDB')
12240         Pastel_yellow  = HEX('FFFFDB')
12250         Pastel_green   = HEX('DBFFDB')
12260         Pastel_cyan    = HEX('DBFFFF')
12270         Pastel_blue    = HEX('DBDBFF')
12280         Pastel_rose    = HEX('FFDBFF')
12290         Brick          = HEX('B66D6D')
12300         Light_khaki    = HEX('B6B66D')
12310         Dull_green     = HEX('6DB66D')
12320         Dull_cyan      = HEX('6DB6B6')
12330         Steel_blue     = HEX('6D6DB6')
12340         dull_pink      = HEX('B66DB6')
12350         Brown          = HEX('6D2424')
12360         Khaki          = HEX('6D6D24')
12370         Dusky_green    = HEX('246D24')
12380         Dusky_blue     = HEX('246D6D')
12390         Midnight_blue  = HEX('24246D')
12400         Plum           = HEX('6D246D')
12410         Dusky_pink     = HEX('B64992')
12420         Buff           = HEX('B69249')
12430         Avocado        = HEX('92B649')
12440         Dull_turquoise = HEX('49B692')
12450         Dull_blue      = HEX('4992B6')
12460         Faded_purple   = HEX('9249B6')
12470         Cerise         = HEX('920049')
12480         Tan_normal     = HEX('924900')
12490         Grass_green    = HEX('499200')
12500         Sea_green      = HEX('009249')
12510         Ultramarine    = HEX('004992')
12520         Deep_purple    = HEX('490092')
12530         Light_orange   = HEX('ffb624')
12540         pinkish        = HEX('C0037B')
12550         light_red      = HEX('FC1729')
12560         wg_striped     = light_green
12570         br_striped     = Dark_red
12580         wr_striped     = light_red
12590         bg_striped     = Dark_green
12600   END SELect
12610   mode%=RMODE
12620   RETurn mode%                     : REMark return current screen mode now
12630 END DEFine set_colours%
12640 :
12650 DEFine FuNction fen_app2 (adresse,tableau$,nr_sects%,selk%,phys%)
12660 REMark les paramtres sont: l'adresse (0 la premire fois)
12670 REMark le tableau de cha•ne, le nombre de sections,
12680 REMark le drapeau si on veut des touches de sƒlection et si sƒlection=action,
12690 REMark un tableau d'entiers contenant les tailles x,y et les origines
12700 REMark et les touches  exclure des touches de sƒlection
12710 LOCal app_attributs%(3,1),temp%,bord%,pap%,ori_x%,ori_y%
12720 LOCal tail_x%,tail_y%,fleches%,barrre%
12730 :
12740   IF adresse:RECHP adresse              : REMark si c'est un 2nd appel, on enlve l'ancienne
12750   tail_x%=phys%(0)                      : REMark (largeur)    Ceci est pour la clartƒ
12760   tail_y%=phys%(1)                      : REMark (hauteur)    seulement, on pourrait
12770   ori_x%=phys%(2)                       : REMark            utiliser directement le
12780   ori_y%=phys%(3)                       : REMark origs x,y    tableau phys% dans AP_MAKE
12790 :
12800   adresse=AP_MAKE2(selk%,tail_x%,tail_y%,ori_x%,ori_y%,nr_sects%,tableau$)
12810   IF adresse<0: RETurn 0                : REMark si erreur, on retourne 0
12820                                         : REMark Maintenant on dƒtermine les attributs
12830   bord%=sp.appbd                        : REMark couleur bordure
12840   pap%=sp.appbg                         : REMark couleur "papier"
12850   touche_sel%=9                         : REMark touche de sƒlection - TAB
12860   fleches%=sp.scrbararr                 : REMark couleur des flches
12870   barre%=sp.scrbar                      : REMark couleur de la barre d'ascenseur
12880 :
12890   app_attributs%(0,0)=1
12900   app_attributs%(0,1)=sp.appihigh       : REMark larg. & coul. trait poste courant
12910 :
12920   app_attributs%(1,0)=sp.appiunabg
12930   app_attributs%(1,1)=sp.appiunafg      : REMark unavailable
12940 :
12950   app_attributs%(2,0)=sp.appiavabg
12960   app_attributs%(2,1)=sp.appiavafg      : REMark disponible
12970 :
12980   app_attributs%(3,0)=sp.appiselbg
12990   app_attributs%(3,1)=sp.appiselfg      : REMark sƒlectionnƒ
13000 :
13010   er=AP_PARS(adresse,0,bord%,pap%,touche_sel%,fleches%,barre%,app_attributs%)
13020 :
13030   IF er:RETurn 0
13040 :
13050   RETurn adresse  : REMark on retourne l'adresse
13060 END DEFine fen_app
13070 :
13080 DEFine PROCedure scroll_arrows(rangees%,nbr_objets%,appfen,cty%)
13090 REMark Cette procƒdure dƒcide s'il faut laisser les flches de dƒfilement
13100 REMark params: le nombre de rangƒes, le nombre total d'objets
13110 REMark le pointer vers la sous-fen d'app et la dƒfinition de controle
13120   IF nbr_objets%<=rangees% +1
13130     POKE_W appfen+150,0
13140     cty%(1,2)=nbr_objets%
13150   ELSE
13160     POKE_W appfen+150,6
13170 REMark    cty%(1,2)=rangees%-1             : rem if scroll arrows are there, there is one line less in the window
13180     cty%(1,2)=rangees%
13190  END IF
13200  IF DIMN(cty%,1)=2:cty%(2,2)=cty%(1,2)
13210  cty%(0,1)=1                            : REMark show that definitionhas changed
13220 END DEFine scroll_arrows
13230 :
13240 DEFine PROCedure init
13250 LOCal temp%
13260   text=0:sprite=2:blob=4:pattern=6
13270   retr=256:retn=-256:sous1=254:sous2=252:sous3=250
13280   REMark normal: hit = selects/deselects, do = returns & leaves selected
13290   REMark retr = return even on hit, reset item to available after,
13300   REMark retn = return even on hit, hit selects/deselects, do selects
13310 :
13320   hit$=CHR$(1):do$=CHR$(2):cancel$=CHR$(3):sleep$=CHR$(7):wake$=CHR$(8)
13330   help$=CHR$(4):move$=CHR$(5):size$=CHR$(6):cr$=CHR$(10)
13340 :
13350   appwinoffset%=16                      : REMark appsub win additional y offset
13360   xsize%=500                            : REMark x size divisible by 4!
13370   ysize%=200                            : REMark provisional y size%
13380   lxs=24:lys=12                         : REMark standard item x,y, sizes
13390   inf1y=lys+4                           : REMark y orig of inner border
13400   xesp=lxs+2                            : REMark loose items x spacing
13410   nbr_post%=3                           : REMark nbr of item right & left in title bar
13420   main_coul%=SMSCFG(2)                  : REMark colourways
13430   app_coul%=main_coul%                  : REMark preset colourways
13440   temp%=set_colours% (2)                : REMark always try to set highest colours
13450   IF temp%<2 AND main_coul%>3:main_coul%=0 : REMark old wman
13460   make_colours main_coul%               : REMark set all colours now
13470 :
13480   rep$=SMSCFG(1)                        : REMark dir with files
13490   titre$="Dir: "&rep$&" "
13500 :
13510 RESTORE 13510
13520 :
13530 : REMark make loose item attributes
13540 :
13550   DIM std_iattr(3,3)
13560   RD_IATT std_iattr
13570     DATA 1,sp.litemhigh : REMark current item border size & colour
13580     DATA sp.litemunabg,sp.litemunafg,0,0 : REMark unavailable item paper & ink
13590     DATA sp.litemavabg,sp.litemavafg,0,0 : REMark   available         "    "       "
13600     DATA sp.litemselbg,sp.litemselfg,0,0 : REMark   selected
13610 :
13620 : REMark make data for sprites - uses the modified RD_SPRT function
13630 : REMark
13640 : REMark
13650 :
13660   espace_vide=ALCHP(100)                : REMark GLOBAL var for 0 mask sprites!
13670 :
13680   main_sprite=RD_SPRT(0)                : REMark 0 = normal mask
13690     DATA 11,13,6,5,4
13700     DATA '     www     '
13710     DATA '     waw     '
13720     DATA '     waw     '
13730     DATA '     waw     '
13740     DATA 'wwwwwa awwwww'
13750     DATA 'waaaa   aaaaw'
13760     DATA 'wwwwwa awwwww'
13770     DATA '     waw     '
13780     DATA '     waw     '
13790     DATA '     waw     '
13800     DATA '     www     '
13810 :
13820   move_sprite=RD_SPRT(1)                : REMark 1 = 0 mask
13830   IF temp%>1:move_sprite=6              : REMark or system sprite!
13840     DATA 10,14,5,4,4
13850     DATA 'aaaaaaaaaa    '
13860     DATA 'awwwwwwwwa    '
13870     DATA 'awwrrrrwwa    '
13880     DATA 'awwrrrrwwaaaaa'
13890     DATA 'awwrrwwwwwwwwa'
13900     DATA 'awwwwwwwwrrwwa'
13910     DATA 'aaaaawwrrrrwwa'
13920     DATA '    awwrrrrwwa'
13930     DATA '    awwwwwwwwa'
13940     DATA '    aaaaaaaaaa'
13950 :
13960   zzz_sprite= RD_SPRT(1)
13970   IF temp%>1:zzz_sprite=10
13980     DATA 7,15,0,0,4
13990     DATA 'wwwww          '
14000     DATA '   w           '
14010     DATA '  w  wwww      '
14020     DATA ' w     w       '
14030     DATA 'wwwww w  wwww  '
14040     DATA '     wwww  w   '
14050     DATA '          wwww '
14060 :
14070  size_sprite=RD_SPRT(1)
14080  IF temp%>1:size_sprite=7
14090     DATA 9,14,5,4,4
14100     DATA 'aaaaaaaaaaaaaa'
14110     DATA 'awwwwwwwwwwwwa'
14120     DATA 'awwrrrrrrrrwwa'
14130     DATA 'awwrrrrrrrrwwa'
14140     DATA 'awwrrwwwwwwwwa'
14150     DATA 'awwrrwwrrrrwwa'
14160     DATA 'awwrrwwrrrrwwa'
14170     DATA 'awwwwwwwwwwwwa'
14180     DATA 'aaaaaaaaaaaaaa'
14190 :
14200   make_my_data 1
14210   make_main_wdw
14220   main_lfl%(target_its%)=128
14230 :
14240 END DEFine init
14250 :
14260 DEFine PROCedure make_main_wdw
14270 REMark make the main window
14280 LOCal main_lot,main_iwt,main_iot1
14290 :
14300   REMark first make sure of the wdw sizes (we also come here from change_size!)
14310   xsize%=xsize%-(xsize% MOD 4)                    : REMark make into nbr divisible by 4
14320   IF xsize%<500:xsize%=500
14330 :
14340   REMark calculate the y size of the wdw so that it always looks nice, in that
14350   REMark the rows in the appsub always fill the appsub entirely
14360   ysize%=ysize%+(ysize%&&1)             : REMark make into even number
14370   IF ysize%<100:ysize%=100              : REMark predetermioned min ysize% before calculations
14380   ysize%=ysize%-inf1y-14-appwinoffset%
14390   rangees=ysize% DIV 12                 : REMark GLOBAL var: nbr of rows in appsub
14400   IF rangees>data_count%-1:rangees=data_count%-1
14410   IF rangees<3:rangees=3
14420   ysize%=rangees*12+inf1y+14 +appwinoffset%
14430 :
14440   REMark first calc info obj size - this also influences one loose item, used
14450   REMark in the title bar
14460   longueur_titre=LEN(titre$)*6          : REMark length in pixels
14470   titre_x=INT((xsize%-longueur_titre)/2): REMark x orig
14480 :
14490   REMark first the loose items
14500 :
14510   yinf1y=inf1y+4:
14520   esc%=0:move_it%=esc%+1:size_it%=move_it%+1
14530   sleep_it%=size_it%+1:all_it%=sleep_it%+1:go_it%=all_it%+1
14540   dir_it%=go_it%+1:target_its%=dir_it%+1:make_it%=target_its%+8
14550 :
14560 RESTORE 14560
14570   REMark reset data pointer to here (The Basic Linker does that automatically)
14580 :
14590   temp%=(xsize%-4) DIV 9
14600   main_lit%=15:DIM main_lfl%(main_lit%)   : REMark there are 16 items!
14610   main_lot=RD_LOT(std_iattr,main_lit%)
14620     DATA lxs,lys,4+xesp*0,2,0,0,cancel$,text+retr,'ESC'
14630     DATA lxs,lys,4+xesp*1,2,0,0,move$,sprite+retr,move_sprite
14640     DATA lxs,lys,4+xesp*2,2,0,0,size$,sprite+retn,size_sprite
14650     DATA lxs,lys,xsize%-2-xesp*1,2,0,0,sleep$,sprite+retr,zzz_sprite
14660     DATA lxs,lys,xsize%-2-xesp*2,2,0,0,'A',sous1+retn,"All"
14670     DATA lxs,lys,xsize%-2-xesp*3,2,0,0,'O',sous1+retn,"OK"
14680     DATA 6*6,lys-2,titre_x+30,3,0,0   ,'D',text+retn,rep$
14690     DATA 8*6,lys,8,inf1y+2,0,0,        'G',sous1+retn,"Generic"
14700     DATA 8*6,lys,8+temp%,inf1y+2,0,0,  'T',sous2+retn,"Atari"
14710     DATA 8*6,lys,8+temp%*2,inf1y+2,0,0,'L',sous3+retn,"GoldCard"
14720     DATA 8*6,lys,8+temp%*3,inf1y+2,0,0,'Q',sous1+retn,"Q40/Q60"
14730     DATA 8*6,lys,8+temp%*4,inf1y+2,0,0,'X',sous2+retn,"QXL"
14740     DATA 8*6,lys,8+temp%*5,inf1y+2,0,0,'U',sous2+retn,"Aurora"
14750     DATA 8*6,lys,8+temp%*6,inf1y+2,0,0,'J',sous1+retn,"Java"
14760     DATA 8*6,lys,8+temp%*7,inf1y+2,0,0,'P',sous1+retn,"PtrGen"
14770     DATA 8*6,lys,8+temp%*8,inf1y+2,0,0,'M',sous1+retn,"Make"
14780 :
14790   REMark now info wdw objects - here only one, the title
14800   REMark make the title data: calculate size and x position of this object
14810 :
14820   main_iot1=RD_IOT(0)
14830     DATA longueur_titre,10,4,INT((lys-10)/2),sous1,sp.titlefg,0,0,titre$
14840 :
14850   REMark now the info wdws
14860 :
14870   main_iwt=RD_IWT(2)                              : REMark there are 3
14880 :
14890 REMark first the title bar
14900 :
14910     DATA xsize%-8-(2*nbr_post%*xesp),lys+2,xesp*nbr_post%+4,0         : REMark x,y sizes & origs
14920     DATA 0,0,sp.infwinbd,sp.titlebg     : REMark shadow, border size & col,paper col
14930     DATA 0                              : REMark object pointer (none)
14940 :
14950 REMark now wdw containing title itself
14960 :
14970     DATA longueur_titre+8,lys,titre_x-4,2
14980     DATA 0,0,sp.infwindbd,sp.infwinbg
14990     DATA main_iot1                      : REMark one object
15000 :
15010 REMark inner border
15020 :
15030     DATA xsize%-8,ysize%-inf1y-2,4,inf1y
15040     DATA 0,1,sp.infwinbd,sp.infwinbg
15050     DATA 0
15060 :
15070 REMark now the appsub wdw - use the OUTPTR functions for this
15080 :
15090   app_addr=0                            : REMark there is none yet
15100   mk_main_app 1                         : REMark (this destroys my DATA pointer)
15110   main_app_list = app_addr+180          : REMark list built by OUTPTR
15120 :
15130 : REMark  working defn
15140 :
15150 RESTORE 15150
15160                                         : REMark reset DATA pointer (due to Basic Linker, restore must be ALONE on the line)
15170   main_defn=RD_WDEF
15180     DATA 2,1,sp.winbd,sp.winbg          : REMark shad,border size & col,pap col
15190     DATA xsize%,ysize%,0,0    : REMark x,y size & initial ptr pos
15200     DATA main_sprite,main_lot,main_iwt,main_app_list : REMark sprite,loose items, info wins, appsubs
15210 END DEFine make_main_wdw
15220 :
15230 DEFine PROCedure warn_make
15240 REMark makes warning & info window
15250 REMark uses global vars green etc...
15260 REMark SETS the global var warning_width% (nbr of chars in one line in wdw)
15270 LOCal lys%,warn_lot,warn_iot1,warn_iwt
15280 LOCal x%,y%,warning_paper,warning_brd
15290   RESTORE 15290
15300   x%=188:y%=82
15310   warning_paper=menu_pap
15320   warning_brd=pink                      : REMark pink
15330 REMark  if menu_brd=4:warning_brd=light_green      : rem light green
15340   lys%=10
15350   DIM warn_lfl%(1)
15360   warn_lot=RD_LOT(std_iattr,1)
15370     DATA 30,lys%,4,4,0,0,cancel$,text+retr,'ESC'
15380     DATA 30,lys%,x%-34,4,0,0,'O',text+retr,"OK"
15390 :
15400   warn_iot1=RD_IOT (0)
15410     DATA 12*9,10,0,1,text,red,2,0,'Attention'
15420 :
15430   warn_iwt=RD_IWT(1)
15440     DATA x%-16,y%-32,8,24
15450     DATA 0,1,red,warning_paper
15460     DATA 0
15470 :
15480     DATA 120,12,(x%-112) DIV 2,4
15490     DATA 0,1,menu_brd,warninggpaper
15500     DATA warn_iot1
15510 :
15520   warn_defn=RD_WDEF
15530     DATA 2,1,menu_brd,warning_brd
15540     DATA x%,y%,0,0
15550     DATA main_sprite,warn_lot,warn_iwt,0
15560 :
15570   warning_width%=(x%-16) DIV 6                    : REMark width of warning winfow in chars
15580 :
15590 END DEFine warn_make
15600 :
15610 DEFine FuNction RD_SPRT(drap%)
15620 REMark drap% determine si masque 0
15630   LOCal tmp_patt$(32,32)
15640   LOCal xs,ys,xo,yo, md, l
15650   READ ys,xs,xo,yo,md
15660   DIM tmp_patt$(ys,xs)
15670   FOR l=0 TO ys-1:READ tmp_patt$(l)
15680   l=ALCHP(SPRSP(xs,ys))
15690   SPSET l,xo,yo,md,tmp_patt$(0 TO ys-1,1 TO xs)
15700   IF drap%
15710     POKE_L l+16,espace_vide-(l+16)
15720   END IF
15730   RETurn l
15740 END DEFine RD_SPRT
15750 :
15760 DEFine FuNction RD_WDEF
15770 REMark read window defn
15780   LOCal ldef%(3), lspr, lloose, linfo, lappl, lattr%(3)
15790   RD_WATT lattr%
15800   READ ldef%(0), ldef%(1), ldef%(2), ldef%(3)
15810   READ lspr, lloose, linf, lappl
15820   RETurn MK_WDEF (ldef%, lattr%, lspr, lloose, linf, lappl)
15830 END DEFine RD_WDEF
15840 :
15850 DEFine FuNction RD_LOT (lattr,nitem)
15860   LOCal count(3)
15870   LOCal item, ltyp, a$, lsk$
15880   LOCal ldef%(nitem,6), lptr(3,nitem), lstr$(nitem,85)
15890   lsk$=''
15900   FOR item = 0 TO nitem
15910     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
15920     READ ldef%(item,4), ldef%(item,5)
15930     READ a$: lsk$=lsk$ & a$
15940     READ ltyp
15950     ldef%(item,6)=ltyp: ltyp=(ltyp MOD 256)/2
15960     IF ltyp>10 OR ltyp <0::ltyp=0
15970     IF ltyp
15980       READ lptr(ltyp,count(ltyp))
15990     ELSE
16000       READ lstr$(count(0))
16010     END IF
16020     count(ltyp)=count(ltyp)+1
16030   END FOR item
16040   RETurn MK_LIL (lattr, ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), ldef%(TO, 4 TO 5), lsk$, ldef%(TO, 6), lstr$, lptr(1), lptr(2), lptr(3))
16050 END DEFine RD_LOT
16060 :
16070 DEFine FuNction RD_IWT(nitem)
16080   LOCal item,mc1
16090   LOCal ldef%(nitem,3), latt%(nitem,3), lptr(nitem)
16100   FOR item = 0 TO nitem
16110     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
16120     READ latt%(item,0), latt%(item,1), latt%(item,2), latt%(item,3)
16130     READ lptr(item)
16140   END FOR item
16150   RETurn MK_IWL (ldef%, latt%, lptr)
16160 END DEFine RD_IWT
16170 :
16180 DEFine FuNction RD_IOT(nitem)
16190   LOCal count(3)
16200   LOCal item, ltyp, work1, work2
16210   LOCal ldef%(nitem,4), lptr(3,nitem), lstr$(nitem,85)
16220   FOR item = 0 TO nitem
16230     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
16240     READ ltyp
16250     ldef%(item,4)=ltyp:  ltyp=(ltyp MOD 256)/2
16260     IF ltyp >10:ltyp=0
16270     IF ltyp
16280       READ lptr(0,item),lptr(ltyp,count(ltyp))
16290     ELSE
16300       IF mycolour_mode%=2
16310           READ work1,work2,work3
16320           lptr(0,item)=WL_4_IOL(work1,work2,work3)
16330       ELSE
16340           READ work1
16350           READ work2: work1=work1*256+work2
16360           READ work2: lptr(0,item)=work1*256+work2
16370       END IF
16380       READ lstr$(count(0))
16390     END IF
16400     count(ltyp) = count(ltyp) + 1
16410   END FOR item
16420   RETurn MK_IOL (ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), lptr(0), ldef%(TO, 4), lstr$, lptr(1), lptr(2), lptr(3))
16430 END DEFine RD_IOT
16440 :
16450 DEFine PROCedure RD_IATT (lattr)
16460   LOCal i
16470   READ lattr(0,0), lattr(0,1)
16480   FOR i=1 TO 3: READ lattr(i,0), lattr(i,1), lattr(i,2), lattr(i,3)
16490 END DEFine RD_IATT
16500 :
16510 DEFine PROCedure RD_WATT (lattr%)
16520   READ lattr%(0), lattr%(1),lattr%(2),lattr%(3)
16530 END DEFine RD_WATT
16540 :
16550 DEFine PROCedure sa
16560   SAVE_O "dev8_extras_source_SMSQEMake_bas"
16570 END DEFine sa
16580 :
16590 DEFine PROCedure ccrestore
16600 LOCal lp%,k%,a$,c%,r%,out%
16610   SAVE_O ram1_p_bas
16620   c%=FOP_IN("ram1_p_bas")
16630   out%=FOP_OVER("ram1_SMSQEMake_bas")
16640   IF c%<0 OR out%<0:CLOSE:PRINT "can't open files":STOP
16650   REPeat lp%
16660     IF EOF(#c%):EXIT lp%
16670     INPUT#c%,a$
16680     r%="restore current_line" INSTR a$   : REMark xyxyxywtz   --- LEAVE THIS COMMENT!!!!!!!!
16690     IF r%=0:PRINT#out%,a$:NEXT lp%    : REMark no restore, just ga around again
16700     IF "xyxyxywtz" INSTR a$:PRINT#out%,a$:NEXT lp%
16710     t%=" " INSTR a$
16720     IF t%=0
16730       t%=CHR$(9) INSTR a$
16740     END IF
16750     IF t%=0:PRINT#out%,a$:NEXT lp%:REMark what?
16760     lineno$=a$(1  TO t% )
16770     PRINT#out%,lineno$;" ";"RESTORE";" ";lineno$
16780   END REPeat lp%
16790   CLOSE#c%
16800   CLOSE#out%
16810   save_as_sav
16820 END DEFine ccrestore
16830 :
16840 DEFine PROCedure save_as_sav
16850   LOCal c%
16860   c%=FOP_OVER("ram1_sas_bas" )
16870   IF c%<0:PRINT "can't open command file":STOP
16880   PRINT#c%,'10 load "ram1_SMSQEMake_bas"'
16890   CLOSE#c%
16900   c%=FOP_OVER("ram1_sas2_bas" )
16910   IF c%<0:PRINT "can't open second command file":STOP
16920   PRINT#c%,'qsave_o "ram1_SMSQEMake_sav"'
16930   CLOSE#c%
16940   EW ram1_sas_bas,ram1_sas2_bas
16950 END DEFine save_a_sav
16960 :
