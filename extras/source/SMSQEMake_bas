10 REMark $$asmb=dev8_extras_source_outptr_bin,0,10
20 REMark $$asmb=dev1_booty_qptr_bin,50,172
30 REMark $$asmb=dev8_extras_source_SMSQEMake_bin,0,10
40 REMark $$stak=20000
50 rem compiled with the Basic_Linker
60 compiled=NOT IS_OPEN(#0)
70 IF NOT compiled
80   SCR_SIZE#0, xsz%,ysz%
90   OUTLN#0,xsz%,ysz%-28,0,28
100   WSET -1
110 END IF
120 init
130 main
140 STOP
150 :
160 DEFine PROCedure main
170 LOCal lp%,item%,event%,swnum%,xrel%,yrel%,item%,lp2%,tot%,errors%
180   IF compiled
190      OPEN#1,'con'
200      SCR_SIZE#1,xsz%,ysz%
210      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
220   ELSE
230      SCR_SIZE#1,xsz%,ysz%
240      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
250   END IF
260   POS_PTR main_defn,40*65536+20,1
270   errors%=1
280   IF autostart%:errors%=do_the_work%
290   IF quit_always%:STOP
300   IF quit_no_errors% AND NOT errors%:STOP
310   REPeat lp%
320     RD_PTR main_defn,item%,swnum%,event%,xrel%,yrel%,main_lfl%,main_mfl%,main_cty%
330     IF swnum%= -1
340       SELect ON item%
350                 =esc%         : EXIT lp%          : REMark quit prog
360                 =move_it% : CH_WIN main_defn: REMark move
370                 =size_it% : change_size xrel%,yrel% : REMark change size
380                 =sleep_it%: button main_defn,main_lfl%,main_mfl%,main_cty%,"SMSQ/E Make"
390                 =all_it%
400                      set_statusses                : REMark set all appsub item statusses
410                      main_cty%(0,1)=1   : REMark and redraw
420                 =go_it%       : errors%=do_the_work%
430                                 if errors%
440                                    errors%=warning%("There were errors"&chr$(10)&"Check the highlit items",0,"Errors!")
450                                 endif
460                 =dir_it%: make_new_dir item%
470                 =target_its% TO target_its%+8
480                                 start%=1:machines%=0
490                                 FOR lp2%=target_its% TO target_its%+8
500                                     IF main_lfl%(lp2%):machines%=machines%+start%
510                                     start%=start%*2
520                                 END FOR lp2%
530                                 make_my_data machines%      : REMark make data arrays
540                                 mk_main_app                           : REMark make appsub wdw
550                                 main_app_list=app_addr+180
560                                 AP_IN main_defn,main_app_list : REMark put it in
570                                 DR_WDRW main_defn:main_cty%(0,1)=1 : REMark and redraw entire wdw
580       END SELect
590     END IF
600   END REPeat lp%
610   DR_UNST main_defn
620   IF compiled:CLOSE#1
630   RECHP espace_vide
640 END DEFine main
650 :
660 DEFine FuNction do_the_work%
670 REMark this tries to compile all of the selected files
680 REMark if the file was compiled OK, status (of the link file) set to available
690 REMark if the file was not compiled ok, status left selected
700 LOCal lp%,bound%,a$,my_error%,tot_err%
710   bound%=DIMN(main_mfl%,1)              : REMark number of items to check
720   tot_err%=0
730   IF main_lfl%(del_it%)<>0
740      EW "dev8_extras_del_all_bas";"auto stop"
750   END IF
760   FOR lp%=0 TO bound%
770     a$=mdata$(lp%+1)                              : REMark potential file to compile
780     IF a$(1)<>"*" AND main_mfl%(lp%,0)
790        a$=rep$&a$                       : REMark add dir
800        my_error%=compile_and_check% (a$): REMark try to compile
810        IF NOT my_error%
820              main_mfl%(lp%,0)=1
830              DR_ADRW main_defn,0,main_mfl%,main_cty%
840        END IF
850        tot_err%=tot_err%+my_error%
860     END IF
870   END FOR lp%
880   main_cty%(0,1)=1
890   main_lfl%(go_it%)=1
900   IF main_lfl%(make_it%)=128 AND NOT tot_err%:make_targets
910   RETurn tot_err%
920 END DEFine do_the_work%
930 :
940 DEFine FuNction compile_and_check% (a$)
950 REMark this tries to compile a file, using the "make" prog
960 REMark this returns 1 if errors, else 0
970 LOCal chan%,is_ok$,ch_len
980   ch_len=EXEPF_W ("make";a$&" -w -l"&force_assembly$)                   : REMark exec make & wait
990   IF ch_len<>0
1000      EXEC_W "make";a$&" -w -l"&force_assembly$                 : REMark exec make & wait
1010   END IF
1020   is_ok$=a$(1 TO LEN(a$)-4)             : REMark file name w/o extension
1030   chan%=FOP_IN(is_ok$&'log')            : REMark open log file
1040   IF chan%<0:RETurn 1                             : REMark can't even open it!
1050   ch_len=FLEN(#chan%)                             : REMark file length
1060   GET#chan%\ch_len-10                             : REMark this is where error report is
1070   INPUT#chan%,is_ok$                              : REMark get it
1080   CLOSE#chan%
1090   IF NOT is_ok$=="No errors"
1100     RETurn 1                                      : REMark anything other than "no errors" signals errors
1110   ELSE
1120     RETurn 0
1130   END IF
1140 END DEFine compile_and_check%
1150 :
1160 DEFine PROCedure make_new_dir (poste%)
1170 REMark get athe new dir where the files are located
1180 LOCal quel_rep$
1190   IF MENU_OK
1200     quel_rep$= DIR_SELECT$("The Files are where?",,10,10,main_coul%,app_coul%)
1210   ELSE
1220     quel_rep$=getstr$(poste%,0,main_defn,a_lire$)           : REMark use menu extns if possible
1230   END IF
1240   IF LEN (quel_rep$) >= 5
1250      rep$=quel_rep$(1 TO 5)
1260   END IF
1270   CH_ITEM main_defn,-1,poste%,-1,"",rep$
1280   main_lfl%(poste%)=main_lfl%(poste%)+1
1290 END DEFine make_new_dir
1300 :
1310 DEFine PROCedure make_my_data (what_machine%)
1320 REMark what_machine% = 1 -> generic
1330 REMark what_machine% = 2 -> atari
1340 REMark what_machine% = 4 -> Gold Card
1350 REMark what_machine% = 8 -> Qx0
1360 REMark what_machine% = 16 -> QXL
1370 REMark what_machine% = 32 -> aurora
1380 REMark what_machine% = 64 -> java
1390 REMark what_machine% = 128 -> ptrgen
1400 REMark what_machine% = 256 -> qpc
1410 REMark these may be combined
1420   DIM mdata$(250,50)
1430   REMark first make generic data
1440   data_count%=1                         : REMark general data count (global var)
1450   IF what_machine%&&1
1460     generic_data                        : REMark set up generic data
1470     read_my_datas                       : REMark and read it
1480   END IF
1490   IF what_machine%&&2
1500      atari_data                         : REMark set up machine specific data....
1510      read_my_datas                      : REMark and read it
1520   END IF
1530   IF what_machine%&&4
1540      gc_data                                      : REMark set up machine specific data....
1550      read_my_datas                      : REMark and read it
1560   END IF
1570   IF what_machine%&&8
1580      q40_data                                     : REMark set up machine specific data....
1590      read_my_datas                      : REMark and read it
1600   END IF
1610   IF what_machine%&&16
1620      qxl_data                                     : REMark set up machine specific data....
1630      read_my_datas                      : REMark and read it
1640   END IF
1650   IF what_machine%&&32
1660      aurora_data                        : REMark set up machine specific data....
1670      read_my_datas                      : REMark and read it
1680   END IF
1690   IF what_machine%&&64
1700      java_data                                    : REMark set up machine specific data....
1710      read_my_datas                      : REMark and read it
1720   END IF
1730   IF what_machine%&&128
1740      ptrgen_data                        : REMark set up machine specific data....
1750      read_my_datas                      : REMark and read it
1760   END IF
1770   IF what_machine%&&256
1780      qpc_data                                : REMark set up machine specific data....
1790      read_my_datas                      : REMark and read it
1800   END IF
1810 END DEFine make_my_data
1820 :
1830 DEFine PROCedure read_my_datas
1840 REMark reads datas into the global var mdata$
1850 REMark uses data_count% from make_my_data
1860 LOCal lp%
1870   REPeat lp%
1880     READ mdata$(data_count%)
1890     IF mdata$(data_count%)="":EXIT lp%
1900     data_count%=data_count%+1
1910   END REPeat lp%
1920 END DEFine read_my_datas
1930 :
1940 DEFine PROCedure generic_data
1950 restore 1950
1960   DATA '**** -- SMSQ Generic -- ****'
1970   DATA 'smsq_smsq_loader_link'
1980   DATA 'smsq_smsq_fh_link'
1990   DATA 'smsq_smsq_link'
2000   DATA 'smsq_smsq_cache_link'
2010   DATA 'smsq_sbas_link'
2020   DATA 'smsq_smsq_lang_link'
2030   DATA 'smsq_sbas_lang_link'
2040   DATA 'smsq_sbas_procs_link'
2050   DATA 'smsq_smsq_wman_link'
2060   DATA 'smsq_smsq_hotkey_link'
2070   DATA 'smsq_smsq_vers_link'
2080   DATA 'smsq_home_link'
2090   DATA 'smsq_smsq_1mb_link',''
2100 END DEFine generic_data
2110 :
2120 DEFine PROCedure q40_data
2130 restore 2130
2140   DATA '**** -- Q40/Q60 -- ****'
2150   DATA 'smsq_q40_hwinit_link'
2160   DATA 'smsq_q40_nasty_link'
2170   DATA 'smsq_q40_cache_link'
2180   DATA 'smsq_q40_cachemode_link'
2190   DATA 'smsq_smsq_cache40c_link'
2200   DATA 'smsq_q40_driver_ser_link'
2210   DATA 'smsq_q40_driver_dv3_link'
2220   DATA 'smsq_q40_driver_ql_link'
2230   DATA 'smsq_q40_driver_16_link'
2240   DATA 'smsq_q40_kbd_lang_link'
2250   DATA 'smsq_sbas_procs_link'
2260   DATA 'smsq_q40_sysspr_link',''
2270 END DEFine q40_data
2280 :
2290 DEFine PROCedure atari_data
2300 restore 2300
2310   DATA '**** -- Atari -- ****'
2320   DATA 'smsq_atari_hwinit_link'
2330   DATA 'smsq_atari_nasty_link'
2340   DATA 'smsq_atari_driver_ql_link'
2350   DATA 'smsq_atari_driver_mo_link'
2360   DATA 'smsq_atari_driver_ser_link'
2370   DATA 'smsq_atari_driver_dv3_link'
2380   DATA 'smsq_atari_kbd_lang_link'
2390   DATA 'smsq_atari_sysspr_link'
2400   DATA 'sys_boot_st_host_link',''
2410 END DEFine atari_data
2420 :
2430 DEFine PROCedure gc_data
2440 restore 2440
2450   DATA '**** -- (Super)GoldCard -- ****'
2460   DATA 'smsq_gold_host_link'
2470   DATA 'smsq_gold_hwinit_link'
2480   DATA 'smsq_gold_nasty_link'
2490   DATA 'smsq_gold_nasty_s_link'
2500   DATA 'smsq_gold_driver_ql_link'
2510   DATA 'smsq_gold_driver_8_link'
2520   DATA 'smsq_gold_driver_most_link'
2530   DATA 'smsq_gold_driver_nd_link'
2540   DATA 'smsq_gold_driver_nds_link'
2550   DATA 'smsq_gold_driver_dv3_link'
2560   DATA 'smsq_gold_kbd_lang_link'
2570   DATA 'smsq_gold_kbd_abc_lng_link'
2580   DATA 'smsq_gold_kbd_abc_link'
2590   DATA 'smsq_gold_roms_link'
2600   DATA 'smsq_gold_qimi_link'
2610   DATA 'smsq_gold_sysspr_link',''
2620 END DEFine gc_data
2630 :
2640 DEFine PROCedure aurora_data
2650 restore 2650
2660   DATA '**** -- Aurora -- ****'
2670   DATA 'smsq_gold_host_link'
2680   DATA 'smsq_gold_hwinit_link'
2690   DATA 'smsq_gold_nasty_link'
2700   DATA 'smsq_gold_nasty_s_link'
2710   DATA 'smsq_aurora_driver_8_link'
2720   DATA 'smsq_gold_driver_ql_link'
2730   DATA 'smsq_gold_driver_most_link'
2740   DATA 'smsq_gold_driver_nd_link'
2750   DATA 'smsq_gold_driver_nds_link'
2760   DATA 'smsq_gold_driver_dv3_link'
2770   DATA 'smsq_gold_kbd_lang_link'
2780   DATA 'smsq_gold_kbd_abc_lng_link'
2790   DATA 'smsq_gold_kbd_abc_link'
2800   DATA 'smsq_gold_roms_link'
2810   DATA 'smsq_gold_qimi_link'
2820   DATA 'smsq_aurora_sysspr_link'
2830   DATA 'smsq_gold_sysspr_link',''
2840 END DEFine aurora_data
2850 :
2860 DEFine PROCedure qxl_data
2870 restore 2870
2880   DATA '**** -- QXL -- ****'
2890   DATA 'smsq_qxl_host_link'
2900   DATA 'smsq_qxl_hwinit_link'
2910   DATA 'smsq_qxl_nasty_e_link'
2920   DATA 'smsq_qxl_driver_ql_link'
2930   DATA 'smsq_qxl_driver_16_link'
2940   DATA 'smsq_qxl_driver_most_link'
2950   DATA 'smsq_qxl_driver_nd_link'
2960   DATA 'smsq_qxl_procs_link'
2970   DATA 'smsq_qxl_driver_dv3e_link'
2980   DATA 'smsq_qxl_kbd_lang_link'
2990   DATA 'smsq_qxl_ecache_link'
3000   DATA 'smsq_qxl_sysspr_link',''
3010 END DEFine qxl_data
3020 :
3030 DEFine PROCedure java_data
3040 restore 3040
3050   DATA '**** -- Java -- ****'
3060   DATA 'smsq_java_host_link'
3070   DATA 'smsq_java_smsq_link'
3080   DATA 'smsq_java_smsq_1mb_link'
3090   DATA 'smsq_java_hwinit_link'
3100   DATA 'smsq_java_driver_ql_link'
3110   DATA 'smsq_java_driver_8_link'
3120   DATA 'smsq_java_driver_16_link'
3130   DATA 'smsq_java_driver_most_link'
3140   DATA 'smsq_java_driver_dv3e_link'
3150   DATA 'smsq_java_ip_link'
3160   DATA 'smsq_gold_kbd_lang_link'
3170   DATA 'smsq_gold_sysspr_link',''
3180 END DEFine java_data
3190 :
3200 DEFine PROCedure qpc_data
3210 restore 3210
3220   DATA "**** -- QPC -- ****"
3230   DATA 'smsq_qpc_host_link'
3240   DATA 'smsq_qpc_hwinit_link'
3250   DATA 'smsq_qpc_nasty_e_link'
3260   DATA 'smsq_qpc_driver_ql_link'
3270   DATA 'smsq_qpc_driver_16_link'
3280   DATA 'smsq_qpc_driver_8_link'
3290   DATA 'smsq_qpc_driver_ql_link'
3300   DATA 'smsq_qpc_driver_most_link'
3310   DATA 'smsq_qpc_procs_link'
3320   DATA 'smsq_qpc_driver_dv3e_link'
3330   DATA 'smsq_qpc_kbd_lang_link'
3340   DATA 'smsq_qpc_dos_link'
3350   DATA 'smsq_qpc_ip_link'
3360   DATA 'smsq_qpc_cdaudio_link'
3370   DATA 'smsq_smsq_qpc_link'
3380   DATA 'smsq_qpc_sysspr_link',''
3390 END DEFine qpc_data
3400 :
3410 DEFine PROCedure ptrgen_data
3420 restore 3420
3430   DATA "**** -- PtrGen -- ****"
3440   DATA "ee_wman_link","ee_ptr_link","ee_hot_german_link","ee_hot_french_link"
3450   DATA "ee_hot_english_link",""
3460 END DEFine ptrgen_data
3470 :
3480 DEFine PROCedure make_targets
3490 REMark targets are only made if there was no error during compilation
3500 REMark to achieve this, a primitive approach is used:
3510 REMark a check is made whether an appsub menu item status is still selected
3520 REMark if it is, then the
3530   LOCal lp%,current_target%,tot_targets%
3540   tot_targets%=0                        : REMark no targets to make yet
3550   current_target%=0                               : REMark and no target yet
3560   FOR lp%=0 TO data_count%-2
3570     IF main_mfl%(lp%,0)=16
3580       REMark we're covering a new target
3590       tot_targets%=tot_targets%+current_target% : REMark show old target
3600       current_target%=find_target%(lp%+1) : REMark get new target we're covering
3610     ELSE
3620       IF main_mfl%(lp%,0)<>0:current_target%=0
3630     END IF
3640   END FOR lp%
3650   tot_targets%=tot_targets%+current_target%
3660   REMark at the end, here, tot_targets holds a bitmap
3670   REMark of targets for which compilation was ok
3680   IF (machines%&&1) AND NOT (tot_targets% && 1):RETurn : REMark generic was asked for but not compiled correctly!
3690   IF tot_targets%&&2:atari_target
3700   IF tot_targets%&&4:gc_target
3710   IF tot_targets%&&8:qx0_target
3720   IF tot_targets%&&16:qxl_target
3730   IF tot_targets%&&32:aurora_target
3740   IF tot_targets%&&64:java_target
3750   REMark IF tot_targets%&&128:ptr_gen_target  : rem no need for this
3760   IF tot_targets%&&256:qpc_target
3770 END DEFine make_targets
3780 :
3790 DEFine PROCedure atari_target
3800   cmdl$='m'
3810   p$=rep$&'sys_boot_st_flp'
3820   f$=rep$&'sys_boot_file'
3830   ah$=rep$&'sys_boot_st_host'
3840   ld$=rep$&'smsq_smsq_loader'
3850   hi$=rep$&'smsq_atari_hwinit'
3860   os1f$=rep$&'smsq_smsq_fh_os'
3870   os1$=rep$&'smsq_smsq_os'
3880   ca$=rep$&'smsq_smsq_cache'
3890   os2$=rep$&'smsq_sbas_control'
3900   ns1$=rep$&'smsq_atari_nasty'
3910   ln1$=rep$&'smsq_smsq_lang'
3920   ln2$=rep$&'smsq_atari_kbd_lang'
3930   ln3$=rep$&'smsq_sbas_lang'
3940   ex1$=rep$&'smsq_sbas_procs_x'
3950   ex2$=rep$&'smsq_atari_driver_dv3'
3960   ex3$=rep$&'smsq_atari_driver_ser'
3970   ex4$=rep$&'smsq_atari_driver_mono'
3980   ex5$=rep$&'smsq_atari_driver_ql'
3990   ex6$=rep$&'smsq_smsq_wman'
4000   ex7$=rep$&'smsq_smsq_hotkey'
4010   ex8$=rep$&'smsq_atari_sysspr'
4020   ex9$=rep$&'smsq_home_home'
4030   nl$=rep$&'sys_boot_null'
4040   IF 'm' INSTR cmdl$ = 0: ex4$ = nl$
4050   EW f$, ah$,ld$,hi$,os1f$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$, rep$&'smsq_atari_SMSQ.PRG'
4060 END DEFine atari_target
4070 :
4080 DEFine PROCedure gc_target
4090 REMark there are  2 targets for the Goldcard!
4100   f$=rep$&'sys_boot_file'
4110   v$=rep$&'smsq_smsq_vers'
4120   h$=rep$&'smsq_gold_host'
4130   ld$=rep$&'smsq_smsq_loader'
4140   hi$=rep$&'smsq_gold_hwinit'
4150   os1$=rep$&'smsq_smsq_os'
4160   ca$=rep$&'smsq_smsq_cache'
4170   os2$=rep$&'smsq_sbas_control'
4180   ns1$=rep$&'smsq_gold_nasty'
4190   ns2$=rep$&'smsq_gold_nasty_s'
4200   ln1$=rep$&'smsq_smsq_lang'
4210   ln2$=rep$&'smsq_gold_kbd_lang'
4220   ln2a$=rep$&'smsq_gold_kbd_abc_lang'
4230   ln3$=rep$&'smsq_sbas_lang'
4240   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
4250   ex2a$=rep$&'smsq_gold_kbd_abc'
4260   ex2$=rep$&'smsq_gold_driver_most'
4270   ex3a$=rep$&'smsq_gold_driver_ql'
4280   ex38$=rep$&'smsq_gold_driver_8'
4290   ex3q$=rep$&'smsq_gold_qimi'
4300   ex3w$=rep$&'smsq_smsq_wman'
4310   ex4$=rep$&'smsq_gold_driver_dv3'
4320   ex5$=rep$&'smsq_gold_driver_nd'
4330   ex6$=rep$&'smsq_gold_driver_nds'
4340   ex7$=rep$&'smsq_smsq_hotkey'
4350   ex8$=rep$&'smsq_gold_sysspr'
4360   ex9$=rep$&'smsq_home_home'
4370   nl$=rep$&'sys_boot_null'
4380   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex3a$,ex3q$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,ex1$,ex9$, rep$&'SMSQ_GOLD_gold'
4390   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex38$,ex3q$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,ex1$,ex9$, rep$&'SMSQ_GOLD_gold8'
4400 END DEFine gc_target
4410 :
4420 DEFine PROCedure aurora_target
4430   f$=rep$&'sys_boot_file'
4440   v$=rep$&'smsq_smsq_vers'
4450   h$=rep$&'smsq_gold_host'
4460   ld$=rep$&'smsq_smsq_loader'
4470   hi$=rep$&'smsq_gold_hwinit'
4480   os1$=rep$&'smsq_smsq_os'
4490   ca$=rep$&'smsq_smsq_cache'
4500   os2$=rep$&'smsq_sbas_control'
4510   ns1$=rep$&'smsq_gold_nasty'
4520   ns2$=rep$&'smsq_gold_nasty_s'
4530   ln1$=rep$&'smsq_smsq_lang'
4540   ln2$=rep$&'smsq_gold_kbd_lang'
4550   ln2a$=rep$&'smsq_gold_kbd_abc_lang'
4560   ln3$=rep$&'smsq_sbas_lang'
4570   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
4580   ex2a$=rep$&'smsq_gold_kbd_abc'
4590   ex2$=rep$&'smsq_gold_driver_most'
4600   ex3a$=rep$&'smsq_gold_driver_ql'
4610   ex3b$=rep$&'smsq_aurora_driver_8'
4620   ex3q$=rep$&'smsq_gold_qimi'
4630   ex3w$=rep$&'smsq_smsq_wman'
4640   ex3sq$=rep$&'smsq_aurora_sysspr'
4650   ex3sh$=rep$&'smsq_gold_sysspr'
4660   ex4$=rep$&'smsq_gold_driver_dv3'
4670   ex5$=rep$&'smsq_gold_driver_nd'
4680   ex6$=rep$&'smsq_gold_driver_nds'
4690   ex8$=rep$&"smsq_home_home"
4700   ex9$=rep$&'smsq_smsq_hotkey'
4710   ex10$=rep$&'smsq_gold_roms'
4720   nl$=rep$&'sys_boot_null'
4730   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex3a$,ex3b$,ex3q$,ex3w$,ex3sh$,ex4$,ex5$,ex6$,ex9$,ex10$,ex8$,ex1$, rep$&'SMSQ_Aurora_SMSQE'
4740 END DEFine aurora_target
4750 :
4760 DEFine PROCedure qx0_target
4770   nl$=rep$&'sys_boot_null'
4780   f$=rep$&'sys_boot_file'
4790   qr$=rep$&'sys_boot_q40_rom'
4800   ld$=rep$&'smsq_smsq_loader'
4810   hi$=rep$&'smsq_q40_hwinit'
4820   os1$=rep$&'smsq_smsq_1mb_os' : REMark change for fast memory
4830   ca$=rep$&'smsq_smsq_cache40c'
4840   caq$=rep$&'smsq_q40_cache'
4850   caq4$=rep$&'smsq_q40_cachemode'
4860   os2$=rep$&'smsq_sbas_control'
4870   ns2$=rep$&'smsq_q40_nasty'
4880   ln1$=rep$&'smsq_smsq_lang'
4890   ln2$=rep$&'smsq_q40_kbd_lang'
4900   ln3$=rep$&'smsq_sbas_lang'
4910   ex1$=rep$&'smsq_sbas_procs_x'
4920   ex2$=rep$&'smsq_q40_driver_dv3'
4930   ex3$=rep$&'smsq_q40_driver_ser'
4940   ex4a$=rep$&'smsq_q40_driver_ql'
4950   ex4b$=rep$&'smsq_q40_driver_16'
4960   ex4w$=rep$&'smsq_smsq_wman'
4970   ex5$=rep$&'smsq_q40_sysspr'
4980   ex9$=rep$&'smsq_smsq_hotkey'
4990   ex10$=rep$&'smsq_home_home'
5000   EW f$, qr$,ld$,hi$,os1$,ca$,caq$,os2$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4a$,ex4b$,ex4w$,ex5$,ex9$,ex10$,ns2$,caq4$, rep$&'smsq_q40_rom'
5010 END DEFine qx0_target
5020 :
5030 DEFine PROCedure qxl_target
5040   IF FOP_IN (rep$&'smsq_qxl_qxlh.exe') <> -7: PRINT 'QXLH.EXE exists'; cmd$: QUIT
5050   p$   = rep$&'sys_boot_file'
5060   bl$  = rep$&'smsq_qxl_qxl2_exe'
5070   hst$ = rep$&'smsq_qxl_host'
5080   hi$  = rep$&'smsq_qxl_hwinit'
5090   ld$  = rep$&'smsq_smsq_loader'
5100   os1$ = rep$&'smsq_smsq_os'
5110   ca$  = rep$&'smsq_smsq_cache'
5120   os2$ = rep$&'smsq_sbas_control'
5130   ns1$ = rep$&'smsq_qxl_nasty_e'
5140   ln1$ = rep$&'smsq_smsq_lang'
5150   ln2$ = rep$&'smsq_qxl_kbd_lang'
5160   ln3$ = rep$&'smsq_sbas_lang'
5170   ex1$ = rep$&'smsq_qxl_procs_x'
5180   ex2$ = rep$&'smsq_qxl_driver_most'  : REMark Keyboard before CON
5190   ex3$ = rep$&'smsq_qxl_driver_ql'
5200   ex3b$ = rep$&'smsq_qxl_driver_16'
5210   ex3w$ = rep$&'smsq_smsq_wman'
5220   ex4$ = rep$&'smsq_qxl_driver_nd'
5230   ex5$ = rep$&'smsq_qxl_driver_dv3e'
5240   ex6$= rep$&'smsq_smsq_hotkey'
5250   ex7$ = rep$&'smsq_qxl_sysspr'
5260   ex8$=rep$&'smsq_home_home'
5270   last$ = rep$&'smsq_qxl_ecache'
5280   dml$ = rep$&'smsq_qxl_dummy'
5290   qm$  = rep$&'qmon_qxl_bin'
5300   r$='ram1_smsqe.exe' : f$=rep$&'smsq_qxl_smsqe.exe'
5310   cct$=rep$&"extras_exe_cct"
5320   DELETE f$
5330   EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,last$, r$
5340   EW cct$, bl$,r$, f$
5350   DELETE r$
5360   OPEN #4,f$
5370   lenf=FLEN(#4)
5380   lens% = INT ((lenf+511)/512)
5390   lenb% = lenf - lens% * 512
5400   IF lenb%: lenb% = lenb% + 512
5410   BPUT #4\2, lenb% MOD 256, lenb% DIV 256, lens% MOD 256, lens% DIV 256
5420   CLOSE #4
5430 END DEFine qxl_target
5440 :
5450 DEFine PROCedure java_target
5460   f$=rep$&'sys_boot_file'
5470   v$=rep$&'smsq_smsq_vers'
5480   h$=rep$&'smsq_java_host'
5490   ld$=rep$&'smsq_smsq_loader'
5500   hi$=rep$&'smsq_java_hwinit'
5510   os1$=rep$&'smsq_java_smsq_os'
5520   os3$=rep$&'smsq_java_smsq_1mb_os' : REMark must be after os
5530   ca$=rep$&'smsq_smsq_cache'
5540   os2$=rep$&'smsq_sbas_control'
5550   ln1$=rep$&'smsq_smsq_lang'
5560   ln2$=rep$&'smsq_gold_kbd_lang'
5570   ln3$=rep$&'smsq_sbas_lang'
5580   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
5590   ex2$=rep$&'smsq_java_driver_most'
5600   ex3a$=rep$&'smsq_java_driver_ql'
5610   ex3b$=rep$&'smsq_java_driver_8'
5620   ex3c$=rep$&'smsq_java_driver_16'
5630   ex3w$=rep$&'smsq_smsq_wman'
5640   ex4$=rep$&'smsq_java_driver_dv3e'
5650   ex7$=rep$&'smsq_smsq_hotkey'
5660   ex8$=rep$&'smsq_gold_sysspr'
5670   ex9$=rep$&'smsq_home_home'
5680   ex10$ = rep$&'smsq_java_ip_x'
5690   EW f$,h$,ld$,hi$,os1$,os3$,ca$,os2$,ln1$,ln2$,ln3$,ex2$,ex3a$,ex3b$,ex3c$,ex3w$,ex4$,ex1$,ex7$,ex8$,ex9$,ex10$, rep$&'SMSQ_java_java'
5700 END DEFine java_target
5710 :
5720 DEFine PROCedure qpc_target
5730   p$   = rep$&'sys_boot_file'
5740   hst$ = rep$&'smsq_qpc_host'
5750   hi$  = rep$&'smsq_qpc_hwinit'
5760   ld$  = rep$&'smsq_smsq_loader'
5770   os1$ = rep$&'smsq_smsq_qpc_os'
5780   ca$  = rep$&'smsq_smsq_cache'
5790   os2$ = rep$&'smsq_sbas_control'
5800   ns1$ = rep$&'smsq_qpc_nasty_e'
5810   ln1$ = rep$&'smsq_smsq_lang'
5820   ln2$ = rep$&'smsq_qpc_kbd_lang'
5830   ln3$ = rep$&'smsq_sbas_lang'
5840   ex1$ = rep$&'smsq_sbas_procs_x'
5850   ex2$ = rep$&'smsq_qpc_driver_most'  : REMark Keyboard before CON
5860   ex3$ = rep$&'smsq_qpc_driver_ql'
5870   ex3b$= rep$&'smsq_qpc_driver_16'
5880   ex3c$= rep$&'smsq_qpc_driver_8'
5890   ex3w$= rep$&'smsq_smsq_wman'
5900   ex3s$= rep$&'smsq_qpc_sysspr'
5910   ex4$ = rep$&'smsq_qpc_driver_dv3e'
5920   ex5$ = rep$&'smsq_smsq_hotkey'
5930   ex6$ = rep$&'smsq_qpc_procs_x'
5940   ex7$ = rep$&'smsq_qpc_cdaudio_x'
5950   ex8$ = rep$&'smsq_qpc_dos_x'
5960   ex9$ = rep$&'smsq_qpc_ip_x'
5970   exa$ = rep$&'smsq_home_home'
5980   qm$ =  rep$&"qmon_qpc_smsq"
5990   r$='ram1_smsqe.bin'
6000   EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3c$,ex3w$,ex3s$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$,exa$, r$
6010   COPY_O r$,rep$&'smsq_qpc_smsqe.bin'
6020   DELETE r$
6030   REMark rd$='ram1_smsqe.deb'
6040   REMark EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3c$,ex3w$,ex3s$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$,exa$,qm$, rd$
6050   REMark COPY_O r$,qpc$&'release_smsqe.bin'
6060   REMark COPY_O rd$,qpc$&'exe_smsqe.bin'
6070 END DEFine qpc_target
6080 :
6090 DEFine PROCedure findit
6100 LOCal lp%,a$
6110 restore 6110
6120   DATA p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3c$,ex3w$,ex3s$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$,exa$, r$,""
6130   REPeat lp%
6140     READ a$:IF a$="":EXIT lp%
6150     t%=FOP_IN(a$ )
6160     IF (t%<0)
6170        PRINT "not found "& a$
6180     END IF
6190  END REPeat lp%
6200 END DEFine findit
6210 :
6220 DEFine FuNction find_target% (current%)
6230   IF "SMSQ Generic" INSTR mdata$(current%):RETurn 1
6240   IF "Atari" INSTR mdata$(current%):RETurn 2
6250   IF "GoldCard" INSTR mdata$(current%):RETurn 4
6260   IF "Q40" INSTR mdata$(current%):RETurn 8
6270   IF "QXL" INSTR mdata$(current%):RETurn 16
6280   IF "Aurora" INSTR mdata$(current%):RETurn 32
6290   IF "Java" INSTR mdata$(current%):RETurn 64
6300   IF "Ptr_Gen" INSTR mdata$(current%):RETurn 128
6310   IF "QPC" INSTR mdata$(current%):RETurn 256
6320   RETurn 0                                     : REMark huh???
6330 END DEFine find_target%
6340 :
6350 DEFine PROCedure change_size (x%,y%)
6360 LOCal xrel%,yrel%,temp,a$,ttemp%(main_lit%),abs%,yabs%,t%,bt$,mtemp%(0,0)
6370   main_lfl%(size_it%)=1                 : REMark reset to available
6380   DIM mtemp%(DIMN(main_mfl%,1),DIMN(main_mfl%,2))
6390   ARRCOPY main_lfl%,ttemp%              : REMark keep statusses
6400   ARRCOPY main_mfl%,mtemp%
6410   CH_WIN main_defn,xrel%,yrel%                    : REMark ptr displacement
6420   xsize%=xsize%-xrel%+3
6430   ysize%=ysize%-yrel%                             : REMark new sizes
6440   t%=2^4+2^5
6450   RPTR xabs%,yabs%,t%,t%,xrel%,yrel%,bt$: REMark get absolute coords
6460   IF xsize%>xsz%-16:xsize%=xsz%-16
6470   IF ysize%>ysz%-16:ysize%=ysz%-16      : REMark max values
6480   DR_UNST main_defn                               : REMark no more wdw defn...
6490   DR_REMV main_defn                               : REMark .... now!
6500   app_addr=0                                      : REMark nor any appsub wdw
6510   make_main_wdw                         : REMark make window anew
6520   ARRCOPY ttemp%,main_lfl%
6530   ARRCOPY mtemp%,main_mfl%
6540   t%=lxs*3
6550   xabs%=xabs%-t%:IF xabs%<0:xabs%=0     : REMark position ptr nicely
6560   yabs%=yabs%-8:IF yabs%<0:yabs%=0
6570   SPTR xabs%,yabs%,0                              : REMark now
6580   IF compiled
6590      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
6600   ELSE
6610      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
6620   END IF
6630    POS_PTR main_defn,t%*65536+8,1       : REMark position pointer in wdw
6640 END DEFine change_size
6650 :
6660 DEFine FuNction fen_app (adresse,tableau$,nr_sects%,selk%,phys%,a_exclure$)
6670 rem make a typical QPAC2 appsub wdw, with strings in the items
6680 rem the parameters are: the address (0 at first)
6690 rem the string array, the number of sections
6700 rem a flag whether we want selection keystrokes and whether hit=do
6710 rem an integer array with x,y, sizes & origs
6720 rem a string with chars to be excluded from the selection keystrokes
6730 LOCal app_attributs%(3,1),temp%,bord%,pap%,ori_x%,ori_y%
6740 LOCal tail_x%,tail_y%,fleches%,bar%,bar_sect%,arrows%,selkey%
6750 :
6760   IF adresse:RECHP adresse              : rem if this is a second call, release old
6770   tail_x%=phys%(0)                      : rem x size  - just to make this clear
6780   tail_y%=phys%(1)                      : rem y "size"      we could use this directly
6790   ori_x%=phys%(2)                       : rem
6800   ori_y%=phys%(3)                       : rem origs x,y
6810 :
6820   adresse=AP_MAKE(selk%,tail_x%,tail_y%,ori_x%,ori_y%,nr_sects%,tableau$,a_exclure$)
6830   IF adresse<0: RETurn 0                : rem  error, return 0
6840 :
6850   app_attributs%(0,0)=1
6860   app_attributs%(0,1)=sp.appihigh       : rem current appsub wdw item border size & colour
6870 :
6880   bord%=sp.appbd                        : rem border colour
6890   pap%=sp.appbg                         : rem paper colour
6900   selkey%=9                                       : rem selection keystroke, always TAB
6910   arrows%=sp.scrbararr                            : rem arrow colour
6920   bar%=sp.scrbar                        : rem bar colour
6930   bar_sect%=sp.scrbarsec                : rem bar section
6940 :
6950   app_attributs%(0,0)=1
6960   app_attributs%(0,1)=sp.appihigh       : rem current appsub wdw item border size & colour
6970 :
6980   app_attributs%(1,0)=sp.appiunabg
6990   app_attributs%(1,1)=sp.appiunafg      : rem unavailable
7000 :
7010   app_attributs%(2,0)=sp.appiavabg
7020   app_attributs%(2,1)=sp.appiavafg      : rem available
7030 :
7040   app_attributs%(3,0)=sp.appiselbg
7050   app_attributs%(3,1)=sp.appiselfg      : rem selected
7060 :
7070   er=AP_PARS(adresse,0,bord%,pap%,selk%,arrows%,bar%,bar_sect%,app_attributs%)
7080 :
7090   IF er:RETurn 0                        : rem OOOOPS
7100 :
7110   RETurn adresse                        : rem return the address!
7120 END DEFine fen_app
7130 :
7140 def fn warning_pos% (mess$,channel%,title$,pos)
7150 rem warning wdw with pointer positioning
7160   pos_ptr main_defn,pos,-1
7170   return warning%(mess$,channel%,title$)
7180 end def warning_pos%
7190 :
7200 define function warning%(mess$,channel%,title$)
7210 rem shows a warning message (mess$) in a small window. hit esc or ok
7220 rem to return; returns 0 if esc, 1 if ok  hit
7230 local item%,lp%,chan%,y%,swnum%,infot%
7240   if channel%
7250     chan%=channel%
7260   else
7270     chan%=fopen("con")
7280   endif
7290   ch_item warn_defn,-3,0,-1,'','Attention'
7300   ch_item warn_defn,-1,0,-1,cancel$,'ESC'
7310   ch_item warn_defn,-1,1,-1,'O','OK'
7320   warn_lfl%(0)=1:warn_lfl%(1)=1
7330   poke_w warn_defn+44,sp.errmg                           : rem border colour of wdw
7340   poke_w warn_defn+46,sp.errbg                           : rem paper colour of wdw
7350   poke_w warn_wdw_addr+14,sp.errbg             : rem paper of title info wdw
7360   obj_addr=peek_l(warn_wdw_addr+16)            : rem ptr to object list
7370   poke_w obj_addr+10,sp.errfg                            : rem info object text  colour
7380   dr_puld warn_defn,-1,-1
7390   dr_iwdf #chan%,warn_defn,0
7400   wm_paper#chan%,sp.errbg
7410   wm_ink#chan%,sp.errfg
7420   cls#chan%
7430   if title$<>"":print#chan%,title$;" :"
7440   print #chan%,mess$
7450   pos_ptr warn_defn,160*65536+10,-1
7460   repeat lp%
7470     rd_ptr warn_defn,item%,swnum%,y%,y%,y%,warn_lfl%
7480     if swnum%=-1:exit lp%
7490   end repeat lp%
7500   if not channel%:close#chan%
7510   dr_unst warn_defn
7520   return item%
7530 end define warning%
7540 :
7550 def proc info_pos (mess$,channel%,pos)
7560 rem warning wdw with positioning
7570   pos_ptr main_defn,pos,-1
7580   info mess$,channel%
7590 end def info_pos
7600 :
7610 define procedure info(mess$,channel%)
7620 rem shows window containing mess$
7630 local chan%,infot%,obj_addr
7640   if channel%
7650     chan%=channel%
7660   else
7670     chan%=fopen("con")
7680   endif
7690   ch_item warn_defn,-3,0,-1,"",'  Info.  '
7700   ch_item warn_defn,-1,0,-1,"",'Info'
7710   ch_item warn_defn,-1,1,-1,"",'Info'
7720   poke_w warn_defn+44,sp.infwinbg                 : rem paper colour of wdw
7730   poke_w warn_defn+46,sp.winbd                              : rem border around wdw
7740   poke_w warn_wdw_addr+14,sp.infwinbg             : rem paper of title info wdw
7750   obj_addr=peek_l(warn_wdw_addr+16)               : rem ptr to object list
7760   poke_w obj_addr+10,sp.infwinfg                  : rem info object text        colour
7770   dr_puld warn_defn,-1,-1
7780   pos_ptr warn_defn,90*65536+70,-1
7790   dr_iwdf #chan%,warn_defn,0
7800   wm_paper#chan%,sp.infwinbg
7810   wm_ink#chan%,sp.infwinfg
7820   cls#chan%
7830   print #chan%,mess$
7840   if not channel%:close#chan%
7850   info_is_pulled_down%=1
7860 end define info
7870 :
7880 def proc info_wait(mess$,channel%)
7890 rem shows info wdw and waits until user clicks
7900   info mess$,channel%
7910   info_wait_to_close
7920 end def info_wait
7930 :
7940 def proc info_wait_to_close
7950 local item%,lp%,chan%,y%,swnum%,infot%
7960   repeat lp%
7970     rd_ptr warn_defn,item%,swnum%,y%,y%,y%,warn_lfl%
7980     if swnum%=-1:exit lp%
7990   end repeat lp%
8000   dr_unst warn_defn
8010 end def info_wait_to_close
8020 :
8030 def proc info_pos_wait (mess$,channel%,pos)
8040 rem positions & shows info wdw and waits until user clicks
8050   info_pos mess$,channel%,pos
8060   info_wait_to_close
8070 end def info_pos_wait
8080 :
8090 def proc stop_info
8100   dr_unst warn_defn
8110   info_is_pulled_down%=0
8120 end def stop_info
8130 :
8140 DEFine PROCedure button (def_trav,menu_drap%,app_drap%,control%,chaine$,type_col%)
8150 REMark les parametres sont, dans l'ordre:
8160 REMark la dƒfinition de la fen‘tre, suivie des tableaux pour
8170 REMark les postes du menu dƒliƒ, les postes de la sous-fen‘tre d'appl.
8180 REMark et la dƒfinition de contr˜le, suivi du nom du bouton et
8190 REMark du type couleurs.
8200 REMark Il est supposƒ que vous avez ouvert une fen‘tre #1 si
8210 REMark le programme est compilƒ
8220   DR_UNST def_trav
8230   IF compiled: CLOSE#1
8240   button_wait chaine$
8250 rem  BTN chaine$,type_col%
8260   IF compiled
8270     OPEN#1,"CON_"
8280     DR_PPOS def_trav,-1,-1,menu_drap%,app_drap%,control%
8290   ELSE
8300     DR_PULD def_trav,-1,-1,menu_drap%,app_drap%,control%
8310   END IF
8320 END DEFine button
8330 :
8340 DEFine PROCedure make_colours (colourway%)
8350 REMark this initialises the GLOBAL variables menu_pap, menu_brd, menu_inv,
8360 REMark menu_ink and menu_bar according to the colourway passed as parameter.
8370 REMark More importantly, this sets the sp.xxx GLOBAL variables.
8380 REMark The GLOBAl variable mycolour_mode% must have been set prior to calling
8390 REMark this function, preferrably via the function set_colours%.
8400 REMark Moreover the colours black, white,red, green,wg_stripes, etc...
8410 REMark must have been set prior to calling this procedure:
8420 REMark Preferrably, they were also set via the function set_colours%
8430 :
8440 rem this function needs the extension WL_MK16 (outptr_bin)
8450 :
8460 REMark the colourways% are:
8470 REMark 0= white/green
8480 REMark 1= black/red
8490 REMark 2= white/red
8500 REMark 3= black/green
8510 remark 4-7= use system palettes
8520 :
8530 rem the Global war mycolour_mode% is:
8540 rem 0 = we are in QL mode 4
8550 rem 1 = we are in PAL mode (256 colours)
8560 rem 2 - we are in 24 bit colour mode
8570 rem preferrably, this is set via the function set_colours%
8580 :
8590   if colourway%>7 or colourway%<0: colourway%=0: REMark must be between 0 & 7
8600 :
8610 rem first make sure that what we request is possible.
8620 rem colourways 4-7 are only possible if we are in 8 or 24 bit colour mode
8630 rem if it is requested in another mode, it is set to 0
8640 :
8650   if colourway%>3
8660      make_my_palette colourway%-4: rem set colours according to system palette
8670      return                         : rem premature exit
8680   endif
8690 :
8700 rem this now is only the old colourways
8710   select on mycolour_mode%
8720     =0                                  : rem QL 4 colours
8730       SELect ON colourway%
8740           =0:                           : remark white/green
8750                 menu_pap=white
8760                 menu_brd=green
8770                 menu_ink=black
8780                 menu_bar=wg_striped
8790                 menu_inv=red
8800           =1:                           : remark black/red
8810                 menu_pap=black
8820                 menu_brd=red
8830                 menu_ink=white
8840                 menu_bar=br_striped
8850                 menu_inv=green
8860           =2:                           : remark white/red
8870                 menu_pap=white
8880                 menu_brd=red
8890                 menu_ink=black
8900                 menu_bar=wr_striped
8910                 menu_inv=green
8920           =3:                           : remark black/green
8930                 menu_pap=black
8940                 menu_brd=green
8950                 menu_ink=white
8960                 menu_bar=bg_striped
8970                 menu_inv=red
8980       END SELect
8990     =1                                  : rem pal colours
9000       SELect ON colourway%
9010           =0:                           : remark white/green
9020                 menu_pap=white+256
9030                 menu_brd=green+256
9040                 menu_ink=black+256
9050                 menu_bar=wg_striped+256
9060                 menu_inv=red +256
9070           =1:                           : remark black/red
9080                 menu_pap=black+256
9090                 menu_brd=red  +256
9100                 menu_ink=white +256
9110                 menu_bar=br_striped +256
9120                 menu_inv=green +256
9130                 omenu_pap=black+256
9140           =2:                           : remark white/red
9150                 menu_pap=white +256
9160                 menu_brd=red   +256
9170                 menu_ink=black +256
9180                 menu_bar=wr_striped +256
9190                 menu_inv=green +256
9200           =3:                           : remark black/green
9210                 menu_pap=black +256
9220                 menu_brd=green +256
9230                 menu_ink=white +256
9240                 menu_bar=bg_striped +256
9250                 menu_inv=red   +256
9260       END SELect
9270     =2                                  : rem 24 bit colours
9280       SELect ON colourway%
9290           =0:                           : remark white/green
9300                 menu_pap=WL_MK16(white)
9310                 menu_brd=WL_MK16(green)
9320                 menu_ink=WL_MK16(black)
9330                 menu_bar=WL_MK16(wg_striped)
9340                 menu_inv=WL_MK16(red)
9350           =1:                           : remark black/red
9360                 menu_pap=WL_MK16(black)
9370                 menu_brd=WL_MK16(red)
9380                 menu_ink=WL_MK16(white)
9390                 menu_bar=WL_MK16(br_striped)
9400                 menu_inv=WL_MK16(green)
9410           =2:                           : remark white/red
9420                 menu_pap=WL_MK16(white)
9430                 menu_brd=WL_MK16(red)
9440                 menu_ink=WL_MK16(black)
9450                 menu_bar=WL_MK16(wr_striped)
9460                 menu_inv=WL_MK16(green)
9470           =3:                           : remark black/green
9480                 menu_pap=WL_MK16(black)
9490                 menu_brd=WL_MK16(green)
9500                 menu_ink=WL_MK16(white)
9510                 menu_bar=WL_MK16(bg_striped)
9520                 menu_inv=WL_MK16(red)
9530       END SELect
9540   end select
9550 :
9560   make_my_old_palette
9570 :
9580 END DEFine make_colours
9590 :
9600 def proc make_my_palette (cw%)
9610   sp.winbd            = hex('0200') : rem Window border
9620   sp.winbg            = hex('0201') : rem Window background
9630   sp.winfg            = hex('0202') : rem Window foreground
9640   sp.winmg            = hex('0203') : rem Window middleground
9650   sp.titlebg          = hex('0204') : rem Title background
9660   sp.titletextbg  = hex('0205') : rem Title text background
9670   sp.titlefg          = hex('0206') : rem Title foreground
9680   sp.litemhigh        = hex('0207') : rem Loose item highlight
9690   sp.litemavabg   = hex('0208') : rem Loose item available background
9700   sp.litemavafg   = hex('0209') : rem Loose item available foreground
9710   sp.litemselbg   = hex('020a') : rem Loose item selected background
9720   sp.litemselfg   = hex('020b') : rem Loose item selected foreground
9730   sp.litemunabg   = hex('020c') : rem Loose item unavailable background
9740   sp.litemunafg   = hex('020d') : rem Loose item unavailable foreground
9750   sp.infwinbd         = hex('020e') : rem Information window border
9760   sp.infwinbg         = hex('020f') : rem Information window background
9770   sp.infwinfg         = hex('0210') : rem Information window foreground
9780   sp.infwinmg         = hex('0211') : rem Information window middleground
9790   sp.subinfbd         = hex('0212') : rem Subsidiary information window border
9800   sp.subinfbg         = hex('0213') : rem Subsidiary information window background
9810   sp.subinffg         = hex('0214') : rem Subsidiary information window foreground
9820   sp.subinfmg         = hex('0215') : rem Subsidiary information window middleground
9830   sp.appbd            = hex('0216') : rem Application window border
9840   sp.appbg            = hex('0217') : rem Application window background
9850   sp.appfg            = hex('0218') : rem Application window foreground
9860   sp.appmg            = hex('0219') : rem Application window middleground
9870   sp.appihigh         = hex('021a') : rem Application window item highlight
9880   sp.appiavabg        = hex('021b') : rem Application window item available background
9890   sp.appiavafg        = hex('021c') : rem Application window item available foreground
9900   sp.appiselbg        = hex('021d') : rem Application window item selected background
9910   sp.appiselfg        = hex('021e') : rem Application window item selected foreground
9920   sp.appiunabg        = hex('021f') : rem Application window item unavailable background
9930   sp.appiunafg        = hex('0220') : rem Application window item unavailable foreground
9940   sp.scrbar           = hex('0221') : rem Pan/scroll bar
9950   sp.scrbarsec        = hex('0222') : rem Pan/scroll bar section
9960   sp.scrbararr        = hex('0223') : rem Pan/scroll bar arrow
9970   sp.buthigh          = hex('0224') : rem Button highlight
9980   sp.butbd            = hex('0225') : rem Button border
9990   sp.butbg            = hex('0226') : rem Button background
10000   sp.butfg            = hex('0227') : rem Button foreground
10010   sp.hintbd           = hex('0228') : rem Hint border
10020   sp.hintbg           = hex('0229') : rem Hint background
10030   sp.hintfg           = hex('022a') : rem Hint foreground
10040   sp.hintmg           = hex('022b') : rem Hint middleground
10050   sp.errbg            = hex('022c') : rem Error message background
10060   sp.errfg            = hex('022d') : rem Error message foreground
10070   sp.errmg            = hex('022e') : rem Error message middleground
10080   sp.shaded           = hex('022f') : rem sp.shaded area
10090   sp.3ddark           = hex('0230') : rem Dark 3D border shade
10100   sp.3dlight          = hex('0231') : rem Light 3D border shade
10110   sp.vertfill         = hex('0232') : rem Vertical area fill
10120   sp.subtitbg         = hex('0233')  : rem Subtitle background
10130   sp.subtittxtbg  = hex('0234')  : rem Subtitle text background
10140   sp.subtitfg         = hex('0235')  : rem Subtitle foreground
10150   sp.mindexbg         = hex('0236')  : rem Menu index background
10160   sp.mindexfg         = hex('0237')  : rem Menu index foreground
10170   sp.separator        = hex('0238')  : rem Seperator lines etc.
10180   if not (is_open(#0) or is_open(#1))
10190     open#1,'con'
10200     sp_jobpal -1,cw%
10210     close#1
10220   else
10230     sp_jobpal -1,cw%
10240   endif
10250 end def make_my_palette
10260 :
10270 def proc make_my_old_palette
10280 rem this translates the colours used in the wdws into 4 colour mode...
10290   sp.winbd          =         menu_ink   : rem Window border
10300   sp.winbg          =         menu_pap   : rem Window background
10310   sp.winfg          =         menu_pap   : rem Window foreground
10320   sp.winmg          =         menu_pap   : rem Window middleground
10330   sp.titlebg        =         menu_bar   : rem Title background
10340   sp.titletextbg=   menu_pap   : rem Title text background
10350   sp.titlefg        =         menu_ink   : rem Title foreground
10360   sp.litemhigh      =         menu_brd   : rem Loose item highlight
10370   sp.litemavabg =   menu_pap   : rem Loose item available background
10380   sp.litemavafg =   menu_ink   : rem Loex fifiose item available foreground
10390   sp.litemselbg =   menu_brd   : rem Loose item selected background
10400   sp.litemselfg =   menu_ink   : rem Loose item selected foreground
10410   sp.litemunabg =   menu_pap   : rem Loose item unavailable background
10420   sp.litemunafg =   menu_brd   : rem Loose item unavailable foreground
10430   sp.infwinbd       =         menu_brd   : rem Information window border
10440   sp.infwinbg       =         menu_pap   : rem Information window background
10450   sp.infwinfg       =         menu_ink   : rem Information window foreground
10460   sp.infwinmg       =         menu_pap   : rem Information window foreground
10470   sp.subinfbd       =         menu_brd   : rem Information window border (sub)
10480   sp.subinfbg       =         menu_pap   : rem Information window background
10490   sp.subinffg       =         menu_ink   : rem Information window foreground
10500   sp.subinfmg       =         menu_pap   : rem Information window foreground
10510   sp.appbd          =         menu_brd   : rem Application window border
10520   sp.appbg          =         menu_pap   : rem Application window background
10530   sp.appfg          =         menu_ink   : rem Application window foreground
10540   sp.appmg          =         menu_pap   : rem Application window middleground
10550   sp.appihigh       =         menu_ink   : rem Application window item highlight
10560   sp.appiavabg      =         menu_pap   : rem Application window item available background
10570   sp.appiavafg      =         menu_ink   : rem Application window item available foreground
10580   sp.appiselbg      =         menu_brd   : rem Application window item selected background
10590   sp.appiselfg      =         menu_ink   : rem Application window item selected foreground
10600   sp.appiunabg      =         menu_pap   : rem Application window item unavailable background
10610   sp.appiunafg      =         menu_brd   : rem Application window item unavailable foreground
10620   sp.scrbar         =         menu_ink   : rem Pan/scroll bar
10630   sp.scrbarsec      =         menu_pap   : rem bar section
10640   sp.scrbararr      =         menu_brd   : rem Pan/scroll bar arrow
10650   sp.buthigh        =         menu_ink   : rem Button highlight
10660   sp.butbd          =         menu_brd   : rem Button border
10670   sp.butbg          =         menu_pap   : rem Button background
10680   sp.butfg          =         menu_ink   : rem Button foreground
10690   sp.hintbd         =         menu_brd   : rem Hint border
10700   sp.hintbg         =         menu_ink   : rem Hint background
10710   sp.hintfg         =         menu_pap   : rem Hint foreground
10720   sp.hintmg         =         menu_ink   : rem Hint middleground
10730   sp.errbg          =         menu_inv   : rem Error message background
10740   sp.errfg          =         menu_ink   : rem Error message foreground
10750   sp.errmg          =         menu_ink   : rem Error message middleground
10760   sp.shaded         =         0            : rem sp.shaded area
10770   sp.3ddark         =         0            : rem Dark 3D border shade
10780   sp.3dlight        =         white        : rem Light 3D border shade
10790   sp.vertfill       =         menu_pap   : rem Vertical area fill
10800   sp.subtitbg       =         menu_pap   : rem Subtitle background
10810   sp.subtittxtbg=   menu_pap   : rem Subtitle text background
10820   sp.subtitfg       =         menu_ink   : rem Subtitle foreground
10830   sp.mindexbg       =         menu_pap   : rem Menu index background
10840   sp.mindexfg       =         menu_brd   : rem same,        foreground
10850   sp.separator      =         menu_brd   : rem Seperator lines etc.
10860 end def make_my_old_palette
10870 :
10880 DEFine FuNction centr$(ctr$,leng)
10890 rem this "centres" the string ctr$ (i.e. left padds it with spaces) so that
10900 rem it is centred in a window of leng width (leng is length in chars)
10910   LOCal l%,cta$
10920   cta$=strip_spaces$(ctr$)
10930   l%=INT(leng-LEN(cta$))/2
10940   if l%<0:return cta$
10950   return FILL$(' ',l%)&cta$&FILL$(' ',l%)
10960 END DEFine centr$
10970 :
10980 DEFine PROCedure scroll_arrows(rangees%,nbr_objets%,appfen,cty%)
10990 rem Cette procƒdure dƒcide s'il faut laisser les flches de dƒfilement
11000 rem params: le nombre de rangƒes, le nombre total d'objets
11010 rem le pointer vers la sous-fen d'app et la dƒfinition de controle
11020   IF nbr_objets%<=rangees%
11030     POKE_W appfen+150,0       : rem no y offset to start of menu
11040     cty%(1,2)=nbr_objets%     : rem nbr of columns/rows
11050 rem  ELSE
11060 rem    POKE_W appfen+150,6
11070 rem    cty%(1,2)=rangees%-1      : rem if scroll arrows are there, there is one line less in the window
11080  END IF
11090  IF DIMN(cty%,1)=2:cty%(2,2)=cty%(1,2)
11100  cty%(0,1)=1                            : rem show that definitionhas changed
11110 END DEFine scroll_arrows
11120 :
11130 DEFine FuNction getstr$(poste%,drap%,defn,a$)
11140 REMark opens and sets a window over a loose item and proposes a sting to be edited.
11150 REMark parameters:
11160 REMark - item nbr
11170 REMark - falg whether result MUST be a numbrr (flag<>0)
11180 REMark - the working definition
11190 REMark - the string o edit
11200 rem returns the string or ""
11210 rem uses the global (!) vars sp.litemselbg and sp.litemselfg
11220 rem
11230 local b$,chan%
11240   chan%=FOPEN("con")
11250   b$=getstr2$(poste%,drap%,defn,a$,chan%)
11260   close#chan%
11270   return b$
11280 END DEFine getstr$
11290 :
11300 DEFine FuNction getstr2$(poste%,drap%,defn,a$,chan%)
11310 REMark sets a window over a loose item and proposes a sting to be edited.
11320 REMark parameters:
11330 REMark - item nbr
11340 REMark - falg whether result MUST be a numbrr (flag<>0)
11350 REMark - the working definition
11360 REMark - the string o edit
11370 remark - a screen channel to be used
11380 rem returns the string or ""
11390 rem uses the global (!) vars sp.litemselbg and sp.litemselfg
11400 rem
11410   LOCal b$,res
11420   b$=" ":b$=a$           : REMark c'est curieusement nƒcessaire
11430   wm_PAPER#chan%,sp.litemselbg
11440   wm_INK#chan%,sp.litemselfg
11450   DR_LWDF#chan%,defn,poste%             : rem open channel over item
11460   CLS#chan%
11470   res=GET_STR(#chan%,drap%,b$)                                : rem get string
11480   IF res<0:b$=""                        : rem ooops
11490   CH_ITEM defn,-1,poste%,-1,"",b$
11500   RETurn b$
11510 END DEFine getstr2$
11520 :
11530 def fn showcont$(string$,length%)
11540 rem this adds '...' at the end of a string, to show that it continues
11550 local l%,lengt%
11560   l%=len(string$)
11570   if not l%:return ""                             : rem empty string
11580   lengt%=length% div 6
11590   if l%<=lengt%:return string$                    : rem entire string fits wdw
11600   return string$(1 to lengt%-3)&"..."
11610 end def showcont$
11620 :
11630 def fn get_free_text$(defn,item%,prompt$,length%)
11640 rem this opens wdw over loose-item item% of the working definition defn
11650 rem allows free text to be entered
11660 rem params: working defintion,item over which wdw is to be opened
11670 rem           first value of string, length of max string in wdw, if this is...
11680 rem              -1, the length will be determined here
11690 local chan%,string$,tempsizes%(3)
11700   chan%=fopen('con')
11710   if chan%<0:return prompt$
11720   dr_lwdf#chan%,defn,item%              : rem open wdw over item
11730   paper#chan%,menu_brd
11740   ink#chan%,menu_ink
11750   cls#chan%
11760   string$=wedit$(#chan%,prompt$)
11770   if length%=-1
11780      wsipo#chan%,tempsizes%
11790      length%=tempsizes%(0)
11800   endif
11810   close#chan%
11820   if length%
11830     ch_item defn,-1,item%,-1,"",showcont$(string$,length%)
11840   else
11850     ch_item defn,-1,item%,-1,"",string$
11860   endif
11870   return string$
11880 end def get_free_text$
11890 :
11900 def fn do_caps(a$,caps%)
11910 rem set string to capitales etc,caps% determines whether the string....
11920 REMark ...must be lower case (=0), upper case(=1), all words capitalized (=2),
11930 REMark ... first word only capitalized (=3), or nothing(=-1)
11940   SELect ON caps%
11950    =0: MIN a$
11960    =1: MAJ a$
11970    =2: CAP2 a$
11980    =3: CAP a$
11990   END SELect
12000   return a$
12010 end def do_caps
12020 :
12030 def fn set_colours% (force_mode%)
12040 rem this sets colour variables according to the colour mode wished, if possible.
12050 rem force_mode%=
12060 rem       0 if ql colours wished (mode 4 only)
12070 rem       1 if pal colours wished
12080 rem       2 if 24 bit colours wished
12090 rem This function returns the true QL mode set (4,8,32,33 etc.).
12100 rem This function also sets the GLOBAL variable mycolour_mode% to the
12110 rem mode 0,1,2 as above - whatever is possible on the machine, i.e.
12120 rem if colour_24 is wished on a 4 colour machine (which won't succeed)
12130 rem this function will set set that variable to 0, i.e. QL mode.
12140 rem
12150 rem This function needs the outptr toolkit
12160 local mode%
12170   mode%=rmode                                     : rem get current mode now
12180   select on force_mode%
12190       = 0                                         : rem mode 4 wished
12200        select on mode%
12210             = 4                                   : rem already on, so do nothing
12220             = 0,1,8 : mode 4            : rem old modes, set to 4 colours
12230             = remainder
12240               colour_ql                           : rem if higher colour mode, set to "mode 4"
12250               mode 4
12260        end select
12270        mode%=0                                    : rem new mode we're in  - 4 colour mode
12280       = 1                                         : rem PALette wished
12290        select on mode%
12300             = 16,32,33 : colour_pal:mode%=1  : rem we're in high colour mode, but PAL colours wished
12310             = remainder
12320               if mode%<>4:mode 4
12330               mode%=0
12340        end select
12350       = 2
12360        select on mode%
12370             = 32,33 : colour_24 :mode%=2          : rem we're in high col mode 24 bit colours
12380             = 16 : colour_pal : mode%=1
12390             = remainder
12400               if mode%<>4:mode 4
12410               mode%=0
12420        end select
12430   end select
12440   mycolour_mode%=mode%                            : rem set this global variable now:
12450   rem now set some colour variables
12460   select on mode%
12470        = 0                                        : rem Ql 4 mode colours
12480           Black            = 0
12490           White            = 7
12500           Red              = 2
12510           Green            = 4
12520           grey             = 255
12530           dull_pink      = 19
12540           Blue             = 0
12550           Magenta        = 2
12560           Yellow           = 7
12570           Cyan             = 0
12580           Dark_slate     = 0
12590           Slate_grey     = 255
12600           Dark_grey      = 255
12610           Light_grey     = 255
12620           Ash_grey       = 255
12630           Dark_red       = 210
12640           Light_green    = 220
12650           Mustard        = 242
12660           Dark_green     = 228
12670           Sea_blue       = 0
12680           Dark_blue      = 0
12690           Purple           = 0
12700           Shocking_pink  = 234
12710           Orange           = 234
12720           Lime_green     = 4
12730           Apple_green    = 4
12740           Bright_blue    = 0
12750           Mauve            = 0
12760           Peach            = 7
12770           Light_yellow   = 7
12780           Light_blue     = 0
12790           Sky_blue       = 0
12800           Rose_pink      = 234
12810           Pink             = 234
12820           Beige            = 7
12830           Pastel_pink    = 234
12840           Pastel_yellow  = 7
12850           Pastel_green   = 4
12860           Pastel_cyan    = 0
12870           Pastel_blue    = 0
12880           Pastel_rose    = 234
12890           Brick            = 210
12900           Light_khaki    = 7
12910           Dull_green     = 228
12920           Dull_cyan      = 0
12930           Steel_blue     = 0
12940           Dull_pink      = 234
12950           Brown            = 0
12960           Khaki            = 7
12970           Dusky_green    = 4
12980           Dusky_blue     = 0
12990           Midnight_blue  = 0
13000           Plum             = 0
13010           Dusky_pink     = 234
13020           Buff             = 0
13030           Avocado        = 4
13040           Dull_turquoise = 4
13050           Dull_blue      = 0
13060           Faded_purple   = 234
13070           Cerise           = 2
13080           Tan_normal     = 0
13090           Grass_green    = 4
13100           Sea_green      = 4
13110           Ultramarine    = 0
13120           Deep_purple    = 234
13130           Light_orange   = 7
13140           light_red      = 2
13150           wg_striped     = 92
13160           br_striped     = 82
13170           wr_striped     = 107
13180           bg_striped     = 100
13190        = 1                              : rem Colour_pal
13200           Black            = 0
13210           White            = 1
13220           Red              = 2
13230           Green            = 3
13240           Blue             = 4
13250           Magenta        = 5
13260           Yellow           = 6
13270           Cyan             = 7
13280           Dark_slate     = 8
13290           Slate_grey     = 9
13300           Dark_grey      = 10
13310           Grey             = 11
13320           Light_grey     = 12
13330           Ash_grey       = 13
13340           Dark_red       = 14
13350           Light_green    = 15
13360           Mustard        = 16
13370           Dark_green     = 17
13380           Sea_blue       = 18
13390           Dark_blue      = 19
13400           Purple           = 20
13410           Shocking_pink  = 21
13420           Orange           = 22
13430           Lime_green     = 23
13440           Apple_green    = 24
13450           Bright_blue    = 25
13460           Mauve            = 26
13470           Peach            = 27
13480           Light_yellow   = 28
13490           Light_blue     = 29
13500           Sky_blue       = 30
13510           Rose_pink      = 31
13520           Pink             = 32
13530           Beige            = 33
13540           Pastel_pink    = 34
13550           Pastel_yellow  = 35
13560           Pastel_green   = 36
13570           Pastel_cyan    = 37
13580           Pastel_blue    = 38
13590           Pastel_rose    = 39
13600           Brick            = 40
13610           Light_khaki    = 41
13620           Dull_green     = 42
13630           Dull_cyan      = 43
13640           Steel_blue     = 44
13650           Dull_pink      = 45
13660           Brown            = 46
13670           Khaki            = 47
13680           Dusky_green    = 48
13690           Dusky_blue     = 49
13700           Midnight_blue  = 50
13710           Plum             = 51
13720           Dusky_pink     = 52
13730           Buff             = 53
13740           Avocado        = 54
13750           Dull_turquoise = 55
13760           Dull_blue      = 56
13770           Faded_purple   = 57
13780           Cerise           = 58
13790           Tan_normal     = 59
13800           Grass_green    = 60
13810           Sea_green      = 61
13820           Ultramarine    = 62
13830           Deep_purple    = 63
13840           pinkish        = 77
13850           light_red      = 109
13860           Light_orange   = 208
13870           wg_striped     = light_green
13880           br_striped     = dark_red
13890           wr_striped     = light_red
13900           bg_striped     = dark_green
13910        = 2                              : rem colour_24
13920           Black            = hex('000000')
13930           White            = hex('FFFFFF')
13940           Red              = hex('FF0000')
13950           Green            = hex('00FF00')
13960           Blue             = hex('0000FF')
13970           Magenta        = hex('FF00FF')
13980           Yellow           = hex('FFFF00')
13990           Cyan             = hex('00FFFF')
14000           Dark_slate     = hex('242424')
14010           Slate_grey     = hex('494949')
14020           Dark_grey      = hex('6D6D6D')
14030           Grey             = hex('929292')
14040           Light_grey     = hex('B6B6B6')
14050           Ash_grey       = hex('DBDBDB')
14060           Dark_red       = hex('920000')
14070           Light_green    = hex('B6FFB6')
14080           Mustard        = hex('929200')
14090           Dark_green     = hex('009200')
14100           Sea_blue       = hex('009292')
14110           Dark_blue      = hex('000092')
14120           Purple           = hex('920092')
14130           Shocking_pink  = hex('FF0092')
14140           Orange           = hex('FF9200')
14150           Lime_green     = hex('92FF00')
14160           Apple_green    = hex('00FF92')
14170           Bright_blue    = hex('0092FF')
14180           Mauve            = hex('9200FF')
14190           Peach            = hex('FFB6B6')
14200           Light_yellow   = hex('FFFFB6')
14210           Light_blue     = hex('B6FFFF')
14220           Sky_blue       = hex('B6B6FF')
14230           Rose_pink      = hex('FFB6FF')
14240           Pink             = hex('FFB6DB')
14250           Beige            = hex('FFDBB6')
14260           Pastel_pink    = hex('FFDBDB')
14270           Pastel_yellow  = hex('FFFFDB')
14280           Pastel_green   = hex('DBFFDB')
14290           Pastel_cyan    = hex('DBFFFF')
14300           Pastel_blue    = hex('DBDBFF')
14310           Pastel_rose    = hex('FFDBFF')
14320           Brick            = hex('B66D6D')
14330           Light_khaki    = hex('B6B66D')
14340           Dull_green     = hex('6DB66D')
14350           Dull_cyan      = hex('6DB6B6')
14360           Steel_blue     = hex('6D6DB6')
14370           Dull_pink      = hex('B66DB6')
14380           Brown            = hex('6D2424')
14390           Khaki            = hex('6D6D24')
14400           Dusky_green    = hex('246D24')
14410           Dusky_blue     = hex('246D6D')
14420           Midnight_blue  = hex('24246D')
14430           Plum             = hex('6D246D')
14440           Dusky_pink     = hex('B64992')
14450           Buff             = hex('B69249')
14460           Avocado        = hex('92B649')
14470           Dull_turquoise = hex('49B692')
14480           Dull_blue      = hex('4992B6')
14490           Faded_purple   = hex('9249B6')
14500           Cerise           = hex('920049')
14510           Tan_normal     = hex('924900')
14520           Grass_green    = hex('499200')
14530           Sea_green      = hex('009249')
14540           Ultramarine    = hex('004992')
14550           Deep_purple    = hex('490092')
14560           Light_orange   = hex('ffb624')
14570           pinkish        = hex('C0037B')
14580           Light_red      = hex('FC1729')
14590           wg_striped     = light_green
14600           br_striped     = dark_red
14610           wr_striped     = light_red
14620           bg_striped     = dark_green
14630   end select
14640   mode%=rmode
14650   ret mode%                             : rem return current screen mode now
14660 end def set_colours%
14670 :
14680 DEFine PROCedure init
14690 LOCal temp%,targets%,start%,lp%
14700   text=0:sprite=2:blob=4:pattern=6
14710   retr=256:retn=-256:sous1=254:sous2=252:sous3=250
14720   REMark normal: hit = selects/deselects, do = returns & leaves selected
14730   REMark retr = return even on hit, reset item to available after,
14740   REMark retn = return even on hit, hit selects/deselects, do selects
14750 :
14760   hit$=CHR$(1):do$=CHR$(2):cancel$=CHR$(3):sleep$=CHR$(7):wake$=CHR$(8)
14770   help$=CHR$(4):move$=CHR$(5):size$=CHR$(6):cr$=CHR$(10)
14780 :
14790   appwinoffset%=16                      : REMark appsub win additional y offset
14800   xsize%=500                                      : REMark x size divisible by 4!
14810   ysize%=200                                      : REMark provisional y size%
14820   lxs=24:lys=12                         : REMark standard item x,y, sizes
14830   inf1y=lys+4                                     : REMark y orig of inner border
14840   xesp=lxs+2                                      : REMark loose items x spacing
14850   nbr_post%=4                                     : REMark nbr of item right & left in title bar
14860   main_coul%=SMSCFG(2)                            : REMark colourways
14870   app_coul%=main_coul%                            : REMark preset colourways
14880   temp%=set_colours% (2)                : REMark always try to set highest colours
14890   rem IF temp%<2 AND main_coul%>3:main_coul%=0 : REMark old wman
14900   make_colours main_coul%               : REMark set all colours now
14910 :
14920   rep$=SMSCFG(1)                        : REMark dir with files
14930   titre$="Dir: "&rep$&" "
14940 :
14950 restore 14950
14960 :
14970 : REMark make loose item attributes
14980 :
14990   DIM std_iattr(3,3)
15000   RD_IATT std_iattr
15010     DATA 1,sp.litemhigh : REMark current item border size & colour
15020     DATA sp.litemunabg,sp.litemunafg,0,0 : REMark unavailable item paper & ink
15030     DATA sp.litemavabg,sp.litemavafg,0,0 : REMark   available               "    "           "
15040     DATA sp.litemselbg,sp.litemselfg,0,0 : REMark   selected
15050 :
15060 : REMark make data for sprites - uses the modified RD_SPRT function
15070 : REMark
15080 : REMark
15090 :
15100   espace_vide=ALCHP(100)                : REMark GLOBAL var for 0 mask sprites!
15110 :
15120   main_sprite=RD_SPRT(0)                : REMark 0 = normal mask
15130     DATA 11,13,6,5,4
15140     DATA '     www     '
15150     DATA '     waw     '
15160     DATA '     waw     '
15170     DATA '     waw     '
15180     DATA 'wwwwwa awwwww'
15190     DATA 'waaaa   aaaaw'
15200     DATA 'wwwwwa awwwww'
15210     DATA '     waw     '
15220     DATA '     waw     '
15230     DATA '     waw     '
15240     DATA '     www     '
15250 :
15260   move_sprite=RD_SPRT(1)                : REMark 1 = 0 mask
15270   IF temp%>1:move_sprite=6              : REMark or system sprite!
15280     DATA 10,14,5,4,4
15290     DATA 'aaaaaaaaaa    '
15300     DATA 'awwwwwwwwa    '
15310     DATA 'awwrrrrwwa    '
15320     DATA 'awwrrrrwwaaaaa'
15330     DATA 'awwrrwwwwwwwwa'
15340     DATA 'awwwwwwwwrrwwa'
15350     DATA 'aaaaawwrrrrwwa'
15360     DATA '    awwrrrrwwa'
15370     DATA '    awwwwwwwwa'
15380     DATA '    aaaaaaaaaa'
15390 :
15400   zzz_sprite= RD_SPRT(1)
15410   IF temp%>1:zzz_sprite=10
15420     DATA 7,15,0,0,4
15430     DATA 'wwwww          '
15440     DATA '   w           '
15450     DATA '  w  wwww      '
15460     DATA ' w     w       '
15470     DATA 'wwwww w  wwww  '
15480     DATA '     wwww  w   '
15490     DATA '          wwww '
15500 :
15510  size_sprite=RD_SPRT(1)
15520  IF temp%>1:size_sprite=7
15530     DATA 9,14,5,4,4
15540     DATA 'aaaaaaaaaaaaaa'
15550     DATA 'awwwwwwwwwwwwa'
15560     DATA 'awwrrrrrrrrwwa'
15570     DATA 'awwrrrrrrrrwwa'
15580     DATA 'awwrrwwwwwwwwa'
15590     DATA 'awwrrwwrrrrwwa'
15600     DATA 'awwrrwwrrrrwwa'
15610     DATA 'awwwwwwwwwwwwa'
15620     DATA 'aaaaaaaaaaaaaa'
15630 :
15640   targets%=0
15650   REMark IF NOT compiled:cmd$="-tj-tg-st-mk-as"
15660   quit_always%=0
15670   quit_no_errors%=0
15680   autostart%=0
15690   do_make_it%=0
15700   force_assembly$=""
15710   delete_first%=0
15720   select_all%=0
15730   IF cmd$<>''
15740       IF '-qa' INSTR cmd$:quit_always%=1
15750       IF '-q0' INSTR cmd$:quit_no_errors%=1
15760       IF '-as' INSTR cmd$:autostart%=1
15770       IF '-mk' INSTR cmd$:do_make_it%=1
15780       IF '-de' INSTR cmd$:delete_first%=1
15790       IF '-sa' INSTR cmd$:select_all%=1
15800       IF '-tg' INSTR cmd$:taedrgets%=targets%+1 : REMark Generic
15810       IF '-ta' INSTR cmd$:targets%=targets%+2 : REMark Atari
15820       IF '-to' INSTR cmd$:targets%=targets%+4 : REMark gOld
15830       IF '-tq' INSTR cmd$:targets%=targets%+8 : REMark Q40
15840       IF '-tx' INSTR cmd$:targets%=targets%+16 : REMark qXl
15850       IF '-tu' INSTR cmd$:targets%=targets%+32 : REMark aUrora
15860       IF '-tj' INSTR cmd$:targets%=targets%+64 : REMark Java
15870       IF '-tr' INSTR cmd$:targets%=targets%+128 : REMark ptRgen
15880       IF '-tc' INSTR cmd$:targets%=targets%+256 : REMark qpC
15890       IF '-ta' INSTR cmd$:targets%=255+256  : REMark all targets
15900       IF '-fa' INSTR cmd$:force_assembly$=" -f": REMark force assembly of all files
15910   ELSE
15920     targets%=1
15930   END IF
15940   make_my_data targets%
15950   make_main_wdw
15960   IF (select_all%):main_lfl%(all_it%)=129:set_statusses
15970   start%=1
15980   FOR lp%=target_its% TO target_its%+8
15990     IF targets% && start%: main_lfl%(lp%)=129
16000     start%=start%*2
16010   END FOR lp%
16020   IF do_make_it%:main_lfl%(make_it%)=129
16030   IF delete_first%:main_lfl%(del_it%)=129
16040 :
16050   warn_make
16060 :
16070 END DEFine init
16080 :
16090 DEFine PROCedure make_main_wdw
16100 REMark make the main window
16110 LOCal main_lot,main_iwt,main_iot1
16120 :
16130   REMark first make sure of the wdw sizes (we also come here from change_size!)
16140   xsize%=xsize%-(xsize% MOD 4)                                : REMark make into nbr divisible by 4
16150   IF xsize%<500:xsize%=500
16160 :
16170   REMark calculate the y size of the wdw so that it always looks nice, in that
16180   REMark the rows in the appsub always fill the appsub entirely
16190   ysize%=ysize%+(ysize%&&1)             : REMark make into even number
16200   IF ysize%<100:ysize%=100              : REMark predetermioned min ysize% before calculations
16210   ysize%=ysize%-inf1y-14-appwinoffset%
16220   rangees=ysize% DIV 12                 : REMark GLOBAL var: nbr of rows in appsub
16230   IF rangees>data_count%-1:rangees=data_count%-1
16240   IF rangees<3:rangees=3
16250   ysize%=rangees*12+inf1y+14 +appwinoffset%
16260 :
16270   REMark first calc info obj size - this also influences one loose item, used
16280   REMark in the title bar
16290   longueur_titre=LEN(titre$)*6                    : REMark length in pixels
16300   titre_x=INT((xsize%-longueur_titre)/2): REMark x orig
16310 :
16320   REMark first the loose items
16330 :
16340   yinf1y=inf1y+4
16350   esc%=0:move_it%=esc%+1:size_it%=move_it%+1
16360   sleep_it%=size_it%+1:all_it%=sleep_it%+1:go_it%=all_it%+1
16370   dir_it%=go_it%+1:target_its%=dir_it%+1:make_it%=target_its%+9
16380   del_it%=make_it%+1
16390 :
16400 restore 16400
16410   REMark reset data pointer to here
16420 :
16430   temp%=(xsize%-4) DIV 9
16440   main_lit%=17:DIM main_lfl%(main_lit%)   : REMark there are 18 items!
16450   main_lot=RD_LOT(std_iattr,main_lit%)
16460     DATA lxs,lys,4+xesp*0,2,0,0,cancel$,text+retr,'ESC'
16470     DATA lxs,lys,4+xesp*1,2,0,0,move$,sprite+retr,move_sprite
16480     DATA lxs,lys,4+xesp*2,2,0,0,size$,sprite+retn,size_sprite
16490     DATA lxs,lys,xsize%-2-xesp*1,2,0,0,sleep$,sprite+retr,zzz_sprite
16500     DATA lxs,lys,xsize%-2-xesp*3,2,0,0,'A',sous1+retn,"All"
16510     DATA lxs,lys,xsize%-2-xesp*4,2,0,0,'O',sous1+retn,"OK"
16520     DATA 6*6,lys-2,titre_x+30,3,0,0   ,'D',text+retn,rep$
16530     DATA 8*6,lys,8,inf1y+2,0,0,        'G',sous1+retn,"Generic"
16540     DATA 8*6,lys,8+temp%,inf1y+2,0,0,  'T',sous2+retn,"Atari"
16550     DATA 8*6,lys,8+temp%*2,inf1y+2,0,0,'L',sous3+retn,"GoldCard"
16560     DATA 8*6,lys,8+temp%*3,inf1y+2,0,0,'Q',sous1+retn,"Q40/Q60"
16570     DATA 8*6,lys,8+temp%*4,inf1y+2,0,0,'X',sous2+retn,"QXL"
16580     DATA 8*6,lys,8+temp%*5,inf1y+2,0,0,'U',sous2+retn,"Aurora"
16590     DATA 8*6,lys,8+temp%*6,inf1y+2,0,0,'J',sous1+retn,"Java"
16600     DATA 8*6,lys,8+temp%*7,inf1y+2,0,0,'P',sous1+retn,"PtrGen"
16610     DATA 8*6,lys,8+temp%*8,inf1y+2,0,0,'C',sous3+retn,"QPC"
16620     DATA 4*6,lys,xsize%-2-xesp*2,2,0,0,'M',sous1+retn,"Make"
16630     DATA lxs,lys,4+xesp*3,2,0,0,"E",sous2+retn,"DEL"
16640 :
16650   REMark now info wdw objects - here only one, the title
16660   REMark make the title data: calculate size and x position of this object
16670 :
16680   main_iot1=RD_IOT(0)
16690     DATA longueur_titre,10,4,INT((lys-10)/2),sous1,sp.titlefg,0,0,titre$
16700 :
16710   REMark now the info wdws
16720 :
16730   main_iwt=RD_IWT(2)                                          : REMark there are 3
16740 :
16750 REMark first the title bar
16760 :
16770     DATA xsize%-8-(2*nbr_post%*xesp),lys+2,xesp*nbr_post%+4,0               : REMark x,y sizes & origs
16780     DATA 0,0,sp.infwinbd,sp.titlebg     : REMark shadow, border size & col,paper col
16790     DATA 0                                        : REMark object pointer (none)
16800 :
16810 REMark now wdw containing title itself
16820 :
16830     DATA longueur_titre+8,lys,titre_x-4,2
16840     DATA 0,0,sp.infwindbd,sp.infwinbg
16850     DATA main_iot1                      : REMark one object
16860 :
16870 REMark inner border
16880 :
16890     DATA xsize%-8,ysize%-inf1y-2,4,inf1y
16900     DATA 0,1,sp.infwinbd,sp.infwinbg
16910     DATA 0
16920 :
16930 REMark now the appsub wdw - use the OUTPTR functions for this
16940 :
16950   app_addr=0                                      : REMark there is none yet
16960   mk_main_app 1                         : REMark (this destroys my DATA pointer)
16970   main_app_list = app_addr+180                    : REMark list built by OUTPTR
16980 :
16990 : REMark  working defn
17000 :
17010 restore 17010
17020                                                   : REMark reset DATA pointer (due to Basic Linker, restore must be ALONE on the line)
17030   main_defn=RD_WDEF
17040     DATA 2,1,sp.winbd,sp.winbg                    : REMark shad,border size & col,pap col
17050     DATA xsize%,ysize%,0,0    : REMark x,y size & initial ptr pos
17060     DATA main_sprite,main_lot,main_iwt,main_app_list : REMark sprite,loose items, info wins, appsubs
17070 END DEFine make_main_wdw
17080 :
17090 DEFine PROCedure mk_main_app
17100 REMark make main application sub window
17110 LOCal t2%,temp%,sections%,att_phys%(3)
17120   DIM main_cty%(0):DIM main_mfl%(0,0)
17130   sections%=2
17140   temp%=sections% :t2%=rangees
17150   att_phys%(0)=xsize%-16                : REMark xsize calculated on main wdw x size
17160   att_phys%(1)=t2%                      : REMark y size,nominal,in rows
17170   att_phys%(2)=4                        : REMark x origin x
17180   att_phys%(3)=inf1y +appwinoffset%     : REMark y origin
17190   app_addr=fen_app(app_addr,mdata$,temp%,1,att_phys%,"DO ")
17200   IF app_addr
17210       DIM main_cty%(sections%,2)        : REMark ctrl defn
17220       main_cty%(0,0)=1                            : REMark show 1 section
17230       main_cty%(1,2)=t2%
17240       IF sections%=2:main_cty%(2,2)=t2% : REMark but allow 2
17250       IF temp%
17260              DIM main_mfl%(temp%-1,0)   : REMark flag array
17270              set_statusses              : REMark and set all statusses
17280       ELSE
17290              POKE_W app_addr,PEEK_W(app_addr)+8
17300       END IF
17310    REMark   scroll_arrows t2%,temp%,app_addr,main_cty%
17320   END IF
17330 END DEFine mk_main_app
17340 :
17350 DEFine PROCedure set_statusses
17360 REMark set all statusses of menu appsub objects
17370 LOCal temp%,lp%,stat%
17380   temp%=DIMN(main_mfl%,1)               : REMark how many are there?
17390   stat%=main_lfl%(all_it%)              : REMark status of "all" item
17400   FOR lp%=0 TO temp%
17410     IF mdata$(lp%+1,1)='*'
17420       main_mfl%(lp%,0)=16               : REMark comments set to unavailable
17430     ELSE
17440       main_mfl%(lp%,0)=stat%            : REMark all other to status
17450     END IF
17460   END FOR lp%
17470 END DEFine set_statusses
17480 :
17490 def proc make_standard_sprites
17500 rem This makes standard move, sleep and wake sprites and sets the
17510 rem GLOBAL vars move_sprite,, zzz_sprite, wake_sprite to them.
17520 rem If there are system sprites, these are used.
17530 rem This also makes the old "main_sprite" pointer sprite.
17540 :
17550   espace_vide=ALCHP(100)         : rem for pointers with no mask
17560 :
17570 restore 17570
17580   main_sprite=RD_SPRT(0)         : REMark ici un masque normal
17590     DATA 11,13,6,5,4
17600     DATA '     www     '
17610     DATA '     waw     '
17620     DATA '     waw     '
17630     DATA '     waw     '
17640     DATA 'wwwwwa awwwww'
17650     DATA 'waaaa   aaaaw'
17660     DATA 'wwwwwa awwwww'
17670     DATA '     waw     '
17680     DATA '     waw     '
17690     DATA '     waw     '
17700     DATA '     www     '
17710 :
17720   if sys_tspr_addr(6)<>0
17730     move_sprite=6
17740   else
17750 restore 17750
17760     move_sprite=RD_SPRT(1)         : REMark ici un masque 0
17770     DATA 10,14,5,4,4
17780     DATA 'aaaaaaaaaa    '
17790     DATA 'awwwwwwwwa    '
17800     DATA 'awwrrrrwwa    '
17810     DATA 'awwrrrrwwaaaaa'
17820     DATA 'awwrrwwwwwwwwa'
17830     DATA 'awwwwwwwwrrwwa'
17840     DATA 'aaaaawwrrrrwwa'
17850     DATA '    awwrrrrwwa'
17860     DATA '    awwwwwwwwa'
17870     DATA '    aaaaaaaaaa'
17880   endif
17890 :
17900   if sys_tspr_addr(10)<>0
17910     sleep_sprite=10
17920   else
17930 restore 17930
17940     sleep_sprite= RD_SPRT(1)             : REMark ici un masque 0
17950     DATA 7,15,0,0,4
17960     DATA 'wwwww          '
17970     DATA '   w           '
17980     DATA '  w  wwww      '
17990     DATA ' w     w       '
18000     DATA 'wwwww w  wwww  '
18010     DATA '     wwww  w   '
18020     DATA '          wwww '
18030   endif
18040 :
18050   if sys_tspr_addr(11)<>0
18060     wake_sprite=11
18070   else
18080 restore 18080
18090     wake_sprite=RD_SPRT(1)          : REMark ici un masque 0
18100     DATA 9,14,0,0,4
18110     DATA "             w"
18120     DATA "            w "
18130     DATA "      w   ww  "
18140     DATA "     ww www   "
18150     DATA "    wwwwww    "
18160     DATA "   www ww     "
18170     DATA "  w   w      "
18180     DATA " w            "
18190     DATA "w             "
18200   endif
18210 :
18220   if sys_tspr_addr(7)<>0
18230     size_sprite=7
18240   else
18250 restore 18250
18260     size_sprite=RD_SPRT(1)
18270     DATA 9,14,5,4,4
18280     DATA 'aaaaaaaaaaaaaa'
18290     DATA 'awwwwwwwwwwwwa'
18300     DATA 'awwrrrrrrrrwwa'
18310     DATA 'awwrrrrrrrrwwa'
18320     DATA 'awwrrwwwwwwwwa'
18330     DATA 'awwrrwwrrrrwwa'
18340     DATA 'awwrrwwrrrrwwa'
18350     DATA 'awwwwwwwwwwwwa'
18360     DATA 'aaaaaaaaaaaaaa'
18370   endif
18380 :
18390 end def make_sprites
18400 :
18410 DEFine PROCedure warn_make
18420 rem makes warning & info window
18430 rem uses global vars sp.xxx
18440 rem SETS the global var warning_width% (nbr of chars in one line in wdw)
18450 local lys%,warn_lot,warn_iot1,warn_iwt
18460 local x%,y%,warning_paper,warning_brd
18470 restore 18470
18480   x%=200:y%=82
18490   lys%=10
18500   DIM warn_lfl%(1)
18510   warn_lot=RD_LOT(std_iattr,1)
18520     DATA 30,lys%,4,4,0,0,cancel$,text+retr,'ESC'
18530     DATA 30,lys%,x%-34,4,0,0,'O',text+retr,"OK"
18540 :
18550   warn_iot1=RD_IOT (0)
18560     DATA 12*9,10,0,1,text,sp.errfg,2,0,'Attention'
18570 :
18580   warn_iwt=RD_IWT(1)
18590     DATA x%-16,y%-32,8,24
18600     DATA 0,1,sp.winbd,sp.errbg
18610     DATA 0
18620 :
18630     DATA 110,12,(x%-110) div 2,4
18640     DATA 0,1,sp.winbd,sp.errbg
18650     DATA warn_iot1
18660 :
18670   warn_defn=RD_WDEF
18680     DATA 2,1,sp.winbd,sp.errbg
18690     DATA x%,y%,0,0
18700     DATA main_sprite,warn_lot,warn_iwt,0
18710 :
18720   warning_width%=(x%-16) div 6                    : rem width of warning window in chars
18730   info_is_pulled_down%=0
18740   warn_wdw_addr=peek_l(warn_defn+100)+20: rem ptr to 2nd info wdw
18750  rem warn_wdw_addr=peek_l(warn_wdw_addr+16): rem ptr to object list
18760 END DEFine warn_make
18770 :
18780 DEFine PROCedure about_make (titre$,version$)
18790 local lxs%,lys%,xesp%,inf1y%,abt_lot,longueur_titre%,titre_x%
18800 local abt_iwt,abt_iot1,abt_iot2
18810   ab_xt%=200:ab_yt%=120    : REMark tailles x et y: la taille x devrait
18820                                    : REMark ‘tre un chiffre divisible par 4
18830   lxs%=24:lys%=10                    : REMark tailles x et y pour postes standard
18840   inf1y%=lys%+4            : REMark posi. y de la fen. d'inf. intƒrieure
18850   xesp%=lxs%+2
18860   nbr_post%=0                      : REMark nombre de postes dans un coin
18870 :
18880 restore 18880
18890 wolfsoft_sprite=rD_SPrT(0)
18900     DATA 23,51,0,0,4
18910     DATA 'rrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrr'
18920     DATA 'rrgggggggggggggggggggggggggggggggggggggggggggggggrr'
18930     DATA 'rrggggggggrrrrrrrrrrrrrrrrgggggggggggggggggggggggrr'
18940     DATA 'rrgggggggggggggggggggggrgggggggggggggggggggggggggrr'
18950     DATA 'rrggggggggggggggggggggrggggggggggggggggggggggggggrr'
18960     DATA 'rrggggggggagggggagggrrrrrrrrrrrrrggaaaaaaaaggggggrr'
18970     DATA 'rrggggggggagggggaggggggggggggggggggagggggggggggggrr'
18980     DATA 'rrggggggggaggaggaggaaaaaaaggaggggggaaaaaaagggggggrr'
18990     DATA 'rrggggggggagaaagaggaawwaaaggaggggggagggggggggggggrr'
19000     DATA 'rrggggggggaaagaaaggaawwwwaggaggggggagggggggggggggrr'
19010     DATA 'rrggrrrrrgaagggaaggawwwwaaggaggggggagggggrrrrrrrgrr'
19020     DATA 'rrgrgggggrgggggggggaaawwaaggaggggggggggrrrgggggggrr'
19030     DATA 'rrgrggggggrggggggggaaaaaaaggaaaaaagggrrggrgggggggrr'
19040     DATA 'rrgrgggggggggggggrrrgggggggggggrrrggrggggrgggggggrr'
19050     DATA 'rrggrrrrrrggggggrgggrrgggggggrrggggggggggrgggggggrr'
19060     DATA 'rrggggggggrrrgggrgggggrrgggrrggggggggggggrgggggggrr'
19070     DATA 'rrgggggggggggrggrgggggggrggrgggggggggggggrgggggggrr'
19080     DATA 'rrggggggggggggrgrggggggggrgrrrrrrrgggggggrgggggggrr'
19090     DATA 'rrgrggggggggggrgrggggggggrgrgggggggggggggrgggggggrr'
19100     DATA 'rrggrgggggggrrgggrggggggrggrgggggggggggggrgggggggrr'
19110     DATA 'rrgggrrrrrrrggggggrrrrrrgggrgggggggggggggrgggggggrr'
19120     DATA 'rrgggggggggggggggggggggggggggggggggggggggggggggggrr'
19130     DATA 'rrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrr'
19140 :
19150   yinf1y%=inf1y%+4:
19160 :
19170   DIM abt_lfl%(0)
19180   abt_lot=RD_LOT(std_iattr,0)
19190     DATA ab_xt%-16,lys%,8,ab_yt%-lys%-8,0,0,cancel$,text+retr,'Super!'
19200 :
19210 : REMark  D'abord le titre qui est un objet d'une sous-fen‘tre d'info.
19220 :
19230   longueur_titre%=LEN(" Version "&version$)*6               : REMark on calcule la taille et la
19240   titre_x%=INT((ab_xt%-longueur_titre%)/2)  : REMark position x de cet objet
19250   abt_iot2=RD_IOT(1)
19260     DATA longueur_titre%,lys%,4,8,text,sp.winfg,0,0," Version "&version$
19270     DATA 52,24,ab_xt%-60,40,sprite,0,wolfsoft_sprite
19280   longueur_titre%=LEN(titre$)*6     : REMark on calcule la taille et la
19290   titre_x%=INT((ab_xt%-longueur_titre%)/2)  : REMark position x de cet objet
19300   abt_iot1=RD_IOT(0)
19310     DATA longueur_titre%,lys%,4,INT((lys%-10)/2),text,sp.titlefg,0,0,titre$
19320   abt_iwt=RD_IWT(2)               : REMark on veut 3 sous-fen. d'info
19330 REMark d'abord la fen‘tre contenant la barre entourant le titre
19340     DATA ab_xt%-8-(2*nbr_post%*xesp%),lys%+2,xesp%*nbr_post%+4,0  : REMark taille x,y, origines x,y
19350     DATA 0,0,sp.infwinbd,sp.titlebg     : REMark ombre, taille & couleur bordure
19360                                    : REMark puis couleur papier
19370     DATA 0                         : REMark pointeur vers objets
19380 REMark maintenant la fen‘tre qui contient le titre
19390     DATA longueur_titre%+8,lys%,titre_x%-2,2   : REMark on utilise titre_x% etc
19400     DATA 0,0, sp.infwinbd,sp.titletextbg
19410     DATA abt_iot1                 : REMark un objet, le titre
19420 REMark maintenant la fen‘tre de la bordure intƒrieure
19430     DATA ab_xt%-8,ab_yt%-inf1y%-2,4,inf1y%
19440     DATA 0,1,sp.infwinbd,sp.infwinbg
19450     DATA abt_iot2
19460   abt_defn=RD_WDEF
19470     DATA 2,1,sp.winbd,sp.winbg: REMark ombre, larg. & coul. bord., coul. papier
19480     DATA ab_xt%,ab_yt%,10,10 : REMark taille x,y et position pointeur
19490     DATA main_sprite,abt_lot,abt_iwt,0
19500 :
19510 end def about_make
19520 :
19530 DEFine PROCedure about
19540   LOCal loop%,item%,dummy%,chan%
19550   DR_PULD abt_defn,-1,-1,abt_lfl%
19560   chan%=fopen('con')
19570   dr_iwdf#chan%,abt_defn,2
19580   wm_ink#chan%,sp.infwinfg
19590   wm_paper#chan%,sp.infwinbg
19600   at#chan%,4,2
19610   print#chan%,"Copyright (c) 2014"
19620   at#chan%,5,2
19630   print#chan%,"W. Lenerz"
19640   close#chan%
19650   REPeat loop%
19660     RD_PTR abt_defn,item%,dummy%,dummy%,dummy%,dummy%,abt_lfl%
19670     SELect ON item%
19680             =0       : EXIT loop%
19690     END SELect
19700   END REPeat loop%
19710   DR_UNST abt_defn
19720 END DEFine about
19730 :
19740 def proc do_popup(string$)
19750 rem this pops up a small "hint" window showing the string$ passed
19760 rem as parameter.
19770 rem The string MUST be 1 line long and MUST fit
19780 rem inside the window (YOU must make sure of that!)
19790   do_popup2 string$,0
19800 end def do_popup
19810 :
19820 def proc popup_make
19830 rem make a small window defn
19840 local x%,y%,p_iot,popsub(0)
19850 restore 19850
19860   p_sprt=rd_sprt(0)                               : rem make an invisible pointer, all transparent
19870     data 1,2,0,0,4
19880     data '  '
19890   x%=14                                 : rem bogus window sizes
19900   y%=10
19910   popsub(0)=rd_appw                               : rem the info wdw
19920     DATA x%,y%,0,0
19930     DATA 0,0, sp.hintbd,sp.hintbg
19940     DATA 0,""
19950   popup_defn=RD_WDEF                              : rem the working defn
19960     DATA 0,1,sp.hintbd,sp.hintbg        : rem no shadow, border size & col, paper col
19970     DATA x%,y%,40,2
19980     DATA p_sprt,0,0,mk_awl(popsub)
19990 end def popup_make
20000 :
20010 def proc do_popup2(string$,main_defn)
20020 rem This pops up a small "hint" window showing the string$ passed
20030 rem as parameter.
20040 rem main_defn is the working defintion of the window over which the hint
20050 rem window is to hover. Position the pointer there where you need it before
20060 rem calling this.
20070 rem If the string is too long to fit into the window (which can only be as long
20080 rem as the outline of the working defn) then the string is split up at the
20090 rem first convenient space and put on a second line, and so on.
20100 rem If this would lead to the hint window being too high, only as many
20110 rem lines as possible will be displayed.
20120 rem !!!!!
20130 rem This presumes that a char is 10 pixels high & 6 pixels wide (standard ql 6x10 matrix)..
20140 rem !!!!!
20150 rem The string may contain the char chr$(10) - there will be a newline.
20160 rem
20170 rem note : do not try to pass a screen chaneel, open & close one here.
20180 rem
20190 local dummy%,lp%,address,con%,temp%,lastspace%,xs%,ys%,temp$
20200 local hint$(10),slen% ,maxlength%,popup_lfl%(0),popup_mfl%(0,0)
20210   if string$="":return                            : rem empty string, do nothing
20220   string$=strip_spaces$(string$)        : rem no spaces at ends
20230   con%=fopen("con")
20240   if main_defn<>0
20250     xs%=peek_w (main_defn+32)
20260     ys%=peek_w(main_defn+34)            : rem get max sizes the hint wdw may have
20270   else
20280     scr_size#con%,xs,ys%
20290   endif
20300   xs%=(xs%-4) div 6                               : rem max nbr of chars in 1 line
20310   ys%=(ys%-4) div 10                              : rem max nbr of lines in wdw
20320   if xs%=0 or ys%=0:close#con%:return   : rem nothing will really fit, just leave
20330   dim hint$(ys%,xs%)                              : rem array to contain string
20340   rem now parse the string, find newlines, make sure of string length
20350   dummy%=1                                        : rem start of current string section
20360   temp%=0                                         : rem line
20370   slen%=0                                         : rem current length of string
20380   for lp%=1 to len(string$)
20390     if string$(lp%)=chr$(10)
20400       hint$(temp%)=string$(dummy% to lp%-1): rem cut string here
20410       temp%=temp%+1
20420       if temp%=ys%:exit lp%             : rem max nbr of lines reached
20430       dummy%=lp%+1                      : rem this is where new start of string is
20440       lastspace%=lp%
20450     else
20460       if string$(lp%)=" "
20470           lastspace%=lp%                          : rem this is where last space is
20480       endif
20490     endif
20500     slen%=slen%+1                       : rem one more char
20510     if slen% > xs%
20520       hint$(temp%)=string$(dummy% to lastspace%-1) : rem too many chars in line
20530       slen%=0
20540       temp%=temp%+1                               : rem we need nother line
20550       if temp%=ys%:exit lp%             : rem max nbr of lines reached
20560       dummy%=lastspace%+1
20570     endif
20580   end for lp%
20590   if temp%<ys%
20600      hint$(temp%)=string$(dummy% to)       : rem add last part
20610      temp%=temp%+1
20620   endif
20630   maxlength%=0
20640   for lp%=0 to temp%
20650     temp$=strip_spaces$(hint$(lp%))     : rem use intermediary var, because of QLIB
20660     if maxlength%<len(temp$):maxlength%=len (temp$)
20670   end for lp%
20680   lp%=(maxlength%*6)+4                            : rem hint window length
20690   poke_w popup_defn+32,lp%              : rem set window xsize
20700   poke_w popup_defn+34,temp%*10+2       : rem
20710   address=peek_l(popup_defn+112)        : rem ptr to app sub wdw...
20720   address=peek_l(address)               : rem ...now
20730   poke_w address,lp%-2                            : rem set its length
20740   poke_w address+2,temp%*10             : rem set its height
20750   dr_puld popup_defn,-1,-1,popup_lfl%,popup_mfl%
20760   dr_awdf#con%,popup_defn,0             : rem pull wdw over info wdw
20770   wm_paper#con%,sp.hintbg
20780   wm_ink#con%,sp.hintfg
20790   for lp%=0 to dimn(hint$,1)
20800     if hint$(lp%)<>"":print#con%,strip_spaces$(hint$(lp%))
20810   end for lp%
20820   temp%=0:slen%=0:xs%=0:ys%=0:dummy%=0
20830   RD_PTR popup_defn,temp%,dummy%,slen%,xs%,ys%,popup_lfl%,popup_mfl%: rem comes back immediatley
20840   RD_PTR popup_defn,temp%,dummy%,slen%,xs%,ys%,popup_lfl%,popup_mfl%
20850   close#con%
20860   dr_unst popup_defn
20870 end def do_popup2
20880 :
20890 DEFine FuNction RD_SPRT(drap%)
20900 REMark drap% determine si masque 0
20910   LOCal tmp_patt$(32,32)
20920   LOCal xs,ys,xo,yo, md, l
20930   READ ys,xs,xo,yo,md
20940   DIM tmp_patt$(ys,xs)
20950   FOR l=0 TO ys-1:READ tmp_patt$(l)
20960   l=ALCHP(SPRSP(xs,ys))
20970   SPSET l,xo,yo,md,tmp_patt$(0 TO ys-1,1 TO xs)
20980   IF drap%
20990     POKE_L l+16,espace_vide-(l+16)
21000   END IF
21010   RETurn l
21020 END DEFine RD_SPRT
21030 :
21040 DEFine FuNction RD_WDEF
21050 rem read window defn
21060   LOCal ldef%(3), lspr, lloose, linfo, lappl, lattr%(3)
21070   RD_WATT lattr%
21080   READ ldef%(0), ldef%(1), ldef%(2), ldef%(3)
21090   READ lspr, lloose, linf, lappl
21100   RETurn MK_WDEF (ldef%, lattr%, lspr, lloose, linf, lappl)
21110 END DEFine RD_WDEF
21120 :
21130 DEFine FuNction RD_LOT (lattr,nitem)
21140   LOCal count(3)
21150   LOCal item, ltyp, a$, lsk$
21160   LOCal ldef%(nitem,6), lptr(3,nitem), lstr$(nitem,85)
21170   lsk$=''
21180   FOR item = 0 TO nitem
21190     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
21200     READ ldef%(item,4), ldef%(item,5)
21210     READ a$: lsk$=lsk$ & a$
21220     READ ltyp
21230     ldef%(item,6)=ltyp: ltyp=(ltyp MOD 256)/2
21240     IF ltyp>10 or ltyp <0:ltyp=0
21250     IF ltyp
21260       READ lptr(ltyp,count(ltyp))
21270     ELSE
21280       READ lstr$(count(0))
21290     END IF
21300     count(ltyp)=count(ltyp)+1
21310   END FOR item
21320   RETurn MK_LIL (lattr, ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), ldef%(TO, 4 TO 5), lsk$, ldef%(TO, 6), lstr$, lptr(1), lptr(2), lptr(3))
21330 END DEFine RD_LOT
21340 :
21350 DEFine FuNction RD_IWT(nitem)
21360   LOCal item,mc1
21370   LOCal ldef%(nitem,3), latt%(nitem,3), lptr(nitem)
21380   FOR item = 0 TO nitem
21390     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
21400     READ latt%(item,0), latt%(item,1), latt%(item,2), latt%(item,3)
21410     READ lptr(item)
21420   END FOR item
21430   RETurn MK_IWL (ldef%, latt%, lptr)
21440 END DEFine RD_IWT
21450 :
21460 DEFine FuNction RD_IOT(nitem)
21470   LOCal count(3)
21480   LOCal item, ltyp, work1, work2
21490   LOCal ldef%(nitem,4), lptr(3,nitem), lstr$(nitem,85)
21500   FOR item = 0 TO nitem
21510     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
21520     READ ltyp
21530     ldef%(item,4)=ltyp:  ltyp=(ltyp MOD 256)/2
21540     IF ltyp >10:ltyp=0
21550     IF ltyp
21560       READ lptr(0,item),lptr(ltyp,count(ltyp))
21570     ELSE
21580       if mycolour_mode%=2
21590           read work1,work2,work3
21600           lptr(0,item)=WL_4_IOL(work1,work2,work3)
21610       else
21620           READ work1
21630           READ work2: work1=work1*256+work2
21640           READ work2: lptr(0,item)=work1*256+work2
21650       endif
21660       READ lstr$(count(0))
21670     END IF
21680     count(ltyp) = count(ltyp) + 1
21690   END FOR item
21700   RETurn MK_IOL (ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), lptr(0), ldef%(TO, 4), lstr$, lptr(1), lptr(2), lptr(3))
21710 END DEFine RD_IOT
21720 :
21730 DEFine PROCedure RD_IATT (lattr)
21740   LOCal i
21750   READ lattr(0,0), lattr(0,1)
21760   FOR i=1 TO 3: READ lattr(i,0), lattr(i,1), lattr(i,2), lattr(i,3)
21770 END DEFine RD_IATT
21780 :
21790 DEFine PROCedure RD_WATT (lattr%)
21800   READ lattr%(0), lattr%(1),lattr%(2),lattr%(3)
21810 END DEFine RD_WATT
21820 :
21830 DEFine FuNction RD_APPW
21840   LOCal ldef%(3), lspr, sk$, lattr%(3)
21850   READ ldef%(0), ldef%(1), ldef%(2), ldef%(3)
21860   RD_WATT lattr%
21870   READ lspr, sk$
21880   RETurn MK_APPW (ldef%, lattr%, lspr,sk$)
21890 END DEFine
21900 :
