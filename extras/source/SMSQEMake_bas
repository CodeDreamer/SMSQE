100 REMark $$asmb=dev8_extras_source_outptr_bin,0,10
110 REMark $$asmb=dev1_booty_qptr_bin,50,172
120 REMark $$asmb=dev8_extras_source_SMSQEMake_bin,0,10
130 REMark rem $$asmb=dev8_extras_cline_bin,0,10      <---- NOOOOOOOOOOOOOOOOOOOOOOOOOOOOO!
140 REMark $$stak=20000
150 REMark Make sure the compiled version does not contain the CURRENT_LINE keyword
160 REMark use the ccrestore procedure herein to elmimenate it!!!!!!!!!!!!!!!!
170 compiled=NOT IS_OPEN(#0)
180 IF NOT compiled
190   SCR_SIZE#0, xsz%,ysz%
200   OUTLN#0,xsz%,ysz%-28,0,28
210   WSET -1
220 END IF
230 init
240 main
250 STOP
260 DEFine PROCedure make_new_dir (poste%)
270 REMark get athe new dir where the files are located
280 LOCal quel_rep$
290   IF MENU_OK
300     quel_rep$= DIR_SELECT$("The Files are where?",,10,10,main_coul%,app_coul%)
310   ELSE
320     quel_rep$=getstr$(poste%,0,main_defn,a_lire$)       : REMark use menu extns if possible
330   END IF
340   IF LEN (quel_rep$) >= 5
350      rep$=quel_rep$(1 TO 5)
360   END IF
370   CH_ITEM main_defn,-1,poste%,-1,"",rep$
380   main_lfl%(poste%)=main_lfl%(poste%)+1
390 END DEFine make_new_dir
400 :
410 DEFine PROCedure main
420 LOCal lp%,item%,event%,swnum%,xrel%,yrel%,item%,lp2%,tot%
430   IF compiled
440      OPEN#1,'con'
450      SCR_SIZE#1,xsz%,ysz%
460      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
470   ELSE
480      SCR_SIZE#1,xsz%,ysz%
490      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
500   END IF
510   POS_PTR main_defn,40*65536+20,1
520   REPeat lp%
530     RD_PTR main_defn,item%,swnum%,event%,xrel%,yrel%,main_lfl%,main_mfl%,main_cty%
540     IF swnum%= -1
550       SELect ON item%
560               =esc%     : EXIT lp%      : REMark quit prog
570               =move_it% : CH_WIN main_defn: REMark move
580               =size_it% : change_size xrel%,yrel% : REMark change size
590               =sleep_it%: button main_defn,main_lfl%,main_mfl%,main_cty%,"SMSQ/E Make"
600               =all_it%
610                  set_statusses          : REMark set all appsub item statusses
620                  main_cty%(0,1)=1       : REMark and redraw
630               =go_it%   : do_the_work
640               =dir_it%: make_new_dir item%
650               =target_its% TO target_its%+7
660                           start%=1:machines%=0
670                           FOR lp2%=target_its% TO target_its%+7
680                               IF main_lfl%(lp2%):machines%=machines%+start%
690                               start%=start%*2
700                           END FOR lp2%
710                           make_my_data machines%        : REMark make data arrays
720                           mk_main_app                   : REMark make appsub wdw
730                           main_app_list=app_addr+180
740                           AP_IN main_defn,main_app_list : REMark put it in
750                           DR_WDRW main_defn:main_cty%(0,1)=1 : REMark and redraw entire wdw
760       END SELect
770     END IF
780   END REPeat lp%
790   DR_UNST main_defn
800   IF compiled:CLOSE#1
810   RECHP espace_vide
820 END DEFine main
830 :
840 DEFine PROCedure button (def_trav,menu_drap%,app_drap%,control%,chaine$)
850 REMark set up as button
860   DR_UNST def_trav                      : REMark no more working defn
870   IF compiled: CLOSE#1                  : REMark more wdw
880   BUTTON_WAIT chaine$                   : REMark use menu extensions
890   IF compiled
900     OPEN#1,"CON"
910     DR_PPOS def_trav,-1,-1,menu_drap%,app_drap%,control%
920   ELSE
930     DR_PULD def_trav,-1,-1,menu_drap%,app_drap%,control%
940   END IF
950 END DEFine button
960 :
970 DEFine PROCedure mk_main_app
980 REMark make main application sub window
990 LOCal t2%,temp%,sections%,att_phys%(3)
1000   DIM main_cty%(0):DIM main_mfl%(0,0)
1010   sections%=2
1020   temp%=sections% :t2%=rangees
1030   att_phys%(0)=xsize%-16                : REMark xsize calculated on main wdw x size
1040   att_phys%(1)=t2%                      : REMark y size,nominal,in rows
1050   att_phys%(2)=4                        : REMark x origin x
1060   att_phys%(3)=inf1y +appwinoffset%     : REMark y origin
1070   app_addr=fen_app2(app_addr,mdata$,temp%,1,att_phys%,"DO ")
1080   IF app_addr
1090       DIM main_cty%(sections%,2)        : REMark ctrl defn
1100       main_cty%(0,0)=1                  : REMark show 1 section
1110       main_cty%(1,2)=t2%
1120       IF sections%=2:main_cty%(2,2)=t2% : REMark but allow 2
1130       IF temp%
1140            DIM main_mfl%(temp%-1,0)     : REMark flag array
1150            set_statusses                : REMark and set all statusses
1160       ELSE
1170            POKE_W app_addr,PEEK_W(app_addr)+8
1180       END IF
1190    REMark   scroll_arrows t2%,temp%,app_addr,main_cty%
1200   END IF
1210 END DEFine mk_main_app
1220 :
1230 DEFine PROCedure set_statusses
1240 REMark set all statusses of menu appsub objects
1250 LOCal temp%,lp%,stat%
1260   temp%=DIMN(main_mfl%,1)               : REMark how many are there?
1270   stat%=main_lfl%(all_it%)              : REMark status of "all" item
1280   FOR lp%=0 TO temp%
1290     IF mdata$(lp%+1,1)='*'
1300       main_mfl%(lp%,0)=16               : REMark comments set to unavailable
1310     ELSE
1320       main_mfl%(lp%,0)=stat%            : REMark all other to status
1330     END IF
1340   END FOR lp%
1350 END DEFine set_statusses
1360 :
1370 DEFine PROCedure make_my_data (what_machine%)
1380 REMark what_machine% = 1 -> generic
1390 REMark what_machine% = 2 -> atari
1400 REMark what_machine% = 4 -> Gold Card
1410 REMark what_machine% = 8 -> Qx0
1420 REMark what_machine% = 16 -> QXL
1430 REMark what_machine% = 32 -> aurora
1440 REMark what_machine% = 64 -> java
1450 REMark what_machine% = 128 -> ptrgen
1460 REMark these may be combined
1470   DIM mdata$(150,50)
1480   REMark first make generic data
1490   data_count%=1                         : REMark general data count (global var)
1500   IF what_machine%&&1
1510     generic_data                        : REMark set up generic data
1520     read_my_datas                       : REMark and read it
1530   END IF
1540   IF what_machine%&&2
1550      atari_data                         : REMark set up machine specific data....
1560      read_my_datas                      : REMark and read it
1570   END IF
1580   IF what_machine%&&4
1590      gc_data                            : REMark set up machine specific data....
1600      read_my_datas                      : REMark and read it
1610   END IF
1620   IF what_machine%&&8
1630      q40_data                           : REMark set up machine specific data....
1640      read_my_datas                      : REMark and read it
1650   END IF
1660   IF what_machine%&&16
1670      qxl_data                           : REMark set up machine specific data....
1680      read_my_datas                      : REMark and read it
1690   END IF
1700   IF what_machine%&&32
1710      aurora_data                        : REMark set up machine specific data....
1720      read_my_datas                      : REMark and read it
1730   END IF
1740   IF what_machine%&&64
1750      java_data                          : REMark set up machine specific data....
1760      read_my_datas                      : REMark and read it
1770   END IF
1780   IF what_machine%&&128
1790      ptrgen_data                        : REMark set up machine specific data....
1800      read_my_datas                      : REMark and read it
1810   END IF
1820 END DEFine make_my_data
1830 :
1840 DEFine PROCedure read_my_datas
1850 REMark reads datas into the global var mdata$
1860 REMark uses data_count% from make_my_data
1870 LOCal lp%
1880   REPeat lp%
1890     READ mdata$(data_count%)
1900     IF mdata$(data_count%)="":EXIT lp%
1910     data_count%=data_count%+1
1920   END REPeat lp%
1930 END DEFine read_my_datas
1940 :
1950 DEFine PROCedure generic_data
1960 RESTORE 1960
1970   DATA '**** -- SMSQ Generic -- ****'
1980   DATA 'smsq_smsq_loader_link'
1990   DATA 'smsq_smsq_fh_link'
2000   DATA 'smsq_smsq_link'
2010   DATA 'smsq_smsq_cache_link'
2020   DATA 'smsq_sbas_link'
2030   DATA 'smsq_smsq_lang_link'
2040   DATA 'smsq_sbas_lang_link'
2050   DATA 'smsq_sbas_procs_link'
2060   DATA 'smsq_smsq_wman_link'
2070   DATA 'smsq_smsq_hotkey_link'
2080   DATA 'smsq_smsq_vers_link'
2090   DATA 'smsq_home_link'
2100   DATA 'smsq_smsq_1mb_link',''
2110 END DEFine generic_data
2120 :
2130 DEFine PROCedure q40_data
2140 RESTORE 2140
2150   DATA '**** -- Q40/Q60 -- ****'
2160   DATA 'smsq_q40_hwinit_link'
2170   DATA 'smsq_q40_nasty_link'
2180   DATA 'smsq_q40_cache_link'
2190   DATA 'smsq_q40_cachemode_link'
2200   DATA 'smsq_smsq_cache40c_link'
2210   DATA 'smsq_q40_driver_ser_link'
2220   DATA 'smsq_q40_driver_dv3_link'
2230   DATA 'smsq_q40_driver_ql_link'
2240   DATA 'smsq_q40_driver_16_link'
2250   DATA 'smsq_q40_kbd_lang_link'
2260   DATA 'smsq_sbas_procs_link'
2270   DATA 'smsq_q40_sysspr_link',''
2280 END DEFine q40_data
2290 :
2300 DEFine PROCedure atari_data
2310 RESTORE 2310
2320   DATA '**** -- Atari -- ****'
2330   DATA 'smsq_atari_hwinit_link'
2340   DATA 'smsq_atari_nasty_link'
2350   DATA 'smsq_atari_driver_ql_link'
2360   DATA 'smsq_atari_driver_mo_link'
2370   DATA 'smsq_atari_driver_ser_link'
2380   DATA 'smsq_atari_driver_dv3_link'
2390   DATA 'smsq_atari_kbd_lang_link'
2400   DATA 'smsq_atari_sysspr_link'
2410   DATA 'sys_boot_st_host_link',''
2420 END DEFine atari_data
2430 :
2440 DEFine PROCedure gc_data
2450 RESTORE 2450
2460   DATA '**** -- (Super)GoldCard -- ****'
2470   DATA 'smsq_gold_host_link'
2480   DATA 'smsq_gold_hwinit_link'
2490   DATA 'smsq_gold_nasty_link'
2500   DATA 'smsq_gold_nasty_s_link'
2510   DATA 'smsq_gold_driver_ql_link'
2520   DATA 'smsq_gold_driver_8_link'
2530   DATA 'smsq_gold_driver_most_link'
2540   DATA 'smsq_gold_driver_nd_link'
2550   DATA 'smsq_gold_driver_nds_link'
2560   DATA 'smsq_gold_driver_dv3_link'
2570   DATA 'smsq_gold_kbd_lang_link'
2580   DATA 'smsq_gold_kbd_abc_lng_link'
2590   DATA 'smsq_gold_kbd_abc_link'
2600   DATA 'smsq_gold_roms_link'
2610   DATA 'smsq_gold_qimi_link'
2620   DATA 'smsq_gold_sysspr_link',''
2630 END DEFine gc_data
2640 :
2650 DEFine PROCedure aurora_data
2660 RESTORE 2660
2670   DATA '**** -- Aurora -- ****'
2680   DATA 'smsq_gold_host_link'
2690   DATA 'smsq_gold_hwinit_link'
2700   DATA 'smsq_gold_nasty_link'
2710   DATA 'smsq_gold_nasty_s_link'
2720   DATA 'smsq_aurora_driver_8_link'
2730   DATA 'smsq_gold_driver_ql_link'
2740   DATA 'smsq_gold_driver_most_link'
2750   DATA 'smsq_gold_driver_nd_link'
2760   DATA 'smsq_gold_driver_nds_link'
2770   DATA 'smsq_gold_driver_dv3_link'
2780   DATA 'smsq_gold_kbd_lang_link'
2790   DATA 'smsq_gold_kbd_abc_lng_link'
2800   DATA 'smsq_gold_kbd_abc_link'
2810   DATA 'smsq_gold_roms_link'
2820   DATA 'smsq_gold_qimi_link'
2830   DATA 'smsq_aurora_sysspr_link'
2840   DATA 'smsq_gold_sysspr_link',''
2850 END DEFine aurora_data
2860 :
2870 DEFine PROCedure qxl_data
2880 RESTORE 2880
2890   DATA '**** -- QXL -- ****'
2900   DATA 'smsq_qxl_host_link'
2910   DATA 'smsq_qxl_hwinit_link'
2920   DATA 'smsq_qxl_nasty_e_link'
2930   DATA 'smsq_qxl_driver_ql_link'
2940   DATA 'smsq_qxl_driver_16_link'
2950   DATA 'smsq_qxl_driver_most_link'
2960   DATA 'smsq_qxl_driver_nd_link'
2970   DATA 'smsq_qxl_procs_link'
2980   DATA 'smsq_qxl_driver_dv3e_link'
2990   DATA 'smsq_qxl_kbd_lang_link'
3000   DATA 'smsq_qxl_ecache_link'
3010   DATA 'smsq_qxl_sysspr_link',''
3020 END DEFine qxl_data
3030 :
3040 DEFine PROCedure java_data
3050 RESTORE 3050
3060   DATA '**** -- Java -- ****'
3070   DATA 'smsq_java_host_link'
3080   DATA 'smsq_java_smsq_link'
3090   DATA 'smsq_java_smsq_1mb_link'
3100   DATA 'smsq_java_rtc_link'
3110   DATA 'smsq_java_hwinit_link'
3120   DATA 'smsq_java_driver_ql_link'
3130   DATA 'smsq_java_driver_8_link'
3140   DATA 'smsq_java_driver_16_link'
3150   DATA 'smsq_java_driver_most_link'
3160   DATA 'smsq_gold_kbd_lang_link'
3170   DATA 'smsq_gold_sysspr_link',''
3180 END DEFine java_data
3190 :
3200 DEFine PROCedure ptrgen_data
3210 RESTORE 3210
3220   DATA "**** -- PtrGen -- ****"
3230   DATA "ee_wman_link","ee_ptr_link",""
3240 END DEFine ptrgen_data
3250 :
3260 DEFine PROCedure do_the_work
3270 REMark this tries to compile all of the selected files
3280 REMark if the file was compiled OK, status (ogf the link file) set to available
3290 REMark if the file was not compiled ok, status left selected
3300 LOCal lp%,bound%,a$,my_error%,tot_err%
3310   bound%=DIMN(main_mfl%,1)              : REMark number of items to check
3320   tot_err%=0
3330   FOR lp%=0 TO bound%
3340     a$=mdata$(lp%+1)                    : REMark potential file to compile
3350     IF a$(1)<>"*" AND main_mfl%(lp%,0)
3360        a$=rep$&a$                       : REMark add dir
3370        my_error%=compile_and_check% (a$): REMark try to compile
3380        IF NOT my_error%
3390            main_mfl%(lp%,0)=1
3400            DR_ADRW main_defn,0,main_mfl%,main_cty%
3410        END IF
3420        tot_err%=tot_err%+my_error%
3430     END IF
3440   END FOR lp%
3450   main_cty%(0,1)=1
3460   main_lfl%(go_it%)=1
3470   IF main_lfl%(make_it%)=128 AND NOT tot_err%:make_targets
3480 END DEFine do_the_work
3490 :
3500 DEFine FuNction compile_and_check% (a$)
3510 REMark this tries to compile a file, using the "make" prog
3520 REMark this returns 1 if errors, else 0
3530 LOCal chan%,is_ok$,ch_len
3540   EXEP_W "make";a$&" -w -l"             : REMark exec make & wait
3550   is_ok$=a$(1 TO LEN(a$)-4)             : REMark file name w/o extension
3560   chan%=FOP_IN(is_ok$&'log')            : REMark open log file
3570   IF chan%<0:RETurn 1                   : REMark can't even open it!
3580   ch_len=FLEN(#chan%)                   : REMark file length
3590   GET#chan%\ch_len-10                   : REMark this is where error report is
3600   INPUT#chan%,is_ok$                    : REMark get it
3610   CLOSE#chan%
3620   IF NOT is_ok$=="No errors"
3630     RETurn 1                            : REMark anything other than "no errors" signals errors
3640   ELSE
3650     RETurn 0
3660   END IF
3670 END DEFine compile_and_check%
3680 :
3690 DEFine PROCedure make_targets
3700 REMark targets are only made if there was no error during compilation
3710 REMark to achieve this, a primitive approach is used:
3720 REMark a check is made whether an appsub menu item status is still selected
3730 REMark if it is, then the
3740   LOCal lp%,current_target%,tot_targets%
3750   tot_targets%=0                        : REMark no targets to make yet
3760   current_target%=0                     : REMark and no target yet
3770   FOR lp%=0 TO data_count%-2
3780     IF main_mfl%(lp%,0)=16
3790       REMark we're covering a new target
3800       tot_targets%=tot_targets%+current_target% : REMark show old target
3810       current_target%=find_target%(lp%+1) : REMark get new target we're covering
3820     ELSE
3830       IF main_mfl%(lp%,0)<>0:current_target%=0
3840     END IF
3850   END FOR lp%
3860   tot_targets%=tot_targets%+current_target%
3870   REMark at the end, here, tot_targets holds a bitmap
3880   REMark of targets for which compilation was ok
3890   IF (machines%&&1) AND NOT (tot_targets% && 1):RETurn : REMark generic was asked for but not compiled correctly!
3900   IF tot_targets%&&2:atari_target
3910   IF tot_targets%&&4:gc_target
3920   IF tot_targets%&&8:qx0_target
3930   IF tot_targets%&&16:qxl_target
3940   IF tot_targets%&&32:aurora_target
3950   IF tot_targets%&&64:java_target
3960 END DEFine make_targets
3970 :
3980 DEFine PROCedure atari_target
3990   cmd$='m'
4000   p$=rep$&'sys_boot_st_flp'
4010   f$=rep$&'sys_boot_file'
4020   ah$=rep$&'sys_boot_st_host'
4030   ld$=rep$&'smsq_smsq_loader'
4040   hi$=rep$&'smsq_atari_hwinit'
4050   os1f$=rep$&'smsq_smsq_fh_os'
4060   os1$=rep$&'smsq_smsq_os'
4070   ca$=rep$&'smsq_smsq_cache'
4080   os2$=rep$&'smsq_sbas_control'
4090   ns1$=rep$&'smsq_atari_nasty'
4100   ln1$=rep$&'smsq_smsq_lang'
4110   ln2$=rep$&'smsq_atari_kbd_lang'
4120   ln3$=rep$&'smsq_sbas_lang'
4130   ex1$=rep$&'smsq_sbas_procs_x'
4140   ex2$=rep$&'smsq_atari_driver_dv3'
4150   ex3$=rep$&'smsq_atari_driver_ser'
4160   ex4$=rep$&'smsq_atari_driver_mono'
4170   ex5$=rep$&'smsq_atari_driver_ql'
4180   ex6$=rep$&'smsq_smsq_wman'
4190   ex7$=rep$&'smsq_smsq_hotkey'
4200   ex8$=rep$&'smsq_atari_sysspr'
4210   ex9$=rep$&'smsq_home_home'
4220   nl$=rep$&'sys_boot_null'
4230   IF 'm' INSTR cmd$ = 0: ex4$ = nl$
4240   EW f$, ah$,ld$,hi$,os1f$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$, rep$&'smsq_atari_SMSQ.PRG'
4250 END DEFine atari_target
4260 :
4270 DEFine PROCedure gc_target
4280 REMark there are  2 targets for the Goldcard!
4290   f$=rep$&'sys_boot_file'
4300   v$=rep$&'smsq_smsq_vers'
4310   h$=rep$&'smsq_gold_host'
4320   ld$=rep$&'smsq_smsq_loader'
4330   hi$=rep$&'smsq_gold_hwinit'
4340   os1$=rep$&'smsq_smsq_os'
4350   ca$=rep$&'smsq_smsq_cache'
4360   os2$=rep$&'smsq_sbas_control'
4370   ns1$=rep$&'smsq_gold_nasty'
4380   ns2$=rep$&'smsq_gold_nasty_s'
4390   ln1$=rep$&'smsq_smsq_lang'
4400   ln2$=rep$&'smsq_gold_kbd_lang'
4410   ln2a$=rep$&'smsq_gold_kbd_abc_lang'
4420   ln3$=rep$&'smsq_sbas_lang'
4430   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
4440   ex2a$=rep$&'smsq_gold_kbd_abc'
4450   ex2$=rep$&'smsq_gold_driver_most'
4460   ex3a$=rep$&'smsq_gold_driver_ql'
4470   ex38$=rep$&'smsq_gold_driver_8'
4480   ex3q$=rep$&'smsq_gold_qimi'
4490   ex3w$=rep$&'smsq_smsq_wman'
4500   ex4$=rep$&'smsq_gold_driver_dv3'
4510   ex5$=rep$&'smsq_gold_driver_nd'
4520   ex6$=rep$&'smsq_gold_driver_nds'
4530   ex7$=rep$&'smsq_smsq_hotkey'
4540   ex8$=rep$&'smsq_gold_sysspr'
4550   ex9$=rep$&'smsq_home_home'
4560   nl$=rep$&'sys_boot_null'
4570   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex3a$,ex3q$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,ex1$,ex9$, rep$&'SMSQ_GOLD_gold'
4580   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex38$,ex3q$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,ex1$,ex9$, rep$&'SMSQ_GOLD_gold8'
4590 END DEFine gc_target
4600 :
4610 DEFine PROCedure aurora_target
4620   f$='dev8_sys_boot_file'
4630   v$='dev8_smsq_smsq_vers'
4640   h$='dev8_smsq_gold_host'
4650   ld$='dev8_smsq_smsq_loader'
4660   hi$='dev8_smsq_gold_hwinit'
4670   os1$='dev8_smsq_smsq_os'
4680   ca$='dev8_smsq_smsq_cache'
4690   os2$='dev8_smsq_sbas_control'
4700   ns1$='dev8_smsq_gold_nasty'
4710   ns2$='dev8_smsq_gold_nasty_s'
4720   ln1$='dev8_smsq_smsq_lang'
4730   ln2$='dev8_smsq_gold_kbd_lang'
4740   ln2a$='dev8_smsq_gold_kbd_abc_lang'
4750   ln3$='dev8_smsq_sbas_lang'
4760   ex1$='dev8_smsq_sbas_procs_x'   : REMark this must be after "roms"
4770   ex2a$='dev8_smsq_gold_kbd_abc'
4780   ex2$='dev8_smsq_gold_driver_most'
4790   ex3a$='dev8_smsq_gold_driver_ql'
4800   ex3b$='dev8_smsq_aurora_driver_8'
4810   ex3q$='dev8_smsq_gold_qimi'
4820   ex3w$='dev8_smsq_smsq_wman'
4830   ex3sq$='dev8_smsq_aurora_sysspr'
4840   ex3sh$='dev8_smsq_gold_sysspr'
4850   ex4$='dev8_smsq_gold_driver_dv3'
4860   ex5$='dev8_smsq_gold_driver_nd'
4870   ex6$='dev8_smsq_gold_driver_nds'
4880   ex8$="dev8_smsq_home_home"
4890   ex9$='dev8_smsq_smsq_hotkey'
4900   ex10$='dev8_smsq_gold_roms'
4910   nl$='dev8_sys_boot_null'
4920   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex3a$,ex3b$,ex3q$,ex3w$,ex3sh$,ex4$,ex5$,ex6$,ex9$,ex10$,ex8$,ex1$, 'dev8_SMSQ_Aurora_SMSQE'
4930 END DEFine aurora_target
4940 :
4950 DEFine PROCedure qx0_target
4960   nl$=rep$&'sys_boot_null'
4970   f$=rep$&'sys_boot_file'
4980   qr$=rep$&'sys_boot_q40_rom'
4990   ld$=rep$&'smsq_smsq_loader'
5000   hi$=rep$&'smsq_q40_hwinit'
5010   os1$=rep$&'smsq_smsq_1mb_os' : REMark change for fast memory
5020   ca$=rep$&'smsq_smsq_cache40c'
5030   caq$=rep$&'smsq_q40_cache'
5040   caq4$=rep$&'smsq_q40_cachemode'
5050   os2$=rep$&'smsq_sbas_control'
5060   ns2$=rep$&'smsq_q40_nasty'
5070   ln1$=rep$&'smsq_smsq_lang'
5080   ln2$=rep$&'smsq_q40_kbd_lang'
5090   ln3$=rep$&'smsq_sbas_lang'
5100   ex1$=rep$&'smsq_sbas_procs_x'
5110   ex2$=rep$&'smsq_q40_driver_dv3'
5120   ex3$=rep$&'smsq_q40_driver_ser'
5130   ex4a$=rep$&'smsq_q40_driver_ql'
5140   ex4b$=rep$&'smsq_q40_driver_16'
5150   ex4w$=rep$&'smsq_smsq_wman'
5160   ex5$=rep$&'smsq_q40_sysspr'
5170   ex9$=rep$&'smsq_smsq_hotkey'
5180   ex10$=rep$&'smsq_home_home'
5190   EW f$, qr$,ld$,hi$,os1$,ca$,caq$,os2$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4a$,ex4b$,ex4w$,ex5$,ex9$,ex10$,ns2$,caq4$, rep$&'smsq_q40_rom'
5200 END DEFine qx0_target
5210 :
5220 DEFine PROCedure qxl_target
5230   IF FOP_IN (rep$&'smsq_qxl_qxlh.exe') <> -7: INPUT 'QXLH.EXE exists'; cmd$: QUIT
5240   p$   = rep$&'sys_boot_file'
5250   bl$  = rep$&'smsq_qxl_qxl2_exe'
5260   hst$ = rep$&'smsq_qxl_host'
5270   hi$  = rep$&'smsq_qxl_hwinit'
5280   ld$  = rep$&'smsq_smsq_loader'
5290   os1$ = rep$&'smsq_smsq_os'
5300   ca$  = rep$&'smsq_smsq_cache'
5310   os2$ = rep$&'smsq_sbas_control'
5320   ns1$ = rep$&'smsq_qxl_nasty_e'
5330   ln1$ = rep$&'smsq_smsq_lang'
5340   ln2$ = rep$&'smsq_qxl_kbd_lang'
5350   ln3$ = rep$&'smsq_sbas_lang'
5360   ex1$ = rep$&'smsq_qxl_procs_x'
5370   ex2$ = rep$&'smsq_qxl_driver_most'  : REMark Keyboard before CON
5380   ex3$ = rep$&'smsq_qxl_driver_ql'
5390   ex3b$ = rep$&'smsq_qxl_driver_16'
5400   ex3w$ = rep$&'smsq_smsq_wman'
5410   ex4$ = rep$&'smsq_qxl_driver_nd'
5420   ex5$ = rep$&'smsq_qxl_driver_dv3e'
5430   ex6$= rep$&'smsq_smsq_hotkey'
5440   ex7$ = rep$&'smsq_qxl_sysspr'
5450   ex8$=rep$&'smsq_home_home'
5460   last$ = rep$&'smsq_qxl_ecache'
5470   dml$ = rep$&'smsq_qxl_dummy'
5480   qm$  = rep$&'qmon_qxl_bin'
5490   r$='ram1_smsqe.exe' : f$=rep$&'smsq_qxl_smsqe.exe'
5500   DELETE f$
5510   EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,last$, r$
5520   EW cct, bl$,r$, f$
5530   DELETE r$
5540   OPEN #4,f$
5550   lenf=FLEN(#4)
5560   lens% = INT ((lenf+511)/512)
5570   lenb% = lenf - lens% * 512
5580   IF lenb%: lenb% = lenb% + 512
5590   BPUT #4\2, lenb% MOD 256, lenb% DIV 256, lens% MOD 256, lens% DIV 256
5600   CLOSE #4
5610 END DEFine qxl_target
5620 :
5630 DEFine PROCedure java_target
5640   f$=rep$&'sys_boot_file'
5650   v$=rep$&'smsq_smsq_vers'
5660   h$=rep$&'smsq_java_host'
5670   rtc$=rep$&'smsq_java_rtc'
5680   ld$=rep$&'smsq_smsq_loader'
5690   hi$=rep$&'smsq_java_hwinit'
5700   os1$=rep$&'smsq_java_smsq_os'
5710   os3$=rep$&'smsq_java_smsq_1mb_os' : REMark must be after os
5720   ca$=rep$&'smsq_smsq_cache'
5730   os2$=rep$&'smsq_sbas_control'
5740   ln1$=rep$&'smsq_smsq_lang'
5750   ln2$=rep$&'smsq_gold_kbd_lang'
5760   ln3$=rep$&'smsq_sbas_lang'
5770   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
5780   ex2$=rep$&'smsq_java_driver_most'
5790   ex3a$=rep$&'smsq_java_driver_ql'
5800   ex3b$=rep$&'smsq_java_driver_8'
5810   ex3c$=rep$&'smsq_java_driver_16'
5820   ex3w$=rep$&'smsq_smsq_wman'
5830   ex7$=rep$&'smsq_smsq_hotkey'
5840   ex8$=rep$&'smsq_gold_sysspr'
5850   ex9$=rep$&'smsq_home_home'
5860   nl$=rep$&'sys_boot_null'
5870   EW f$, v$,h$,ld$,hi$,os1$,os3$,ca$,os2$,rtc$,ln1$,ln2$,ln3$,ex2$,ex3a$,ex3b$,ex3c$,ex3w$,ex1$,ex7$,ex8$,ex9$, rep$&'SMSQ_java_java'
5880 END DEFine java_target
5890 :
5900 DEFine FuNction find_target% (current%)
5910   IF "SMSQ Generic" INSTR mdata$(current%):RETurn 1
5920   IF "Atari" INSTR mdata$(current%):RETurn 2
5930   IF "GoldCard" INSTR mdata$(current%):RETurn 4
5940   IF "Q40" INSTR mdata$(current%):RETurn 8
5950   IF "QXL" INSTR mdata$(current%):RETurn 16
5960   IF "Aurora" INSTR mdata$(current%):RETurn 32
5970   IF "Java" INSTR mdata$(current%):RETurn 64
5980   RETurn 0                             : REMark huh???
5990 END DEFine find_target%
6000 :
6010 DEFine PROCedure change_size (x%,y%)
6020 LOCal xrel%,yrel%,temp,a$,ttemp%(main_lit%),abs%,yabs%,t%,bt$
6030   main_lfl%(size_it%)=1                 : REMark reset to available
6040   ARRCOPY main_lfl%,ttemp%              : REMark keep statusses
6050   CH_WIN main_defn,xrel%,yrel%          : REMark ptr displacement
6060   xsize%=xsize%-xrel%+3
6070   ysize%=ysize%-yrel%                   : REMark new sizes
6080   t%=2^4+2^5
6090   RPTR xabs%,yabs%,t%,t%,xrel%,yrel%,bt$: REMark get absolute coords
6100   IF xsize%>xsz%-16:xsize%=xsz%-16
6110   IF ysize%>ysz%-16:ysize%=ysz%-16      : REMark max values
6120   DR_UNST main_defn                     : REMark no more wdw defn...
6130   DR_REMV main_defn                     : REMark .... now!
6140   app_addr=0                            : REMark nor any appsub wdw
6150   make_main_wdw                         : REMark make window anew
6160   ARRCOPY ttemp%,main_lfl%
6170   t%=lxs*3
6180   xabs%=xabs%-t%:IF xabs%<0:xabs%=0     : REMark position ptr nicely
6190   yabs%=yabs%-8:IF yabs%<0:yabs%=0
6200   SPTR xabs%,yabs%,0                    : REMark now
6210   IF compiled
6220      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
6230   ELSE
6240      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
6250   END IF
6260    POS_PTR main_defn,t%*65536+8,1       : REMark position pointer in wdw
6270 END DEFine change_size
6280 :
6290 REMark as of here, standard functions/procs (except init)
6300 :
6310 DEFine FuNction warning_pos% (mess$,channel%,title$,pos)
6320 REMark warning wdw with positioning
6330   POS_PTR main_defn,pos,-1
6340   RETurn warning%(mess$,channel%,title$)
6350 END DEFine warning_pos%
6360 :
6370 DEFine FuNction warning%(mess$,channel%,title$)
6380 REMark shows a warning message (mess$) in a small window. Hit ESC or OK
6390 REMark to return; returns 0 if ESC, 1 if OK  hit
6400 LOCal item%,lp%,chan%,y%,swnum%
6410   IF channel%
6420     chan%=channel%
6430   ELSE
6440     chan%=FOPEN("con")
6450   END IF
6460   CH_ITEM warn_defn,-3,0,-1,'','Attention'
6470   CH_ITEM warn_defn,-1,0,-1,cancel$,'ESC'
6480   CH_ITEM warn_defn,-1,1,-1,'O','OK'
6490   warn_lfl%(0)=1:warn_lfl%(1)=1
6500   POKE_W warn_defn+46,pink                        : REMark paper colour of wdw
6510   POKE_W warn_defn+44,red                         : REMark border around wdw
6520   DR_PULD warn_defn,-1,-1
6530   DR_IWDF #chan%,warn_defn,0
6540   PAPER#chan%,white :INK#chan%,red
6550   IF title$<>"":PRINT#chan%,title$;" :"
6560   PRINT #chan%,mess$
6570   POS_PTR warn_defn,160*65536+10,-1
6580   REPeat lp%
6590     RD_PTR warn_defn,item%,swnum%,y%,y%,y%,warn_lfl%
6600     IF swnum%=-1:EXIT lp%
6610   END REPeat lp%
6620   IF NOT channel%:CLOSE#chan%
6630   DR_UNST warn_defn
6640   RETurn item%
6650 END DEFine warning%
6660 :
6670 DEFine PROCedure info_pos (mess$,channel%,pos)
6680 REMark warning wdw with positioning
6690   POS_PTR main_defn,pos,-1
6700   info mess$,channel%
6710 END DEFine info_pos
6720 :
6730 DEFine PROCedure info(mess$,channel%)
6740 REMark shows window containing mess$
6750   LOCal chan%
6760   IF channel%
6770     chan%=channel%
6780   ELSE
6790     chan%=FOPEN("con")
6800   END IF
6810   CH_ITEM warn_defn,-3,0,-1,"",'  Info.  '
6820   CH_ITEM warn_defn,-1,0,-1,"",'Info'
6830   CH_ITEM warn_defn,-1,1,-1,"",'Info'
6840   POKE_W warn_defn+44,green                       : REMark border
6850   POKE_W warn_defn+46,light_green                  : REMark paper col
6860   DR_PULD warn_defn,-1,-1
6870   POS_PTR warn_defn,90*65536+70,-1
6880   DR_IWDF #chan%,warn_defn,0
6890   PAPER#chan%,white:INK#chan%,black
6900   PRINT #chan%,mess$
6910   IF NOT channel%:CLOSE#chan%
6920 END DEFine info
6930 :
6940 DEFine PROCedure make_colours (colourway%)
6950 REMark this initialises the GLOBAL variables menu_pap, menu_brd, menu_inv,
6960 REMark menu_ink and menu_bar according to the colourway passed as parameter.
6970 REMark The GLOBAl variable mycolour_mode% must have been set prior to calling
6980 REMark this function, preferrably via the function set_colours%.
6990 REMark Moreover the colours black, white,red, green,wg_stripes, etc...
7000 REMark must have been set prior to calling this procedure:
7010 REMark Preferrably, they were also set via the function set_colours%
7020 :
7030 REMark this function needs the extension WL_MK16 (outptr_bin)
7040 :
7050 REMark the colourways% are:
7060 REMark 0= white/green
7070 REMark 1= black/red
7080 REMark 2= white/red
7090 REMark 3= black/green
7100 REMark 4= use system palette (only available in modes 32 and 33)
7110 :
7120 REMark the Global war mycolour_mode% is:
7130 REMark 0 = we are in QL mode 4
7140 REMark 1 = we are in PAL mode (256 colours)
7150 REMark 2 - we are in 24 bit colour mode
7160 REMark preferrably, this is set via the function set_colours%
7170 :
7180   IF colourway%>7 OR colourway%<0: colourway%=0: REMark must be between 0 & 7
7190 :
7200 REMark first make sure that what we request is possible.
7210 REMark colourway 4 is only possible if we are in 24 bit colour mode
7220 REMark if it is requested in another mode, it is set to 0
7230 :
7240   IF colourway%>3
7250     IF mycolour_mode%< 1
7260        colourway%=0
7270      ELSE
7280        make_my_palette colourway%-4:RETurn   : REMark set colours according to system palette
7290     END IF
7300   END IF
7310 :
7320   SELect ON mycolour_mode%
7330     =0                                  : REMark QL 4 colours
7340       SELect ON colourway%
7350           =0:                           : REMark white/green
7360                 menu_pap=white
7370                 menu_brd=green
7380                 menu_ink=black
7390                 menu_bar=wg_striped
7400                 menu_inv=red
7410           =1:                           : REMark black/red
7420                 menu_pap=black
7430                 menu_brd=red
7440                 menu_ink=white
7450                 menu_bar=br_striped
7460                 menu_inv=green
7470           =2:                           : REMark white/red
7480                 menu_pap=white
7490                 menu_brd=red
7500                 menu_ink=black
7510                 menu_bar=wr_striped
7520                 menu_inv=green
7530           =3:                           : REMark black/green
7540                 menu_pap=black
7550                 menu_brd=green
7560                 menu_ink=white
7570                 menu_bar=bg_striped
7580                 menu_inv=red
7590       END SELect
7600     =1                                  : REMark pal colours
7610       SELect ON colourway%
7620           =0:                           : REMark white/green
7630                 menu_pap=white+256
7640                 menu_brd=green+256
7650                 menu_ink=black+256
7660                 menu_bar=wg_striped+256
7670                 menu_inv=red +256
7680           =1:                           : REMark black/red
7690                 menu_pap=black+256
7700                 menu_brd=red  +256
7710                 menu_ink=white +256
7720                 menu_bar=br_striped +256
7730                 menu_inv=green +256
7740                 omenu_pap=black+256
7750           =2:                           : REMark white/red
7760                 menu_pap=white +256
7770                 menu_brd=red   +256
7780                 menu_ink=black +256
7790                 menu_bar=wr_striped +256
7800                 menu_inv=green +256
7810           =3:                           : REMark black/green
7820                 menu_pap=black +256
7830                 menu_brd=green +256
7840                 menu_ink=white +256
7850                 menu_bar=bg_striped +256
7860                 menu_inv=red   +256
7870       END SELect
7880     =2                                  : REMark 24 bit colours
7890       SELect ON colourway%
7900           =0:                           : REMark white/green
7910                 menu_pap=WL_MK16(white)
7920                 menu_brd=WL_MK16(green)
7930                 menu_ink=WL_MK16(black)
7940                 menu_bar=WL_MK16(wg_striped)
7950                 menu_inv=WL_MK16(red)
7960           =1:                           : REMark black/red
7970                 menu_pap=WL_MK16(black)
7980                 menu_brd=WL_MK16(red)
7990                 menu_ink=WL_MK16(white)
8000                 menu_bar=WL_MK16(br_striped)
8010                 menu_inv=WL_MK16(green)
8020           =2:                           : REMark white/red
8030                 menu_pap=WL_MK16(white)
8040                 menu_brd=WL_MK16(red)
8050                 menu_ink=WL_MK16(black)
8060                 menu_bar=WL_MK16(wr_striped)
8070                 menu_inv=WL_MK16(green)
8080           =3:                           : REMark black/green
8090                 menu_pap=WL_MK16(black)
8100                 menu_brd=WL_MK16(green)
8110                 menu_ink=WL_MK16(white)
8120                 menu_bar=WL_MK16(bg_striped)
8130                 menu_inv=WL_MK16(red)
8140       END SELect
8150   END SELect
8160 :
8170   make_my_old_palette
8180 END DEFine make_colours
8190 :
8200 DEFine PROCedure make_my_palette (cw%)
8210   sp.winbd            = HEX('0200') : REMark Window border
8220   sp.winbg            = HEX('0201') : REMark Window background
8230   sp.winfg            = HEX('0202') : REMark Window foreground
8240   sp.winmg            = HEX('0203') : REMark Window middleground
8250   sp.titlebg          = HEX('0204') : REMark Title background
8260   sp.titletextbg      = HEX('0205') : REMark Title text background
8270   sp.titlefg          = HEX('0206') : REMark Title foreground
8280   sp.litemhigh        = HEX('0207') : REMark Loose item highlight
8290   sp.litemavabg       = HEX('0208') : REMark Loose item available background
8300   sp.litemavafg       = HEX('0209') : REMark Loose item available foreground
8310   sp.litemselbg       = HEX('020a') : REMark Loose item selected background
8320   sp.litemselfg       = HEX('020b') : REMark Loose item  elected foreground
8330   sp.litemunabg       = HEX('020c') : REMark Loose item unavailable background
8340   sp.litemunafg       = HEX('020d') : REMark Loose item unavailable foreground
8350   sp.infwinbd         = HEX('020e') : REMark Information window border
8360   sp.infwinbg         = HEX('020f') : REMark Information window background
8370   sp.infwinfg         = HEX('0210') : REMark Information window foreground
8380   sp.infwinmg         = HEX('0211') : REMark Information window middleground
8390   sp.subinfbd         = HEX('0212') : REMark Subsidiary information window border
8400   sp.subinfbg         = HEX('0213') : REMark Subsidiary information window background
8410   sp.subinffg         = HEX('0214') : REMark Subsidiary information window foreground
8420   sp.subinfmg         = HEX('0215') : REMark Subsidiary information window middleground
8430   sp.appbd            = HEX('0216') : REMark Application window border
8440   sp.appbg            = HEX('0217') : REMark Application window background
8450   sp.appfg            = HEX('0218') : REMark Application window foreground
8460   sp.appmg            = HEX('0219') : REMark Application window middleground
8470   sp.appihigh         = HEX('021a') : REMark Application window item highlight
8480   sp.appiavabg        = HEX('021b') : REMark Application window item available background
8490   sp.appiavafg        = HEX('021c') : REMark Application window item available foreground
8500   sp.appiselbg        = HEX('021d') : REMark Application window item selected background
8510   sp.appiselfg        = HEX('021e') : REMark Application window item selected foreground
8520   sp.appiunabg        = HEX('021f') : REMark Application window item unavailable background
8530   sp.appiunafg        = HEX('0220') : REMark Application window item unavailable foreground
8540   sp.scrbar           = HEX('0221') : REMark Pan/scroll bar
8550   sp.scrbarsec        = HEX('0222') : REMark Pan/scroll bar section
8560   sp.scrbararr        = HEX('0223') : REMark Pan/scroll bar arrow
8570   sp.buthigh          = HEX('0224') : REMark Button highlight
8580   sp.butbd            = HEX('0225') : REMark Button border
8590   sp.butbg            = HEX('0226') : REMark Button background
8600   sp.butfg            = HEX('0227') : REMark Button foreground
8610   sp.hintbd           = HEX('0228') : REMark Hint border
8620   sp.hintbg           = HEX('0229') : REMark Hint background
8630   sp.hintfg           = HEX('022a') : REMark Hint foreground
8640   sp.hintmg           = HEX('022b') : REMark Hint middleground
8650   sp.errbg            = HEX('022c') : REMark Error message background
8660   sp.errfg            = HEX('022d') : REMark Error message foreground
8670   sp.errmg            = HEX('022e') : REMark Error message middleground
8680   sp.shaded           = HEX('022f') : REMark sp.shaded area
8690   sp.3ddark           = HEX('0230') : REMark Dark 3D border shade
8700   sp.3dlight          = HEX('0231') : REMark Light 3D border shade
8710   sp.vertfill         = HEX('0232') : REMark Vertical area fill
8720   IF NOT (IS_OPEN(#0) OR IS_OPEN(#1))
8730     OPEN#1,'con'
8740     SP_JOBPAL -1,cw%
8750     CLOSE#1
8760   ELSE
8770     SP_JOBPAL -1,cw%
8780   END IF
8790 END DEFine make_my_palette
8800 :
8810 DEFine PROCedure make_my_old_palette
8820 REMark this translates the colours used in the wdws into 4 colour mode...
8830   sp.winbd          = menu_brd          : REMark Window border
8840   sp.winbg          = menu_pap          : REMark Window background
8850   sp.winfg          = menu_pap          : REMark Window foreground
8860   sp.titletextbg    = menu_pap          : REMark Title text background
8870   sp.titlefg        = menu_ink          : REMark Title foreground
8880   sp.titlebg        = menu_bar          : REMark Title background
8890   sp.litemhigh      = menu_ink          : REMark Loose item highlight
8900   sp.litemavabg     = menu_pap          : REMark Loose item available background
8910   sp.litemavafg     = menu_ink          : REMark Loex fifiose item available foreground
8920   sp.litemselbg     = menu_brd          : REMark Loose item selected background
8930   sp.litemselfg     = menu_ink          : REMark Loose item selected foreground
8940   sp.litemunabg     = menu_pap          : REMark Loose item unavailable background
8950   sp.litemunafg     = menu_brd          : REMark Loose item unavailable foreground
8960   sp.infwinbd       = menu_brd          : REMark Information window border
8970   sp.infwinbg       = menu_pap          : REMark Information window background
8980   sp.infwinfg       = menu_ink          : REMark Information window foreground
8990   sp.appbd          = menu_brd          : REMark Application window border
9000   sp.appbg          = menu_pap          : REMark Application window background
9010   sp.appfg          = menu_ink          : REMark Application window foreground
9020   sp.appmg          = menu_pap          : REMark Application window middleground
9030   sp.appihigh       = menu_ink          : REMark Application window item highlight
9040   sp.appiavabg      = menu_pap          : REMark Application window item available background
9050   sp.appiavafg      = menu_ink          : REMark Application window item available foreground
9060   sp.appiselbg      = menu_brd          : REMark Application window item selected background
9070   sp.appiselfg      = menu_ink          : REMark Application window item selected foreground
9080   sp.appiunabg      = menu_pap          : REMark Application window item unavailable background
9090   sp.appiunafg      = menu_brd          : REMark Application window item unavailable foreground
9100   sp.scrbar         = menu_ink          : REMark Pan/scroll bar
9110   sp.scrbararr      = menu_brd          : REMark Pan/scroll bar arrow
9120   sp.buthigh        = menu_ink          : REMark Button highlight
9130   sp.butbd          = menu_brd          : REMark Button border
9140   sp.butbg          = menu_pap          : REMark Button background
9150   sp.butfg          = menu_ink          : REMark Button foreground
9160 END DEFine mk_my_old_palette
9170 :
9180 DEFine FuNction getstr$(poste%,drap%,defn,a$)
9190 REMark ouvre une fen‘tre sur un poste de menu dƒliƒ et propose
9200 REMark une cha•ne  ƒditer
9210 REMark les paramtres sont
9220 REMark - le numƒro du poste
9230 REMark - un drapeau si le rƒsultat doit ‘tre un chiffre (si drap%<>0)
9240 REMark - la dƒfinition de travail dans laquelle se trouve le poste
9250 REMark - la cha•ne qu'on peut ƒditer
9260 REMark returns the string or ""
9270 REMark uses the global (!) vars menu_pap% & menu_brd%
9280 REMark
9290   LOCal chan%,b$,res
9300   b$=" ":b$=a$   : REMark c'est curieusement nƒcessaire
9310   chan%=FOPEN("con")
9320   PAPER#chan%,menu_brd
9330   INK#chan%,menu_ink
9340   DR_LWDF#chan%,defn,poste%             : REMark open channel over item
9350   CLS#chan%
9360   res=GET_STR(#chan%,drap%,b$)          : REMark get string
9370   CLOSE#chan%
9380   IF res<0:b$=""                        : REMark ooops
9390   CH_ITEM defn,-1,poste%,-1,"",b$
9400   RETurn b$
9410 END DEFine getstr$
9420 :
9430 DEFine FuNction showcont$(string$,length%)
9440 REMark this adds '...' at the end of a string, to show that it continues
9450 LOCal l%,lengt%
9460   l%=LEN(string$)
9470   IF NOT l%:RETurn ""                   : REMark empty string
9480   lengt%=length% DIV 6
9490   IF l%<=lengt%:RETurn string$          : REMark entire string fits wdw
9500   RETurn string$(1 TO lengt%-3)&"..."
9510 END DEFine showcont$
9520 :
9530 DEFine FuNction get_free_text$(defn,item%,prompt$,length%)
9540 REMark this opens wdw over loose-item item% of the working definition defn
9550 REMark allows free text to be entered
9560 REMark params: working defintion,item over which wdw is to be opened
9570 REMark         first value of string, length of max string in wdw, if this is...
9580 REMark            -1, the length will be determined here
9590 LOCal chan%,string$,tempsizes%(3)
9600   chan%=FOPEN('con')
9610   IF chan%<0:RETurn prompt$
9620   DR_LWDF#chan%,defn,item%              : REMark open wdw over item
9630   PAPER#chan%,menu_brd
9640   INK#chan%,menu_ink
9650   CLS#chan%
9660   string$=WEDIT$(#chan%,prompt$)
9670   IF length%=-1
9680      WSIZE#chan%,tempsizes%
9690      length%=tempsizes%(0)
9700   END IF
9710   CLOSE#chan%
9720   IF length%
9730     CH_ITEM defn,-1,item%,-1,"",showcont$(string$,length%)
9740   ELSE
9750     CH_ITEM defn,-1,item%,-1,"",string$
9760   END IF
9770   RETurn string$
9780 END DEFine get_free_text$
9790 :
9800 DEFine FuNction do_caps(a$,caps%)
9810 REMark set string to capitales etc,caps% determines whether the string....
9820 REMark ...must be lower case (=0), upper case(=1), all words capitalized (=2),
9830 REMark ... first word only capitalized (=3), or nothing(=-1)
9840   SELect ON caps%
9850    =0: MIN a$
9860    =1: MAJ a$
9870    =2: CAP2 a$
9880    =3: CAP a$
9890   END SELect
9900   RETurn a$
9910 END DEFine do_caps
9920 :
9930 DEFine FuNction centr$(ctr$,leng)
9940 REMark this "centres" the string ctr$ (i.e. left padds it with spaces) so that
9950 REMark it is centred in a window of leng width (leng is length in chars)
9960   LOCal l%,a$
9970   a$=ctr$
9980   l%=INT(leng-LEN(a$))/2
9990   IF l%<0:RETurn a$
10000   a$=FILL$(' ',l%)&a$&FILL$(' ',l%)
10010   RETurn a$
10020 END DEFine centr$
10030 :
10040 DEFine FuNction set_colours% (force_mode%)
10050 REMark this sets colour variables according to the colour mode wished, if possible.
10060 REMark force_mode%=
10070 REMark     0 if ql colours wished (mode 4 only)
10080 REMark     1 if pal colours wished
10090 REMark     2 if 24 bit colours wished
10100 REMark This function returns the true QL mode set (4,8,32,33 etc.).
10110 REMark This function also sets the GLOBAL variable mycolour_mode% to the
10120 REMark mode 0,1,2 as above - whatever is possible on the machine, i.e.
10130 REMark if colour_24 is wished on a 4 colour machine (which won't succeed)
10140 REMark this function will set set that variable to 0, i.e. QL mode.
10150 REMark
10160 REMark This function needs the outptr toolkit
10170 LOCal mode%
10180   mode%=RMODE                           : REMark get current mode now
10190   SELect ON force_mode%
10200       = 0                               : REMark mode 4 wished
10210        SELect ON mode%
10220           = 4                           : REMark already on, so do nothing
10230           = 0,1,8 : MODE 4              : REMark old modes, set to 4 colours
10240           = REMAINDER
10250             COLOUR_QL                   : REMark if higher colour mode, set to "mode 4"
10260             MODE 4
10270        END SELect
10280        mode%=0                          : REMark new mode we're in  - 4 colour mode
10290       = 1                               : REMark PALette wished
10300        SELect ON mode%
10310           = 16,32,33 : COLOUR_PAL:mode%=1: REMark we're in high colour mode, but PAL colours wished
10320           = REMAINDER
10330             IF mode%<>4:MODE 4
10340             mode%=0
10350        END SELect
10360       = 2
10370        SELect ON mode%
10380           = 32,33 : COLOUR_24 :mode%=2  : REMark we're in high col mode 24 bit colours
10390           = 16 : COLOUR_PAL : mode%=1
10400           = REMAINDER
10410             IF mode%<>4:MODE 4
10420             mode%=0
10430        END SELect
10440   END SELect
10450   mycolour_mode%=mode%                  : REMark set this global variable now:
10460   REMark now set some colour variables
10470   SELect ON mode%
10480        = 0                              : REMark Ql 4 mode colours
10490         black          = 0
10500         white          = 7
10510         red            = 2
10520         green          = 4
10530         grey           = 255
10540         dull_pink      = 19
10550         Blue           = 0
10560         Magenta        = 2
10570         Yellow         = 7
10580         Cyan           = 0
10590         Dark_slate     = 0
10600         Slate_grey     = 255
10610         Dark_grey      = 255
10620         Light_grey     = 255
10630         Ash_grey       = 255
10640         Dark_red       = 210
10650         light_green    = 220
10660         Mustard        = 242
10670         Dark_green     = 228
10680         Sea_blue       = 0
10690         Dark_blue      = 0
10700         Purple         = 0
10710         Shocking_pink  = 234
10720         Orange         = 234
10730         Lime_green     = 4
10740         Apple_green    = 4
10750         Bright_blue    = 0
10760         Mauve          = 0
10770         Peach          = 7
10780         Light_yellow   = 7
10790         Light_blue     = 0
10800         Sky_blue       = 0
10810         Rose_pink      = 234
10820         pink           = 234
10830         Beige          = 7
10840         Pastel_pink    = 234
10850         Pastel_yellow  = 7
10860         Pastel_green   = 4
10870         Pastel_cyan    = 0
10880         Pastel_blue    = 0
10890         Pastel_rose    = 234
10900         Brick          = 210
10910         Light_khaki    = 7
10920         Dull_green     = 228
10930         Dull_cyan      = 0
10940         Steel_blue     = 0
10950         dull_pink      = 234
10960         Brown          = 0
10970         Khaki          = 7
10980         Dusky_green    = 4
10990         Dusky_blue     = 0
11000         Midnight_blue  = 0
11010         Plum           = 0
11020         Dusky_pink     = 234
11030         Buff           = 0
11040         Avocado        = 4
11050         Dull_turquoise = 4
11060         Dull_blue      = 0
11070         Faded_purple   = 234
11080         Cerise         = 2
11090         Tan_normal     = 0
11100         Grass_green    = 4
11110         Sea_green      = 4
11120         Ultramarine    = 0
11130         Deep_purple    = 234
11140         Light_orange   = 7
11150         light_red      = 2
11160         wg_striped     = 92
11170         br_striped     = 82
11180         wr_striped     = 107
11190         bg_striped     = 100
11200        = 1                      : REMark Colour_pal
11210         black          = 0
11220         white          = 1
11230         red            = 2
11240         green          = 3
11250         Blue           = 4
11260         Magenta        = 5
11270         Yellow         = 6
11280         Cyan           = 7
11290         Dark_slate     = 8
11300         Slate_grey     = 9
11310         Dark_grey      = 10
11320         grey           = 11
11330         Light_grey     = 12
11340         Ash_grey       = 13
11350         Dark_red       = 14
11360         light_green    = 15
11370         Mustard        = 16
11380         Dark_green     = 17
11390         Sea_blue       = 18
11400         Dark_blue      = 19
11410         Purple         = 20
11420         Shocking_pink  = 21
11430         Orange         = 22
11440         Lime_green     = 23
11450         Apple_green    = 24
11460         Bright_blue    = 25
11470         Mauve          = 26
11480         Peach          = 27
11490         Light_yellow   = 28
11500         Light_blue     = 29
11510         Sky_blue       = 30
11520         Rose_pink      = 31
11530         pink           = 32
11540         Beige          = 33
11550         Pastel_pink    = 34
11560         Pastel_yellow  = 35
11570         Pastel_green   = 36
11580         Pastel_cyan    = 37
11590         Pastel_blue    = 38
11600         Pastel_rose    = 39
11610         Brick          = 40
11620         Light_khaki    = 41
11630         Dull_green     = 42
11640         Dull_cyan      = 43
11650         Steel_blue     = 44
11660         dull_pink      = 45
11670         Brown          = 46
11680         Khaki          = 47
11690         Dusky_green    = 48
11700         Dusky_blue     = 49
11710         Midnight_blue  = 50
11720         Plum           = 51
11730         Dusky_pink     = 52
11740         Buff           = 53
11750         Avocado        = 54
11760         Dull_turquoise = 55
11770         Dull_blue      = 56
11780         Faded_purple   = 57
11790         Cerise         = 58
11800         Tan_normal     = 59
11810         Grass_green    = 60
11820         Sea_green      = 61
11830         Ultramarine    = 62
11840         Deep_purple    = 63
11850         pinkish        = 77
11860         light_red      = 109
11870         Light_orange   = 208
11880         wg_striped     = light_green
11890         br_striped     = Dark_red
11900         wr_striped     = light_red
11910         bg_striped     = Dark_green
11920        = 2                      : REMark colour_24
11930         black          = HEX('000000')
11940         white          = HEX('FFFFFF')
11950         red            = HEX('FF0000')
11960         green          = HEX('00FF00')
11970         Blue           = HEX('0000FF')
11980         Magenta        = HEX('FF00FF')
11990         Yellow         = HEX('FFFF00')
12000         Cyan           = HEX('00FFFF')
12010         Dark_slate     = HEX('242424')
12020         Slate_grey     = HEX('494949')
12030         Dark_grey      = HEX('6D6D6D')
12040         grey           = HEX('929292')
12050         Light_grey     = HEX('B6B6B6')
12060         Ash_grey       = HEX('DBDBDB')
12070         Dark_red       = HEX('920000')
12080         light_green    = HEX('B6FFB6')
12090         Mustard        = HEX('929200')
12100         Dark_green     = HEX('009200')
12110         Sea_blue       = HEX('009292')
12120         Dark_blue      = HEX('000092')
12130         Purple         = HEX('920092')
12140         Shocking_pink  = HEX('FF0092')
12150         Orange         = HEX('FF9200')
12160         Lime_green     = HEX('92FF00')
12170         Apple_green    = HEX('00FF92')
12180         Bright_blue    = HEX('0092FF')
12190         Mauve          = HEX('9200FF')
12200         Peach          = HEX('FFB6B6')
12210         Light_yellow   = HEX('FFFFB6')
12220         Light_blue     = HEX('B6FFFF')
12230         Sky_blue       = HEX('B6B6FF')
12240         Rose_pink      = HEX('FFB6FF')
12250         pink           = HEX('FFB6DB')
12260         Beige          = HEX('FFDBB6')
12270         Pastel_pink    = HEX('FFDBDB')
12280         Pastel_yellow  = HEX('FFFFDB')
12290         Pastel_green   = HEX('DBFFDB')
12300         Pastel_cyan    = HEX('DBFFFF')
12310         Pastel_blue    = HEX('DBDBFF')
12320         Pastel_rose    = HEX('FFDBFF')
12330         Brick          = HEX('B66D6D')
12340         Light_khaki    = HEX('B6B66D')
12350         Dull_green     = HEX('6DB66D')
12360         Dull_cyan      = HEX('6DB6B6')
12370         Steel_blue     = HEX('6D6DB6')
12380         dull_pink      = HEX('B66DB6')
12390         Brown          = HEX('6D2424')
12400         Khaki          = HEX('6D6D24')
12410         Dusky_green    = HEX('246D24')
12420         Dusky_blue     = HEX('246D6D')
12430         Midnight_blue  = HEX('24246D')
12440         Plum           = HEX('6D246D')
12450         Dusky_pink     = HEX('B64992')
12460         Buff           = HEX('B69249')
12470         Avocado        = HEX('92B649')
12480         Dull_turquoise = HEX('49B692')
12490         Dull_blue      = HEX('4992B6')
12500         Faded_purple   = HEX('9249B6')
12510         Cerise         = HEX('920049')
12520         Tan_normal     = HEX('924900')
12530         Grass_green    = HEX('499200')
12540         Sea_green      = HEX('009249')
12550         Ultramarine    = HEX('004992')
12560         Deep_purple    = HEX('490092')
12570         Light_orange   = HEX('ffb624')
12580         pinkish        = HEX('C0037B')
12590         light_red      = HEX('FC1729')
12600         wg_striped     = light_green
12610         br_striped     = Dark_red
12620         wr_striped     = light_red
12630         bg_striped     = Dark_green
12640   END SELect
12650   mode%=RMODE
12660   RETurn mode%                     : REMark return current screen mode now
12670 END DEFine set_colours%
12680 :
12690 DEFine FuNction fen_app2 (adresse,tableau$,nr_sects%,selk%,phys%)
12700 REMark les paramtres sont: l'adresse (0 la premire fois)
12710 REMark le tableau de cha•ne, le nombre de sections,
12720 REMark le drapeau si on veut des touches de sƒlection et si sƒlection=action,
12730 REMark un tableau d'entiers contenant les tailles x,y et les origines
12740 REMark et les touches  exclure des touches de sƒlection
12750 LOCal app_attributs%(3,1),temp%,bord%,pap%,ori_x%,ori_y%
12760 LOCal tail_x%,tail_y%,fleches%,barrre%
12770 :
12780   IF adresse:RECHP adresse              : REMark si c'est un 2nd appel, on enlve l'ancienne
12790   tail_x%=phys%(0)                      : REMark (largeur)    Ceci est pour la clartƒ
12800   tail_y%=phys%(1)                      : REMark (hauteur)    seulement, on pourrait
12810   ori_x%=phys%(2)                       : REMark            utiliser directement le
12820   ori_y%=phys%(3)                       : REMark origs x,y    tableau phys% dans AP_MAKE
12830 :
12840   adresse=AP_MAKE2(selk%,tail_x%,tail_y%,ori_x%,ori_y%,nr_sects%,tableau$)
12850   IF adresse<0: RETurn 0                : REMark si erreur, on retourne 0
12860                                         : REMark Maintenant on dƒtermine les attributs
12870   bord%=sp.appbd                        : REMark couleur bordure
12880   pap%=sp.appbg                         : REMark couleur "papier"
12890   touche_sel%=9                         : REMark touche de sƒlection - TAB
12900   fleches%=sp.scrbararr                 : REMark couleur des flches
12910   barre%=sp.scrbar                      : REMark couleur de la barre d'ascenseur
12920 :
12930   app_attributs%(0,0)=1
12940   app_attributs%(0,1)=sp.appihigh       : REMark larg. & coul. trait poste courant
12950 :
12960   app_attributs%(1,0)=sp.appiunabg
12970   app_attributs%(1,1)=sp.appiunafg      : REMark unavailable
12980 :
12990   app_attributs%(2,0)=sp.appiavabg
13000   app_attributs%(2,1)=sp.appiavafg      : REMark disponible
13010 :
13020   app_attributs%(3,0)=sp.appiselbg
13030   app_attributs%(3,1)=sp.appiselfg      : REMark sƒlectionnƒ
13040 :
13050   er=AP_PARS(adresse,0,bord%,pap%,touche_sel%,fleches%,barre%,app_attributs%)
13060 :
13070   IF er:RETurn 0
13080 :
13090   RETurn adresse  : REMark on retourne l'adresse
13100 END DEFine fen_app
13110 :
13120 DEFine PROCedure scroll_arrows(rangees%,nbr_objets%,appfen,cty%)
13130 REMark Cette procƒdure dƒcide s'il faut laisser les flches de dƒfilement
13140 REMark params: le nombre de rangƒes, le nombre total d'objets
13150 REMark le pointer vers la sous-fen d'app et la dƒfinition de controle
13160   IF nbr_objets%<=rangees% +1
13170     POKE_W appfen+150,0
13180     cty%(1,2)=nbr_objets%
13190   ELSE
13200     POKE_W appfen+150,6
13210 REMark    cty%(1,2)=rangees%-1             : rem if scroll arrows are there, there is one line less in the window
13220     cty%(1,2)=rangees%
13230  END IF
13240  IF DIMN(cty%,1)=2:cty%(2,2)=cty%(1,2)
13250  cty%(0,1)=1                            : REMark show that definitionhas changed
13260 END DEFine scroll_arrows
13270 :
13280 DEFine PROCedure init
13290 LOCal temp%
13300   text=0:sprite=2:blob=4:pattern=6
13310   retr=256:retn=-256:sous1=254:sous2=252:sous3=250
13320   REMark normal: hit = selects/deselects, do = returns & leaves selected
13330   REMark retr = return even on hit, reset item to available after,
13340   REMark retn = return even on hit, hit selects/deselects, do selects
13350 :
13360   hit$=CHR$(1):do$=CHR$(2):cancel$=CHR$(3):sleep$=CHR$(7):wake$=CHR$(8)
13370   help$=CHR$(4):move$=CHR$(5):size$=CHR$(6):cr$=CHR$(10)
13380 :
13390   appwinoffset%=16                      : REMark appsub win additional y offset
13400   xsize%=500                            : REMark x size divisible by 4!
13410   ysize%=200                            : REMark provisional y size%
13420   lxs=24:lys=12                         : REMark standard item x,y, sizes
13430   inf1y=lys+4                           : REMark y orig of inner border
13440   xesp=lxs+2                            : REMark loose items x spacing
13450   nbr_post%=3                           : REMark nbr of item right & left in title bar
13460   main_coul%=SMSCFG(2)                  : REMark colourways
13470   app_coul%=main_coul%                  : REMark preset colourways
13480   temp%=set_colours% (2)                : REMark always try to set highest colours
13490   IF temp%<2 AND main_coul%>3:main_coul%=0 : REMark old wman
13500   make_colours main_coul%               : REMark set all colours now
13510 :
13520   rep$=SMSCFG(1)                        : REMark dir with files
13530   titre$="Dir: "&rep$&" "
13540 :
13550 RESTORE 13550
13560 :
13570 : REMark make loose item attributes
13580 :
13590   DIM std_iattr(3,3)
13600   RD_IATT std_iattr
13610     DATA 1,sp.litemhigh : REMark current item border size & colour
13620     DATA sp.litemunabg,sp.litemunafg,0,0 : REMark unavailable item paper & ink
13630     DATA sp.litemavabg,sp.litemavafg,0,0 : REMark   available         "    "       "
13640     DATA sp.litemselbg,sp.litemselfg,0,0 : REMark   selected
13650 :
13660 : REMark make data for sprites - uses the modified RD_SPRT function
13670 : REMark
13680 : REMark
13690 :
13700   espace_vide=ALCHP(100)                : REMark GLOBAL var for 0 mask sprites!
13710 :
13720   main_sprite=RD_SPRT(0)                : REMark 0 = normal mask
13730     DATA 11,13,6,5,4
13740     DATA '     www     '
13750     DATA '     waw     '
13760     DATA '     waw     '
13770     DATA '     waw     '
13780     DATA 'wwwwwa awwwww'
13790     DATA 'waaaa   aaaaw'
13800     DATA 'wwwwwa awwwww'
13810     DATA '     waw     '
13820     DATA '     waw     '
13830     DATA '     waw     '
13840     DATA '     www     '
13850 :
13860   move_sprite=RD_SPRT(1)                : REMark 1 = 0 mask
13870   IF temp%>1:move_sprite=6              : REMark or system sprite!
13880     DATA 10,14,5,4,4
13890     DATA 'aaaaaaaaaa    '
13900     DATA 'awwwwwwwwa    '
13910     DATA 'awwrrrrwwa    '
13920     DATA 'awwrrrrwwaaaaa'
13930     DATA 'awwrrwwwwwwwwa'
13940     DATA 'awwwwwwwwrrwwa'
13950     DATA 'aaaaawwrrrrwwa'
13960     DATA '    awwrrrrwwa'
13970     DATA '    awwwwwwwwa'
13980     DATA '    aaaaaaaaaa'
13990 :
14000   zzz_sprite= RD_SPRT(1)
14010   IF temp%>1:zzz_sprite=10
14020     DATA 7,15,0,0,4
14030     DATA 'wwwww          '
14040     DATA '   w           '
14050     DATA '  w  wwww      '
14060     DATA ' w     w       '
14070     DATA 'wwwww w  wwww  '
14080     DATA '     wwww  w   '
14090     DATA '          wwww '
14100 :
14110  size_sprite=RD_SPRT(1)
14120  IF temp%>1:size_sprite=7
14130     DATA 9,14,5,4,4
14140     DATA 'aaaaaaaaaaaaaa'
14150     DATA 'awwwwwwwwwwwwa'
14160     DATA 'awwrrrrrrrrwwa'
14170     DATA 'awwrrrrrrrrwwa'
14180     DATA 'awwrrwwwwwwwwa'
14190     DATA 'awwrrwwrrrrwwa'
14200     DATA 'awwrrwwrrrrwwa'
14210     DATA 'awwwwwwwwwwwwa'
14220     DATA 'aaaaaaaaaaaaaa'
14230 :
14240   make_my_data 1
14250   make_main_wdw
14260   main_lfl%(target_its%)=128
14270 :
14280 END DEFine init
14290 :
14300 DEFine PROCedure make_main_wdw
14310 REMark make the main window
14320 LOCal main_lot,main_iwt,main_iot1
14330 :
14340   REMark first make sure of the wdw sizes (we also come here from change_size!)
14350   xsize%=xsize%-(xsize% MOD 4)                    : REMark make into nbr divisible by 4
14360   IF xsize%<500:xsize%=500
14370 :
14380   REMark calculate the y size of the wdw so that it always looks nice, in that
14390   REMark the rows in the appsub always fill the appsub entirely
14400   ysize%=ysize%+(ysize%&&1)             : REMark make into even number
14410   IF ysize%<100:ysize%=100              : REMark predetermioned min ysize% before calculations
14420   ysize%=ysize%-inf1y-14-appwinoffset%
14430   rangees=ysize% DIV 12                 : REMark GLOBAL var: nbr of rows in appsub
14440   IF rangees>data_count%-1:rangees=data_count%-1
14450   IF rangees<3:rangees=3
14460   ysize%=rangees*12+inf1y+14 +appwinoffset%
14470 :
14480   REMark first calc info obj size - this also influences one loose item, used
14490   REMark in the title bar
14500   longueur_titre=LEN(titre$)*6          : REMark length in pixels
14510   titre_x=INT((xsize%-longueur_titre)/2): REMark x orig
14520 :
14530   REMark first the loose items
14540 :
14550   yinf1y=inf1y+4:
14560   esc%=0:move_it%=esc%+1:size_it%=move_it%+1
14570   sleep_it%=size_it%+1:all_it%=sleep_it%+1:go_it%=all_it%+1
14580   dir_it%=go_it%+1:target_its%=dir_it%+1:make_it%=target_its%+8
14590 :
14600 RESTORE 14600
14610   REMark reset data pointer to here (The Basic Linker does that automatically)
14620 :
14630   temp%=(xsize%-4) DIV 9
14640   main_lit%=15:DIM main_lfl%(main_lit%)   : REMark there are 16 items!
14650   main_lot=RD_LOT(std_iattr,main_lit%)
14660     DATA lxs,lys,4+xesp*0,2,0,0,cancel$,text+retr,'ESC'
14670     DATA lxs,lys,4+xesp*1,2,0,0,move$,sprite+retr,move_sprite
14680     DATA lxs,lys,4+xesp*2,2,0,0,size$,sprite+retn,size_sprite
14690     DATA lxs,lys,xsize%-2-xesp*1,2,0,0,sleep$,sprite+retr,zzz_sprite
14700     DATA lxs,lys,xsize%-2-xesp*2,2,0,0,'A',sous1+retn,"All"
14710     DATA lxs,lys,xsize%-2-xesp*3,2,0,0,'O',sous1+retn,"OK"
14720     DATA 6*6,lys-2,titre_x+30,3,0,0   ,'D',text+retn,rep$
14730     DATA 8*6,lys,8,inf1y+2,0,0,        'G',sous1+retn,"Generic"
14740     DATA 8*6,lys,8+temp%,inf1y+2,0,0,  'T',sous2+retn,"Atari"
14750     DATA 8*6,lys,8+temp%*2,inf1y+2,0,0,'L',sous3+retn,"GoldCard"
14760     DATA 8*6,lys,8+temp%*3,inf1y+2,0,0,'Q',sous1+retn,"Q40/Q60"
14770     DATA 8*6,lys,8+temp%*4,inf1y+2,0,0,'X',sous2+retn,"QXL"
14780     DATA 8*6,lys,8+temp%*5,inf1y+2,0,0,'U',sous2+retn,"Aurora"
14790     DATA 8*6,lys,8+temp%*6,inf1y+2,0,0,'J',sous1+retn,"Java"
14800     DATA 8*6,lys,8+temp%*7,inf1y+2,0,0,'P',sous1+retn,"PtrGen"
14810     DATA 8*6,lys,8+temp%*8,inf1y+2,0,0,'M',sous1+retn,"Make"
14820 :
14830   REMark now info wdw objects - here only one, the title
14840   REMark make the title data: calculate size and x position of this object
14850 :
14860   main_iot1=RD_IOT(0)
14870     DATA longueur_titre,10,4,INT((lys-10)/2),sous1,sp.titlefg,0,0,titre$
14880 :
14890   REMark now the info wdws
14900 :
14910   main_iwt=RD_IWT(2)                              : REMark there are 3
14920 :
14930 REMark first the title bar
14940 :
14950     DATA xsize%-8-(2*nbr_post%*xesp),lys+2,xesp*nbr_post%+4,0         : REMark x,y sizes & origs
14960     DATA 0,0,sp.infwinbd,sp.titlebg     : REMark shadow, border size & col,paper col
14970     DATA 0                              : REMark object pointer (none)
14980 :
14990 REMark now wdw containing title itself
15000 :
15010     DATA longueur_titre+8,lys,titre_x-4,2
15020     DATA 0,0,sp.infwindbd,sp.infwinbg
15030     DATA main_iot1                      : REMark one object
15040 :
15050 REMark inner border
15060 :
15070     DATA xsize%-8,ysize%-inf1y-2,4,inf1y
15080     DATA 0,1,sp.infwinbd,sp.infwinbg
15090     DATA 0
15100 :
15110 REMark now the appsub wdw - use the OUTPTR functions for this
15120 :
15130   app_addr=0                            : REMark there is none yet
15140   mk_main_app 1                         : REMark (this destroys my DATA pointer)
15150   main_app_list = app_addr+180          : REMark list built by OUTPTR
15160 :
15170 : REMark  working defn
15180 :
15190 RESTORE 15190
15200                                         : REMark reset DATA pointer (due to Basic Linker, restore must be ALONE on the line)
15210   main_defn=RD_WDEF
15220     DATA 2,1,sp.winbd,sp.winbg          : REMark shad,border size & col,pap col
15230     DATA xsize%,ysize%,0,0    : REMark x,y size & initial ptr pos
15240     DATA main_sprite,main_lot,main_iwt,main_app_list : REMark sprite,loose items, info wins, appsubs
15250 END DEFine make_main_wdw
15260 :
15270 DEFine PROCedure warn_make
15280 REMark makes warning & info window
15290 REMark uses global vars green etc...
15300 REMark SETS the global var warning_width% (nbr of chars in one line in wdw)
15310 LOCal lys%,warn_lot,warn_iot1,warn_iwt
15320 LOCal x%,y%,warning_paper,warning_brd
15330   RESTORE 15330
15340   x%=188:y%=82
15350   warning_paper=menu_pap
15360   warning_brd=pink                      : REMark pink
15370 REMark  if menu_brd=4:warning_brd=light_green      : rem light green
15380   lys%=10
15390   DIM warn_lfl%(1)
15400   warn_lot=RD_LOT(std_iattr,1)
15410     DATA 30,lys%,4,4,0,0,cancel$,text+retr,'ESC'
15420     DATA 30,lys%,x%-34,4,0,0,'O',text+retr,"OK"
15430 :
15440   warn_iot1=RD_IOT (0)
15450     DATA 12*9,10,0,1,text,red,2,0,'Attention'
15460 :
15470   warn_iwt=RD_IWT(1)
15480     DATA x%-16,y%-32,8,24
15490     DATA 0,1,red,warning_paper
15500     DATA 0
15510 :
15520     DATA 120,12,(x%-112) DIV 2,4
15530     DATA 0,1,menu_brd,warninggpaper
15540     DATA warn_iot1
15550 :
15560   warn_defn=RD_WDEF
15570     DATA 2,1,menu_brd,warning_brd
15580     DATA x%,y%,0,0
15590     DATA main_sprite,warn_lot,warn_iwt,0
15600 :
15610   warning_width%=(x%-16) DIV 6                    : REMark width of warning winfow in chars
15620 :
15630 END DEFine warn_make
15640 :
15650 DEFine FuNction RD_SPRT(drap%)
15660 REMark drap% determine si masque 0
15670   LOCal tmp_patt$(32,32)
15680   LOCal xs,ys,xo,yo, md, l
15690   READ ys,xs,xo,yo,md
15700   DIM tmp_patt$(ys,xs)
15710   FOR l=0 TO ys-1:READ tmp_patt$(l)
15720   l=ALCHP(SPRSP(xs,ys))
15730   SPSET l,xo,yo,md,tmp_patt$(0 TO ys-1,1 TO xs)
15740   IF drap%
15750     POKE_L l+16,espace_vide-(l+16)
15760   END IF
15770   RETurn l
15780 END DEFine RD_SPRT
15790 :
15800 DEFine FuNction RD_WDEF
15810 REMark read window defn
15820   LOCal ldef%(3), lspr, lloose, linfo, lappl, lattr%(3)
15830   RD_WATT lattr%
15840   READ ldef%(0), ldef%(1), ldef%(2), ldef%(3)
15850   READ lspr, lloose, linf, lappl
15860   RETurn MK_WDEF (ldef%, lattr%, lspr, lloose, linf, lappl)
15870 END DEFine RD_WDEF
15880 :
15890 DEFine FuNction RD_LOT (lattr,nitem)
15900   LOCal count(3)
15910   LOCal item, ltyp, a$, lsk$
15920   LOCal ldef%(nitem,6), lptr(3,nitem), lstr$(nitem,85)
15930   lsk$=''
15940   FOR item = 0 TO nitem
15950     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
15960     READ ldef%(item,4), ldef%(item,5)
15970     READ a$: lsk$=lsk$ & a$
15980     READ ltyp
15990     ldef%(item,6)=ltyp: ltyp=(ltyp MOD 256)/2
16000     IF ltyp>10 OR ltyp <0::ltyp=0
16010     IF ltyp
16020       READ lptr(ltyp,count(ltyp))
16030     ELSE
16040       READ lstr$(count(0))
16050     END IF
16060     count(ltyp)=count(ltyp)+1
16070   END FOR item
16080   RETurn MK_LIL (lattr, ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), ldef%(TO, 4 TO 5), lsk$, ldef%(TO, 6), lstr$, lptr(1), lptr(2), lptr(3))
16090 END DEFine RD_LOT
16100 :
16110 DEFine FuNction RD_IWT(nitem)
16120   LOCal item,mc1
16130   LOCal ldef%(nitem,3), latt%(nitem,3), lptr(nitem)
16140   FOR item = 0 TO nitem
16150     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
16160     READ latt%(item,0), latt%(item,1), latt%(item,2), latt%(item,3)
16170     READ lptr(item)
16180   END FOR item
16190   RETurn MK_IWL (ldef%, latt%, lptr)
16200 END DEFine RD_IWT
16210 :
16220 DEFine FuNction RD_IOT(nitem)
16230   LOCal count(3)
16240   LOCal item, ltyp, work1, work2
16250   LOCal ldef%(nitem,4), lptr(3,nitem), lstr$(nitem,85)
16260   FOR item = 0 TO nitem
16270     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
16280     READ ltyp
16290     ldef%(item,4)=ltyp:  ltyp=(ltyp MOD 256)/2
16300     IF ltyp >10:ltyp=0
16310     IF ltyp
16320       READ lptr(0,item),lptr(ltyp,count(ltyp))
16330     ELSE
16340       IF mycolour_mode%=2
16350           READ work1,work2,work3
16360           lptr(0,item)=WL_4_IOL(work1,work2,work3)
16370       ELSE
16380           READ work1
16390           READ work2: work1=work1*256+work2
16400           READ work2: lptr(0,item)=work1*256+work2
16410       END IF
16420       READ lstr$(count(0))
16430     END IF
16440     count(ltyp) = count(ltyp) + 1
16450   END FOR item
16460   RETurn MK_IOL (ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), lptr(0), ldef%(TO, 4), lstr$, lptr(1), lptr(2), lptr(3))
16470 END DEFine RD_IOT
16480 :
16490 DEFine PROCedure RD_IATT (lattr)
16500   LOCal i
16510   READ lattr(0,0), lattr(0,1)
16520   FOR i=1 TO 3: READ lattr(i,0), lattr(i,1), lattr(i,2), lattr(i,3)
16530 END DEFine RD_IATT
16540 :
16550 DEFine PROCedure RD_WATT (lattr%)
16560   READ lattr%(0), lattr%(1),lattr%(2),lattr%(3)
16570 END DEFine RD_WATT
16580 :
16590 DEFine PROCedure sa
16600   SAVE_O "dev8_extras_source_SMSQEMake_bas"
16610 END DEFine sa
16620 :
16630 DEFine PROCedure ccrestore
16640 LOCal lp%,k%,a$,c%,r%,out%
16650   SAVE_O ram1_p_bas
16660   c%=FOP_IN("ram1_p_bas")
16670   out%=FOP_OVER("ram1_SMSQEMake_bas")
16680   IF c%<0 OR out%<0:CLOSE:PRINT "can't open files":STOP
16690   REPeat lp%
16700     IF EOF(#c%):EXIT lp%
16710     INPUT#c%,a$
16720     r%="restore current_line" INSTR a$   : REMark xyxyxywtz   --- LEAVE THIS COMMENT!!!!!!!!
16730     IF r%=0:PRINT#out%,a$:NEXT lp%    : REMark no restore, just ga around again
16740     IF "xyxyxywtz" INSTR a$:PRINT#out%,a$:NEXT lp%
16750     t%=" " INSTR a$
16760     IF t%=0
16770       t%=CHR$(9) INSTR a$
16780     END IF
16790     IF t%=0:PRINT#out%,a$:NEXT lp%:REMark what?
16800     lineno$=a$(1  TO t% )
16810     PRINT#out%,lineno$;" ";"RESTORE";" ";lineno$
16820   END REPeat lp%
16830   CLOSE#c%
16840   CLOSE#out%
16850   save_as_sav
16860 END DEFine ccrestore
16870 :
16880 DEFine PROCedure save_as_sav
16890   LOCal c%
16900   c%=FOP_OVER("ram1_sas_bas" )
16910   IF c%<0:PRINT "can't open command file":STOP
16920   PRINT#c%,'10 load "ram1_SMSQEMake_bas"'
16930   CLOSE#c%
16940   c%=FOP_OVER("ram1_sas2_bas" )
16950   IF c%<0:PRINT "can't open second command file":STOP
16960   PRINT#c%,'qsave_o "ram1_SMSQEMake_sav"'
16970   CLOSE#c%
16980   EW ram1_sas_bas,ram1_sas2_bas
16990 END DEFine save_a_sav
17000 :
