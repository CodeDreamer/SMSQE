100 REMark $$asmb=dev8_extras_source_outptr_bin,0,10
110 REMark $$asmb=dev1_booty_qptr_bin,50,172
120 REMark $$asmb=dev8_extras_source_SMSQEMake_bin,0,10
130 REMark $$stak=20000
140 REMark Make sure the compiled version does not contain the CURRENT_LINE keyword
150 REMark use the ccrestore procedure herein to elmimenate it!!!!!!!!!!!!!!!!
160 compiled=NOT IS_OPEN(#0)
170 IF NOT compiled
180   SCR_SIZE#0, xsz%,ysz%
190   OUTLN#0,xsz%,ysz%-28,0,28
200   WSET -1
210 END IF
220 init
230 main
240 STOP
250 DEFine PROCedure make_new_dir (poste%)
260 REMark get athe new dir where the files are located
270 LOCal quel_rep$
280   IF MENU_OK
290     quel_rep$= DIR_SELECT$("The Files are where?",,10,10,main_coul%,app_coul%)
300   ELSE
310     quel_rep$=getstr$(poste%,0,main_defn,a_lire$)       : REMark use menu extns if possible
320   END IF
330   IF LEN (quel_rep$) >= 5
340      rep$=quel_rep$(1 TO 5)
350   END IF
360   CH_ITEM main_defn,-1,poste%,-1,"",rep$
370   main_lfl%(poste%)=main_lfl%(poste%)+1
380 END DEFine make_new_dir
390 :
400 DEFine PROCedure main
410 LOCal lp%,item%,event%,swnum%,xrel%,yrel%,item%,lp2%,tot%,errors%
420   IF compiled
430      OPEN#1,'con'
440      SCR_SIZE#1,xsz%,ysz%
450      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
460   ELSE
470      SCR_SIZE#1,xsz%,ysz%
480      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
490   END IF
500   POS_PTR main_defn,40*65536+20,1
510   errors%=1
520   IF autostart%:errors%=do_the_work%
530   IF quit_always%:STOP
540   IF quit_no_errors% AND NOT errors%:STOP
550   REPeat lp%
560     RD_PTR main_defn,item%,swnum%,event%,xrel%,yrel%,main_lfl%,main_mfl%,main_cty%
570     IF swnum%= -1
580       SELect ON item%
590               =esc%     : EXIT lp%      : REMark quit prog
600               =move_it% : CH_WIN main_defn: REMark move
610               =size_it% : change_size xrel%,yrel% : REMark change size
620               =sleep_it%: button main_defn,main_lfl%,main_mfl%,main_cty%,"SMSQ/E Make"
630               =all_it%
640                  set_statusses          : REMark set all appsub item statusses
650                  main_cty%(0,1)=1       : REMark and redraw
660               =go_it%   : errors%=do_the_work%
670               =dir_it%: make_new_dir item%
680               =target_its% TO target_its%+8
690                           start%=1:machines%=0
700                           FOR lp2%=target_its% TO target_its%+8
710                               IF main_lfl%(lp2%):machines%=machines%+start%
720                               start%=start%*2
730                           END FOR lp2%
740                           make_my_data machines%        : REMark make data arrays
750                           mk_main_app                   : REMark make appsub wdw
760                           main_app_list=app_addr+180
770                           AP_IN main_defn,main_app_list : REMark put it in
780                           DR_WDRW main_defn:main_cty%(0,1)=1 : REMark and redraw entire wdw
790       END SELect
800     END IF
810   END REPeat lp%
820   DR_UNST main_defn
830   IF compiled:CLOSE#1
840   RECHP espace_vide
850 END DEFine main
860 :
870 DEFine PROCedure button (def_trav,menu_drap%,app_drap%,control%,chaine$)
880 REMark set up as button
890   DR_UNST def_trav                      : REMark no more working defn
900   IF compiled: CLOSE#1                  : REMark more wdw
910   BUTTON_WAIT chaine$                   : REMark use menu extensions
920   IF compiled
930     OPEN#1,"CON"
940     DR_PPOS def_trav,-1,-1,menu_drap%,app_drap%,control%
950   ELSE
960     DR_PULD def_trav,-1,-1,menu_drap%,app_drap%,control%
970   END IF
980 END DEFine button
990 :
1000 DEFine PROCedure mk_main_app
1010 REMark make main application sub window
1020 LOCal t2%,temp%,sections%,att_phys%(3)
1030   DIM main_cty%(0):DIM main_mfl%(0,0)
1040   sections%=2
1050   temp%=sections% :t2%=rangees
1060   att_phys%(0)=xsize%-16                : REMark xsize calculated on main wdw x size
1070   att_phys%(1)=t2%                      : REMark y size,nominal,in rows
1080   att_phys%(2)=4                        : REMark x origin x
1090   att_phys%(3)=inf1y +appwinoffset%     : REMark y origin
1100   app_addr=fen_app2(app_addr,mdata$,temp%,1,att_phys%,"DO ")
1110   IF app_addr
1120       DIM main_cty%(sections%,2)        : REMark ctrl defn
1130       main_cty%(0,0)=1                  : REMark show 1 section
1140       main_cty%(1,2)=t2%
1150       IF sections%=2:main_cty%(2,2)=t2% : REMark but allow 2
1160       IF temp%
1170            DIM main_mfl%(temp%-1,0)     : REMark flag array
1180            set_statusses                : REMark and set all statusses
1190       ELSE
1200            POKE_W app_addr,PEEK_W(app_addr)+8
1210       END IF
1220    REMark   scroll_arrows t2%,temp%,app_addr,main_cty%
1230   END IF
1240 END DEFine mk_main_app
1250 :
1260 DEFine PROCedure set_statusses
1270 REMark set all statusses of menu appsub objects
1280 LOCal temp%,lp%,stat%
1290   temp%=DIMN(main_mfl%,1)               : REMark how many are there?
1300   stat%=main_lfl%(all_it%)              : REMark status of "all" item
1310   FOR lp%=0 TO temp%
1320     IF mdata$(lp%+1,1)='*'
1330       main_mfl%(lp%,0)=16               : REMark comments set to unavailable
1340     ELSE
1350       main_mfl%(lp%,0)=stat%            : REMark all other to status
1360     END IF
1370   END FOR lp%
1380 END DEFine set_statusses
1390 :
1400 DEFine PROCedure make_my_data (what_machine%)
1410 REMark what_machine% = 1 -> generic
1420 REMark what_machine% = 2 -> atari
1430 REMark what_machine% = 4 -> Gold Card
1440 REMark what_machine% = 8 -> Qx0
1450 REMark what_machine% = 16 -> QXL
1460 REMark what_machine% = 32 -> aurora
1470 REMark what_machine% = 64 -> java
1480 REMark what_machine% = 128 -> ptrgen
1490 REMark what_machine% = 256 -> qpc
1500 REMark these may be combined
1510   DIM mdata$(250,50)
1520   REMark first make generic data
1530   data_count%=1                         : REMark general data count (global var)
1540   IF what_machine%&&1
1550     generic_data                        : REMark set up generic data
1560     read_my_datas                       : REMark and read it
1570   END IF
1580   IF what_machine%&&2
1590      atari_data                         : REMark set up machine specific data....
1600      read_my_datas                      : REMark and read it
1610   END IF
1620   IF what_machine%&&4
1630      gc_data                            : REMark set up machine specific data....
1640      read_my_datas                      : REMark and read it
1650   END IF
1660   IF what_machine%&&8
1670      q40_data                           : REMark set up machine specific data....
1680      read_my_datas                      : REMark and read it
1690   END IF
1700   IF what_machine%&&16
1710      qxl_data                           : REMark set up machine specific data....
1720      read_my_datas                      : REMark and read it
1730   END IF
1740   IF what_machine%&&32
1750      aurora_data                        : REMark set up machine specific data....
1760      read_my_datas                      : REMark and read it
1770   END IF
1780   IF what_machine%&&64
1790      java_data                          : REMark set up machine specific data....
1800      read_my_datas                      : REMark and read it
1810   END IF
1820   IF what_machine%&&128
1830      ptrgen_data                        : REMark set up machine specific data....
1840      read_my_datas                      : REMark and read it
1850   END IF
1860   IF what_machine%&&256
1870      qpc_data                        : REMark set up machine specific data....
1880      read_my_datas                      : REMark and read it
1890   END IF
1900 END DEFine make_my_data
1910 :
1920 DEFine PROCedure read_my_datas
1930 REMark reads datas into the global var mdata$
1940 REMark uses data_count% from make_my_data
1950 LOCal lp%
1960   REPeat lp%
1970     READ mdata$(data_count%)
1980     IF mdata$(data_count%)="":EXIT lp%
1990     data_count%=data_count%+1
2000   END REPeat lp%
2010 END DEFine read_my_datas
2020 :
2030 DEFine PROCedure generic_data
2040 RESTORE 2040
2050   DATA '**** -- SMSQ Generic -- ****'
2060   DATA 'smsq_smsq_loader_link'
2070   DATA 'smsq_smsq_fh_link'
2080   DATA 'smsq_smsq_link'
2090   DATA 'smsq_smsq_cache_link'
2100   DATA 'smsq_sbas_link'
2110   DATA 'smsq_smsq_lang_link'
2120   DATA 'smsq_sbas_lang_link'
2130   DATA 'smsq_sbas_procs_link'
2140   DATA 'smsq_smsq_wman_link'
2150   DATA 'smsq_smsq_hotkey_link'
2160   DATA 'smsq_smsq_vers_link'
2170   DATA 'smsq_home_link'
2180   DATA 'smsq_smsq_1mb_link',''
2190 END DEFine generic_data
2200 :
2210 DEFine PROCedure q40_data
2220 RESTORE 2220
2230   DATA '**** -- Q40/Q60 -- ****'
2240   DATA 'smsq_q40_hwinit_link'
2250   DATA 'smsq_q40_nasty_link'
2260   DATA 'smsq_q40_cache_link'
2270   DATA 'smsq_q40_cachemode_link'
2280   DATA 'smsq_smsq_cache40c_link'
2290   DATA 'smsq_q40_driver_ser_link'
2300   DATA 'smsq_q40_driver_dv3_link'
2310   DATA 'smsq_q40_driver_ql_link'
2320   DATA 'smsq_q40_driver_16_link'
2330   DATA 'smsq_q40_kbd_lang_link'
2340   DATA 'smsq_sbas_procs_link'
2350   DATA 'smsq_q40_sysspr_link',''
2360 END DEFine q40_data
2370 :
2380 DEFine PROCedure atari_data
2390 RESTORE 2390
2400   DATA '**** -- Atari -- ****'
2410   DATA 'smsq_atari_hwinit_link'
2420   DATA 'smsq_atari_nasty_link'
2430   DATA 'smsq_atari_driver_ql_link'
2440   DATA 'smsq_atari_driver_mo_link'
2450   DATA 'smsq_atari_driver_ser_link'
2460   DATA 'smsq_atari_driver_dv3_link'
2470   DATA 'smsq_atari_kbd_lang_link'
2480   DATA 'smsq_atari_sysspr_link'
2490   DATA 'sys_boot_st_host_link',''
2500 END DEFine atari_data
2510 :
2520 DEFine PROCedure gc_data
2530 RESTORE 2530
2540   DATA '**** -- (Super)GoldCard -- ****'
2550   DATA 'smsq_gold_host_link'
2560   DATA 'smsq_gold_hwinit_link'
2570   DATA 'smsq_gold_nasty_link'
2580   DATA 'smsq_gold_nasty_s_link'
2590   DATA 'smsq_gold_driver_ql_link'
2600   DATA 'smsq_gold_driver_8_link'
2610   DATA 'smsq_gold_driver_most_link'
2620   DATA 'smsq_gold_driver_nd_link'
2630   DATA 'smsq_gold_driver_nds_link'
2640   DATA 'smsq_gold_driver_dv3_link'
2650   DATA 'smsq_gold_kbd_lang_link'
2660   DATA 'smsq_gold_kbd_abc_lng_link'
2670   DATA 'smsq_gold_kbd_abc_link'
2680   DATA 'smsq_gold_roms_link'
2690   DATA 'smsq_gold_qimi_link'
2700   DATA 'smsq_gold_sysspr_link',''
2710 END DEFine gc_data
2720 :
2730 DEFine PROCedure aurora_data
2740 RESTORE 2740
2750   DATA '**** -- Aurora -- ****'
2760   DATA 'smsq_gold_host_link'
2770   DATA 'smsq_gold_hwinit_link'
2780   DATA 'smsq_gold_nasty_link'
2790   DATA 'smsq_gold_nasty_s_link'
2800   DATA 'smsq_aurora_driver_8_link'
2810   DATA 'smsq_gold_driver_ql_link'
2820   DATA 'smsq_gold_driver_most_link'
2830   DATA 'smsq_gold_driver_nd_link'
2840   DATA 'smsq_gold_driver_nds_link'
2850   DATA 'smsq_gold_driver_dv3_link'
2860   DATA 'smsq_gold_kbd_lang_link'
2870   DATA 'smsq_gold_kbd_abc_lng_link'
2880   DATA 'smsq_gold_kbd_abc_link'
2890   DATA 'smsq_gold_roms_link'
2900   DATA 'smsq_gold_qimi_link'
2910   DATA 'smsq_aurora_sysspr_link'
2920   DATA 'smsq_gold_sysspr_link',''
2930 END DEFine aurora_data
2940 :
2950 DEFine PROCedure qxl_data
2960 RESTORE 2960
2970   DATA '**** -- QXL -- ****'
2980   DATA 'smsq_qxl_host_link'
2990   DATA 'smsq_qxl_hwinit_link'
3000   DATA 'smsq_qxl_nasty_e_link'
3010   DATA 'smsq_qxl_driver_ql_link'
3020   DATA 'smsq_qxl_driver_16_link'
3030   DATA 'smsq_qxl_driver_most_link'
3040   DATA 'smsq_qxl_driver_nd_link'
3050   DATA 'smsq_qxl_procs_link'
3060   DATA 'smsq_qxl_driver_dv3e_link'
3070   DATA 'smsq_qxl_kbd_lang_link'
3080   DATA 'smsq_qxl_ecache_link'
3090   DATA 'smsq_qxl_sysspr_link',''
3100 END DEFine qxl_data
3110 :
3120 DEFine PROCedure java_data
3130 RESTORE 3130
3140   DATA '**** -- Java -- ****'
3150   DATA 'smsq_java_host_link'
3160   DATA 'smsq_java_smsq_link'
3170   DATA 'smsq_java_smsq_1mb_link'
3180   DATA 'smsq_java_rtc_link'
3190   DATA 'smsq_java_hwinit_link'
3200   DATA 'smsq_java_driver_ql_link'
3210   DATA 'smsq_java_driver_8_link'
3220   DATA 'smsq_java_driver_16_link'
3230   DATA 'smsq_java_driver_most_link'
3240   DATA 'smsq_gold_kbd_lang_link'
3250   DATA 'smsq_gold_sysspr_link',''
3260 END DEFine java_data
3270 :
3280 DEFine PROCedure qpc_data
3290 RESTORE 3290
3300   DATA "**** -- QPC -- ****"
3310   DATA 'smsq_qpc_host_link'
3320   DATA 'smsq_qpc_hwinit_link'
3330   DATA 'smsq_qpc_nasty_e_link'
3340   DATA 'smsq_qpc_driver_ql_link'
3350   DATA 'smsq_qpc_driver_16_link'
3360   DATA 'smsq_qpc_driver_8_link'
3370   DATA 'smsq_qpc_driver_ql_link'
3380   DATA 'smsq_qpc_driver_most_link'
3390   DATA 'smsq_qpc_procs_link'
3400   DATA 'smsq_qpc_driver_dv3e_link'
3410   DATA 'smsq_qpc_kbd_lang_link'
3420   DATA 'smsq_qpc_dos_link'
3430   DATA 'smsq_qpc_ip_link'
3440   DATA 'smsq_qpc_cdaudio_link'
3450   DATA 'smsq_smsq_qpc_link'
3460   DATA 'smsq_qpc_sysspr_link',''
3470 END DEFine qpc_data
3480 :
3490 DEFine PROCedure ptrgen_data
3500 RESTORE 3500
3510   DATA "**** -- PtrGen -- ****"
3520   DATA "ee_wman_link","ee_ptr_link",""
3530 END DEFine ptrgen_data
3540 :
3550 DEFine FuNction do_the_work%
3560 REMark this tries to compile all of the selected files
3570 REMark if the file was compiled OK, status (of the link file) set to available
3580 REMark if the file was not compiled ok, status left selected
3590 LOCal lp%,bound%,a$,my_error%,tot_err%
3600   bound%=DIMN(main_mfl%,1)              : REMark number of items to check
3610   tot_err%=0
3620   FOR lp%=0 TO bound%
3630     a$=mdata$(lp%+1)                    : REMark potential file to compile
3640     IF a$(1)<>"*" AND main_mfl%(lp%,0)
3650        a$=rep$&a$                       : REMark add dir
3660        my_error%=compile_and_check% (a$): REMark try to compile
3670        IF NOT my_error%
3680            main_mfl%(lp%,0)=1
3690            DR_ADRW main_defn,0,main_mfl%,main_cty%
3700        END IF
3710        tot_err%=tot_err%+my_error%
3720     END IF
3730   END FOR lp%
3740   main_cty%(0,1)=1
3750   main_lfl%(go_it%)=1
3760   IF main_lfl%(make_it%)=128 AND NOT tot_err%:make_targets
3770   RETurn tot_err%
3780 END DEFine do_the_work%
3790 :
3800 DEFine FuNction compile_and_check% (a$)
3810 REMark this tries to compile a file, using the "make" prog
3820 REMark this returns 1 if errors, else 0
3830 LOCal chan%,is_ok$,ch_len
3840   ch_len=EXEPF_W ("make";a$&" -w -l"&force_assembly$)             : REMark exec make & wait
3850   IF ch_len<>0
3860      EXEC_W "make";a$&" -w -l"&force_assembly$             : REMark exec make & wait
3870   END IF
3880   is_ok$=a$(1 TO LEN(a$)-4)             : REMark file name w/o extension
3890   chan%=FOP_IN(is_ok$&'log')            : REMark open log file
3900   IF chan%<0:RETurn 1                   : REMark can't even open it!
3910   ch_len=FLEN(#chan%)                   : REMark file length
3920   GET#chan%\ch_len-10                   : REMark this is where error report is
3930   INPUT#chan%,is_ok$                    : REMark get it
3940   CLOSE#chan%
3950   IF NOT is_ok$=="No errors"
3960     RETurn 1                            : REMark anything other than "no errors" signals errors
3970   ELSE
3980     RETurn 0
3990   END IF
4000 END DEFine compile_and_check%
4010 :
4020 DEFine PROCedure make_targets
4030 REMark targets are only made if there was no error during compilation
4040 REMark to achieve this, a primitive approach is used:
4050 REMark a check is made whether an appsub menu item status is still selected
4060 REMark if it is, then the
4070   LOCal lp%,current_target%,tot_targets%
4080   tot_targets%=0                        : REMark no targets to make yet
4090   current_target%=0                     : REMark and no target yet
4100   FOR lp%=0 TO data_count%-2
4110     IF main_mfl%(lp%,0)=16
4120       REMark we're covering a new target
4130       tot_targets%=tot_targets%+current_target% : REMark show old target
4140       current_target%=find_target%(lp%+1) : REMark get new target we're covering
4150     ELSE
4160       IF main_mfl%(lp%,0)<>0:current_target%=0
4170     END IF
4180   END FOR lp%
4190   tot_targets%=tot_targets%+current_target%
4200   REMark at the end, here, tot_targets holds a bitmap
4210   REMark of targets for which compilation was ok
4220   IF (machines%&&1) AND NOT (tot_targets% && 1):RETurn : REMark generic was asked for but not compiled correctly!
4230   IF tot_targets%&&2:atari_target
4240   IF tot_targets%&&4:gc_target
4250   IF tot_targets%&&8:qx0_target
4260   IF tot_targets%&&16:qxl_target
4270   IF tot_targets%&&32:aurora_target
4280   IF tot_targets%&&64:java_target
4290   REMark IF tot_targets%&&128:ptr_gen_target  : rem no need for this
4300   IF tot_targets%&&256:qpc_target
4310 END DEFine make_targets
4320 :
4330 DEFine PROCedure atari_target
4340   cmdl$='m'
4350   p$=rep$&'sys_boot_st_flp'
4360   f$=rep$&'sys_boot_file'
4370   ah$=rep$&'sys_boot_st_host'
4380   ld$=rep$&'smsq_smsq_loader'
4390   hi$=rep$&'smsq_atari_hwinit'
4400   os1f$=rep$&'smsq_smsq_fh_os'
4410   os1$=rep$&'smsq_smsq_os'
4420   ca$=rep$&'smsq_smsq_cache'
4430   os2$=rep$&'smsq_sbas_control'
4440   ns1$=rep$&'smsq_atari_nasty'
4450   ln1$=rep$&'smsq_smsq_lang'
4460   ln2$=rep$&'smsq_atari_kbd_lang'
4470   ln3$=rep$&'smsq_sbas_lang'
4480   ex1$=rep$&'smsq_sbas_procs_x'
4490   ex2$=rep$&'smsq_atari_driver_dv3'
4500   ex3$=rep$&'smsq_atari_driver_ser'
4510   ex4$=rep$&'smsq_atari_driver_mono'
4520   ex5$=rep$&'smsq_atari_driver_ql'
4530   ex6$=rep$&'smsq_smsq_wman'
4540   ex7$=rep$&'smsq_smsq_hotkey'
4550   ex8$=rep$&'smsq_atari_sysspr'
4560   ex9$=rep$&'smsq_home_home'
4570   nl$=rep$&'sys_boot_null'
4580   IF 'm' INSTR cmdl$ = 0: ex4$ = nl$
4590   EW f$, ah$,ld$,hi$,os1f$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$, rep$&'smsq_atari_SMSQ.PRG'
4600 END DEFine atari_target
4610 :
4620 DEFine PROCedure gc_target
4630 REMark there are  2 targets for the Goldcard!
4640   f$=rep$&'sys_boot_file'
4650   v$=rep$&'smsq_smsq_vers'
4660   h$=rep$&'smsq_gold_host'
4670   ld$=rep$&'smsq_smsq_loader'
4680   hi$=rep$&'smsq_gold_hwinit'
4690   os1$=rep$&'smsq_smsq_os'
4700   ca$=rep$&'smsq_smsq_cache'
4710   os2$=rep$&'smsq_sbas_control'
4720   ns1$=rep$&'smsq_gold_nasty'
4730   ns2$=rep$&'smsq_gold_nasty_s'
4740   ln1$=rep$&'smsq_smsq_lang'
4750   ln2$=rep$&'smsq_gold_kbd_lang'
4760   ln2a$=rep$&'smsq_gold_kbd_abc_lang'
4770   ln3$=rep$&'smsq_sbas_lang'
4780   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
4790   ex2a$=rep$&'smsq_gold_kbd_abc'
4800   ex2$=rep$&'smsq_gold_driver_most'
4810   ex3a$=rep$&'smsq_gold_driver_ql'
4820   ex38$=rep$&'smsq_gold_driver_8'
4830   ex3q$=rep$&'smsq_gold_qimi'
4840   ex3w$=rep$&'smsq_smsq_wman'
4850   ex4$=rep$&'smsq_gold_driver_dv3'
4860   ex5$=rep$&'smsq_gold_driver_nd'
4870   ex6$=rep$&'smsq_gold_driver_nds'
4880   ex7$=rep$&'smsq_smsq_hotkey'
4890   ex8$=rep$&'smsq_gold_sysspr'
4900   ex9$=rep$&'smsq_home_home'
4910   nl$=rep$&'sys_boot_null'
4920   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex3a$,ex3q$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,ex1$,ex9$, rep$&'SMSQ_GOLD_gold'
4930   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex38$,ex3q$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,ex1$,ex9$, rep$&'SMSQ_GOLD_gold8'
4940 END DEFine gc_target
4950 :
4960 DEFine PROCedure aurora_target
4970   f$=rep$&'sys_boot_file'
4980   v$=rep$&'smsq_smsq_vers'
4990   h$=rep$&'smsq_gold_host'
5000   ld$=rep$&'smsq_smsq_loader'
5010   hi$=rep$&'smsq_gold_hwinit'
5020   os1$=rep$&'smsq_smsq_os'
5030   ca$=rep$&'smsq_smsq_cache'
5040   os2$=rep$&'smsq_sbas_control'
5050   ns1$=rep$&'smsq_gold_nasty'
5060   ns2$=rep$&'smsq_gold_nasty_s'
5070   ln1$=rep$&'smsq_smsq_lang'
5080   ln2$=rep$&'smsq_gold_kbd_lang'
5090   ln2a$=rep$&'smsq_gold_kbd_abc_lang'
5100   ln3$=rep$&'smsq_sbas_lang'
5110   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
5120   ex2a$=rep$&'smsq_gold_kbd_abc'
5130   ex2$=rep$&'smsq_gold_driver_most'
5140   ex3a$=rep$&'smsq_gold_driver_ql'
5150   ex3b$=rep$&'smsq_aurora_driver_8'
5160   ex3q$=rep$&'smsq_gold_qimi'
5170   ex3w$=rep$&'smsq_smsq_wman'
5180   ex3sq$=rep$&'smsq_aurora_sysspr'
5190   ex3sh$=rep$&'smsq_gold_sysspr'
5200   ex4$=rep$&'smsq_gold_driver_dv3'
5210   ex5$=rep$&'smsq_gold_driver_nd'
5220   ex6$=rep$&'smsq_gold_driver_nds'
5230   ex8$=rep$&"smsq_home_home"
5240   ex9$=rep$&'smsq_smsq_hotkey'
5250   ex10$=rep$&'smsq_gold_roms'
5260   nl$=rep$&'sys_boot_null'
5270   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex3a$,ex3b$,ex3q$,ex3w$,ex3sh$,ex4$,ex5$,ex6$,ex9$,ex10$,ex8$,ex1$, rep$&'SMSQ_Aurora_SMSQE'
5280 END DEFine aurora_target
5290 :
5300 DEFine PROCedure qx0_target
5310   nl$=rep$&'sys_boot_null'
5320   f$=rep$&'sys_boot_file'
5330   qr$=rep$&'sys_boot_q40_rom'
5340   ld$=rep$&'smsq_smsq_loader'
5350   hi$=rep$&'smsq_q40_hwinit'
5360   os1$=rep$&'smsq_smsq_1mb_os' : REMark change for fast memory
5370   ca$=rep$&'smsq_smsq_cache40c'
5380   caq$=rep$&'smsq_q40_cache'
5390   caq4$=rep$&'smsq_q40_cachemode'
5400   os2$=rep$&'smsq_sbas_control'
5410   ns2$=rep$&'smsq_q40_nasty'
5420   ln1$=rep$&'smsq_smsq_lang'
5430   ln2$=rep$&'smsq_q40_kbd_lang'
5440   ln3$=rep$&'smsq_sbas_lang'
5450   ex1$=rep$&'smsq_sbas_procs_x'
5460   ex2$=rep$&'smsq_q40_driver_dv3'
5470   ex3$=rep$&'smsq_q40_driver_ser'
5480   ex4a$=rep$&'smsq_q40_driver_ql'
5490   ex4b$=rep$&'smsq_q40_driver_16'
5500   ex4w$=rep$&'smsq_smsq_wman'
5510   ex5$=rep$&'smsq_q40_sysspr'
5520   ex9$=rep$&'smsq_smsq_hotkey'
5530   ex10$=rep$&'smsq_home_home'
5540   EW f$, qr$,ld$,hi$,os1$,ca$,caq$,os2$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4a$,ex4b$,ex4w$,ex5$,ex9$,ex10$,ns2$,caq4$, rep$&'smsq_q40_rom'
5550 END DEFine qx0_target
5560 :
5570 DEFine PROCedure qxl_target
5580   IF FOP_IN (rep$&'smsq_qxl_qxlh.exe') <> -7: PRINT 'QXLH.EXE exists'; cmd$: QUIT
5590   p$   = rep$&'sys_boot_file'
5600   bl$  = rep$&'smsq_qxl_qxl2_exe'
5610   hst$ = rep$&'smsq_qxl_host'
5620   hi$  = rep$&'smsq_qxl_hwinit'
5630   ld$  = rep$&'smsq_smsq_loader'
5640   os1$ = rep$&'smsq_smsq_os'
5650   ca$  = rep$&'smsq_smsq_cache'
5660   os2$ = rep$&'smsq_sbas_control'
5670   ns1$ = rep$&'smsq_qxl_nasty_e'
5680   ln1$ = rep$&'smsq_smsq_lang'
5690   ln2$ = rep$&'smsq_qxl_kbd_lang'
5700   ln3$ = rep$&'smsq_sbas_lang'
5710   ex1$ = rep$&'smsq_qxl_procs_x'
5720   ex2$ = rep$&'smsq_qxl_driver_most'  : REMark Keyboard before CON
5730   ex3$ = rep$&'smsq_qxl_driver_ql'
5740   ex3b$ = rep$&'smsq_qxl_driver_16'
5750   ex3w$ = rep$&'smsq_smsq_wman'
5760   ex4$ = rep$&'smsq_qxl_driver_nd'
5770   ex5$ = rep$&'smsq_qxl_driver_dv3e'
5780   ex6$= rep$&'smsq_smsq_hotkey'
5790   ex7$ = rep$&'smsq_qxl_sysspr'
5800   ex8$=rep$&'smsq_home_home'
5810   last$ = rep$&'smsq_qxl_ecache'
5820   dml$ = rep$&'smsq_qxl_dummy'
5830   qm$  = rep$&'qmon_qxl_bin'
5840   r$='ram1_smsqe.exe' : f$=rep$&'smsq_qxl_smsqe.exe'
5850   DELETE f$
5860   EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,last$, r$
5870   EW cct, bl$,r$, f$
5880   DELETE r$
5890   OPEN #4,f$
5900   lenf=FLEN(#4)
5910   lens% = INT ((lenf+511)/512)
5920   lenb% = lenf - lens% * 512
5930   IF lenb%: lenb% = lenb% + 512
5940   BPUT #4\2, lenb% MOD 256, lenb% DIV 256, lens% MOD 256, lens% DIV 256
5950   CLOSE #4
5960 END DEFine qxl_target
5970 :
5980 DEFine PROCedure java_target
5990   f$=rep$&'sys_boot_file'
6000   v$=rep$&'smsq_smsq_vers'
6010   h$=rep$&'smsq_java_host'
6020   rtc$=rep$&'smsq_java_rtc'
6030   ld$=rep$&'smsq_smsq_loader'
6040   hi$=rep$&'smsq_java_hwinit'
6050   os1$=rep$&'smsq_java_smsq_os'
6060   os3$=rep$&'smsq_java_smsq_1mb_os' : REMark must be after os
6070   ca$=rep$&'smsq_smsq_cache'
6080   os2$=rep$&'smsq_sbas_control'
6090   ln1$=rep$&'smsq_smsq_lang'
6100   ln2$=rep$&'smsq_gold_kbd_lang'
6110   ln3$=rep$&'smsq_sbas_lang'
6120   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
6130   ex2$=rep$&'smsq_java_driver_most'
6140   ex3a$=rep$&'smsq_java_driver_ql'
6150   ex3b$=rep$&'smsq_java_driver_8'
6160   ex3c$=rep$&'smsq_java_driver_16'
6170   ex3w$=rep$&'smsq_smsq_wman'
6180   ex7$=rep$&'smsq_smsq_hotkey'
6190   ex8$=rep$&'smsq_gold_sysspr'
6200   ex9$=rep$&'smsq_home_home'
6210   nl$=rep$&'sys_boot_null'
6220   EW f$, v$,h$,ld$,hi$,os1$,os3$,ca$,os2$,rtc$,ln1$,ln2$,ln3$,ex2$,ex3a$,ex3b$,ex3c$,ex3w$,ex1$,ex7$,ex8$,ex9$, rep$&'SMSQ_java_java'
6230 END DEFine java_target
6240 :
6250 DEFine PROCedure qpc_target
6260   p$   = rep$&'sys_boot_file'
6270   hst$ = rep$&'smsq_qpc_host'
6280   hi$  = rep$&'smsq_qpc_hwinit'
6290   ld$  = rep$&'smsq_smsq_loader'
6300   os1$ = rep$&'smsq_smsq_qpc_os'
6310   ca$  = rep$&'smsq_smsq_cache'
6320   os2$ = rep$&'smsq_sbas_control'
6330   ns1$ = rep$&'smsq_qpc_nasty_e'
6340   ln1$ = rep$&'smsq_smsq_lang'
6350   ln2$ = rep$&'smsq_qpc_kbd_lang'
6360   ln3$ = rep$&'smsq_sbas_lang'
6370   ex1$ = rep$&'smsq_sbas_procs_x'
6380   ex2$ = rep$&'smsq_qpc_driver_most'  : REMark Keyboard before CON
6390   ex3$ = rep$&'smsq_qpc_driver_ql'
6400   ex3b$= rep$&'smsq_qpc_driver_16'
6410   ex3c$= rep$&'smsq_qpc_driver_8'
6420   ex3w$= rep$&'smsq_smsq_wman'
6430   ex3s$= rep$&'smsq_qpc_sysspr'
6440   ex4$ = rep$&'smsq_qpc_driver_dv3e'
6450   ex5$ = rep$&'smsq_smsq_hotkey'
6460   ex6$ = rep$&'smsq_qpc_procs_x'
6470   ex7$ = rep$&'smsq_qpc_cdaudio_x'
6480   ex8$ = rep$&'smsq_qpc_dos_x'
6490   ex9$ = rep$&'smsq_qpc_ip_x'
6500   exa$ = rep$&'smsq_home_home'
6510   qm$ =  rep$&"qmon_qpc_smsq"
6520   r$='ram1_smsqe.bin'
6530   EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3c$,ex3w$,ex3s$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$,exa$, r$
6540   COPY_O r$,rep$&'smsq_qpc_smsqe_bin'
6550   DELETE r$
6560   REMark rd$='ram1_smsqe.deb'
6570   REMark EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3c$,ex3w$,ex3s$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$,exa$,qm$, rd$
6580   REMark COPY_O r$,qpc$&'release_smsqe.bin'
6590   REMark COPY_O rd$,qpc$&'exe_smsqe.bin'
6600 END DEFine qpc_target
6610 :
6620 DEFine PROCedure findit
6630 LOCal lp%,a$
6640 RESTORE 6640
6650   DATA p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3c$,ex3w$,ex3s$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$,exa$, r$,""
6660   REPeat lp%
6670     READ a$:IF a$="":EXIT lp%
6680     t%=FOP_IN(a$ )
6690     IF (t%<0)
6700        PRINT "not found "& a$
6710     END IF
6720  END REPeat lp%
6730 END DEFine findit
6740 :
6750 DEFine FuNction find_target% (current%)
6760   IF "SMSQ Generic" INSTR mdata$(current%):RETurn 1
6770   IF "Atari" INSTR mdata$(current%):RETurn 2
6780   IF "GoldCard" INSTR mdata$(current%):RETurn 4
6790   IF "Q40" INSTR mdata$(current%):RETurn 8
6800   IF "QXL" INSTR mdata$(current%):RETurn 16
6810   IF "Aurora" INSTR mdata$(current%):RETurn 32
6820   IF "Java" INSTR mdata$(current%):RETurn 64
6830   IF "Ptr_Gen" INSTR mdata$(current%):RETurn 128
6840   IF "QPC" INSTR mdata$(current%):RETurn 256
6850   RETurn 0                             : REMark huh???
6860 END DEFine find_target%
6870 :
6880 DEFine PROCedure change_size (x%,y%)
6890 LOCal xrel%,yrel%,temp,a$,ttemp%(main_lit%),abs%,yabs%,t%,bt$,mtemp%(0,0)
6900   main_lfl%(size_it%)=1                 : REMark reset to available
6910   DIM mtemp%(DIMN(main_mfl%,1),DIMN(main_mfl%,2))
6920   ARRCOPY main_lfl%,ttemp%              : REMark keep statusses
6930   ARRCOPY main_mfl%,mtemp%
6940   CH_WIN main_defn,xrel%,yrel%          : REMark ptr displacement
6950   xsize%=xsize%-xrel%+3
6960   ysize%=ysize%-yrel%                   : REMark new sizes
6970   t%=2^4+2^5
6980   RPTR xabs%,yabs%,t%,t%,xrel%,yrel%,bt$: REMark get absolute coords
6990   IF xsize%>xsz%-16:xsize%=xsz%-16
7000   IF ysize%>ysz%-16:ysize%=ysz%-16      : REMark max values
7010   DR_UNST main_defn                     : REMark no more wdw defn...
7020   DR_REMV main_defn                     : REMark .... now!
7030   app_addr=0                            : REMark nor any appsub wdw
7040   make_main_wdw                         : REMark make window anew
7050   ARRCOPY ttemp%,main_lfl%
7060   ARRCOPY mtemp%,main_mfl%
7070   t%=lxs*3
7080   xabs%=xabs%-t%:IF xabs%<0:xabs%=0     : REMark position ptr nicely
7090   yabs%=yabs%-8:IF yabs%<0:yabs%=0
7100   SPTR xabs%,yabs%,0                    : REMark now
7110   IF compiled
7120      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
7130   ELSE
7140      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
7150   END IF
7160    POS_PTR main_defn,t%*65536+8,1       : REMark position pointer in wdw
7170 END DEFine change_size
7180 :
7190 REMark as of here, standard functions/procs (except init)
7200 :
7210 DEFine FuNction warning_pos% (mess$,channel%,title$,pos)
7220 REMark warning wdw with positioning
7230   POS_PTR main_defn,pos,-1
7240   RETurn warning%(mess$,channel%,title$)
7250 END DEFine warning_pos%
7260 :
7270 DEFine FuNction warning%(mess$,channel%,title$)
7280 REMark shows a warning message (mess$) in a small window. Hit ESC or OK
7290 REMark to return; returns 0 if ESC, 1 if OK  hit
7300 LOCal item%,lp%,chan%,y%,swnum%
7310   IF channel%
7320     chan%=channel%
7330   ELSE
7340     chan%=FOPEN("con")
7350   END IF
7360   CH_ITEM warn_defn,-3,0,-1,'','Attention'
7370   CH_ITEM warn_defn,-1,0,-1,cancel$,'ESC'
7380   CH_ITEM warn_defn,-1,1,-1,'O','OK'
7390   warn_lfl%(0)=1:warn_lfl%(1)=1
7400   POKE_W warn_defn+46,pink                        : REMark paper colour of wdw
7410   POKE_W warn_defn+44,red                         : REMark border around wdw
7420   DR_PULD warn_defn,-1,-1
7430   DR_IWDF #chan%,warn_defn,0
7440   PAPER#chan%,white :INK#chan%,red
7450   IF title$<>"":PRINT#chan%,title$;" :"
7460   PRINT #chan%,mess$
7470   POS_PTR warn_defn,160*65536+10,-1
7480   REPeat lp%
7490     RD_PTR warn_defn,item%,swnum%,y%,y%,y%,warn_lfl%
7500     IF swnum%=-1:EXIT lp%
7510   END REPeat lp%
7520   IF NOT channel%:CLOSE#chan%
7530   DR_UNST warn_defn
7540   RETurn item%
7550 END DEFine warning%
7560 :
7570 DEFine PROCedure info_pos (mess$,channel%,pos)
7580 REMark warning wdw with positioning
7590   POS_PTR main_defn,pos,-1
7600   info mess$,channel%
7610 END DEFine info_pos
7620 :
7630 DEFine PROCedure info(mess$,channel%)
7640 REMark shows window containing mess$
7650   LOCal chan%
7660   IF channel%
7670     chan%=channel%
7680   ELSE
7690     chan%=FOPEN("con")
7700   END IF
7710   CH_ITEM warn_defn,-3,0,-1,"",'  Info.  '
7720   CH_ITEM warn_defn,-1,0,-1,"",'Info'
7730   CH_ITEM warn_defn,-1,1,-1,"",'Info'
7740   POKE_W warn_defn+44,green                       : REMark border
7750   POKE_W warn_defn+46,light_green                  : REMark paper col
7760   DR_PULD warn_defn,-1,-1
7770   POS_PTR warn_defn,90*65536+70,-1
7780   DR_IWDF #chan%,warn_defn,0
7790   PAPER#chan%,white:INK#chan%,black
7800   PRINT #chan%,mess$
7810   IF NOT channel%:CLOSE#chan%
7820 END DEFine info
7830 :
7840 DEFine PROCedure make_colours (colourway%)
7850 REMark this initialises the GLOBAL variables menu_pap, menu_brd, menu_inv,
7860 REMark menu_ink and menu_bar according to the colourway passed as parameter.
7870 REMark The GLOBAl variable mycolour_mode% must have been set prior to calling
7880 REMark this function, preferrably via the function set_colours%.
7890 REMark Moreover the colours black, white,red, green,wg_stripes, etc...
7900 REMark must have been set prior to calling this procedure:
7910 REMark Preferrably, they were also set via the function set_colours%
7920 :
7930 REMark this function needs the extension WL_MK16 (outptr_bin)
7940 :
7950 REMark the colourways% are:
7960 REMark 0= white/green
7970 REMark 1= black/red
7980 REMark 2= white/red
7990 REMark 3= black/green
8000 REMark 4= use system palette (only available in modes 32 and 33)
8010 :
8020 REMark the Global war mycolour_mode% is:
8030 REMark 0 = we are in QL mode 4
8040 REMark 1 = we are in PAL mode (256 colours)
8050 REMark 2 - we are in 24 bit colour mode
8060 REMark preferrably, this is set via the function set_colours%
8070 :
8080   IF colourway%>7 OR colourway%<0: colourway%=0: REMark must be between 0 & 7
8090 :
8100 REMark first make sure that what we request is possible.
8110 REMark colourway 4 is only possible if we are in 24 bit colour mode
8120 REMark if it is requested in another mode, it is set to 0
8130 :
8140   IF colourway%>3
8150     IF mycolour_mode%< 1
8160        colourway%=0
8170      ELSE
8180        make_my_palette colourway%-4:RETurn   : REMark set colours according to system palette
8190     END IF
8200   END IF
8210 :
8220   SELect ON mycolour_mode%
8230     =0                                  : REMark QL 4 colours
8240       SELect ON colourway%
8250           =0:                           : REMark white/green
8260                 menu_pap=white
8270                 menu_brd=green
8280                 menu_ink=black
8290                 menu_bar=wg_striped
8300                 menu_inv=red
8310           =1:                           : REMark black/red
8320                 menu_pap=black
8330                 menu_brd=red
8340                 menu_ink=white
8350                 menu_bar=br_striped
8360                 menu_inv=green
8370           =2:                           : REMark white/red
8380                 menu_pap=white
8390                 menu_brd=red
8400                 menu_ink=black
8410                 menu_bar=wr_striped
8420                 menu_inv=green
8430           =3:                           : REMark black/green
8440                 menu_pap=black
8450                 menu_brd=green
8460                 menu_ink=white
8470                 menu_bar=bg_striped
8480                 menu_inv=red
8490       END SELect
8500     =1                                  : REMark pal colours
8510       SELect ON colourway%
8520           =0:                           : REMark white/green
8530                 menu_pap=white+256
8540                 menu_brd=green+256
8550                 menu_ink=black+256
8560                 menu_bar=wg_striped+256
8570                 menu_inv=red +256
8580           =1:                           : REMark black/red
8590                 menu_pap=black+256
8600                 menu_brd=red  +256
8610                 menu_ink=white +256
8620                 menu_bar=br_striped +256
8630                 menu_inv=green +256
8640                 omenu_pap=black+256
8650           =2:                           : REMark white/red
8660                 menu_pap=white +256
8670                 menu_brd=red   +256
8680                 menu_ink=black +256
8690                 menu_bar=wr_striped +256
8700                 menu_inv=green +256
8710           =3:                           : REMark black/green
8720                 menu_pap=black +256
8730                 menu_brd=green +256
8740                 menu_ink=white +256
8750                 menu_bar=bg_striped +256
8760                 menu_inv=red   +256
8770       END SELect
8780     =2                                  : REMark 24 bit colours
8790       SELect ON colourway%
8800           =0:                           : REMark white/green
8810                 menu_pap=WL_MK16(white)
8820                 menu_brd=WL_MK16(green)
8830                 menu_ink=WL_MK16(black)
8840                 menu_bar=WL_MK16(wg_striped)
8850                 menu_inv=WL_MK16(red)
8860           =1:                           : REMark black/red
8870                 menu_pap=WL_MK16(black)
8880                 menu_brd=WL_MK16(red)
8890                 menu_ink=WL_MK16(white)
8900                 menu_bar=WL_MK16(br_striped)
8910                 menu_inv=WL_MK16(green)
8920           =2:                           : REMark white/red
8930                 menu_pap=WL_MK16(white)
8940                 menu_brd=WL_MK16(red)
8950                 menu_ink=WL_MK16(black)
8960                 menu_bar=WL_MK16(wr_striped)
8970                 menu_inv=WL_MK16(green)
8980           =3:                           : REMark black/green
8990                 menu_pap=WL_MK16(black)
9000                 menu_brd=WL_MK16(green)
9010                 menu_ink=WL_MK16(white)
9020                 menu_bar=WL_MK16(bg_striped)
9030                 menu_inv=WL_MK16(red)
9040       END SELect
9050   END SELect
9060 :
9070   make_my_old_palette
9080 END DEFine make_colours
9090 :
9100 DEFine PROCedure make_my_palette (cw%)
9110   sp.winbd            = HEX('0200') : REMark Window border
9120   sp.winbg            = HEX('0201') : REMark Window background
9130   sp.winfg            = HEX('0202') : REMark Window foreground
9140   sp.winmg            = HEX('0203') : REMark Window middleground
9150   sp.titlebg          = HEX('0204') : REMark Title background
9160   sp.titletextbg      = HEX('0205') : REMark Title text background
9170   sp.titlefg          = HEX('0206') : REMark Title foreground
9180   sp.litemhigh        = HEX('0207') : REMark Loose item highlight
9190   sp.litemavabg       = HEX('0208') : REMark Loose item available background
9200   sp.litemavafg       = HEX('0209') : REMark Loose item available foreground
9210   sp.litemselbg       = HEX('020a') : REMark Loose item selected background
9220   sp.litemselfg       = HEX('020b') : REMark Loose item  elected foreground
9230   sp.litemunabg       = HEX('020c') : REMark Loose item unavailable background
9240   sp.litemunafg       = HEX('020d') : REMark Loose item unavailable foreground
9250   sp.infwinbd         = HEX('020e') : REMark Information window border
9260   sp.infwinbg         = HEX('020f') : REMark Information window background
9270   sp.infwinfg         = HEX('0210') : REMark Information window foreground
9280   sp.infwinmg         = HEX('0211') : REMark Information window middleground
9290   sp.subinfbd         = HEX('0212') : REMark Subsidiary information window border
9300   sp.subinfbg         = HEX('0213') : REMark Subsidiary information window background
9310   sp.subinffg         = HEX('0214') : REMark Subsidiary information window foreground
9320   sp.subinfmg         = HEX('0215') : REMark Subsidiary information window middleground
9330   sp.appbd            = HEX('0216') : REMark Application window border
9340   sp.appbg            = HEX('0217') : REMark Application window background
9350   sp.appfg            = HEX('0218') : REMark Application window foreground
9360   sp.appmg            = HEX('0219') : REMark Application window middleground
9370   sp.appihigh         = HEX('021a') : REMark Application window item highlight
9380   sp.appiavabg        = HEX('021b') : REMark Application window item available background
9390   sp.appiavafg        = HEX('021c') : REMark Application window item available foreground
9400   sp.appiselbg        = HEX('021d') : REMark Application window item selected background
9410   sp.appiselfg        = HEX('021e') : REMark Application window item selected foreground
9420   sp.appiunabg        = HEX('021f') : REMark Application window item unavailable background
9430   sp.appiunafg        = HEX('0220') : REMark Application window item unavailable foreground
9440   sp.scrbar           = HEX('0221') : REMark Pan/scroll bar
9450   sp.scrbarsec        = HEX('0222') : REMark Pan/scroll bar section
9460   sp.scrbararr        = HEX('0223') : REMark Pan/scroll bar arrow
9470   sp.buthigh          = HEX('0224') : REMark Button highlight
9480   sp.butbd            = HEX('0225') : REMark Button border
9490   sp.butbg            = HEX('0226') : REMark Button background
9500   sp.butfg            = HEX('0227') : REMark Button foreground
9510   sp.hintbd           = HEX('0228') : REMark Hint border
9520   sp.hintbg           = HEX('0229') : REMark Hint background
9530   sp.hintfg           = HEX('022a') : REMark Hint foreground
9540   sp.hintmg           = HEX('022b') : REMark Hint middleground
9550   sp.errbg            = HEX('022c') : REMark Error message background
9560   sp.errfg            = HEX('022d') : REMark Error message foreground
9570   sp.errmg            = HEX('022e') : REMark Error message middleground
9580   sp.shaded           = HEX('022f') : REMark sp.shaded area
9590   sp.3ddark           = HEX('0230') : REMark Dark 3D border shade
9600   sp.3dlight          = HEX('0231') : REMark Light 3D border shade
9610   sp.vertfill         = HEX('0232') : REMark Vertical area fill
9620   IF NOT (IS_OPEN(#0) OR IS_OPEN(#1))
9630     OPEN#1,'con'
9640     SP_JOBPAL -1,cw%
9650     CLOSE#1
9660   ELSE
9670     SP_JOBPAL -1,cw%
9680   END IF
9690 END DEFine make_my_palette
9700 :
9710 DEFine PROCedure make_my_old_palette
9720 REMark this translates the colours used in the wdws into 4 colour mode...
9730   sp.winbd          = menu_brd          : REMark Window border
9740   sp.winbg          = menu_pap          : REMark Window background
9750   sp.winfg          = menu_pap          : REMark Window foreground
9760   sp.titletextbg    = menu_pap          : REMark Title text background
9770   sp.titlefg        = menu_ink          : REMark Title foreground
9780   sp.titlebg        = menu_bar          : REMark Title background
9790   sp.litemhigh      = menu_ink          : REMark Loose item highlight
9800   sp.litemavabg     = menu_pap          : REMark Loose item available background
9810   sp.litemavafg     = menu_ink          : REMark Loex fifiose item available foreground
9820   sp.litemselbg     = menu_brd          : REMark Loose item selected background
9830   sp.litemselfg     = menu_ink          : REMark Loose item selected foreground
9840   sp.litemunabg     = menu_pap          : REMark Loose item unavailable background
9850   sp.litemunafg     = menu_brd          : REMark Loose item unavailable foreground
9860   sp.infwinbd       = menu_brd          : REMark Information window border
9870   sp.infwinbg       = menu_pap          : REMark Information window background
9880   sp.infwinfg       = menu_ink          : REMark Information window foreground
9890   sp.appbd          = menu_brd          : REMark Application window border
9900   sp.appbg          = menu_pap          : REMark Application window background
9910   sp.appfg          = menu_ink          : REMark Application window foreground
9920   sp.appmg          = menu_pap          : REMark Application window middleground
9930   sp.appihigh       = menu_ink          : REMark Application window item highlight
9940   sp.appiavabg      = menu_pap          : REMark Application window item available background
9950   sp.appiavafg      = menu_ink          : REMark Application window item available foreground
9960   sp.appiselbg      = menu_brd          : REMark Application window item selected background
9970   sp.appiselfg      = menu_ink          : REMark Application window item selected foreground
9980   sp.appiunabg      = menu_pap          : REMark Application window item unavailable background
9990   sp.appiunafg      = menu_brd          : REMark Application window item unavailable foreground
10000   sp.scrbar         = menu_ink          : REMark Pan/scroll bar
10010   sp.scrbararr      = menu_brd          : REMark Pan/scroll bar arrow
10020   sp.buthigh        = menu_ink          : REMark Button highlight
10030   sp.butbd          = menu_brd          : REMark Button border
10040   sp.butbg          = menu_pap          : REMark Button background
10050   sp.butfg          = menu_ink          : REMark Button foreground
10060 END DEFine mk_my_old_palette
10070 :
10080 DEFine FuNction getstr$(poste%,drap%,defn,a$)
10090 REMark ouvre une fen‘tre sur un poste de menu dƒliƒ et propose
10100 REMark une cha•ne  ƒditer
10110 REMark les paramtres sont
10120 REMark - le numƒro du poste
10130 REMark - un drapeau si le rƒsultat doit ‘tre un chiffre (si drap%<>0)
10140 REMark - la dƒfinition de travail dans laquelle se trouve le poste
10150 REMark - la cha•ne qu'on peut ƒditer
10160 REMark returns the string or ""
10170 REMark uses the global (!) vars menu_pap% & menu_brd%
10180 REMark
10190   LOCal chan%,b$,res
10200   b$=" ":b$=a$   : REMark c'est curieusement nƒcessaire
10210   chan%=FOPEN("con")
10220   PAPER#chan%,menu_brd
10230   INK#chan%,menu_ink
10240   DR_LWDF#chan%,defn,poste%             : REMark open channel over item
10250   CLS#chan%
10260   res=GET_STR(#chan%,drap%,b$)          : REMark get string
10270   CLOSE#chan%
10280   IF res<0:b$=""                        : REMark ooops
10290   CH_ITEM defn,-1,poste%,-1,"",b$
10300   RETurn b$
10310 END DEFine getstr$
10320 :
10330 DEFine FuNction showcont$(string$,length%)
10340 REMark this adds '...' at the end of a string, to show that it continues
10350 LOCal l%,lengt%
10360   l%=LEN(string$)
10370   IF NOT l%:RETurn ""                   : REMark empty string
10380   lengt%=length% DIV 6
10390   IF l%<=lengt%:RETurn string$          : REMark entire string fits wdw
10400   RETurn string$(1 TO lengt%-3)&"..."
10410 END DEFine showcont$
10420 :
10430 DEFine FuNction get_free_text$(defn,item%,prompt$,length%)
10440 REMark this opens wdw over loose-item item% of the working definition defn
10450 REMark allows free text to be entered
10460 REMark params: working defintion,item over which wdw is to be opened
10470 REMark         first value of string, length of max string in wdw, if this is...
10480 REMark            -1, the length will be determined here
10490 LOCal chan%,string$,tempsizes%(3)
10500   chan%=FOPEN('con')
10510   IF chan%<0:RETurn prompt$
10520   DR_LWDF#chan%,defn,item%              : REMark open wdw over item
10530   PAPER#chan%,menu_brd
10540   INK#chan%,menu_ink
10550   CLS#chan%
10560   string$=WEDIT$(#chan%,prompt$)
10570   IF length%=-1
10580      WSIZE#chan%,tempsizes%
10590      length%=tempsizes%(0)
10600   END IF
10610   CLOSE#chan%
10620   IF length%
10630     CH_ITEM defn,-1,item%,-1,"",showcont$(string$,length%)
10640   ELSE
10650     CH_ITEM defn,-1,item%,-1,"",string$
10660   END IF
10670   RETurn string$
10680 END DEFine get_free_text$
10690 :
10700 DEFine FuNction do_caps(a$,caps%)
10710 REMark set string to capitales etc,caps% determines whether the string....
10720 REMark ...must be lower case (=0), upper case(=1), all words capitalized (=2),
10730 REMark ... first word only capitalized (=3), or nothing(=-1)
10740   SELect ON caps%
10750    =0: MIN a$
10760    =1: MAJ a$
10770    =2: CAP2 a$
10780    =3: CAP a$
10790   END SELect
10800   RETurn a$
10810 END DEFine do_caps
10820 :
10830 DEFine FuNction centr$(ctr$,leng)
10840 REMark this "centres" the string ctr$ (i.e. left padds it with spaces) so that
10850 REMark it is centred in a window of leng width (leng is length in chars)
10860   LOCal l%,a$
10870   a$=ctr$
10880   l%=INT(leng-LEN(a$))/2
10890   IF l%<0:RETurn a$
10900   a$=FILL$(' ',l%)&a$&FILL$(' ',l%)
10910   RETurn a$
10920 END DEFine centr$
10930 :
10940 DEFine FuNction set_colours% (force_mode%)
10950 REMark this sets colour variables according to the colour mode wished, if possible.
10960 REMark force_mode%=
10970 REMark     0 if ql colours wished (mode 4 only)
10980 REMark     1 if pal colours wished
10990 REMark     2 if 24 bit colours wished
11000 REMark This function returns the true QL mode set (4,8,32,33 etc.).
11010 REMark This function also sets the GLOBAL variable mycolour_mode% to the
11020 REMark mode 0,1,2 as above - whatever is possible on the machine, i.e.
11030 REMark if colour_24 is wished on a 4 colour machine (which won't succeed)
11040 REMark this function will set set that variable to 0, i.e. QL mode.
11050 REMark
11060 REMark This function needs the outptr toolkit
11070 LOCal mode%
11080   mode%=RMODE                           : REMark get current mode now
11090   SELect ON force_mode%
11100       = 0                               : REMark mode 4 wished
11110        SELect ON mode%
11120           = 4                           : REMark already on, so do nothing
11130           = 0,1,8 : MODE 4              : REMark old modes, set to 4 colours
11140           = REMAINDER
11150             COLOUR_QL                   : REMark if higher colour mode, set to "mode 4"
11160             MODE 4
11170        END SELect
11180        mode%=0                          : REMark new mode we're in  - 4 colour mode
11190       = 1                               : REMark PALette wished
11200        SELect ON mode%
11210           = 16,32,33 : COLOUR_PAL:mode%=1: REMark we're in high colour mode, but PAL colours wished
11220           = REMAINDER
11230             IF mode%<>4:MODE 4
11240             mode%=0
11250        END SELect
11260       = 2
11270        SELect ON mode%
11280           = 32,33 : COLOUR_24 :mode%=2  : REMark we're in high col mode 24 bit colours
11290           = 16 : COLOUR_PAL : mode%=1
11300           = REMAINDER
11310             IF mode%<>4:MODE 4
11320             mode%=0
11330        END SELect
11340   END SELect
11350   mycolour_mode%=mode%                  : REMark set this global variable now:
11360   REMark now set some colour variables
11370   SELect ON mode%
11380        = 0                              : REMark Ql 4 mode colours
11390         black          = 0
11400         white          = 7
11410         red            = 2
11420         green          = 4
11430         grey           = 255
11440         dull_pink      = 19
11450         Blue           = 0
11460         Magenta        = 2
11470         Yellow         = 7
11480         Cyan           = 0
11490         Dark_slate     = 0
11500         Slate_grey     = 255
11510         Dark_grey      = 255
11520         Light_grey     = 255
11530         Ash_grey       = 255
11540         Dark_red       = 210
11550         light_green    = 220
11560         Mustard        = 242
11570         Dark_green     = 228
11580         Sea_blue       = 0
11590         Dark_blue      = 0
11600         Purple         = 0
11610         Shocking_pink  = 234
11620         Orange         = 234
11630         Lime_green     = 4
11640         Apple_green    = 4
11650         Bright_blue    = 0
11660         Mauve          = 0
11670         Peach          = 7
11680         Light_yellow   = 7
11690         Light_blue     = 0
11700         Sky_blue       = 0
11710         Rose_pink      = 234
11720         pink           = 234
11730         Beige          = 7
11740         Pastel_pink    = 234
11750         Pastel_yellow  = 7
11760         Pastel_green   = 4
11770         Pastel_cyan    = 0
11780         Pastel_blue    = 0
11790         Pastel_rose    = 234
11800         Brick          = 210
11810         Light_khaki    = 7
11820         Dull_green     = 228
11830         Dull_cyan      = 0
11840         Steel_blue     = 0
11850         dull_pink      = 234
11860         Brown          = 0
11870         Khaki          = 7
11880         Dusky_green    = 4
11890         Dusky_blue     = 0
11900         Midnight_blue  = 0
11910         Plum           = 0
11920         Dusky_pink     = 234
11930         Buff           = 0
11940         Avocado        = 4
11950         Dull_turquoise = 4
11960         Dull_blue      = 0
11970         Faded_purple   = 234
11980         Cerise         = 2
11990         Tan_normal     = 0
12000         Grass_green    = 4
12010         Sea_green      = 4
12020         Ultramarine    = 0
12030         Deep_purple    = 234
12040         Light_orange   = 7
12050         light_red      = 2
12060         wg_striped     = 92
12070         br_striped     = 82
12080         wr_striped     = 107
12090         bg_striped     = 100
12100        = 1                      : REMark Colour_pal
12110         black          = 0
12120         white          = 1
12130         red            = 2
12140         green          = 3
12150         Blue           = 4
12160         Magenta        = 5
12170         Yellow         = 6
12180         Cyan           = 7
12190         Dark_slate     = 8
12200         Slate_grey     = 9
12210         Dark_grey      = 10
12220         grey           = 11
12230         Light_grey     = 12
12240         Ash_grey       = 13
12250         Dark_red       = 14
12260         light_green    = 15
12270         Mustard        = 16
12280         Dark_green     = 17
12290         Sea_blue       = 18
12300         Dark_blue      = 19
12310         Purple         = 20
12320         Shocking_pink  = 21
12330         Orange         = 22
12340         Lime_green     = 23
12350         Apple_green    = 24
12360         Bright_blue    = 25
12370         Mauve          = 26
12380         Peach          = 27
12390         Light_yellow   = 28
12400         Light_blue     = 29
12410         Sky_blue       = 30
12420         Rose_pink      = 31
12430         pink           = 32
12440         Beige          = 33
12450         Pastel_pink    = 34
12460         Pastel_yellow  = 35
12470         Pastel_green   = 36
12480         Pastel_cyan    = 37
12490         Pastel_blue    = 38
12500         Pastel_rose    = 39
12510         Brick          = 40
12520         Light_khaki    = 41
12530         Dull_green     = 42
12540         Dull_cyan      = 43
12550         Steel_blue     = 44
12560         dull_pink      = 45
12570         Brown          = 46
12580         Khaki          = 47
12590         Dusky_green    = 48
12600         Dusky_blue     = 49
12610         Midnight_blue  = 50
12620         Plum           = 51
12630         Dusky_pink     = 52
12640         Buff           = 53
12650         Avocado        = 54
12660         Dull_turquoise = 55
12670         Dull_blue      = 56
12680         Faded_purple   = 57
12690         Cerise         = 58
12700         Tan_normal     = 59
12710         Grass_green    = 60
12720         Sea_green      = 61
12730         Ultramarine    = 62
12740         Deep_purple    = 63
12750         pinkish        = 77
12760         light_red      = 109
12770         Light_orange   = 208
12780         wg_striped     = light_green
12790         br_striped     = Dark_red
12800         wr_striped     = light_red
12810         bg_striped     = Dark_green
12820        = 2                      : REMark colour_24
12830         black          = HEX('000000')
12840         white          = HEX('FFFFFF')
12850         red            = HEX('FF0000')
12860         green          = HEX('00FF00')
12870         Blue           = HEX('0000FF')
12880         Magenta        = HEX('FF00FF')
12890         Yellow         = HEX('FFFF00')
12900         Cyan           = HEX('00FFFF')
12910         Dark_slate     = HEX('242424')
12920         Slate_grey     = HEX('494949')
12930         Dark_grey      = HEX('6D6D6D')
12940         grey           = HEX('929292')
12950         Light_grey     = HEX('B6B6B6')
12960         Ash_grey       = HEX('DBDBDB')
12970         Dark_red       = HEX('920000')
12980         light_green    = HEX('B6FFB6')
12990         Mustard        = HEX('929200')
13000         Dark_green     = HEX('009200')
13010         Sea_blue       = HEX('009292')
13020         Dark_blue      = HEX('000092')
13030         Purple         = HEX('920092')
13040         Shocking_pink  = HEX('FF0092')
13050         Orange         = HEX('FF9200')
13060         Lime_green     = HEX('92FF00')
13070         Apple_green    = HEX('00FF92')
13080         Bright_blue    = HEX('0092FF')
13090         Mauve          = HEX('9200FF')
13100         Peach          = HEX('FFB6B6')
13110         Light_yellow   = HEX('FFFFB6')
13120         Light_blue     = HEX('B6FFFF')
13130         Sky_blue       = HEX('B6B6FF')
13140         Rose_pink      = HEX('FFB6FF')
13150         pink           = HEX('FFB6DB')
13160         Beige          = HEX('FFDBB6')
13170         Pastel_pink    = HEX('FFDBDB')
13180         Pastel_yellow  = HEX('FFFFDB')
13190         Pastel_green   = HEX('DBFFDB')
13200         Pastel_cyan    = HEX('DBFFFF')
13210         Pastel_blue    = HEX('DBDBFF')
13220         Pastel_rose    = HEX('FFDBFF')
13230         Brick          = HEX('B66D6D')
13240         Light_khaki    = HEX('B6B66D')
13250         Dull_green     = HEX('6DB66D')
13260         Dull_cyan      = HEX('6DB6B6')
13270         Steel_blue     = HEX('6D6DB6')
13280         dull_pink      = HEX('B66DB6')
13290         Brown          = HEX('6D2424')
13300         Khaki          = HEX('6D6D24')
13310         Dusky_green    = HEX('246D24')
13320         Dusky_blue     = HEX('246D6D')
13330         Midnight_blue  = HEX('24246D')
13340         Plum           = HEX('6D246D')
13350         Dusky_pink     = HEX('B64992')
13360         Buff           = HEX('B69249')
13370         Avocado        = HEX('92B649')
13380         Dull_turquoise = HEX('49B692')
13390         Dull_blue      = HEX('4992B6')
13400         Faded_purple   = HEX('9249B6')
13410         Cerise         = HEX('920049')
13420         Tan_normal     = HEX('924900')
13430         Grass_green    = HEX('499200')
13440         Sea_green      = HEX('009249')
13450         Ultramarine    = HEX('004992')
13460         Deep_purple    = HEX('490092')
13470         Light_orange   = HEX('ffb624')
13480         pinkish        = HEX('C0037B')
13490         light_red      = HEX('FC1729')
13500         wg_striped     = light_green
13510         br_striped     = Dark_red
13520         wr_striped     = light_red
13530         bg_striped     = Dark_green
13540   END SELect
13550   mode%=RMODE
13560   RETurn mode%                     : REMark return current screen mode now
13570 END DEFine set_colours%
13580 :
13590 DEFine FuNction fen_app2 (adresse,tableau$,nr_sects%,selk%,phys%)
13600 REMark les paramtres sont: l'adresse (0 la premire fois)
13610 REMark le tableau de cha•ne, le nombre de sections,
13620 REMark le drapeau si on veut des touches de sƒlection et si sƒlection=action,
13630 REMark un tableau d'entiers contenant les tailles x,y et les origines
13640 REMark et les touches  exclure des touches de sƒlection
13650 LOCal app_attributs%(3,1),temp%,bord%,pap%,ori_x%,ori_y%
13660 LOCal tail_x%,tail_y%,fleches%,barrre%
13670 :
13680   IF adresse:RECHP adresse              : REMark si c'est un 2nd appel, on enlve l'ancienne
13690   tail_x%=phys%(0)                      : REMark (largeur)    Ceci est pour la clartƒ
13700   tail_y%=phys%(1)                      : REMark (hauteur)    seulement, on pourrait
13710   ori_x%=phys%(2)                       : REMark            utiliser directement le
13720   ori_y%=phys%(3)                       : REMark origs x,y    tableau phys% dans AP_MAKE
13730 :
13740   adresse=AP_MAKE2(selk%,tail_x%,tail_y%,ori_x%,ori_y%,nr_sects%,tableau$)
13750   IF adresse<0: RETurn 0                : REMark si erreur, on retourne 0
13760                                         : REMark Maintenant on dƒtermine les attributs
13770   bord%=sp.appbd                        : REMark couleur bordure
13780   pap%=sp.appbg                         : REMark couleur "papier"
13790   touche_sel%=9                         : REMark touche de sƒlection - TAB
13800   fleches%=sp.scrbararr                 : REMark couleur des flches
13810   bar%=sp.scrbar                        : REMark couleur de la barre d'ascenseur
13820   bar_sect%=sp.scrbarsec                : REMark bar section
13830 :
13840   app_attributs%(0,0)=1
13850   app_attributs%(0,1)=sp.appihigh       : REMark larg. & coul. trait poste courant
13860 :
13870   app_attributs%(1,0)=sp.appiunabg
13880   app_attributs%(1,1)=sp.appiunafg      : REMark unavailable
13890 :
13900   app_attributs%(2,0)=sp.appiavabg
13910   app_attributs%(2,1)=sp.appiavafg      : REMark disponible
13920 :
13930   app_attributs%(3,0)=sp.appiselbg
13940   app_attributs%(3,1)=sp.appiselfg      : REMark sƒlectionnƒ
13950 :
13960   er=AP_PARS(adresse,0,bord%,pap%,touche_sel%,fleches%,bar%,bar_sect%,app_attributs%)
13970 :
13980   IF er:RETurn 0
13990 :
14000   RETurn adresse  : REMark on retourne l'adresse
14010 END DEFine fen_app
14020 :
14030 DEFine PROCedure scroll_arrows(rangees%,nbr_objets%,appfen,cty%)
14040 REMark Cette procƒdure dƒcide s'il faut laisser les flches de dƒfilement
14050 REMark params: le nombre de rangƒes, le nombre total d'objets
14060 REMark le pointer vers la sous-fen d'app et la dƒfinition de controle
14070   IF nbr_objets%<=rangees% +1
14080     POKE_W appfen+150,0
14090     cty%(1,2)=nbr_objets%
14100   ELSE
14110     POKE_W appfen+150,6
14120 REMark    cty%(1,2)=rangees%-1             : rem if scroll arrows are there, there is one line less in the window
14130     cty%(1,2)=rangees%
14140  END IF
14150  IF DIMN(cty%,1)=2:cty%(2,2)=cty%(1,2)
14160  cty%(0,1)=1                            : REMark show that definitionhas changed
14170 END DEFine scroll_arrows
14180 :
14190 DEFine PROCedure init
14200 LOCal temp%,targets%,start%,lp%
14210   text=0:sprite=2:blob=4:pattern=6
14220   retr=256:retn=-256:sous1=254:sous2=252:sous3=250
14230   REMark normal: hit = selects/deselects, do = returns & leaves selected
14240   REMark retr = return even on hit, reset item to available after,
14250   REMark retn = return even on hit, hit selects/deselects, do selects
14260 :
14270   hit$=CHR$(1):do$=CHR$(2):cancel$=CHR$(3):sleep$=CHR$(7):wake$=CHR$(8)
14280   help$=CHR$(4):move$=CHR$(5):size$=CHR$(6):cr$=CHR$(10)
14290 :
14300   appwinoffset%=16                      : REMark appsub win additional y offset
14310   xsize%=500                            : REMark x size divisible by 4!
14320   ysize%=200                            : REMark provisional y size%
14330   lxs=24:lys=12                         : REMark standard item x,y, sizes
14340   inf1y=lys+4                           : REMark y orig of inner border
14350   xesp=lxs+2                            : REMark loose items x spacing
14360   nbr_post%=4                           : REMark nbr of item right & left in title bar
14370   main_coul%=SMSCFG(2)                  : REMark colourways
14380   app_coul%=main_coul%                  : REMark preset colourways
14390   temp%=set_colours% (2)                : REMark always try to set highest colours
14400   IF temp%<2 AND main_coul%>3:main_coul%=0 : REMark old wman
14410   make_colours main_coul%               : REMark set all colours now
14420 :
14430   rep$=SMSCFG(1)                        : REMark dir with files
14440   titre$="Dir: "&rep$&" "
14450 :
14460 RESTORE 14460
14470 :
14480 : REMark make loose item attributes
14490 :
14500   DIM std_iattr(3,3)
14510   RD_IATT std_iattr
14520     DATA 1,sp.litemhigh : REMark current item border size & colour
14530     DATA sp.litemunabg,sp.litemunafg,0,0 : REMark unavailable item paper & ink
14540     DATA sp.litemavabg,sp.litemavafg,0,0 : REMark   available         "    "       "
14550     DATA sp.litemselbg,sp.litemselfg,0,0 : REMark   selected
14560 :
14570 : REMark make data for sprites - uses the modified RD_SPRT function
14580 : REMark
14590 : REMark
14600 :
14610   espace_vide=ALCHP(100)                : REMark GLOBAL var for 0 mask sprites!
14620 :
14630   main_sprite=RD_SPRT(0)                : REMark 0 = normal mask
14640     DATA 11,13,6,5,4
14650     DATA '     www     '
14660     DATA '     waw     '
14670     DATA '     waw     '
14680     DATA '     waw     '
14690     DATA 'wwwwwa awwwww'
14700     DATA 'waaaa   aaaaw'
14710     DATA 'wwwwwa awwwww'
14720     DATA '     waw     '
14730     DATA '     waw     '
14740     DATA '     waw     '
14750     DATA '     www     '
14760 :
14770   move_sprite=RD_SPRT(1)                : REMark 1 = 0 mask
14780   IF temp%>1:move_sprite=6              : REMark or system sprite!
14790     DATA 10,14,5,4,4
14800     DATA 'aaaaaaaaaa    '
14810     DATA 'awwwwwwwwa    '
14820     DATA 'awwrrrrwwa    '
14830     DATA 'awwrrrrwwaaaaa'
14840     DATA 'awwrrwwwwwwwwa'
14850     DATA 'awwwwwwwwrrwwa'
14860     DATA 'aaaaawwrrrrwwa'
14870     DATA '    awwrrrrwwa'
14880     DATA '    awwwwwwwwa'
14890     DATA '    aaaaaaaaaa'
14900 :
14910   zzz_sprite= RD_SPRT(1)
14920   IF temp%>1:zzz_sprite=10
14930     DATA 7,15,0,0,4
14940     DATA 'wwwww          '
14950     DATA '   w           '
14960     DATA '  w  wwww      '
14970     DATA ' w     w       '
14980     DATA 'wwwww w  wwww  '
14990     DATA '     wwww  w   '
15000     DATA '          wwww '
15010 :
15020  size_sprite=RD_SPRT(1)
15030  IF temp%>1:size_sprite=7
15040     DATA 9,14,5,4,4
15050     DATA 'aaaaaaaaaaaaaa'
15060     DATA 'awwwwwwwwwwwwa'
15070     DATA 'awwrrrrrrrrwwa'
15080     DATA 'awwrrrrrrrrwwa'
15090     DATA 'awwrrwwwwwwwwa'
15100     DATA 'awwrrwwrrrrwwa'
15110     DATA 'awwrrwwrrrrwwa'
15120     DATA 'awwwwwwwwwwwwa'
15130     DATA 'aaaaaaaaaaaaaa'
15140 :
15150   targets%=0
15160   REMark IF NOT compiled:cmd$="-tj-tg-st-mk-as"
15170   quit_always%=0:quit_no_errors%=1:autostart%=0
15180   domakeit%=0
15190   all_targets%=0
15200   force_assembly$=""
15210   IF cmd$<>''
15220       IF '-qa' INSTR cmd$:quit_always%=1
15230       IF '-q0' INSTR cmd$:quit_no_errors%=1
15240       IF '-as' INSTR cmd$:autostart%=1
15250       IF '-st' INSTR cmd$:select_all%=1
15260       IF '-mk' INSTR cmd$:do_make_it%=1
15270       IF '-tg' INSTR cmd$:taedrgets%=targets%+1 : REMark Generic
15280       IF '-ta' INSTR cmd$:targets%=targets%+2 : REMark Atari
15290       IF '-to' INSTR cmd$:targets%=targets%+4 : REMark gOld
15300       IF '-tq' INSTR cmd$:targets%=targets%+8 : REMark Q40
15310       IF '-tx' INSTR cmd$:targets%=targets%+16 : REMark qXl
15320       IF '-tu' INSTR cmd$:targets%=targets%+32 : REMark aUrora
15330       IF '-tj' INSTR cmd$:targets%=targets%+64 : REMark Java
15340       IF '-tr' INSTR cmd$:targets%=targets%+128 : REMark ptRgen
15350       IF '-tc' INSTR cmd$:targets%=targets%+256 : REMark qpC
15360       IF '-at' INSTR cmd$:targets%=255+256  : REMark all targets
15370       IF '-fa' INSTR cmd$:force_assembly$=" -f": REMark force assembly of all files
15380   ELSE
15390     targets%=1
15400   END IF
15410   make_my_data targets%
15420   make_main_wdw
15430   IF (select_all%):main_lfl%(all_it%)=129:set_statusses
15440   start%=1
15450   FOR lp%=target_its% TO target_its%+8
15460     IF targets% && start%: main_lfl%(lp%)=129
15470     start%=start%*2
15480   END FOR lp%
15490   IF do_make_it%:main_lfl%(make_it%)=129
15500 :
15510 END DEFine init
15520 :
15530 DEFine PROCedure make_main_wdw
15540 REMark make the main window
15550 LOCal main_lot,main_iwt,main_iot1
15560 :
15570   REMark first make sure of the wdw sizes (we also come here from change_size!)
15580   xsize%=xsize%-(xsize% MOD 4)                    : REMark make into nbr divisible by 4
15590   IF xsize%<500:xsize%=500
15600 :
15610   REMark calculate the y size of the wdw so that it always looks nice, in that
15620   REMark the rows in the appsub always fill the appsub entirely
15630   ysize%=ysize%+(ysize%&&1)             : REMark make into even number
15640   IF ysize%<100:ysize%=100              : REMark predetermioned min ysize% before calculations
15650   ysize%=ysize%-inf1y-14-appwinoffset%
15660   rangees=ysize% DIV 12                 : REMark GLOBAL var: nbr of rows in appsub
15670   IF rangees>data_count%-1:rangees=data_count%-1
15680   IF rangees<3:rangees=3
15690   ysize%=rangees*12+inf1y+14 +appwinoffset%
15700 :
15710   REMark first calc info obj size - this also influences one loose item, used
15720   REMark in the title bar
15730   longueur_titre=LEN(titre$)*6          : REMark length in pixels
15740   titre_x=INT((xsize%-longueur_titre)/2): REMark x orig
15750 :
15760   REMark first the loose items
15770 :
15780   yinf1y=inf1y+4
15790   esc%=0:move_it%=esc%+1:size_it%=move_it%+1
15800   sleep_it%=size_it%+1:all_it%=sleep_it%+1:go_it%=all_it%+1
15810   dir_it%=go_it%+1:target_its%=dir_it%+1:make_it%=target_its%+9
15820 :
15830 RESTORE 15830
15840   REMark reset data pointer to here
15850 :
15860   temp%=(xsize%-4) DIV 9
15870   main_lit%=16:DIM main_lfl%(main_lit%)   : REMark there are 16 items!
15880   main_lot=RD_LOT(std_iattr,main_lit%)
15890     DATA lxs,lys,4+xesp*0,2,0,0,cancel$,text+retr,'ESC'
15900     DATA lxs,lys,4+xesp*1,2,0,0,move$,sprite+retr,move_sprite
15910     DATA lxs,lys,4+xesp*2,2,0,0,size$,sprite+retn,size_sprite
15920     DATA lxs,lys,xsize%-2-xesp*1,2,0,0,sleep$,sprite+retr,zzz_sprite
15930     DATA lxs,lys,xsize%-2-xesp*3,2,0,0,'A',sous1+retn,"All"
15940     DATA lxs,lys,xsize%-2-xesp*4,2,0,0,'O',sous1+retn,"OK"
15950     DATA 6*6,lys-2,titre_x+30,3,0,0   ,'D',text+retn,rep$
15960     DATA 8*6,lys,8,inf1y+2,0,0,        'G',sous1+retn,"Generic"
15970     DATA 8*6,lys,8+temp%,inf1y+2,0,0,  'T',sous2+retn,"Atari"
15980     DATA 8*6,lys,8+temp%*2,inf1y+2,0,0,'L',sous3+retn,"GoldCard"
15990     DATA 8*6,lys,8+temp%*3,inf1y+2,0,0,'Q',sous1+retn,"Q40/Q60"
16000     DATA 8*6,lys,8+temp%*4,inf1y+2,0,0,'X',sous2+retn,"QXL"
16010     DATA 8*6,lys,8+temp%*5,inf1y+2,0,0,'U',sous2+retn,"Aurora"
16020     DATA 8*6,lys,8+temp%*6,inf1y+2,0,0,'J',sous1+retn,"Java"
16030     DATA 8*6,lys,8+temp%*7,inf1y+2,0,0,'P',sous1+retn,"PtrGen"
16040     DATA 8*6,lys,8+temp%*8,inf1y+2,0,0,'C',sous3+retn,"QPC"
16050     DATA 4*6,lys,xsize%-2-xesp*2,2,0,0,'M',sous1+retn,"Make"
16060 :
16070   REMark now info wdw objects - here only one, the title
16080   REMark make the title data: calculate size and x position of this object
16090 :
16100   main_iot1=RD_IOT(0)
16110     DATA longueur_titre,10,4,INT((lys-10)/2),sous1,sp.titlefg,0,0,titre$
16120 :
16130   REMark now the info wdws
16140 :
16150   main_iwt=RD_IWT(2)                              : REMark there are 3
16160 :
16170 REMark first the title bar
16180 :
16190     DATA xsize%-8-(2*nbr_post%*xesp),lys+2,xesp*nbr_post%+4,0         : REMark x,y sizes & origs
16200     DATA 0,0,sp.infwinbd,sp.titlebg     : REMark shadow, border size & col,paper col
16210     DATA 0                              : REMark object pointer (none)
16220 :
16230 REMark now wdw containing title itself
16240 :
16250     DATA longueur_titre+8,lys,titre_x-4,2
16260     DATA 0,0,sp.infwindbd,sp.infwinbg
16270     DATA main_iot1                      : REMark one object
16280 :
16290 REMark inner border
16300 :
16310     DATA xsize%-8,ysize%-inf1y-2,4,inf1y
16320     DATA 0,1,sp.infwinbd,sp.infwinbg
16330     DATA 0
16340 :
16350 REMark now the appsub wdw - use the OUTPTR functions for this
16360 :
16370   app_addr=0                            : REMark there is none yet
16380   mk_main_app 1                         : REMark (this destroys my DATA pointer)
16390   main_app_list = app_addr+180          : REMark list built by OUTPTR
16400 :
16410 : REMark  working defn
16420 :
16430 RESTORE 16430
16440                                         : REMark reset DATA pointer (due to Basic Linker, restore must be ALONE on the line)
16450   main_defn=RD_WDEF
16460     DATA 2,1,sp.winbd,sp.winbg          : REMark shad,border size & col,pap col
16470     DATA xsize%,ysize%,0,0    : REMark x,y size & initial ptr pos
16480     DATA main_sprite,main_lot,main_iwt,main_app_list : REMark sprite,loose items, info wins, appsubs
16490 END DEFine make_main_wdw
16500 :
16510 DEFine PROCedure warn_make
16520 REMark makes warning & info window
16530 REMark uses global vars green etc...
16540 REMark SETS the global var warning_width% (nbr of chars in one line in wdw)
16550 LOCal lys%,warn_lot,warn_iot1,warn_iwt
16560 LOCal x%,y%,warning_paper,warning_brd
16570   RESTORE 16570
16580   x%=188:y%=82
16590   warning_paper=menu_pap
16600   warning_brd=pink                      : REMark pink
16610 REMark  if menu_brd=4:warning_brd=light_green      : rem light green
16620   lys%=10
16630   DIM warn_lfl%(1)
16640   warn_lot=RD_LOT(std_iattr,1)
16650     DATA 30,lys%,4,4,0,0,cancel$,text+retr,'ESC'
16660     DATA 30,lys%,x%-34,4,0,0,'O',text+retr,"OK"
16670 :
16680   warn_iot1=RD_IOT (0)
16690     DATA 12*9,10,0,1,text,red,2,0,'Attention'
16700 :
16710   warn_iwt=RD_IWT(1)
16720     DATA x%-16,y%-32,8,24
16730     DATA 0,1,red,warning_paper
16740     DATA 0
16750 :
16760     DATA 120,12,(x%-112) DIV 2,4
16770     DATA 0,1,menu_brd,warninggpaper
16780     DATA warn_iot1
16790 :
16800   warn_defn=RD_WDEF
16810     DATA 2,1,menu_brd,warning_brd
16820     DATA x%,y%,0,0
16830     DATA main_sprite,warn_lot,warn_iwt,0
16840 :
16850   warning_width%=(x%-16) DIV 6                    : REMark width of warning winfow in chars
16860 :
16870 END DEFine warn_make
16880 :
16890 DEFine FuNction RD_SPRT(drap%)
16900 REMark drap% determine si masque 0
16910   LOCal tmp_patt$(32,32)
16920   LOCal xs,ys,xo,yo, md, l
16930   READ ys,xs,xo,yo,md
16940   DIM tmp_patt$(ys,xs)
16950   FOR l=0 TO ys-1:READ tmp_patt$(l)
16960   l=ALCHP(SPRSP(xs,ys))
16970   SPSET l,xo,yo,md,tmp_patt$(0 TO ys-1,1 TO xs)
16980   IF drap%
16990     POKE_L l+16,espace_vide-(l+16)
17000   END IF
17010   RETurn l
17020 END DEFine RD_SPRT
17030 :
17040 DEFine FuNction RD_WDEF
17050 REMark read window defn
17060   LOCal ldef%(3), lspr, lloose, linfo, lappl, lattr%(3)
17070   RD_WATT lattr%
17080   READ ldef%(0), ldef%(1), ldef%(2), ldef%(3)
17090   READ lspr, lloose, linf, lappl
17100   RETurn MK_WDEF (ldef%, lattr%, lspr, lloose, linf, lappl)
17110 END DEFine RD_WDEF
17120 :
17130 DEFine FuNction RD_LOT (lattr,nitem)
17140   LOCal count(3)
17150   LOCal item, ltyp, a$, lsk$
17160   LOCal ldef%(nitem,6), lptr(3,nitem), lstr$(nitem,85)
17170   lsk$=''
17180   FOR item = 0 TO nitem
17190     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
17200     READ ldef%(item,4), ldef%(item,5)
17210     READ a$: lsk$=lsk$ & a$
17220     READ ltyp
17230     ldef%(item,6)=ltyp: ltyp=(ltyp MOD 256)/2
17240     IF ltyp>10 OR ltyp <0::ltyp=0
17250     IF ltyp
17260       READ lptr(ltyp,count(ltyp))
17270     ELSE
17280       READ lstr$(count(0))
17290     END IF
17300     count(ltyp)=count(ltyp)+1
17310   END FOR item
17320   RETurn MK_LIL (lattr, ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), ldef%(TO, 4 TO 5), lsk$, ldef%(TO, 6), lstr$, lptr(1), lptr(2), lptr(3))
17330 END DEFine RD_LOT
17340 :
17350 DEFine FuNction RD_IWT(nitem)
17360   LOCal item,mc1
17370   LOCal ldef%(nitem,3), latt%(nitem,3), lptr(nitem)
17380   FOR item = 0 TO nitem
17390     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
17400     READ latt%(item,0), latt%(item,1), latt%(item,2), latt%(item,3)
17410     READ lptr(item)
17420   END FOR item
17430   RETurn MK_IWL (ldef%, latt%, lptr)
17440 END DEFine RD_IWT
17450 :
17460 DEFine FuNction RD_IOT(nitem)
17470   LOCal count(3)
17480   LOCal item, ltyp, work1, work2
17490   LOCal ldef%(nitem,4), lptr(3,nitem), lstr$(nitem,85)
17500   FOR item = 0 TO nitem
17510     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
17520     READ ltyp
17530     ldef%(item,4)=ltyp:  ltyp=(ltyp MOD 256)/2
17540     IF ltyp >10:ltyp=0
17550     IF ltyp
17560       READ lptr(0,item),lptr(ltyp,count(ltyp))
17570     ELSE
17580       IF mycolour_mode%=2
17590           READ work1,work2,work3
17600           lptr(0,item)=WL_4_IOL(work1,work2,work3)
17610       ELSE
17620           READ work1
17630           READ work2: work1=work1*256+work2
17640           READ work2: lptr(0,item)=work1*256+work2
17650       END IF
17660       READ lstr$(count(0))
17670     END IF
17680     count(ltyp) = count(ltyp) + 1
17690   END FOR item
17700   RETurn MK_IOL (ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), lptr(0), ldef%(TO, 4), lstr$, lptr(1), lptr(2), lptr(3))
17710 END DEFine RD_IOT
17720 :
17730 DEFine PROCedure RD_IATT (lattr)
17740   LOCal i
17750   READ lattr(0,0), lattr(0,1)
17760   FOR i=1 TO 3: READ lattr(i,0), lattr(i,1), lattr(i,2), lattr(i,3)
17770 END DEFine RD_IATT
17780 :
17790 DEFine PROCedure RD_WATT (lattr%)
17800   READ lattr%(0), lattr%(1),lattr%(2),lattr%(3)
17810 END DEFine RD_WATT
17820 :
17830 DEFine PROCedure sa
17840   SAVE_O "dev8_extras_source_SMSQEMake_bas"
17850 END DEFine sa
17860 :
17870 DEFine PROCedure ccrestore
17880 LOCal lp%,k%,a$,c%,r%,out%
17890   SAVE_O ram1_p_bas
17900   c%=FOP_IN("ram1_p_bas")
17910   out%=FOP_OVER("ram1_SMSQEMake_bas")
17920   IF c%<0 OR out%<0:CLOSE:PRINT "can't open files":STOP
17930   REPeat lp%
17940     IF EOF(#c%):EXIT lp%
17950     INPUT#c%,a$
17960     r%="restore current_line" INSTR a$   : REMark xyxyxywtz   --- LEAVE THIS COMMENT!!!!!!!!
17970     IF r%=0:PRINT#out%,a$:NEXT lp%    : REMark no restore, just ga around again
17980     IF "xyxyxywtz" INSTR a$:PRINT#out%,a$:NEXT lp%
17990     t%=" " INSTR a$
18000     IF t%=0
18010       t%=CHR$(9) INSTR a$
18020     END IF
18030     IF t%=0:PRINT#out%,a$:NEXT lp%:REMark what?
18040     lineno$=a$(1  TO t% )
18050     PRINT#out%,lineno$;" ";"RESTORE";" ";lineno$
18060   END REPeat lp%
18070   CLOSE#c%
18080   CLOSE#out%
18090   save_as_sav
18100 END DEFine ccrestore
18110 :
18120 DEFine PROCedure save_as_sav
18130   LOCal c%
18140   c%=FOP_OVER("ram1_sas_bas" )
18150   IF c%<0:PRINT "can't open command file":STOP
18160   PRINT#c%,'10 load "ram1_SMSQEMake_bas"'
18170   CLOSE#c%
18180   c%=FOP_OVER("ram1_sas2_bas" )
18190   IF c%<0:PRINT "can't open second command file":STOP
18200   PRINT#c%,'qsave_o "ram1_SMSQEMake_sav"'
18210   CLOSE#c%
18220   EW ram1_sas_bas,ram1_sas2_bas
18230 END DEFine save_a_sav
18240 :
18250 DEFine PROCedure p
18260   EX "dev8_extras_source_SMSQEMake_bas";"-tj-tg-as-mk-st-q0"
18270 END DEFine p
