10 REMark $$asmb=dev8_extras_source_outptr_bin,0,10
20 REMark $$asmb=dev1_booty_qptr_bin,50,172
30 REMark $$asmb=dev8_extras_source_SMSQEMake_bin,0,10
40 REMark $$stak=20000
50 rem compiled with the Basic_Linker
60 compiled=NOT IS_OPEN(#0)
70 IF NOT compiled
80   SCR_SIZE#0, xsz%,ysz%
90   OUTLN#0,xsz%,ysz%-28,0,28
100   WSET -1
110 END IF
120 init
130 main
140 STOP
150 :
160 DEFine PROCedure main
170 LOCal lp%,item%,event%,swnum%,xrel%,yrel%,item%,lp2%,tot%,errors%
180   IF compiled
190      OPEN#1,'con'
200      SCR_SIZE#1,xsz%,ysz%
210      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
220   ELSE
230      SCR_SIZE#1,xsz%,ysz%
240      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
250   END IF
260   POS_PTR main_defn,40*65536+20,1
270   IF autostart%
280      errors%=do_the_work%
290      IF quit_always%:STOP
300      IF quit_no_errors% AND NOT errors%:STOP
310      if errors%
320           errors%=warning%("There were errors"&chr$(10)&"Check the highlit items",0,"Errors!")
330      endif
340   endif
350   REPeat lp%
360     RD_PTR main_defn,item%,swnum%,event%,xrel%,yrel%,main_lfl%,main_mfl%,main_cty%
370     IF swnum%= -1
380       SELect ON item%
390                 =esc%         : EXIT lp%          : REMark quit prog
400                 =move_it% : CH_WIN main_defn: REMark move
410                 =size_it% : change_size xrel%,yrel% : REMark change size
420                 =sleep_it%: button main_defn,main_lfl%,main_mfl%,main_cty%,"SMSQ/E Make"
430                 =all_it%
440                      set_statusses                : REMark set all appsub item statusses
450                      main_cty%(0,1)=1   : REMark and redraw
460                 =go_it%       : errors%=do_the_work%
470                                 if errors%
480                                    errors%=warning%("There were errors"&chr$(10)&"Check the highlit items",0,"Errors!")
490                                 endif
500                 =dir_it%: make_new_dir item%
510                 =target_its% TO target_its%+8
520                                 start%=1:machines%=0
530                                 FOR lp2%=target_its% TO target_its%+8
540                                     IF main_lfl%(lp2%):machines%=machines%+start%
550                                     start%=start%*2
560                                 END FOR lp2%
570                                 make_my_data machines%      : REMark make data arrays
580                                 mk_main_app                           : REMark make appsub wdw
590                                 main_app_list=app_addr+180
600                                 AP_IN main_defn,main_app_list : REMark put it in
610                                 DR_WDRW main_defn:main_cty%(0,1)=1 : REMark and redraw entire wdw
620       END SELect
630     END IF
640   END REPeat lp%
650   DR_UNST main_defn
660   IF compiled:CLOSE#1
670   RECHP espace_vide
680 END DEFine main
690 :
700 DEFine FuNction do_the_work%
710 REMark this tries to compile all of the selected files
720 REMark if the file was compiled OK, status (of the link file) set to available
730 REMark if the file was not compiled ok, status left selected
740 LOCal lp%,bound%,a$,my_error%,tot_err%
750   bound%=DIMN(main_mfl%,1)              : REMark number of items to check
760   tot_err%=0
770   IF main_lfl%(del_it%)<>0
780      EW "dev8_extras_del_all_bas";"auto stop"
790   END IF
800   FOR lp%=0 TO bound%
810     a$=mdata$(lp%+1)                              : REMark potential file to compile
820     IF a$(1)<>"*" AND main_mfl%(lp%,0)
830        a$=rep$&a$                       : REMark add dir
840        my_error%=compile_and_check% (a$): REMark try to compile
850        IF NOT my_error%
860              main_mfl%(lp%,0)=1
870              DR_ADRW main_defn,0,main_mfl%,main_cty%
880        END IF
890        tot_err%=tot_err%+my_error%
900     END IF
910   END FOR lp%
920   main_cty%(0,1)=1
930   main_lfl%(go_it%)=1
940   IF main_lfl%(make_it%)=128 AND NOT tot_err%:make_targets
950   RETurn tot_err%
960 END DEFine do_the_work%
970 :
980 DEFine FuNction compile_and_check% (a$)
990 REMark this tries to compile a file, using the "make" prog
1000 REMark this returns 1 if errors, else 0
1010 LOCal chan%,is_ok$,ch_len
1020   ch_len=EXEPF_W ("make";a$&" -w -l"&force_assembly$)                   : REMark exec make & wait
1030   IF ch_len<>0
1040      EXEC_W "make";a$&" -w -l"&force_assembly$                 : REMark exec make & wait
1050   END IF
1060   is_ok$=a$(1 TO LEN(a$)-4)             : REMark file name w/o extension
1070   chan%=FOP_IN(is_ok$&'log')            : REMark open log file
1080   IF chan%<0:RETurn 1                             : REMark can't even open it!
1090   ch_len=FLEN(#chan%)                             : REMark file length
1100   GET#chan%\ch_len-10                             : REMark this is where error report is
1110   INPUT#chan%,is_ok$                              : REMark get it
1120   CLOSE#chan%
1130   IF NOT is_ok$=="No errors"
1140     RETurn 1                                      : REMark anything other than "no errors" signals errors
1150   ELSE
1160     RETurn 0
1170   END IF
1180 END DEFine compile_and_check%
1190 :
1200 DEFine PROCedure make_new_dir (poste%)
1210 REMark get athe new dir where the files are located
1220 LOCal quel_rep$
1230   IF MENU_OK
1240     quel_rep$= DIR_SELECT$("The Files are where?",,10,10,main_coul%,app_coul%)
1250   ELSE
1260     quel_rep$=getstr$(poste%,0,main_defn,a_lire$)           : REMark use menu extns if possible
1270   END IF
1280   IF LEN (quel_rep$) >= 5
1290      rep$=quel_rep$(1 TO 5)
1300   END IF
1310   CH_ITEM main_defn,-1,poste%,-1,"",rep$
1320   main_lfl%(poste%)=main_lfl%(poste%)+1
1330 END DEFine make_new_dir
1340 :
1350 DEFine PROCedure make_my_data (what_machine%)
1360 REMark what_machine% = 1 -> generic
1370 REMark what_machine% = 2 -> atari
1380 REMark what_machine% = 4 -> Gold Card
1390 REMark what_machine% = 8 -> Qx0
1400 REMark what_machine% = 16 -> QXL
1410 REMark what_machine% = 32 -> aurora
1420 REMark what_machine% = 64 -> java
1430 REMark what_machine% = 128 -> ptrgen
1440 REMark what_machine% = 256 -> qpc
1450 REMark these may be combined
1460   DIM mdata$(250,50)
1470   REMark first make generic data
1480   data_count%=1                         : REMark general data count (global var)
1490   IF what_machine%&&1
1500     generic_data                        : REMark set up generic data
1510     read_my_datas                       : REMark and read it
1520   END IF
1530   IF what_machine%&&2
1540      atari_data                         : REMark set up machine specific data....
1550      read_my_datas                      : REMark and read it
1560   END IF
1570   IF what_machine%&&4
1580      gc_data                                      : REMark set up machine specific data....
1590      read_my_datas                      : REMark and read it
1600   END IF
1610   IF what_machine%&&8
1620      q40_data                                     : REMark set up machine specific data....
1630      read_my_datas                      : REMark and read it
1640   END IF
1650   IF what_machine%&&16
1660      qxl_data                                     : REMark set up machine specific data....
1670      read_my_datas                      : REMark and read it
1680   END IF
1690   IF what_machine%&&32
1700      aurora_data                        : REMark set up machine specific data....
1710      read_my_datas                      : REMark and read it
1720   END IF
1730   IF what_machine%&&64
1740      java_data                                    : REMark set up machine specific data....
1750      read_my_datas                      : REMark and read it
1760   END IF
1770   IF what_machine%&&128
1780      ptrgen_data                        : REMark set up machine specific data....
1790      read_my_datas                      : REMark and read it
1800   END IF
1810   IF what_machine%&&256
1820      qpc_data                                : REMark set up machine specific data....
1830      read_my_datas                      : REMark and read it
1840   END IF
1850 END DEFine make_my_data
1860 :
1870 DEFine PROCedure read_my_datas
1880 REMark reads datas into the global var mdata$
1890 REMark uses data_count% from make_my_data
1900 LOCal lp%
1910   REPeat lp%
1920     READ mdata$(data_count%)
1930     IF mdata$(data_count%)="":EXIT lp%
1940     data_count%=data_count%+1
1950   END REPeat lp%
1960 END DEFine read_my_datas
1970 :
1980 DEFine PROCedure generic_data
1990 restore 1990
2000   DATA '**** -- SMSQ Generic -- ****'
2010   DATA 'smsq_smsq_loader_link'
2020   DATA 'smsq_smsq_fh_link'
2030   DATA 'smsq_smsq_link'
2040   DATA 'smsq_smsq_cache_link'
2050   DATA 'smsq_sbas_link'
2060   DATA 'smsq_smsq_lang_link'
2070   DATA 'smsq_sbas_lang_link'
2080   DATA 'smsq_sbas_procs_link'
2090   DATA 'smsq_smsq_wman_link'
2100   DATA 'smsq_smsq_hotkey_link'
2110   DATA 'smsq_smsq_vers_link'
2120   DATA 'smsq_home_link'
2130   DATA 'smsq_recent_link'
2140   DATA 'smsq_smsq_1mb_link',''
2150 END DEFine generic_data
2160 :
2170 DEFine PROCedure q40_data
2180 restore 2180
2190   DATA '**** -- Q40/Q60 -- ****'
2200   DATA 'smsq_q40_hwinit_link'
2210   DATA 'smsq_q40_nasty_link'
2220   DATA 'smsq_q40_cache_link'
2230   DATA 'smsq_q40_cachemode_link'
2240   DATA 'smsq_smsq_cache40c_link'
2250   DATA 'smsq_q40_driver_ser_link'
2260   DATA 'smsq_q40_driver_dv3_link'
2270   DATA 'smsq_q40_driver_ql_link'
2280   DATA 'smsq_q40_driver_16_link'
2290   DATA 'smsq_q40_kbd_lang_link'
2300   DATA 'smsq_sbas_procs_link'
2310   DATA 'smsq_q40_sysspr_link',''
2320 END DEFine q40_data
2330 :
2340 DEFine PROCedure atari_data
2350 restore 2350
2360   DATA '**** -- Atari -- ****'
2370   DATA 'smsq_atari_hwinit_link'
2380   DATA 'smsq_atari_nasty_link'
2390   DATA 'smsq_atari_driver_ql_link'
2400   DATA 'smsq_atari_driver_mo_link'
2410   DATA 'smsq_atari_driver_ser_link'
2420   DATA 'smsq_atari_driver_dv3_link'
2430   DATA 'smsq_atari_kbd_lang_link'
2440   DATA 'smsq_atari_sysspr_link'
2450   DATA 'sys_boot_st_host_link',''
2460 END DEFine atari_data
2470 :
2480 DEFine PROCedure gc_data
2490 restore 2490
2500   DATA '**** -- (Super)GoldCard -- ****'
2510   DATA 'smsq_gold_host_link'
2520   DATA 'smsq_gold_hwinit_link'
2530   DATA 'smsq_gold_nasty_link'
2540   DATA 'smsq_gold_nasty_s_link'
2550   DATA 'smsq_gold_driver_ql_link'
2560   DATA 'smsq_gold_driver_8_link'
2570   DATA 'smsq_gold_driver_most_link'
2580   DATA 'smsq_gold_driver_nd_link'
2590   DATA 'smsq_gold_driver_nds_link'
2600   DATA 'smsq_gold_driver_dv3_link'
2610   DATA 'smsq_gold_kbd_lang_link'
2620   DATA 'smsq_gold_kbd_abc_lng_link'
2630   DATA 'smsq_gold_kbd_abc_link'
2640   DATA 'smsq_gold_roms_link'
2650   DATA 'smsq_gold_qimi_link'
2660   DATA 'smsq_gold_sysspr_link',''
2670 END DEFine gc_data
2680 :
2690 DEFine PROCedure aurora_data
2700 restore 2700
2710   DATA '**** -- Aurora -- ****'
2720   DATA 'smsq_gold_host_link'
2730   DATA 'smsq_gold_hwinit_link'
2740   DATA 'smsq_gold_nasty_link'
2750   DATA 'smsq_gold_nasty_s_link'
2760   DATA 'smsq_aurora_driver_8_link'
2770   DATA 'smsq_gold_driver_ql_link'
2780   DATA 'smsq_gold_driver_most_link'
2790   DATA 'smsq_gold_driver_nd_link'
2800   DATA 'smsq_gold_driver_nds_link'
2810   DATA 'smsq_gold_driver_dv3_link'
2820   DATA 'smsq_gold_kbd_lang_link'
2830   DATA 'smsq_gold_kbd_abc_lng_link'
2840   DATA 'smsq_gold_kbd_abc_link'
2850   DATA 'smsq_gold_roms_link'
2860   DATA 'smsq_gold_qimi_link'
2870   DATA 'smsq_aurora_sysspr_link'
2880   DATA 'smsq_gold_sysspr_link',''
2890 END DEFine aurora_data
2900 :
2910 DEFine PROCedure qxl_data
2920 restore 2920
2930   DATA '**** -- QXL -- ****'
2940   DATA 'smsq_qxl_host_link'
2950   DATA 'smsq_qxl_hwinit_link'
2960   DATA 'smsq_qxl_nasty_e_link'
2970   DATA 'smsq_qxl_driver_ql_link'
2980   DATA 'smsq_qxl_driver_16_link'
2990   DATA 'smsq_qxl_driver_most_link'
3000   DATA 'smsq_qxl_driver_nd_link'
3010   DATA 'smsq_qxl_procs_link'
3020   DATA 'smsq_qxl_driver_dv3e_link'
3030   DATA 'smsq_qxl_kbd_lang_link'
3040   DATA 'smsq_qxl_ecache_link'
3050   DATA 'smsq_qxl_sysspr_link',''
3060 END DEFine qxl_data
3070 :
3080 DEFine PROCedure java_data
3090 restore 3090
3100   DATA '**** -- Java -- ****'
3110   DATA 'smsq_java_host_link'
3120   DATA 'smsq_java_smsq_link'
3130   DATA 'smsq_java_smsq_1mb_link'
3140   DATA 'smsq_java_hwinit_link'
3150   DATA 'smsq_java_driver_ql_link'
3160   DATA 'smsq_java_driver_8_link'
3170   DATA 'smsq_java_driver_16_link'
3180   DATA 'smsq_java_driver_most_link'
3190   DATA 'smsq_java_driver_dv3e_link'
3200   DATA 'smsq_java_ip_link'
3210   DATA 'smsq_gold_kbd_lang_link'
3220   DATA 'smsq_gold_sysspr_link',''
3230 END DEFine java_data
3240 :
3250 DEFine PROCedure qpc_data
3260 restore 3260
3270   DATA "**** -- QPC -- ****"
3280   DATA 'smsq_qpc_host_link'
3290   DATA 'smsq_qpc_hwinit_link'
3300   DATA 'smsq_qpc_nasty_e_link'
3310   DATA 'smsq_qpc_driver_ql_link'
3320   DATA 'smsq_qpc_driver_16_link'
3330   DATA 'smsq_qpc_driver_8_link'
3340   DATA 'smsq_qpc_driver_ql_link'
3350   DATA 'smsq_qpc_driver_most_link'
3360   DATA 'smsq_qpc_procs_link'
3370   DATA 'smsq_qpc_driver_dv3e_link'
3380   DATA 'smsq_qpc_kbd_lang_link'
3390   DATA 'smsq_qpc_dos_link'
3400   DATA 'smsq_qpc_ip_link'
3410   DATA 'smsq_qpc_cdaudio_link'
3420   DATA 'smsq_smsq_qpc_link'
3430   DATA 'smsq_qpc_sysspr_link',''
3440 END DEFine qpc_data
3450 :
3460 DEFine PROCedure ptrgen_data
3470 restore 3470
3480   DATA "**** -- PtrGen -- ****"
3490   DATA "ee_wman_link","ee_ptr_link","ee_hot_german_link","ee_hot_french_link"
3500   DATA "ee_hot_english_link",""
3510 END DEFine ptrgen_data
3520 :
3530 DEFine PROCedure make_targets
3540 REMark targets are only made if there was no error during compilation
3550 REMark to achieve this, a primitive approach is used:
3560 REMark a check is made whether an appsub menu item status is still selected
3570 REMark if it is, then the
3580   LOCal lp%,current_target%,tot_targets%
3590   tot_targets%=0                        : REMark no targets to make yet
3600   current_target%=0                               : REMark and no target yet
3610   FOR lp%=0 TO data_count%-2
3620     IF main_mfl%(lp%,0)=16
3630       REMark we're covering a new target
3640       tot_targets%=tot_targets%+current_target% : REMark show old target
3650       current_target%=find_target%(lp%+1) : REMark get new target we're covering
3660     ELSE
3670       IF main_mfl%(lp%,0)<>0:current_target%=0
3680     END IF
3690   END FOR lp%
3700   tot_targets%=tot_targets%+current_target%
3710   REMark at the end, here, tot_targets holds a bitmap
3720   REMark of targets for which compilation was ok
3730   IF (machines%&&1) AND NOT (tot_targets% && 1):RETurn : REMark generic was asked for but not compiled correctly!
3740   IF tot_targets%&&2:atari_target
3750   IF tot_targets%&&4:gc_target
3760   IF tot_targets%&&8:qx0_target
3770   IF tot_targets%&&16:qxl_target
3780   IF tot_targets%&&32:aurora_target
3790   IF tot_targets%&&64:java_target
3800   REMark IF tot_targets%&&128:ptr_gen_target  : rem no need for this
3810   IF tot_targets%&&256:qpc_target
3820 END DEFine make_targets
3830 :
3840 DEFine PROCedure atari_target
3850   cmdl$='m'
3860   p$=rep$&'sys_boot_st_flp'
3870   f$=rep$&'sys_boot_file'
3880   ah$=rep$&'sys_boot_st_host'
3890   ld$=rep$&'smsq_smsq_loader'
3900   hi$=rep$&'smsq_atari_hwinit'
3910   os1f$=rep$&'smsq_smsq_fh_os'
3920   os1$=rep$&'smsq_smsq_os'
3930   ca$=rep$&'smsq_smsq_cache'
3940   os2$=rep$&'smsq_sbas_control'
3950   ns1$=rep$&'smsq_atari_nasty'
3960   ln1$=rep$&'smsq_smsq_lang'
3970   ln2$=rep$&'smsq_atari_kbd_lang'
3980   ln3$=rep$&'smsq_sbas_lang'
3990   ex1$=rep$&'smsq_sbas_procs_x'
4000   ex2$=rep$&'smsq_atari_driver_dv3'
4010   ex3$=rep$&'smsq_atari_driver_ser'
4020   ex4$=rep$&'smsq_atari_driver_mono'
4030   ex5$=rep$&'smsq_atari_driver_ql'
4040   ex6$=rep$&'smsq_smsq_wman'
4050   ex7$=rep$&'smsq_smsq_hotkey'
4060   ex8$=rep$&'smsq_atari_sysspr'
4070   ex9$=rep$&'smsq_home_home'
4080   ex10$=rep$&'smsq_recent_recent'
4090   nl$=rep$&'sys_boot_null'
4100   IF 'm' INSTR cmdl$ = 0: ex4$ = nl$
4110   EW f$, ah$,ld$,hi$,os1f$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$, ex10$,rep$&'smsq_atari_SMSQ.PRG'
4120 END DEFine atari_target
4130 :
4140 DEFine PROCedure gc_target
4150 REMark there are  2 targets for the Goldcard!
4160   f$=rep$&'sys_boot_file'
4170   v$=rep$&'smsq_smsq_vers'
4180   h$=rep$&'smsq_gold_host'
4190   ld$=rep$&'smsq_smsq_loader'
4200   hi$=rep$&'smsq_gold_hwinit'
4210   os1$=rep$&'smsq_smsq_os'
4220   ca$=rep$&'smsq_smsq_cache'
4230   os2$=rep$&'smsq_sbas_control'
4240   ns1$=rep$&'smsq_gold_nasty'
4250   ns2$=rep$&'smsq_gold_nasty_s'
4260   ln1$=rep$&'smsq_smsq_lang'
4270   ln2$=rep$&'smsq_gold_kbd_lang'
4280   ln2a$=rep$&'smsq_gold_kbd_abc_lang'
4290   ln3$=rep$&'smsq_sbas_lang'
4300   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
4310   ex2a$=rep$&'smsq_gold_kbd_abc'
4320   ex2$=rep$&'smsq_gold_driver_most'
4330   ex3a$=rep$&'smsq_gold_driver_ql'
4340   ex38$=rep$&'smsq_gold_driver_8'
4350   ex3q$=rep$&'smsq_gold_qimi'
4360   ex3w$=rep$&'smsq_smsq_wman'
4370   ex4$=rep$&'smsq_gold_driver_dv3'
4380   ex5$=rep$&'smsq_gold_driver_nd'
4390   ex6$=rep$&'smsq_gold_driver_nds'
4400   ex7$=rep$&'smsq_smsq_hotkey'
4410   ex8$=rep$&'smsq_gold_sysspr'
4420   ex9$=rep$&'smsq_home_home'
4430   ex10$=rep$&'smsq_recent_recent'
4440   nl$=rep$&'sys_boot_null'
4450   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex3a$,ex3q$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,ex1$,ex9$,ex10$, rep$&'SMSQ_GOLD_gold'
4460   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex38$,ex3q$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,ex1$,ex9$,ex10$, rep$&'SMSQ_GOLD_gold8'
4470 END DEFine gc_target
4480 :
4490 DEFine PROCedure aurora_target
4500   f$=rep$&'sys_boot_file'
4510   v$=rep$&'smsq_smsq_vers'
4520   h$=rep$&'smsq_gold_host'
4530   ld$=rep$&'smsq_smsq_loader'
4540   hi$=rep$&'smsq_gold_hwinit'
4550   os1$=rep$&'smsq_smsq_os'
4560   ca$=rep$&'smsq_smsq_cache'
4570   os2$=rep$&'smsq_sbas_control'
4580   ns1$=rep$&'smsq_gold_nasty'
4590   ns2$=rep$&'smsq_gold_nasty_s'
4600   ln1$=rep$&'smsq_smsq_lang'
4610   ln2$=rep$&'smsq_gold_kbd_lang'
4620   ln2a$=rep$&'smsq_gold_kbd_abc_lang'
4630   ln3$=rep$&'smsq_sbas_lang'
4640   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
4650   ex2a$=rep$&'smsq_gold_kbd_abc'
4660   ex2$=rep$&'smsq_gold_driver_most'
4670   ex3a$=rep$&'smsq_gold_driver_ql'
4680   ex3b$=rep$&'smsq_aurora_driver_8'
4690   ex3q$=rep$&'smsq_gold_qimi'
4700   ex3w$=rep$&'smsq_smsq_wman'
4710   ex3sq$=rep$&'smsq_aurora_sysspr'
4720   ex3sh$=rep$&'smsq_gold_sysspr'
4730   ex4$=rep$&'smsq_gold_driver_dv3'
4740   ex5$=rep$&'smsq_gold_driver_nd'
4750   ex6$=rep$&'smsq_gold_driver_nds'
4760   ex8$=rep$&"smsq_home_home"
4770   ex9$=rep$&'smsq_smsq_hotkey'
4780   ex10$=rep$&'smsq_gold_roms'
4790   ex11$=rep$&'smsq_recent_recent'
4800   nl$=rep$&'sys_boot_null'
4810   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex3a$,ex3b$,ex3q$,ex3w$,ex3sh$,ex4$,ex5$,ex6$,ex9$,ex10$,ex8$,ex1$,ex11$, rep$&'SMSQ_Aurora_SMSQE'
4820 END DEFine aurora_target
4830 :
4840 DEFine PROCedure qx0_target
4850   nl$=rep$&'sys_boot_null'
4860   f$=rep$&'sys_boot_file'
4870   qr$=rep$&'sys_boot_q40_rom'
4880   ld$=rep$&'smsq_smsq_loader'
4890   hi$=rep$&'smsq_q40_hwinit'
4900   os1$=rep$&'smsq_smsq_1mb_os' : REMark change for fast memory
4910   ca$=rep$&'smsq_smsq_cache40c'
4920   caq$=rep$&'smsq_q40_cache'
4930   caq4$=rep$&'smsq_q40_cachemode'
4940   os2$=rep$&'smsq_sbas_control'
4950   ns2$=rep$&'smsq_q40_nasty'
4960   ln1$=rep$&'smsq_smsq_lang'
4970   ln2$=rep$&'smsq_q40_kbd_lang'
4980   ln3$=rep$&'smsq_sbas_lang'
4990   ex1$=rep$&'smsq_sbas_procs_x'
5000   ex2$=rep$&'smsq_q40_driver_dv3'
5010   ex3$=rep$&'smsq_q40_driver_ser'
5020   ex4a$=rep$&'smsq_q40_driver_ql'
5030   ex4b$=rep$&'smsq_q40_driver_16'
5040   ex4w$=rep$&'smsq_smsq_wman'
5050   ex5$=rep$&'smsq_q40_sysspr'
5060   ex9$=rep$&'smsq_smsq_hotkey'
5070   ex10$=rep$&'smsq_home_home'
5080   ex11$=rep$&'smsq_recent_recent'
5090   EW f$, qr$,ld$,hi$,os1$,ca$,caq$,os2$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4a$,ex4b$,ex4w$,ex5$,ex9$,ex10$,ex11$,ns2$,caq4$, rep$&'smsq_q40_rom'
5100 END DEFine qx0_target
5110 :
5120 DEFine PROCedure qxl_target
5130   IF FOP_IN (rep$&'smsq_qxl_qxlh.exe') <> -7: PRINT 'QXLH.EXE exists'; cmd$: QUIT
5140   p$   = rep$&'sys_boot_file'
5150   bl$  = rep$&'smsq_qxl_qxl2_exe'
5160   hst$ = rep$&'smsq_qxl_host'
5170   hi$  = rep$&'smsq_qxl_hwinit'
5180   ld$  = rep$&'smsq_smsq_loader'
5190   os1$ = rep$&'smsq_smsq_os'
5200   ca$  = rep$&'smsq_smsq_cache'
5210   os2$ = rep$&'smsq_sbas_control'
5220   ns1$ = rep$&'smsq_qxl_nasty_e'
5230   ln1$ = rep$&'smsq_smsq_lang'
5240   ln2$ = rep$&'smsq_qxl_kbd_lang'
5250   ln3$ = rep$&'smsq_sbas_lang'
5260   ex1$ = rep$&'smsq_qxl_procs_x'
5270   ex2$ = rep$&'smsq_qxl_driver_most'  : REMark Keyboard before CON
5280   ex3$ = rep$&'smsq_qxl_driver_ql'
5290   ex3b$ = rep$&'smsq_qxl_driver_16'
5300   ex3w$ = rep$&'smsq_smsq_wman'
5310   ex4$ = rep$&'smsq_qxl_driver_nd'
5320   ex5$ = rep$&'smsq_qxl_driver_dv3e'
5330   ex6$ = rep$&'smsq_smsq_hotkey'
5340   ex7$ = rep$&'smsq_qxl_sysspr'
5350   ex8$ = rep$&'smsq_home_home'
5360   ex10$= rep$&'smsq_recent_recent'
5370   last$ = rep$&'smsq_qxl_ecache'
5380   dml$ = rep$&'smsq_qxl_dummy'
5390   qm$  = rep$&'qmon_qxl_bin'
5400   r$='ram1_smsqe.exe' : f$=rep$&'smsq_qxl_smsqe.exe'
5410   cct$=rep$&"extras_exe_cct"
5420   DELETE f$
5430   EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,ex11$,last$, r$
5440   EW cct$, bl$,r$, f$
5450   DELETE r$
5460   OPEN #4,f$
5470   lenf=FLEN(#4)
5480   lens% = INT ((lenf+511)/512)
5490   lenb% = lenf - lens% * 512
5500   IF lenb%: lenb% = lenb% + 512
5510   BPUT #4\2, lenb% MOD 256, lenb% DIV 256, lens% MOD 256, lens% DIV 256
5520   CLOSE #4
5530 END DEFine qxl_target
5540 :
5550 DEFine PROCedure java_target
5560   f$=rep$&'sys_boot_file'
5570   v$=rep$&'smsq_smsq_vers'
5580   h$=rep$&'smsq_java_host'
5590   ld$=rep$&'smsq_smsq_loader'
5600   hi$=rep$&'smsq_java_hwinit'
5610   os1$=rep$&'smsq_java_smsq_os'
5620   os3$=rep$&'smsq_java_smsq_1mb_os' : REMark must be after os
5630   ca$=rep$&'smsq_smsq_cache'
5640   os2$=rep$&'smsq_sbas_control'
5650   ln1$=rep$&'smsq_smsq_lang'
5660   ln2$=rep$&'smsq_gold_kbd_lang'
5670   ln3$=rep$&'smsq_sbas_lang'
5680   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
5690   ex2$=rep$&'smsq_java_driver_most'
5700   ex3a$=rep$&'smsq_java_driver_ql'
5710   ex3b$=rep$&'smsq_java_driver_8'
5720   ex3c$=rep$&'smsq_java_driver_16'
5730   ex3w$=rep$&'smsq_smsq_wman'
5740   ex4$=rep$&'smsq_java_driver_dv3e'
5750   ex7$=rep$&'smsq_smsq_hotkey'
5760   ex8$=rep$&'smsq_gold_sysspr'
5770   ex9$=rep$&'smsq_home_home'
5780   ex10$ = rep$&'smsq_java_ip_x'
5790   ex11$=rep$&'smsq_recent_recent'
5800   EW f$,h$,ld$,hi$,os1$,os3$,ca$,os2$,ln1$,ln2$,ln3$,ex2$,ex3a$,ex3b$,ex3c$,ex3w$,ex4$,ex1$,ex7$,ex8$,ex9$,ex10$,ex11$, rep$&'SMSQ_java_java'
5810 END DEFine java_target
5820 :
5830 DEFine PROCedure qpc_target
5840   p$   = rep$&'sys_boot_file'
5850   hst$ = rep$&'smsq_qpc_host'
5860   hi$  = rep$&'smsq_qpc_hwinit'
5870   ld$  = rep$&'smsq_smsq_loader'
5880   os1$ = rep$&'smsq_smsq_qpc_os'
5890   ca$  = rep$&'smsq_smsq_cache'
5900   os2$ = rep$&'smsq_sbas_control'
5910   ns1$ = rep$&'smsq_qpc_nasty_e'
5920   ln1$ = rep$&'smsq_smsq_lang'
5930   ln2$ = rep$&'smsq_qpc_kbd_lang'
5940   ln3$ = rep$&'smsq_sbas_lang'
5950   ex1$ = rep$&'smsq_sbas_procs_x'
5960   ex2$ = rep$&'smsq_qpc_driver_most'  : REMark Keyboard before CON
5970   ex3$ = rep$&'smsq_qpc_driver_ql'
5980   ex3b$= rep$&'smsq_qpc_driver_16'
5990   ex3c$= rep$&'smsq_qpc_driver_8'
6000   ex3w$= rep$&'smsq_smsq_wman'
6010   ex3s$= rep$&'smsq_qpc_sysspr'
6020   ex4$ = rep$&'smsq_qpc_driver_dv3e'
6030   ex5$ = rep$&'smsq_smsq_hotkey'
6040   ex6$ = rep$&'smsq_qpc_procs_x'
6050   ex7$ = rep$&'smsq_qpc_cdaudio_x'
6060   ex8$ = rep$&'smsq_qpc_dos_x'
6070   ex9$ = rep$&'smsq_qpc_ip_x'
6080   exa$ = rep$&'smsq_home_home'
6090   exb$ = rep$&'smsq_recent_recent'
6100   qm$ =  rep$&"qmon_qpc_smsq"
6110   r$='ram1_smsqe.bin'
6120   EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3c$,ex3w$,ex3s$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$,exa$,exb$, r$
6130   COPY_O r$,rep$&'smsq_qpc_smsqe.bin'
6140   DELETE r$
6150   REMark rd$='ram1_smsqe.deb'
6160   REMark EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3c$,ex3w$,ex3s$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$,exa$,qm$, rd$
6170   REMark COPY_O r$,qpc$&'release_smsqe.bin'
6180   REMark COPY_O rd$,qpc$&'exe_smsqe.bin'
6190 END DEFine qpc_target
6200 :
6210 DEFine PROCedure findit
6220 LOCal lp%,a$
6230 restore 6230
6240   DATA p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3c$,ex3w$,ex3s$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$,exa$, r$,""
6250   REPeat lp%
6260     READ a$:IF a$="":EXIT lp%
6270     t%=FOP_IN(a$ )
6280     IF (t%<0)
6290        PRINT "not found "& a$
6300     END IF
6310  END REPeat lp%
6320 END DEFine findit
6330 :
6340 DEFine FuNction find_target% (current%)
6350   IF "SMSQ Generic" INSTR mdata$(current%):RETurn 1
6360   IF "Atari" INSTR mdata$(current%):RETurn 2
6370   IF "GoldCard" INSTR mdata$(current%):RETurn 4
6380   IF "Q40" INSTR mdata$(current%):RETurn 8
6390   IF "QXL" INSTR mdata$(current%):RETurn 16
6400   IF "Aurora" INSTR mdata$(current%):RETurn 32
6410   IF "Java" INSTR mdata$(current%):RETurn 64
6420   IF "Ptr_Gen" INSTR mdata$(current%):RETurn 128
6430   IF "QPC" INSTR mdata$(current%):RETurn 256
6440   RETurn 0                                     : REMark huh???
6450 END DEFine find_target%
6460 :
6470 DEFine PROCedure change_size (x%,y%)
6480 LOCal xrel%,yrel%,temp,a$,ttemp%(main_lit%),abs%,yabs%,t%,bt$,mtemp%(0,0)
6490   main_lfl%(size_it%)=1                 : REMark reset to available
6500   DIM mtemp%(DIMN(main_mfl%,1),DIMN(main_mfl%,2))
6510   ARRCOPY main_lfl%,ttemp%              : REMark keep statusses
6520   ARRCOPY main_mfl%,mtemp%
6530   CH_WIN main_defn,xrel%,yrel%                    : REMark ptr displacement
6540   xsize%=xsize%-xrel%+3
6550   ysize%=ysize%-yrel%                             : REMark new sizes
6560   t%=2^4+2^5
6570   RPTR xabs%,yabs%,t%,t%,xrel%,yrel%,bt$: REMark get absolute coords
6580   IF xsize%>xsz%-16:xsize%=xsz%-16
6590   IF ysize%>ysz%-16:ysize%=ysz%-16      : REMark max values
6600   DR_UNST main_defn                               : REMark no more wdw defn...
6610   DR_REMV main_defn                               : REMark .... now!
6620   app_addr=0                                      : REMark nor any appsub wdw
6630   make_main_wdw                         : REMark make window anew
6640   ARRCOPY ttemp%,main_lfl%
6650   ARRCOPY mtemp%,main_mfl%
6660   t%=lxs*3
6670   xabs%=xabs%-t%:IF xabs%<0:xabs%=0     : REMark position ptr nicely
6680   yabs%=yabs%-8:IF yabs%<0:yabs%=0
6690   SPTR xabs%,yabs%,0                              : REMark now
6700   IF compiled
6710      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
6720   ELSE
6730      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
6740   END IF
6750    POS_PTR main_defn,t%*65536+8,1       : REMark position pointer in wdw
6760 END DEFine change_size
6770 :
6780 DEFine FuNction fen_app (adresse,tableau$,nr_sects%,selk%,phys%,a_exclure$)
6790 rem make a typical QPAC2 appsub wdw, with strings in the items
6800 rem the parameters are: the address (0 at first)
6810 rem the string array, the number of sections
6820 rem a flag whether we want selection keystrokes and whether hit=do
6830 rem an integer array with x,y, sizes & origs
6840 rem a string with chars to be excluded from the selection keystrokes
6850 LOCal app_attributs%(3,1),temp%,bord%,pap%,ori_x%,ori_y%
6860 LOCal tail_x%,tail_y%,fleches%,bar%,bar_sect%,arrows%,selkey%
6870 :
6880   IF adresse:RECHP adresse              : rem if this is a second call, release old
6890   tail_x%=phys%(0)                      : rem x size  - just to make this clear
6900   tail_y%=phys%(1)                      : rem y "size"      we could use this directly
6910   ori_x%=phys%(2)                       : rem
6920   ori_y%=phys%(3)                       : rem origs x,y
6930 :
6940   adresse=AP_MAKE(selk%,tail_x%,tail_y%,ori_x%,ori_y%,nr_sects%,tableau$,a_exclure$)
6950   IF adresse<0: RETurn 0                : rem  error, return 0
6960 :
6970   app_attributs%(0,0)=1
6980   app_attributs%(0,1)=sp.appihigh       : rem current appsub wdw item border size & colour
6990 :
7000   bord%=sp.appbd                        : rem border colour
7010   pap%=sp.appbg                         : rem paper colour
7020   selkey%=9                                       : rem selection keystroke, always TAB
7030   arrows%=sp.scrbararr                            : rem arrow colour
7040   bar%=sp.scrbar                        : rem bar colour
7050   bar_sect%=sp.scrbarsec                : rem bar section
7060 :
7070   app_attributs%(0,0)=1
7080   app_attributs%(0,1)=sp.appihigh       : rem current appsub wdw item border size & colour
7090 :
7100   app_attributs%(1,0)=sp.appiunabg
7110   app_attributs%(1,1)=sp.appiunafg      : rem unavailable
7120 :
7130   app_attributs%(2,0)=sp.appiavabg
7140   app_attributs%(2,1)=sp.appiavafg      : rem available
7150 :
7160   app_attributs%(3,0)=sp.appiselbg
7170   app_attributs%(3,1)=sp.appiselfg      : rem selected
7180 :
7190   er=AP_PARS(adresse,0,bord%,pap%,selk%,arrows%,bar%,bar_sect%,app_attributs%)
7200 :
7210   IF er:RETurn 0                        : rem OOOOPS
7220 :
7230   RETurn adresse                        : rem return the address!
7240 END DEFine fen_app
7250 :
7260 def fn warning_pos% (mess$,channel%,title$,pos)
7270 rem warning wdw with pointer positioning
7280   pos_ptr main_defn,pos,-1
7290   return warning%(mess$,channel%,title$)
7300 end def warning_pos%
7310 :
7320 define function warning%(mess$,channel%,title$)
7330 rem shows a warning message (mess$) in a small window. hit esc or ok
7340 rem to return; returns 0 if esc, 1 if ok  hit
7350 local item%,lp%,chan%,y%,swnum%,infot%
7360   if channel%
7370     chan%=channel%
7380   else
7390     chan%=fopen("con")
7400   endif
7410   ch_item warn_defn,-3,0,-1,'','Attention'
7420   ch_item warn_defn,-1,0,-1,cancel$,'ESC'
7430   ch_item warn_defn,-1,1,-1,'O','OK'
7440   warn_lfl%(0)=1:warn_lfl%(1)=1
7450   poke_w warn_defn+44,sp.errmg                           : rem border colour of wdw
7460   poke_w warn_defn+46,sp.errbg                           : rem paper colour of wdw
7470   poke_w warn_wdw_addr+14,sp.errbg             : rem paper of title info wdw
7480   obj_addr=peek_l(warn_wdw_addr+16)            : rem ptr to object list
7490   poke_w obj_addr+10,sp.errfg                            : rem info object text  colour
7500   dr_puld warn_defn,-1,-1
7510   dr_iwdf #chan%,warn_defn,0
7520   wm_paper#chan%,sp.errbg
7530   wm_ink#chan%,sp.errfg
7540   cls#chan%
7550   if title$<>"":print#chan%,title$;" :"
7560   print #chan%,mess$
7570   pos_ptr warn_defn,160*65536+10,-1
7580   repeat lp%
7590     rd_ptr warn_defn,item%,swnum%,y%,y%,y%,warn_lfl%
7600     if swnum%=-1:exit lp%
7610   end repeat lp%
7620   if not channel%:close#chan%
7630   dr_unst warn_defn
7640   return item%
7650 end define warning%
7660 :
7670 def proc info_pos (mess$,channel%,pos)
7680 rem warning wdw with positioning
7690   pos_ptr main_defn,pos,-1
7700   info mess$,channel%
7710 end def info_pos
7720 :
7730 define procedure info(mess$,channel%)
7740 rem shows window containing mess$
7750 local chan%,infot%,obj_addr
7760   if channel%
7770     chan%=channel%
7780   else
7790     chan%=fopen("con")
7800   endif
7810   ch_item warn_defn,-3,0,-1,"",'  Info.  '
7820   ch_item warn_defn,-1,0,-1,"",'Info'
7830   ch_item warn_defn,-1,1,-1,"",'Info'
7840   poke_w warn_defn+44,sp.infwinbg                 : rem paper colour of wdw
7850   poke_w warn_defn+46,sp.winbd                              : rem border around wdw
7860   poke_w warn_wdw_addr+14,sp.infwinbg             : rem paper of title info wdw
7870   obj_addr=peek_l(warn_wdw_addr+16)               : rem ptr to object list
7880   poke_w obj_addr+10,sp.infwinfg                  : rem info object text        colour
7890   dr_puld warn_defn,-1,-1
7900   pos_ptr warn_defn,90*65536+70,-1
7910   dr_iwdf #chan%,warn_defn,0
7920   wm_paper#chan%,sp.infwinbg
7930   wm_ink#chan%,sp.infwinfg
7940   cls#chan%
7950   print #chan%,mess$
7960   if not channel%:close#chan%
7970   info_is_pulled_down%=1
7980 end define info
7990 :
8000 def proc info_wait(mess$,channel%)
8010 rem shows info wdw and waits until user clicks
8020   info mess$,channel%
8030   info_wait_to_close
8040 end def info_wait
8050 :
8060 def proc info_wait_to_close
8070 local item%,lp%,chan%,y%,swnum%,infot%
8080   repeat lp%
8090     rd_ptr warn_defn,item%,swnum%,y%,y%,y%,warn_lfl%
8100     if swnum%=-1:exit lp%
8110   end repeat lp%
8120   dr_unst warn_defn
8130 end def info_wait_to_close
8140 :
8150 def proc info_pos_wait (mess$,channel%,pos)
8160 rem positions & shows info wdw and waits until user clicks
8170   info_pos mess$,channel%,pos
8180   info_wait_to_close
8190 end def info_pos_wait
8200 :
8210 def proc stop_info
8220   dr_unst warn_defn
8230   info_is_pulled_down%=0
8240 end def stop_info
8250 :
8260 DEFine PROCedure button (def_trav,menu_drap%,app_drap%,control%,chaine$,type_col%)
8270 REMark les parametres sont, dans l'ordre:
8280 REMark la dƒfinition de la fen‘tre, suivie des tableaux pour
8290 REMark les postes du menu dƒliƒ, les postes de la sous-fen‘tre d'appl.
8300 REMark et la dƒfinition de contr˜le, suivi du nom du bouton et
8310 REMark du type couleurs.
8320 REMark Il est supposƒ que vous avez ouvert une fen‘tre #1 si
8330 REMark le programme est compilƒ
8340   DR_UNST def_trav
8350   IF compiled: CLOSE#1
8360   button_wait chaine$
8370 rem  BTN chaine$,type_col%
8380   IF compiled
8390     OPEN#1,"CON_"
8400     DR_PPOS def_trav,-1,-1,menu_drap%,app_drap%,control%
8410   ELSE
8420     DR_PULD def_trav,-1,-1,menu_drap%,app_drap%,control%
8430   END IF
8440 END DEFine button
8450 :
8460 DEFine PROCedure make_colours (colourway%)
8470 REMark this initialises the GLOBAL variables menu_pap, menu_brd, menu_inv,
8480 REMark menu_ink and menu_bar according to the colourway passed as parameter.
8490 REMark More importantly, this sets the sp.xxx GLOBAL variables.
8500 REMark The GLOBAl variable mycolour_mode% must have been set prior to calling
8510 REMark this function, preferrably via the function set_colours%.
8520 REMark Moreover the colours black, white,red, green,wg_stripes, etc...
8530 REMark must have been set prior to calling this procedure:
8540 REMark Preferrably, they were also set via the function set_colours%
8550 :
8560 rem this function needs the extension WL_MK16 (outptr_bin)
8570 :
8580 REMark the colourways% are:
8590 REMark 0= white/green
8600 REMark 1= black/red
8610 REMark 2= white/red
8620 REMark 3= black/green
8630 remark 4-7= use system palettes
8640 :
8650 rem the Global war mycolour_mode% is:
8660 rem 0 = we are in QL mode 4
8670 rem 1 = we are in PAL mode (256 colours)
8680 rem 2 - we are in 24 bit colour mode
8690 rem preferrably, this is set via the function set_colours%
8700 :
8710   if colourway%>7 or colourway%<0: colourway%=0: REMark must be between 0 & 7
8720 :
8730 rem first make sure that what we request is possible.
8740 rem colourways 4-7 are only possible if we are in 8 or 24 bit colour mode
8750 rem if it is requested in another mode, it is set to 0
8760 :
8770   if colourway%>3
8780      make_my_palette colourway%-4: rem set colours according to system palette
8790      return                         : rem premature exit
8800   endif
8810 :
8820 rem this now is only the old colourways
8830   select on mycolour_mode%
8840     =0                                  : rem QL 4 colours
8850       SELect ON colourway%
8860           =0:                           : remark white/green
8870                 menu_pap=white
8880                 menu_brd=green
8890                 menu_ink=black
8900                 menu_bar=wg_striped
8910                 menu_inv=red
8920           =1:                           : remark black/red
8930                 menu_pap=black
8940                 menu_brd=red
8950                 menu_ink=white
8960                 menu_bar=br_striped
8970                 menu_inv=green
8980           =2:                           : remark white/red
8990                 menu_pap=white
9000                 menu_brd=red
9010                 menu_ink=black
9020                 menu_bar=wr_striped
9030                 menu_inv=green
9040           =3:                           : remark black/green
9050                 menu_pap=black
9060                 menu_brd=green
9070                 menu_ink=white
9080                 menu_bar=bg_striped
9090                 menu_inv=red
9100       END SELect
9110     =1                                  : rem pal colours
9120       SELect ON colourway%
9130           =0:                           : remark white/green
9140                 menu_pap=white+256
9150                 menu_brd=green+256
9160                 menu_ink=black+256
9170                 menu_bar=wg_striped+256
9180                 menu_inv=red +256
9190           =1:                           : remark black/red
9200                 menu_pap=black+256
9210                 menu_brd=red  +256
9220                 menu_ink=white +256
9230                 menu_bar=br_striped +256
9240                 menu_inv=green +256
9250                 omenu_pap=black+256
9260           =2:                           : remark white/red
9270                 menu_pap=white +256
9280                 menu_brd=red   +256
9290                 menu_ink=black +256
9300                 menu_bar=wr_striped +256
9310                 menu_inv=green +256
9320           =3:                           : remark black/green
9330                 menu_pap=black +256
9340                 menu_brd=green +256
9350                 menu_ink=white +256
9360                 menu_bar=bg_striped +256
9370                 menu_inv=red   +256
9380       END SELect
9390     =2                                  : rem 24 bit colours
9400       SELect ON colourway%
9410           =0:                           : remark white/green
9420                 menu_pap=WL_MK16(white)
9430                 menu_brd=WL_MK16(green)
9440                 menu_ink=WL_MK16(black)
9450                 menu_bar=WL_MK16(wg_striped)
9460                 menu_inv=WL_MK16(red)
9470           =1:                           : remark black/red
9480                 menu_pap=WL_MK16(black)
9490                 menu_brd=WL_MK16(red)
9500                 menu_ink=WL_MK16(white)
9510                 menu_bar=WL_MK16(br_striped)
9520                 menu_inv=WL_MK16(green)
9530           =2:                           : remark white/red
9540                 menu_pap=WL_MK16(white)
9550                 menu_brd=WL_MK16(red)
9560                 menu_ink=WL_MK16(black)
9570                 menu_bar=WL_MK16(wr_striped)
9580                 menu_inv=WL_MK16(green)
9590           =3:                           : remark black/green
9600                 menu_pap=WL_MK16(black)
9610                 menu_brd=WL_MK16(green)
9620                 menu_ink=WL_MK16(white)
9630                 menu_bar=WL_MK16(bg_striped)
9640                 menu_inv=WL_MK16(red)
9650       END SELect
9660   end select
9670 :
9680   make_my_old_palette
9690 :
9700 END DEFine make_colours
9710 :
9720 def proc make_my_palette (cw%)
9730   sp.winbd            = hex('0200') : rem Window border
9740   sp.winbg            = hex('0201') : rem Window background
9750   sp.winfg            = hex('0202') : rem Window foreground
9760   sp.winmg            = hex('0203') : rem Window middleground
9770   sp.titlebg          = hex('0204') : rem Title background
9780   sp.titletextbg  = hex('0205') : rem Title text background
9790   sp.titlefg          = hex('0206') : rem Title foreground
9800   sp.litemhigh        = hex('0207') : rem Loose item highlight
9810   sp.litemavabg   = hex('0208') : rem Loose item available background
9820   sp.litemavafg   = hex('0209') : rem Loose item available foreground
9830   sp.litemselbg   = hex('020a') : rem Loose item selected background
9840   sp.litemselfg   = hex('020b') : rem Loose item selected foreground
9850   sp.litemunabg   = hex('020c') : rem Loose item unavailable background
9860   sp.litemunafg   = hex('020d') : rem Loose item unavailable foreground
9870   sp.infwinbd         = hex('020e') : rem Information window border
9880   sp.infwinbg         = hex('020f') : rem Information window background
9890   sp.infwinfg         = hex('0210') : rem Information window foreground
9900   sp.infwinmg         = hex('0211') : rem Information window middleground
9910   sp.subinfbd         = hex('0212') : rem Subsidiary information window border
9920   sp.subinfbg         = hex('0213') : rem Subsidiary information window background
9930   sp.subinffg         = hex('0214') : rem Subsidiary information window foreground
9940   sp.subinfmg         = hex('0215') : rem Subsidiary information window middleground
9950   sp.appbd            = hex('0216') : rem Application window border
9960   sp.appbg            = hex('0217') : rem Application window background
9970   sp.appfg            = hex('0218') : rem Application window foreground
9980   sp.appmg            = hex('0219') : rem Application window middleground
9990   sp.appihigh         = hex('021a') : rem Application window item highlight
10000   sp.appiavabg        = hex('021b') : rem Application window item available background
10010   sp.appiavafg        = hex('021c') : rem Application window item available foreground
10020   sp.appiselbg        = hex('021d') : rem Application window item selected background
10030   sp.appiselfg        = hex('021e') : rem Application window item selected foreground
10040   sp.appiunabg        = hex('021f') : rem Application window item unavailable background
10050   sp.appiunafg        = hex('0220') : rem Application window item unavailable foreground
10060   sp.scrbar           = hex('0221') : rem Pan/scroll bar
10070   sp.scrbarsec        = hex('0222') : rem Pan/scroll bar section
10080   sp.scrbararr        = hex('0223') : rem Pan/scroll bar arrow
10090   sp.buthigh          = hex('0224') : rem Button highlight
10100   sp.butbd            = hex('0225') : rem Button border
10110   sp.butbg            = hex('0226') : rem Button background
10120   sp.butfg            = hex('0227') : rem Button foreground
10130   sp.hintbd           = hex('0228') : rem Hint border
10140   sp.hintbg           = hex('0229') : rem Hint background
10150   sp.hintfg           = hex('022a') : rem Hint foreground
10160   sp.hintmg           = hex('022b') : rem Hint middleground
10170   sp.errbg            = hex('022c') : rem Error message background
10180   sp.errfg            = hex('022d') : rem Error message foreground
10190   sp.errmg            = hex('022e') : rem Error message middleground
10200   sp.shaded           = hex('022f') : rem sp.shaded area
10210   sp.3ddark           = hex('0230') : rem Dark 3D border shade
10220   sp.3dlight          = hex('0231') : rem Light 3D border shade
10230   sp.vertfill         = hex('0232') : rem Vertical area fill
10240   sp.subtitbg         = hex('0233')  : rem Subtitle background
10250   sp.subtittxtbg  = hex('0234')  : rem Subtitle text background
10260   sp.subtitfg         = hex('0235')  : rem Subtitle foreground
10270   sp.mindexbg         = hex('0236')  : rem Menu index background
10280   sp.mindexfg         = hex('0237')  : rem Menu index foreground
10290   sp.separator        = hex('0238')  : rem Seperator lines etc.
10300   if not (is_open(#0) or is_open(#1))
10310     open#1,'con'
10320     sp_jobpal -1,cw%
10330     close#1
10340   else
10350     sp_jobpal -1,cw%
10360   endif
10370 end def make_my_palette
10380 :
10390 def proc make_my_old_palette
10400 rem this translates the colours used in the wdws into 4 colour mode...
10410   sp.winbd          =         menu_ink   : rem Window border
10420   sp.winbg          =         menu_pap   : rem Window background
10430   sp.winfg          =         menu_pap   : rem Window foreground
10440   sp.winmg          =         menu_pap   : rem Window middleground
10450   sp.titlebg        =         menu_bar   : rem Title background
10460   sp.titletextbg=   menu_pap   : rem Title text background
10470   sp.titlefg        =         menu_ink   : rem Title foreground
10480   sp.litemhigh      =         menu_brd   : rem Loose item highlight
10490   sp.litemavabg =   menu_pap   : rem Loose item available background
10500   sp.litemavafg =   menu_ink   : rem Loex fifiose item available foreground
10510   sp.litemselbg =   menu_brd   : rem Loose item selected background
10520   sp.litemselfg =   menu_ink   : rem Loose item selected foreground
10530   sp.litemunabg =   menu_pap   : rem Loose item unavailable background
10540   sp.litemunafg =   menu_brd   : rem Loose item unavailable foreground
10550   sp.infwinbd       =         menu_brd   : rem Information window border
10560   sp.infwinbg       =         menu_pap   : rem Information window background
10570   sp.infwinfg       =         menu_ink   : rem Information window foreground
10580   sp.infwinmg       =         menu_pap   : rem Information window foreground
10590   sp.subinfbd       =         menu_brd   : rem Information window border (sub)
10600   sp.subinfbg       =         menu_pap   : rem Information window background
10610   sp.subinffg       =         menu_ink   : rem Information window foreground
10620   sp.subinfmg       =         menu_pap   : rem Information window foreground
10630   sp.appbd          =         menu_brd   : rem Application window border
10640   sp.appbg          =         menu_pap   : rem Application window background
10650   sp.appfg          =         menu_ink   : rem Application window foreground
10660   sp.appmg          =         menu_pap   : rem Application window middleground
10670   sp.appihigh       =         menu_ink   : rem Application window item highlight
10680   sp.appiavabg      =         menu_pap   : rem Application window item available background
10690   sp.appiavafg      =         menu_ink   : rem Application window item available foreground
10700   sp.appiselbg      =         menu_brd   : rem Application window item selected background
10710   sp.appiselfg      =         menu_ink   : rem Application window item selected foreground
10720   sp.appiunabg      =         menu_pap   : rem Application window item unavailable background
10730   sp.appiunafg      =         menu_brd   : rem Application window item unavailable foreground
10740   sp.scrbar         =         menu_ink   : rem Pan/scroll bar
10750   sp.scrbarsec      =         menu_pap   : rem bar section
10760   sp.scrbararr      =         menu_brd   : rem Pan/scroll bar arrow
10770   sp.buthigh        =         menu_ink   : rem Button highlight
10780   sp.butbd          =         menu_brd   : rem Button border
10790   sp.butbg          =         menu_pap   : rem Button background
10800   sp.butfg          =         menu_ink   : rem Button foreground
10810   sp.hintbd         =         menu_brd   : rem Hint border
10820   sp.hintbg         =         menu_ink   : rem Hint background
10830   sp.hintfg         =         menu_pap   : rem Hint foreground
10840   sp.hintmg         =         menu_ink   : rem Hint middleground
10850   sp.errbg          =         menu_inv   : rem Error message background
10860   sp.errfg          =         menu_ink   : rem Error message foreground
10870   sp.errmg          =         menu_ink   : rem Error message middleground
10880   sp.shaded         =         0            : rem sp.shaded area
10890   sp.3ddark         =         0            : rem Dark 3D border shade
10900   sp.3dlight        =         white        : rem Light 3D border shade
10910   sp.vertfill       =         menu_pap   : rem Vertical area fill
10920   sp.subtitbg       =         menu_pap   : rem Subtitle background
10930   sp.subtittxtbg=   menu_pap   : rem Subtitle text background
10940   sp.subtitfg       =         menu_ink   : rem Subtitle foreground
10950   sp.mindexbg       =         menu_pap   : rem Menu index background
10960   sp.mindexfg       =         menu_brd   : rem same,        foreground
10970   sp.separator      =         menu_brd   : rem Seperator lines etc.
10980 end def make_my_old_palette
10990 :
11000 DEFine FuNction centr$(ctr$,leng)
11010 rem this "centres" the string ctr$ (i.e. left padds it with spaces) so that
11020 rem it is centred in a window of leng width (leng is length in chars)
11030   LOCal l%,cta$
11040   cta$=strip_spaces$(ctr$)
11050   l%=INT(leng-LEN(cta$))/2
11060   if l%<0:return cta$
11070   return FILL$(' ',l%)&cta$&FILL$(' ',l%)
11080 END DEFine centr$
11090 :
11100 DEFine PROCedure scroll_arrows(rangees%,nbr_objets%,appfen,cty%)
11110 rem Cette procƒdure dƒcide s'il faut laisser les flches de dƒfilement
11120 rem params: le nombre de rangƒes, le nombre total d'objets
11130 rem le pointer vers la sous-fen d'app et la dƒfinition de controle
11140   IF nbr_objets%<=rangees%
11150     POKE_W appfen+150,0       : rem no y offset to start of menu
11160     cty%(1,2)=nbr_objets%     : rem nbr of columns/rows
11170   ELSE
11180     POKE_W appfen+150,6
11190     cty%(1,2)=rangees%-1      : rem if scroll arrows are there, there is one line less in the window
11200   END IF
11210   IF DIMN(cty%,1)=2:cty%(2,2)=cty%(1,2)
11220   cty%(0,1)=1                            : rem show that definition has changed
11230 END DEFine scroll_arrows
11240 :
11250 DEFine FuNction getstr$(poste%,drap%,defn,a$)
11260 REMark opens and sets a window over a loose item and proposes a sting to be edited.
11270 REMark parameters:
11280 REMark - item nbr
11290 REMark - falg whether result MUST be a numbrr (flag<>0)
11300 REMark - the working definition
11310 REMark - the string o edit
11320 rem returns the string or ""
11330 rem uses the global (!) vars sp.litemselbg and sp.litemselfg
11340 rem
11350 local b$,chan%
11360   chan%=FOPEN("con")
11370   b$=getstr2$(poste%,drap%,defn,a$,chan%)
11380   close#chan%
11390   return b$
11400 END DEFine getstr$
11410 :
11420 DEFine FuNction getstr2$(poste%,drap%,defn,a$,chan%)
11430 REMark sets a window over a loose item and proposes a sting to be edited.
11440 REMark parameters:
11450 REMark - item nbr
11460 REMark - falg whether result MUST be a numbrr (flag<>0)
11470 REMark - the working definition
11480 REMark - the string o edit
11490 remark - a screen channel to be used
11500 rem returns the string or ""
11510 rem uses the global (!) vars sp.litemselbg and sp.litemselfg
11520 rem
11530   LOCal b$,res
11540   b$=" ":b$=a$           : REMark c'est curieusement nƒcessaire
11550   wm_PAPER#chan%,sp.litemselbg
11560   wm_INK#chan%,sp.litemselfg
11570   DR_LWDF#chan%,defn,poste%             : rem open channel over item
11580   CLS#chan%
11590   res=GET_STR(#chan%,drap%,b$)                                : rem get string
11600   IF res<0:b$=""                        : rem ooops
11610   CH_ITEM defn,-1,poste%,-1,"",b$
11620   RETurn b$
11630 END DEFine getstr2$
11640 :
11650 def fn showcont$(string$,length%)
11660 rem this adds '...' at the end of a string, to show that it continues
11670 local l%,lengt%
11680   l%=len(string$)
11690   if not l%:return ""                             : rem empty string
11700   lengt%=length% div 6
11710   if l%<=lengt%:return string$                    : rem entire string fits wdw
11720   return string$(1 to lengt%-3)&"..."
11730 end def showcont$
11740 :
11750 def fn get_free_text$(defn,item%,prompt$,length%)
11760 rem this opens wdw over loose-item item% of the working definition defn
11770 rem allows free text to be entered
11780 rem params: working defintion,item over which wdw is to be opened
11790 rem           first value of string, length of max string in wdw, if this is...
11800 rem              -1, the length will be determined here
11810 local chan%,string$,tempsizes%(3)
11820   chan%=fopen('con')
11830   if chan%<0:return prompt$
11840   dr_lwdf#chan%,defn,item%              : rem open wdw over item
11850   paper#chan%,menu_brd
11860   ink#chan%,menu_ink
11870   cls#chan%
11880   string$=wedit$(#chan%,prompt$)
11890   if length%=-1
11900      wsipo#chan%,tempsizes%
11910      length%=tempsizes%(0)
11920   endif
11930   close#chan%
11940   if length%
11950     ch_item defn,-1,item%,-1,"",showcont$(string$,length%)
11960   else
11970     ch_item defn,-1,item%,-1,"",string$
11980   endif
11990   return string$
12000 end def get_free_text$
12010 :
12020 def fn do_caps(a$,caps%)
12030 rem set string to capitales etc,caps% determines whether the string....
12040 REMark ...must be lower case (=0), upper case(=1), all words capitalized (=2),
12050 REMark ... first word only capitalized (=3), or nothing(=-1)
12060   SELect ON caps%
12070    =0: MIN a$
12080    =1: MAJ a$
12090    =2: CAP2 a$
12100    =3: CAP a$
12110   END SELect
12120   return a$
12130 end def do_caps
12140 :
12150 def fn set_colours% (force_mode%)
12160 rem this sets colour variables according to the colour mode wished, if possible.
12170 rem force_mode%=
12180 rem       0 if ql colours wished (mode 4 only)
12190 rem       1 if pal colours wished
12200 rem       2 if 24 bit colours wished
12210 rem This function returns the true QL mode set (4,8,32,33 etc.).
12220 rem This function also sets the GLOBAL variable mycolour_mode% to the
12230 rem mode 0,1,2 as above - whatever is possible on the machine, i.e.
12240 rem if colour_24 is wished on a 4 colour machine (which won't succeed)
12250 rem this function will set set that variable to 0, i.e. QL mode.
12260 rem
12270 rem This function needs the outptr toolkit
12280 local mode%
12290   mode%=rmode                                     : rem get current mode now
12300   select on force_mode%
12310       = 0                                         : rem mode 4 wished
12320        select on mode%
12330             = 4                                   : rem already on, so do nothing
12340             = 0,1,8 : mode 4            : rem old modes, set to 4 colours
12350             = remainder
12360               colour_ql                           : rem if higher colour mode, set to "mode 4"
12370               mode 4
12380        end select
12390        mode%=0                                    : rem new mode we're in  - 4 colour mode
12400       = 1                                         : rem PALette wished
12410        select on mode%
12420             = 16,32,33 : colour_pal:mode%=1  : rem we're in high colour mode, but PAL colours wished
12430             = remainder
12440               if mode%<>4:mode 4
12450               mode%=0
12460        end select
12470       = 2
12480        select on mode%
12490             = 32,33 : colour_24 :mode%=2          : rem we're in high col mode 24 bit colours
12500             = 16 : colour_pal : mode%=1
12510             = remainder
12520               if mode%<>4:mode 4
12530               mode%=0
12540        end select
12550   end select
12560   mycolour_mode%=mode%                            : rem set this global variable now:
12570   rem now set some colour variables
12580   select on mode%
12590        = 0                                        : rem Ql 4 mode colours
12600           Black            = 0
12610           White            = 7
12620           Red              = 2
12630           Green            = 4
12640           grey             = 255
12650           dull_pink      = 19
12660           Blue             = 0
12670           Magenta        = 2
12680           Yellow           = 7
12690           Cyan             = 0
12700           Dark_slate     = 0
12710           Slate_grey     = 255
12720           Dark_grey      = 255
12730           Light_grey     = 255
12740           Ash_grey       = 255
12750           Dark_red       = 210
12760           Light_green    = 220
12770           Mustard        = 242
12780           Dark_green     = 228
12790           Sea_blue       = 0
12800           Dark_blue      = 0
12810           Purple           = 0
12820           Shocking_pink  = 234
12830           Orange           = 234
12840           Lime_green     = 4
12850           Apple_green    = 4
12860           Bright_blue    = 0
12870           Mauve            = 0
12880           Peach            = 7
12890           Light_yellow   = 7
12900           Light_blue     = 0
12910           Sky_blue       = 0
12920           Rose_pink      = 234
12930           Pink             = 234
12940           Beige            = 7
12950           Pastel_pink    = 234
12960           Pastel_yellow  = 7
12970           Pastel_green   = 4
12980           Pastel_cyan    = 0
12990           Pastel_blue    = 0
13000           Pastel_rose    = 234
13010           Brick            = 210
13020           Light_khaki    = 7
13030           Dull_green     = 228
13040           Dull_cyan      = 0
13050           Steel_blue     = 0
13060           Dull_pink      = 234
13070           Brown            = 0
13080           Khaki            = 7
13090           Dusky_green    = 4
13100           Dusky_blue     = 0
13110           Midnight_blue  = 0
13120           Plum             = 0
13130           Dusky_pink     = 234
13140           Buff             = 0
13150           Avocado        = 4
13160           Dull_turquoise = 4
13170           Dull_blue      = 0
13180           Faded_purple   = 234
13190           Cerise           = 2
13200           Tan_normal     = 0
13210           Grass_green    = 4
13220           Sea_green      = 4
13230           Ultramarine    = 0
13240           Deep_purple    = 234
13250           Light_orange   = 7
13260           light_red      = 2
13270           wg_striped     = 92
13280           br_striped     = 82
13290           wr_striped     = 107
13300           bg_striped     = 100
13310        = 1                              : rem Colour_pal
13320           Black            = 0
13330           White            = 1
13340           Red              = 2
13350           Green            = 3
13360           Blue             = 4
13370           Magenta        = 5
13380           Yellow           = 6
13390           Cyan             = 7
13400           Dark_slate     = 8
13410           Slate_grey     = 9
13420           Dark_grey      = 10
13430           Grey             = 11
13440           Light_grey     = 12
13450           Ash_grey       = 13
13460           Dark_red       = 14
13470           Light_green    = 15
13480           Mustard        = 16
13490           Dark_green     = 17
13500           Sea_blue       = 18
13510           Dark_blue      = 19
13520           Purple           = 20
13530           Shocking_pink  = 21
13540           Orange           = 22
13550           Lime_green     = 23
13560           Apple_green    = 24
13570           Bright_blue    = 25
13580           Mauve            = 26
13590           Peach            = 27
13600           Light_yellow   = 28
13610           Light_blue     = 29
13620           Sky_blue       = 30
13630           Rose_pink      = 31
13640           Pink             = 32
13650           Beige            = 33
13660           Pastel_pink    = 34
13670           Pastel_yellow  = 35
13680           Pastel_green   = 36
13690           Pastel_cyan    = 37
13700           Pastel_blue    = 38
13710           Pastel_rose    = 39
13720           Brick            = 40
13730           Light_khaki    = 41
13740           Dull_green     = 42
13750           Dull_cyan      = 43
13760           Steel_blue     = 44
13770           Dull_pink      = 45
13780           Brown            = 46
13790           Khaki            = 47
13800           Dusky_green    = 48
13810           Dusky_blue     = 49
13820           Midnight_blue  = 50
13830           Plum             = 51
13840           Dusky_pink     = 52
13850           Buff             = 53
13860           Avocado        = 54
13870           Dull_turquoise = 55
13880           Dull_blue      = 56
13890           Faded_purple   = 57
13900           Cerise           = 58
13910           Tan_normal     = 59
13920           Grass_green    = 60
13930           Sea_green      = 61
13940           Ultramarine    = 62
13950           Deep_purple    = 63
13960           pinkish        = 77
13970           light_red      = 109
13980           Light_orange   = 208
13990           wg_striped     = light_green
14000           br_striped     = dark_red
14010           wr_striped     = light_red
14020           bg_striped     = dark_green
14030        = 2                              : rem colour_24
14040           Black            = hex('000000')
14050           White            = hex('FFFFFF')
14060           Red              = hex('FF0000')
14070           Green            = hex('00FF00')
14080           Blue             = hex('0000FF')
14090           Magenta        = hex('FF00FF')
14100           Yellow           = hex('FFFF00')
14110           Cyan             = hex('00FFFF')
14120           Dark_slate     = hex('242424')
14130           Slate_grey     = hex('494949')
14140           Dark_grey      = hex('6D6D6D')
14150           Grey             = hex('929292')
14160           Light_grey     = hex('B6B6B6')
14170           Ash_grey       = hex('DBDBDB')
14180           Dark_red       = hex('920000')
14190           Light_green    = hex('B6FFB6')
14200           Mustard        = hex('929200')
14210           Dark_green     = hex('009200')
14220           Sea_blue       = hex('009292')
14230           Dark_blue      = hex('000092')
14240           Purple           = hex('920092')
14250           Shocking_pink  = hex('FF0092')
14260           Orange           = hex('FF9200')
14270           Lime_green     = hex('92FF00')
14280           Apple_green    = hex('00FF92')
14290           Bright_blue    = hex('0092FF')
14300           Mauve            = hex('9200FF')
14310           Peach            = hex('FFB6B6')
14320           Light_yellow   = hex('FFFFB6')
14330           Light_blue     = hex('B6FFFF')
14340           Sky_blue       = hex('B6B6FF')
14350           Rose_pink      = hex('FFB6FF')
14360           Pink             = hex('FFB6DB')
14370           Beige            = hex('FFDBB6')
14380           Pastel_pink    = hex('FFDBDB')
14390           Pastel_yellow  = hex('FFFFDB')
14400           Pastel_green   = hex('DBFFDB')
14410           Pastel_cyan    = hex('DBFFFF')
14420           Pastel_blue    = hex('DBDBFF')
14430           Pastel_rose    = hex('FFDBFF')
14440           Brick            = hex('B66D6D')
14450           Light_khaki    = hex('B6B66D')
14460           Dull_green     = hex('6DB66D')
14470           Dull_cyan      = hex('6DB6B6')
14480           Steel_blue     = hex('6D6DB6')
14490           Dull_pink      = hex('B66DB6')
14500           Brown            = hex('6D2424')
14510           Khaki            = hex('6D6D24')
14520           Dusky_green    = hex('246D24')
14530           Dusky_blue     = hex('246D6D')
14540           Midnight_blue  = hex('24246D')
14550           Plum             = hex('6D246D')
14560           Dusky_pink     = hex('B64992')
14570           Buff             = hex('B69249')
14580           Avocado        = hex('92B649')
14590           Dull_turquoise = hex('49B692')
14600           Dull_blue      = hex('4992B6')
14610           Faded_purple   = hex('9249B6')
14620           Cerise           = hex('920049')
14630           Tan_normal     = hex('924900')
14640           Grass_green    = hex('499200')
14650           Sea_green      = hex('009249')
14660           Ultramarine    = hex('004992')
14670           Deep_purple    = hex('490092')
14680           Light_orange   = hex('ffb624')
14690           pinkish        = hex('C0037B')
14700           Light_red      = hex('FC1729')
14710           wg_striped     = light_green
14720           br_striped     = dark_red
14730           wr_striped     = light_red
14740           bg_striped     = dark_green
14750   end select
14760   mode%=rmode
14770   ret mode%                             : rem return current screen mode now
14780 end def set_colours%
14790 :
14800 DEFine PROCedure init
14810 LOCal temp%,targets%,start%,lp%
14820   text=0:sprite=2:blob=4:pattern=6
14830   retr=256:retn=-256:sous1=254:sous2=252:sous3=250
14840   REMark normal: hit = selects/deselects, do = returns & leaves selected
14850   REMark retr = return even on hit, reset item to available after,
14860   REMark retn = return even on hit, hit selects/deselects, do selects
14870 :
14880   hit$=CHR$(1):do$=CHR$(2):cancel$=CHR$(3):sleep$=CHR$(7):wake$=CHR$(8)
14890   help$=CHR$(4):move$=CHR$(5):size$=CHR$(6):cr$=CHR$(10)
14900 :
14910   appwinoffset%=16                      : REMark appsub win additional y offset
14920   xsize%=500                                      : REMark x size divisible by 4!
14930   ysize%=200                                      : REMark provisional y size%
14940   lxs=24:lys=12                         : REMark standard item x,y, sizes
14950   inf1y=lys+4                                     : REMark y orig of inner border
14960   xesp=lxs+2                                      : REMark loose items x spacing
14970   nbr_post%=4                                     : REMark nbr of item right & left in title bar
14980   main_coul%=SMSCFG(2)                            : REMark colourways
14990   app_coul%=main_coul%                            : REMark preset colourways
15000   temp%=set_colours% (2)                : REMark always try to set highest colours
15010   rem IF temp%<2 AND main_coul%>3:main_coul%=0 : REMark old wman
15020   make_colours main_coul%               : REMark set all colours now
15030 :
15040   rep$=SMSCFG(1)                        : REMark dir with files
15050   titre$="Dir: "&rep$&" "
15060 :
15070 restore 15070
15080 :
15090 : REMark make loose item attributes
15100 :
15110   DIM std_iattr(3,3)
15120   RD_IATT std_iattr
15130     DATA 1,sp.litemhigh : REMark current item border size & colour
15140     DATA sp.litemunabg,sp.litemunafg,0,0 : REMark unavailable item paper & ink
15150     DATA sp.litemavabg,sp.litemavafg,0,0 : REMark   available               "    "           "
15160     DATA sp.litemselbg,sp.litemselfg,0,0 : REMark   selected
15170 :
15180 : REMark make data for sprites - uses the modified RD_SPRT function
15190 : REMark
15200 : REMark
15210 :
15220   espace_vide=ALCHP(100)                : REMark GLOBAL var for 0 mask sprites!
15230 :
15240   main_sprite=RD_SPRT(0)                : REMark 0 = normal mask
15250     DATA 11,13,6,5,4
15260     DATA '     www     '
15270     DATA '     waw     '
15280     DATA '     waw     '
15290     DATA '     waw     '
15300     DATA 'wwwwwa awwwww'
15310     DATA 'waaaa   aaaaw'
15320     DATA 'wwwwwa awwwww'
15330     DATA '     waw     '
15340     DATA '     waw     '
15350     DATA '     waw     '
15360     DATA '     www     '
15370 :
15380   move_sprite=RD_SPRT(1)                : REMark 1 = 0 mask
15390   IF temp%>1:move_sprite=6              : REMark or system sprite!
15400     DATA 10,14,5,4,4
15410     DATA 'aaaaaaaaaa    '
15420     DATA 'awwwwwwwwa    '
15430     DATA 'awwrrrrwwa    '
15440     DATA 'awwrrrrwwaaaaa'
15450     DATA 'awwrrwwwwwwwwa'
15460     DATA 'awwwwwwwwrrwwa'
15470     DATA 'aaaaawwrrrrwwa'
15480     DATA '    awwrrrrwwa'
15490     DATA '    awwwwwwwwa'
15500     DATA '    aaaaaaaaaa'
15510 :
15520   zzz_sprite= RD_SPRT(1)
15530   IF temp%>1:zzz_sprite=10
15540     DATA 7,15,0,0,4
15550     DATA 'wwwww          '
15560     DATA '   w           '
15570     DATA '  w  wwww      '
15580     DATA ' w     w       '
15590     DATA 'wwwww w  wwww  '
15600     DATA '     wwww  w   '
15610     DATA '          wwww '
15620 :
15630  size_sprite=RD_SPRT(1)
15640  IF temp%>1:size_sprite=7
15650     DATA 9,14,5,4,4
15660     DATA 'aaaaaaaaaaaaaa'
15670     DATA 'awwwwwwwwwwwwa'
15680     DATA 'awwrrrrrrrrwwa'
15690     DATA 'awwrrrrrrrrwwa'
15700     DATA 'awwrrwwwwwwwwa'
15710     DATA 'awwrrwwrrrrwwa'
15720     DATA 'awwrrwwrrrrwwa'
15730     DATA 'awwwwwwwwwwwwa'
15740     DATA 'aaaaaaaaaaaaaa'
15750 :
15760   targets%=0
15770   REMark IF NOT compiled:cmd$="-tj-tg-st-mk-as"
15780   quit_always%=0
15790   quit_no_errors%=0
15800   autostart%=0
15810   do_make_it%=0
15820   force_assembly$=""
15830   delete_first%=0
15840   select_all%=0
15850   IF cmd$<>''
15860       IF '-qa' INSTR cmd$:quit_always%=1
15870       IF '-q0' INSTR cmd$:quit_no_errors%=1
15880       IF '-as' INSTR cmd$:autostart%=1
15890       IF '-mk' INSTR cmd$:do_make_it%=1
15900       IF '-de' INSTR cmd$:delete_first%=1
15910       IF '-sa' INSTR cmd$:select_all%=1
15920       IF '-tg' INSTR cmd$:targets%=targets%+1 : REMark Generic
15930       IF '-ti' INSTR cmd$:targets%=targets%+2 : REMark Atari
15940       IF '-to' INSTR cmd$:targets%=targets%+4 : REMark gOld
15950       IF '-tq' INSTR cmd$:targets%=targets%+8 : REMark Q40
15960       IF '-tx' INSTR cmd$:targets%=targets%+16 : REMark qXl
15970       IF '-tu' INSTR cmd$:targets%=targets%+32 : REMark aUrora
15980       IF '-tj' INSTR cmd$:targets%=targets%+64 : REMark Java
15990       IF '-tr' INSTR cmd$:targets%=targets%+128 : REMark ptRgen
16000       IF '-tc' INSTR cmd$:targets%=targets%+256 : REMark qpC
16010       IF '-ta' INSTR cmd$:targets%=255+256  : REMark all targets
16020       IF '-fa' INSTR cmd$:force_assembly$=" -f": REMark force assembly of all files
16030   ELSE
16040     targets%=1
16050   END IF
16060   make_my_data targets%
16070   make_main_wdw
16080   IF (select_all%):main_lfl%(all_it%)=129:set_statusses
16090   start%=1
16100   FOR lp%=target_its% TO target_its%+8
16110     IF targets% && start%: main_lfl%(lp%)=129
16120     start%=start%*2
16130   END FOR lp%
16140   IF do_make_it%:main_lfl%(make_it%)=129
16150   IF delete_first%:main_lfl%(del_it%)=129
16160 :
16170   warn_make
16180 :
16190 END DEFine init
16200 :
16210 DEFine PROCedure make_main_wdw
16220 REMark make the main window
16230 LOCal main_lot,main_iwt,main_iot1
16240 :
16250   REMark first make sure of the wdw sizes (we also come here from change_size!)
16260   xsize%=xsize%-(xsize% MOD 4)                                : REMark make into nbr divisible by 4
16270   IF xsize%<500:xsize%=500
16280 :
16290   REMark calculate the y size of the wdw so that it always looks nice, in that
16300   REMark the rows in the appsub always fill the appsub entirely
16310   ysize%=ysize%+(ysize%&&1)             : REMark make into even number
16320   IF ysize%<100:ysize%=100              : REMark predetermioned min ysize% before calculations
16330   ysize%=ysize%-inf1y-14-appwinoffset%
16340   rangees=ysize% DIV 12                 : REMark GLOBAL var: nbr of rows in appsub
16350   IF rangees>data_count%-1:rangees=data_count%-1
16360   IF rangees<3:rangees=3
16370   ysize%=rangees*12+inf1y+14 +appwinoffset%
16380 :
16390   REMark first calc info obj size - this also influences one loose item, used
16400   REMark in the title bar
16410   longueur_titre=LEN(titre$)*6                    : REMark length in pixels
16420   titre_x=INT((xsize%-longueur_titre)/2): REMark x orig
16430 :
16440   REMark first the loose items
16450 :
16460   yinf1y=inf1y+4
16470   esc%=0:move_it%=esc%+1:size_it%=move_it%+1
16480   sleep_it%=size_it%+1:all_it%=sleep_it%+1:go_it%=all_it%+1
16490   dir_it%=go_it%+1:target_its%=dir_it%+1:make_it%=target_its%+9
16500   del_it%=make_it%+1
16510 :
16520 restore 16520
16530   REMark reset data pointer to here
16540 :
16550   temp%=(xsize%-4) DIV 9
16560   main_lit%=17:DIM main_lfl%(main_lit%)   : REMark there are 18 items!
16570   main_lot=RD_LOT(std_iattr,main_lit%)
16580     DATA lxs,lys,4+xesp*0,2,0,0,cancel$,text+retr,'ESC'
16590     DATA lxs,lys,4+xesp*1,2,0,0,move$,sprite+retr,move_sprite
16600     DATA lxs,lys,4+xesp*2,2,0,0,size$,sprite+retn,size_sprite
16610     DATA lxs,lys,xsize%-2-xesp*1,2,0,0,sleep$,sprite+retr,zzz_sprite
16620     DATA lxs,lys,xsize%-2-xesp*3,2,0,0,'A',sous1+retn,"All"
16630     DATA lxs,lys,xsize%-2-xesp*4,2,0,0,'O',sous1+retn,"OK"
16640     DATA 6*6,lys-2,titre_x+30,3,0,0   ,'D',text+retn,rep$
16650     DATA 8*6,lys,8,inf1y+2,0,0,        'G',sous1+retn,"Generic"
16660     DATA 8*6,lys,8+temp%,inf1y+2,0,0,  'T',sous2+retn,"Atari"
16670     DATA 8*6,lys,8+temp%*2,inf1y+2,0,0,'L',sous3+retn,"GoldCard"
16680     DATA 8*6,lys,8+temp%*3,inf1y+2,0,0,'Q',sous1+retn,"Q40/Q60"
16690     DATA 8*6,lys,8+temp%*4,inf1y+2,0,0,'X',sous2+retn,"QXL"
16700     DATA 8*6,lys,8+temp%*5,inf1y+2,0,0,'U',sous2+retn,"Aurora"
16710     DATA 8*6,lys,8+temp%*6,inf1y+2,0,0,'J',sous1+retn,"Java"
16720     DATA 8*6,lys,8+temp%*7,inf1y+2,0,0,'P',sous1+retn,"PtrGen"
16730     DATA 8*6,lys,8+temp%*8,inf1y+2,0,0,'C',sous3+retn,"QPC"
16740     DATA 4*6,lys,xsize%-2-xesp*2,2,0,0,'M',sous1+retn,"Make"
16750     DATA lxs,lys,4+xesp*3,2,0,0,"E",sous2+retn,"DEL"
16760 :
16770   REMark now info wdw objects - here only one, the title
16780   REMark make the title data: calculate size and x position of this object
16790 :
16800   main_iot1=RD_IOT(0)
16810     DATA longueur_titre,10,4,INT((lys-10)/2),sous1,sp.titlefg,0,0,titre$
16820 :
16830   REMark now the info wdws
16840 :
16850   main_iwt=RD_IWT(2)                                          : REMark there are 3
16860 :
16870 REMark first the title bar
16880 :
16890     DATA xsize%-8-(2*nbr_post%*xesp),lys+2,xesp*nbr_post%+4,0               : REMark x,y sizes & origs
16900     DATA 0,0,sp.infwinbd,sp.titlebg     : REMark shadow, border size & col,paper col
16910     DATA 0                                        : REMark object pointer (none)
16920 :
16930 REMark now wdw containing title itself
16940 :
16950     DATA longueur_titre+8,lys,titre_x-4,2
16960     DATA 0,0,sp.infwindbd,sp.infwinbg
16970     DATA main_iot1                      : REMark one object
16980 :
16990 REMark inner border
17000 :
17010     DATA xsize%-8,ysize%-inf1y-2,4,inf1y
17020     DATA 0,1,sp.infwinbd,sp.infwinbg
17030     DATA 0
17040 :
17050 REMark now the appsub wdw - use the OUTPTR functions for this
17060 :
17070   app_addr=0                                      : REMark there is none yet
17080   mk_main_app 1                         : REMark (this destroys my DATA pointer)
17090   main_app_list = app_addr+180                    : REMark list built by OUTPTR
17100 :
17110 : REMark  working defn
17120 :
17130 restore 17130
17140                                                   : REMark reset DATA pointer (due to Basic Linker, restore must be ALONE on the line)
17150   main_defn=RD_WDEF
17160     DATA 2,1,sp.winbd,sp.winbg                    : REMark shad,border size & col,pap col
17170     DATA xsize%,ysize%,0,0    : REMark x,y size & initial ptr pos
17180     DATA main_sprite,main_lot,main_iwt,main_app_list : REMark sprite,loose items, info wins, appsubs
17190 END DEFine make_main_wdw
17200 :
17210 DEFine PROCedure mk_main_app
17220 REMark make main application sub window
17230 LOCal t2%,temp%,sections%,att_phys%(3)
17240   DIM main_cty%(0):DIM main_mfl%(0,0)
17250   sections%=2
17260   temp%=sections% :t2%=rangees
17270   att_phys%(0)=xsize%-16                : REMark xsize calculated on main wdw x size
17280   att_phys%(1)=t2%                      : REMark y size,nominal,in rows
17290   att_phys%(2)=4                        : REMark x origin x
17300   att_phys%(3)=inf1y +appwinoffset%     : REMark y origin
17310   app_addr=fen_app(app_addr,mdata$,temp%,1,att_phys%,"DO ")
17320   IF app_addr
17330       DIM main_cty%(sections%,2)        : REMark ctrl defn
17340       main_cty%(0,0)=1                            : REMark show 1 section
17350       main_cty%(1,2)=t2%
17360       IF sections%=2:main_cty%(2,2)=t2% : REMark but allow 2
17370       IF temp%
17380              DIM main_mfl%(temp%-1,0)   : REMark flag array
17390              set_statusses              : REMark and set all statusses
17400       ELSE
17410              POKE_W app_addr,PEEK_W(app_addr)+8
17420       END IF
17430    REMark   scroll_arrows t2%,temp%,app_addr,main_cty%
17440   END IF
17450 END DEFine mk_main_app
17460 :
17470 DEFine PROCedure set_statusses
17480 REMark set all statusses of menu appsub objects
17490 LOCal temp%,lp%,stat%
17500   temp%=DIMN(main_mfl%,1)               : REMark how many are there?
17510   stat%=main_lfl%(all_it%)              : REMark status of "all" item
17520   FOR lp%=0 TO temp%
17530     IF mdata$(lp%+1,1)='*'
17540       main_mfl%(lp%,0)=16               : REMark comments set to unavailable
17550     ELSE
17560       main_mfl%(lp%,0)=stat%            : REMark all other to status
17570     END IF
17580   END FOR lp%
17590 END DEFine set_statusses
17600 :
17610 def proc make_standard_sprites
17620 rem This makes standard move, sleep and wake sprites and sets the
17630 rem GLOBAL vars move_sprite,, zzz_sprite, wake_sprite to them.
17640 rem If there are system sprites, these are used.
17650 rem This also makes the old "main_sprite" pointer sprite.
17660 :
17670   espace_vide=ALCHP(100)         : rem for pointers with no mask
17680 :
17690 restore 17690
17700   main_sprite=RD_SPRT(0)         : REMark ici un masque normal
17710     DATA 11,13,6,5,4
17720     DATA '     www     '
17730     DATA '     waw     '
17740     DATA '     waw     '
17750     DATA '     waw     '
17760     DATA 'wwwwwa awwwww'
17770     DATA 'waaaa   aaaaw'
17780     DATA 'wwwwwa awwwww'
17790     DATA '     waw     '
17800     DATA '     waw     '
17810     DATA '     waw     '
17820     DATA '     www     '
17830 :
17840   if sys_tspr_addr(6)<>0
17850     move_sprite=6
17860   else
17870 restore 17870
17880     move_sprite=RD_SPRT(1)         : REMark ici un masque 0
17890     DATA 10,14,5,4,4
17900     DATA 'aaaaaaaaaa    '
17910     DATA 'awwwwwwwwa    '
17920     DATA 'awwrrrrwwa    '
17930     DATA 'awwrrrrwwaaaaa'
17940     DATA 'awwrrwwwwwwwwa'
17950     DATA 'awwwwwwwwrrwwa'
17960     DATA 'aaaaawwrrrrwwa'
17970     DATA '    awwrrrrwwa'
17980     DATA '    awwwwwwwwa'
17990     DATA '    aaaaaaaaaa'
18000   endif
18010 :
18020   if sys_tspr_addr(10)<>0
18030     sleep_sprite=10
18040   else
18050 restore 18050
18060     sleep_sprite= RD_SPRT(1)             : REMark ici un masque 0
18070     DATA 7,15,0,0,4
18080     DATA 'wwwww          '
18090     DATA '   w           '
18100     DATA '  w  wwww      '
18110     DATA ' w     w       '
18120     DATA 'wwwww w  wwww  '
18130     DATA '     wwww  w   '
18140     DATA '          wwww '
18150   endif
18160 :
18170   if sys_tspr_addr(11)<>0
18180     wake_sprite=11
18190   else
18200 restore 18200
18210     wake_sprite=RD_SPRT(1)          : REMark ici un masque 0
18220     DATA 9,14,0,0,4
18230     DATA "             w"
18240     DATA "            w "
18250     DATA "      w   ww  "
18260     DATA "     ww www   "
18270     DATA "    wwwwww    "
18280     DATA "   www ww     "
18290     DATA "  w   w      "
18300     DATA " w            "
18310     DATA "w             "
18320   endif
18330 :
18340   if sys_tspr_addr(7)<>0
18350     size_sprite=7
18360   else
18370 restore 18370
18380     size_sprite=RD_SPRT(1)
18390     DATA 9,14,5,4,4
18400     DATA 'aaaaaaaaaaaaaa'
18410     DATA 'awwwwwwwwwwwwa'
18420     DATA 'awwrrrrrrrrwwa'
18430     DATA 'awwrrrrrrrrwwa'
18440     DATA 'awwrrwwwwwwwwa'
18450     DATA 'awwrrwwrrrrwwa'
18460     DATA 'awwrrwwrrrrwwa'
18470     DATA 'awwwwwwwwwwwwa'
18480     DATA 'aaaaaaaaaaaaaa'
18490   endif
18500 :
18510 end def make_sprites
18520 :
18530 DEFine PROCedure warn_make
18540 rem makes warning & info window
18550 rem uses global vars sp.xxx
18560 rem SETS the global var warning_width% (nbr of chars in one line in wdw)
18570 local lys%,warn_lot,warn_iot1,warn_iwt
18580 local x%,y%,warning_paper,warning_brd
18590 restore 18590
18600   x%=200:y%=82
18610   lys%=10
18620   DIM warn_lfl%(1)
18630   warn_lot=RD_LOT(std_iattr,1)
18640     DATA 30,lys%,4,4,0,0,cancel$,text+retr,'ESC'
18650     DATA 30,lys%,x%-34,4,0,0,'O',text+retr,"OK"
18660 :
18670   warn_iot1=RD_IOT (0)
18680     DATA 12*9,10,0,1,text,sp.errfg,2,0,'Attention'
18690 :
18700   warn_iwt=RD_IWT(1)
18710     DATA x%-16,y%-32,8,24
18720     DATA 0,1,sp.winbd,sp.errbg
18730     DATA 0
18740 :
18750     DATA 110,12,(x%-110) div 2,4
18760     DATA 0,1,sp.winbd,sp.errbg
18770     DATA warn_iot1
18780 :
18790   warn_defn=RD_WDEF
18800     DATA 2,1,sp.winbd,sp.errbg
18810     DATA x%,y%,0,0
18820     DATA main_sprite,warn_lot,warn_iwt,0
18830 :
18840   warning_width%=(x%-16) div 6                    : rem width of warning window in chars
18850   info_is_pulled_down%=0
18860   warn_wdw_addr=peek_l(warn_defn+100)+20: rem ptr to 2nd info wdw
18870  rem warn_wdw_addr=peek_l(warn_wdw_addr+16): rem ptr to object list
18880 END DEFine warn_make
18890 :
18900 DEFine PROCedure about_make (titre$,version$)
18910 local lxs%,lys%,xesp%,inf1y%,abt_lot,longueur_titre%,titre_x%
18920 local abt_iwt,abt_iot1,abt_iot2
18930   ab_xt%=200:ab_yt%=120    : REMark tailles x et y: la taille x devrait
18940                                    : REMark ‘tre un chiffre divisible par 4
18950   lxs%=24:lys%=10                    : REMark tailles x et y pour postes standard
18960   inf1y%=lys%+4            : REMark posi. y de la fen. d'inf. intƒrieure
18970   xesp%=lxs%+2
18980   nbr_post%=0                      : REMark nombre de postes dans un coin
18990 :
19000 restore 19000
19010 wolfsoft_sprite=rD_SPrT(0)
19020     DATA 23,51,0,0,4
19030     DATA 'rrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrr'
19040     DATA 'rrgggggggggggggggggggggggggggggggggggggggggggggggrr'
19050     DATA 'rrggggggggrrrrrrrrrrrrrrrrgggggggggggggggggggggggrr'
19060     DATA 'rrgggggggggggggggggggggrgggggggggggggggggggggggggrr'
19070     DATA 'rrggggggggggggggggggggrggggggggggggggggggggggggggrr'
19080     DATA 'rrggggggggagggggagggrrrrrrrrrrrrrggaaaaaaaaggggggrr'
19090     DATA 'rrggggggggagggggaggggggggggggggggggagggggggggggggrr'
19100     DATA 'rrggggggggaggaggaggaaaaaaaggaggggggaaaaaaagggggggrr'
19110     DATA 'rrggggggggagaaagaggaawwaaaggaggggggagggggggggggggrr'
19120     DATA 'rrggggggggaaagaaaggaawwwwaggaggggggagggggggggggggrr'
19130     DATA 'rrggrrrrrgaagggaaggawwwwaaggaggggggagggggrrrrrrrgrr'
19140     DATA 'rrgrgggggrgggggggggaaawwaaggaggggggggggrrrgggggggrr'
19150     DATA 'rrgrggggggrggggggggaaaaaaaggaaaaaagggrrggrgggggggrr'
19160     DATA 'rrgrgggggggggggggrrrgggggggggggrrrggrggggrgggggggrr'
19170     DATA 'rrggrrrrrrggggggrgggrrgggggggrrggggggggggrgggggggrr'
19180     DATA 'rrggggggggrrrgggrgggggrrgggrrggggggggggggrgggggggrr'
19190     DATA 'rrgggggggggggrggrgggggggrggrgggggggggggggrgggggggrr'
19200     DATA 'rrggggggggggggrgrggggggggrgrrrrrrrgggggggrgggggggrr'
19210     DATA 'rrgrggggggggggrgrggggggggrgrgggggggggggggrgggggggrr'
19220     DATA 'rrggrgggggggrrgggrggggggrggrgggggggggggggrgggggggrr'
19230     DATA 'rrgggrrrrrrrggggggrrrrrrgggrgggggggggggggrgggggggrr'
19240     DATA 'rrgggggggggggggggggggggggggggggggggggggggggggggggrr'
19250     DATA 'rrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrr'
19260 :
19270   yinf1y%=inf1y%+4:
19280 :
19290   DIM abt_lfl%(0)
19300   abt_lot=RD_LOT(std_iattr,0)
19310     DATA ab_xt%-16,lys%,8,ab_yt%-lys%-8,0,0,cancel$,text+retr,'Super!'
19320 :
19330 : REMark  D'abord le titre qui est un objet d'une sous-fen‘tre d'info.
19340 :
19350   longueur_titre%=LEN(" Version "&version$)*6               : REMark on calcule la taille et la
19360   titre_x%=INT((ab_xt%-longueur_titre%)/2)  : REMark position x de cet objet
19370   abt_iot2=RD_IOT(1)
19380     DATA longueur_titre%,lys%,4,8,text,sp.winfg,0,0," Version "&version$
19390     DATA 52,24,ab_xt%-60,40,sprite,0,wolfsoft_sprite
19400   longueur_titre%=LEN(titre$)*6     : REMark on calcule la taille et la
19410   titre_x%=INT((ab_xt%-longueur_titre%)/2)  : REMark position x de cet objet
19420   abt_iot1=RD_IOT(0)
19430     DATA longueur_titre%,lys%,4,INT((lys%-10)/2),text,sp.titlefg,0,0,titre$
19440   abt_iwt=RD_IWT(2)               : REMark on veut 3 sous-fen. d'info
19450 REMark d'abord la fen‘tre contenant la barre entourant le titre
19460     DATA ab_xt%-8-(2*nbr_post%*xesp%),lys%+2,xesp%*nbr_post%+4,0  : REMark taille x,y, origines x,y
19470     DATA 0,0,sp.infwinbd,sp.titlebg     : REMark ombre, taille & couleur bordure
19480                                    : REMark puis couleur papier
19490     DATA 0                         : REMark pointeur vers objets
19500 REMark maintenant la fen‘tre qui contient le titre
19510     DATA longueur_titre%+8,lys%,titre_x%-2,2   : REMark on utilise titre_x% etc
19520     DATA 0,0, sp.infwinbd,sp.titletextbg
19530     DATA abt_iot1                 : REMark un objet, le titre
19540 REMark maintenant la fen‘tre de la bordure intƒrieure
19550     DATA ab_xt%-8,ab_yt%-inf1y%-2,4,inf1y%
19560     DATA 0,1,sp.infwinbd,sp.infwinbg
19570     DATA abt_iot2
19580   abt_defn=RD_WDEF
19590     DATA 2,1,sp.winbd,sp.winbg: REMark ombre, larg. & coul. bord., coul. papier
19600     DATA ab_xt%,ab_yt%,10,10 : REMark taille x,y et position pointeur
19610     DATA main_sprite,abt_lot,abt_iwt,0
19620 :
19630 end def about_make
19640 :
19650 DEFine PROCedure about (datet$)
19660   LOCal loop%,item%,dummy%,chan%
19670   DR_PULD abt_defn,-1,-1,abt_lfl%
19680   chan%=fopen('con')
19690   dr_iwdf#chan%,abt_defn,2
19700   wm_ink#chan%,sp.infwinfg
19710   wm_paper#chan%,sp.infwinbg
19720   at#chan%,4,2
19730   print#chan%,"Copyright (c) "
19740   at#chan%,5,2
19750   print#chan%, datet$
19760   at#chan%,6,2
19770   print#chan%,"Wolfgang H. Lenerz"
19780   close#chan%
19790   REPeat loop%
19800     RD_PTR abt_defn,item%,dummy%,dummy%,dummy%,dummy%,abt_lfl%
19810     SELect ON item%
19820             =0       : EXIT loop%
19830     END SELect
19840   END REPeat loop%
19850   DR_UNST abt_defn
19860 END DEFine about
19870 :
19880 def proc do_popup(string$)
19890 rem this pops up a small "hint" window showing the string$ passed
19900 rem as parameter.
19910 rem The string MUST be 1 line long and MUST fit
19920 rem inside the window (YOU must make sure of that!)
19930   do_popup2 string$,0
19940 end def do_popup
19950 :
19960 def proc popup_make
19970 rem make a small window defn
19980 local x%,y%,p_iot,popsub(0)
19990 restore 19990
20000   p_sprt=rd_sprt(0)                               : rem make an invisible pointer, all transparent
20010     data 1,2,0,0,4
20020     data '  '
20030   x%=14                                 : rem bogus window sizes
20040   y%=10
20050   popsub(0)=rd_appw                               : rem the info wdw
20060     DATA x%,y%,0,0
20070     DATA 0,0, sp.hintbd,sp.hintbg
20080     DATA 0,""
20090   popup_defn=RD_WDEF                              : rem the working defn
20100     DATA 0,1,sp.hintbd,sp.hintbg        : rem no shadow, border size & col, paper col
20110     DATA x%,y%,40,2
20120     DATA p_sprt,0,0,mk_awl(popsub)
20130 end def popup_make
20140 :
20150 def proc do_popup2(string$,main_defn)
20160 rem This pops up a small "hint" window showing the string$ passed
20170 rem as parameter.
20180 rem main_defn is the working defintion of the window over which the hint
20190 rem window is to hover. Position the pointer there where you need it before
20200 rem calling this.
20210 rem If the string is too long to fit into the window (which can only be as long
20220 rem as the outline of the working defn) then the string is split up at the
20230 rem first convenient space and put on a second line, and so on.
20240 rem If this would lead to the hint window being too high, only as many
20250 rem lines as possible will be displayed.
20260 rem !!!!!
20270 rem This presumes that a char is 10 pixels high & 6 pixels wide (standard ql 6x10 matrix)..
20280 rem !!!!!
20290 rem The string may contain the char chr$(10) - there will be a newline.
20300 rem
20310 rem note : do not try to pass a screen chaneel, open & close one here.
20320 rem
20330 local dummy%,lp%,address,con%,temp%,lastspace%,xs%,ys%,temp$
20340 local hint$(10),slen% ,maxlength%,popup_lfl%(0),popup_mfl%(0,0)
20350   if string$="":return                            : rem empty string, do nothing
20360   string$=strip_spaces$(string$)        : rem no spaces at ends
20370   con%=fopen("con")
20380   if main_defn<>0
20390     xs%=peek_w (main_defn+32)
20400     ys%=peek_w(main_defn+34)            : rem get max sizes the hint wdw may have
20410   else
20420     scr_size#con%,xs,ys%
20430   endif
20440   xs%=(xs%-4) div 6                               : rem max nbr of chars in 1 line
20450   ys%=(ys%-4) div 10                              : rem max nbr of lines in wdw
20460   if xs%=0 or ys%=0:close#con%:return   : rem nothing will really fit, just leave
20470   dim hint$(ys%,xs%)                              : rem array to contain string
20480   rem now parse the string, find newlines, make sure of string length
20490   dummy%=1                                        : rem start of current string section
20500   temp%=0                                         : rem line
20510   slen%=0                                         : rem current length of string
20520   for lp%=1 to len(string$)
20530     if string$(lp%)=chr$(10)
20540       hint$(temp%)=string$(dummy% to lp%-1): rem cut string here
20550       temp%=temp%+1
20560       if temp%=ys%:exit lp%             : rem max nbr of lines reached
20570       dummy%=lp%+1                      : rem this is where new start of string is
20580       lastspace%=lp%
20590     else
20600       if string$(lp%)=" "
20610           lastspace%=lp%                          : rem this is where last space is
20620       endif
20630     endif
20640     slen%=slen%+1                       : rem one more char
20650     if slen% > xs%
20660       hint$(temp%)=string$(dummy% to lastspace%-1) : rem too many chars in line
20670       slen%=0
20680       temp%=temp%+1                               : rem we need nother line
20690       if temp%=ys%:exit lp%             : rem max nbr of lines reached
20700       dummy%=lastspace%+1
20710     endif
20720   end for lp%
20730   if temp%<ys%
20740      hint$(temp%)=string$(dummy% to)       : rem add last part
20750      temp%=temp%+1
20760   endif
20770   maxlength%=0
20780   for lp%=0 to temp%
20790     temp$=strip_spaces$(hint$(lp%))     : rem use intermediary var, because of QLIB
20800     if maxlength%<len(temp$):maxlength%=len (temp$)
20810   end for lp%
20820   lp%=(maxlength%*6)+4                            : rem hint window length
20830   poke_w popup_defn+32,lp%              : rem set window xsize
20840   poke_w popup_defn+34,temp%*10+2       : rem
20850   address=peek_l(popup_defn+112)        : rem ptr to app sub wdw...
20860   address=peek_l(address)               : rem ...now
20870   poke_w address,lp%-2                            : rem set its length
20880   poke_w address+2,temp%*10             : rem set its height
20890   dr_puld popup_defn,-1,-1,popup_lfl%,popup_mfl%
20900   dr_awdf#con%,popup_defn,0             : rem pull wdw over info wdw
20910   wm_paper#con%,sp.hintbg
20920   wm_ink#con%,sp.hintfg
20930   for lp%=0 to dimn(hint$,1)
20940     if hint$(lp%)<>"":print#con%,strip_spaces$(hint$(lp%))
20950   end for lp%
20960   temp%=0:slen%=0:xs%=0:ys%=0:dummy%=0
20970   RD_PTR popup_defn,temp%,dummy%,slen%,xs%,ys%,popup_lfl%,popup_mfl%: rem comes back immediatley
20980   RD_PTR popup_defn,temp%,dummy%,slen%,xs%,ys%,popup_lfl%,popup_mfl%
20990   close#con%
21000   dr_unst popup_defn
21010 end def do_popup2
21020 :
21030 DEFine FuNction RD_SPRT(drap%)
21040 REMark drap% determine si masque 0
21050   LOCal tmp_patt$(32,32)
21060   LOCal xs,ys,xo,yo, md, l
21070   READ ys,xs,xo,yo,md
21080   DIM tmp_patt$(ys,xs)
21090   FOR l=0 TO ys-1:READ tmp_patt$(l)
21100   l=ALCHP(SPRSP(xs,ys))
21110   SPSET l,xo,yo,md,tmp_patt$(0 TO ys-1,1 TO xs)
21120   IF drap%
21130     POKE_L l+16,espace_vide-(l+16)
21140   END IF
21150   RETurn l
21160 END DEFine RD_SPRT
21170 :
21180 DEFine FuNction RD_WDEF
21190 rem read window defn
21200   LOCal ldef%(3), lspr, lloose, linfo, lappl, lattr%(3)
21210   RD_WATT lattr%
21220   READ ldef%(0), ldef%(1), ldef%(2), ldef%(3)
21230   READ lspr, lloose, linf, lappl
21240   RETurn MK_WDEF (ldef%, lattr%, lspr, lloose, linf, lappl)
21250 END DEFine RD_WDEF
21260 :
21270 DEFine FuNction RD_LOT (lattr,nitem)
21280   LOCal count(3)
21290   LOCal item, ltyp, a$, lsk$
21300   LOCal ldef%(nitem,6), lptr(3,nitem), lstr$(nitem,85)
21310   lsk$=''
21320   FOR item = 0 TO nitem
21330     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
21340     READ ldef%(item,4), ldef%(item,5)
21350     READ a$: lsk$=lsk$ & a$
21360     READ ltyp
21370     ldef%(item,6)=ltyp: ltyp=(ltyp MOD 256)/2
21380     IF ltyp>10 or ltyp <0:ltyp=0
21390     IF ltyp
21400       READ lptr(ltyp,count(ltyp))
21410     ELSE
21420       READ lstr$(count(0))
21430     END IF
21440     count(ltyp)=count(ltyp)+1
21450   END FOR item
21460   RETurn MK_LIL (lattr, ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), ldef%(TO, 4 TO 5), lsk$, ldef%(TO, 6), lstr$, lptr(1), lptr(2), lptr(3))
21470 END DEFine RD_LOT
21480 :
21490 DEFine FuNction RD_IWT(nitem)
21500   LOCal item,mc1
21510   LOCal ldef%(nitem,3), latt%(nitem,3), lptr(nitem)
21520   FOR item = 0 TO nitem
21530     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
21540     READ latt%(item,0), latt%(item,1), latt%(item,2), latt%(item,3)
21550     READ lptr(item)
21560   END FOR item
21570   RETurn MK_IWL (ldef%, latt%, lptr)
21580 END DEFine RD_IWT
21590 :
21600 DEFine FuNction RD_IOT(nitem)
21610   LOCal count(3)
21620   LOCal item, ltyp, work1, work2
21630   LOCal ldef%(nitem,4), lptr(3,nitem), lstr$(nitem,85)
21640   FOR item = 0 TO nitem
21650     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
21660     READ ltyp
21670     ldef%(item,4)=ltyp:  ltyp=(ltyp MOD 256)/2
21680     IF ltyp >10:ltyp=0
21690     IF ltyp
21700       READ lptr(0,item),lptr(ltyp,count(ltyp))
21710     ELSE
21720       if mycolour_mode%=2
21730           read work1,work2,work3
21740           lptr(0,item)=WL_4_IOL(work1,work2,work3)
21750       else
21760           READ work1
21770           READ work2: work1=work1*256+work2
21780           READ work2: lptr(0,item)=work1*256+work2
21790       endif
21800       READ lstr$(count(0))
21810     END IF
21820     count(ltyp) = count(ltyp) + 1
21830   END FOR item
21840   RETurn MK_IOL (ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), lptr(0), ldef%(TO, 4), lstr$, lptr(1), lptr(2), lptr(3))
21850 END DEFine RD_IOT
21860 :
21870 DEFine PROCedure RD_IATT (lattr)
21880   LOCal i
21890   READ lattr(0,0), lattr(0,1)
21900   FOR i=1 TO 3: READ lattr(i,0), lattr(i,1), lattr(i,2), lattr(i,3)
21910 END DEFine RD_IATT
21920 :
21930 DEFine PROCedure RD_WATT (lattr%)
21940   READ lattr%(0), lattr%(1),lattr%(2),lattr%(3)
21950 END DEFine RD_WATT
21960 :
21970 DEFine FuNction RD_APPW
21980   LOCal ldef%(3), lspr, sk$, lattr%(3)
21990   READ ldef%(0), ldef%(1), ldef%(2), ldef%(3)
22000   RD_WATT lattr%
22010   READ lspr, sk$
22020   RETurn MK_APPW (ldef%, lattr%, lspr,sk$)
22030 END DEFine
22040 :
