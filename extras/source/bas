10 REMark $$asmb=dev8_extras_source_outptr_bin,0,10
20 REMark $$asmb=dev1_booty_qptr_bin,50,172
30 REMark $$asmb=dev8_extras_source_SMSQEMake_bin,0,10
40 REMark $$stak=20000
50 rem compiled with the Basic_Linker
60 compiled=NOT IS_OPEN(#0)
70 IF NOT compiled
80   SCR_SIZE#0, xsz%,ysz%
90   OUTLN#0,xsz%,ysz%-28,0,28
100   WSET -1
110 END IF
120 init
130 main
140 STOP
150 :
160 DEFine PROCedure main
170 LOCal lp%,item%,event%,swnum%,xrel%,yrel%,item%,lp2%,tot%,errors%
180   IF compiled
190      OPEN#1,'con'
200      SCR_SIZE#1,xsz%,ysz%
210      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
220   ELSE
230      SCR_SIZE#1,xsz%,ysz%
240      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
250   END IF
260   POS_PTR main_defn,40*65536+20,1
270   errors%=1
280   IF autostart%:errors%=do_the_work%
290   IF quit_always%:STOP
300   IF quit_no_errors% AND NOT errors%:STOP
310   REPeat lp%
320     RD_PTR main_defn,item%,swnum%,event%,xrel%,yrel%,main_lfl%,main_mfl%,main_cty%
330     IF swnum%= -1
340       SELect ON item%
350                 =esc%         : EXIT lp%          : REMark quit prog
360                 =move_it% : CH_WIN main_defn: REMark move
370                 =size_it% : change_size xrel%,yrel% : REMark change size
380                 =sleep_it%: button main_defn,main_lfl%,main_mfl%,main_cty%,"SMSQ/E Make"
390                 =all_it%
400                      set_statusses                : REMark set all appsub item statusses
410                      main_cty%(0,1)=1   : REMark and redraw
420                 =go_it%       : errors%=do_the_work%
430                                 if errors%
440                                    errors%=warning%("There were errors"&chr$(10)&"Check the highlit items",0,"Errors!")
450                                 endif
460                 =dir_it%: make_new_dir item%
470                 =target_its% TO target_its%+8
480                                 start%=1:machines%=0
490                                 FOR lp2%=target_its% TO target_its%+8
500                                     IF main_lfl%(lp2%):machines%=machines%+start%
510                                     start%=start%*2
520                                 END FOR lp2%
530                                 make_my_data machines%      : REMark make data arrays
540                                 mk_main_app                           : REMark make appsub wdw
550                                 main_app_list=app_addr+180
560                                 AP_IN main_defn,main_app_list : REMark put it in
570                                 DR_WDRW main_defn:main_cty%(0,1)=1 : REMark and redraw entire wdw
580       END SELect
590     END IF
600   END REPeat lp%
610   DR_UNST main_defn
620   IF compiled:CLOSE#1
630   RECHP espace_vide
640 END DEFine main
650 :
660 DEFine FuNction do_the_work%
670 REMark this tries to compile all of the selected files
680 REMark if the file was compiled OK, status (of the link file) set to available
690 REMark if the file was not compiled ok, status left selected
700 LOCal lp%,bound%,a$,my_error%,tot_err%
710   bound%=DIMN(main_mfl%,1)              : REMark number of items to check
720   tot_err%=0
730   FOR lp%=0 TO bound%
740     a$=mdata$(lp%+1)                              : REMark potential file to compile
750     IF a$(1)<>"*" AND main_mfl%(lp%,0)
760        a$=rep$&a$                       : REMark add dir
770        my_error%=compile_and_check% (a$): REMark try to compile
780        IF NOT my_error%
790              main_mfl%(lp%,0)=1
800              DR_ADRW main_defn,0,main_mfl%,main_cty%
810        END IF
820        tot_err%=tot_err%+my_error%
830     END IF
840   END FOR lp%
850   main_cty%(0,1)=1
860   main_lfl%(go_it%)=1
870   IF main_lfl%(make_it%)=128 AND NOT tot_err%:make_targets
880   RETurn tot_err%
890 END DEFine do_the_work%
900 :
910 DEFine FuNction compile_and_check% (a$)
920 REMark this tries to compile a file, using the "make" prog
930 REMark this returns 1 if errors, else 0
940 LOCal chan%,is_ok$,ch_len
950   ch_len=EXEPF_W ("make";a$&" -w -l"&force_assembly$)                   : REMark exec make & wait
960   IF ch_len<>0
970      EXEC_W "make";a$&" -w -l"&force_assembly$                 : REMark exec make & wait
980   END IF
990   is_ok$=a$(1 TO LEN(a$)-4)             : REMark file name w/o extension
1000   chan%=FOP_IN(is_ok$&'log')            : REMark open log file
1010   IF chan%<0:RETurn 1                             : REMark can't even open it!
1020   ch_len=FLEN(#chan%)                             : REMark file length
1030   GET#chan%\ch_len-10                             : REMark this is where error report is
1040   INPUT#chan%,is_ok$                              : REMark get it
1050   CLOSE#chan%
1060   IF NOT is_ok$=="No errors"
1070     RETurn 1                                      : REMark anything other than "no errors" signals errors
1080   ELSE
1090     RETurn 0
1100   END IF
1110 END DEFine compile_and_check%
1120 :
1130 DEFine PROCedure make_new_dir (poste%)
1140 REMark get athe new dir where the files are located
1150 LOCal quel_rep$
1160   IF MENU_OK
1170     quel_rep$= DIR_SELECT$("The Files are where?",,10,10,main_coul%,app_coul%)
1180   ELSE
1190     quel_rep$=getstr$(poste%,0,main_defn,a_lire$)           : REMark use menu extns if possible
1200   END IF
1210   IF LEN (quel_rep$) >= 5
1220      rep$=quel_rep$(1 TO 5)
1230   END IF
1240   CH_ITEM main_defn,-1,poste%,-1,"",rep$
1250   main_lfl%(poste%)=main_lfl%(poste%)+1
1260 END DEFine make_new_dir
1270 :
1280 DEFine PROCedure make_my_data (what_machine%)
1290 REMark what_machine% = 1 -> generic
1300 REMark what_machine% = 2 -> atari
1310 REMark what_machine% = 4 -> Gold Card
1320 REMark what_machine% = 8 -> Qx0
1330 REMark what_machine% = 16 -> QXL
1340 REMark what_machine% = 32 -> aurora
1350 REMark what_machine% = 64 -> java
1360 REMark what_machine% = 128 -> ptrgen
1370 REMark what_machine% = 256 -> qpc
1380 REMark these may be combined
1390   DIM mdata$(250,50)
1400   REMark first make generic data
1410   data_count%=1                         : REMark general data count (global var)
1420   IF what_machine%&&1
1430     generic_data                        : REMark set up generic data
1440     read_my_datas                       : REMark and read it
1450   END IF
1460   IF what_machine%&&2
1470      atari_data                         : REMark set up machine specific data....
1480      read_my_datas                      : REMark and read it
1490   END IF
1500   IF what_machine%&&4
1510      gc_data                                      : REMark set up machine specific data....
1520      read_my_datas                      : REMark and read it
1530   END IF
1540   IF what_machine%&&8
1550      q40_data                                     : REMark set up machine specific data....
1560      read_my_datas                      : REMark and read it
1570   END IF
1580   IF what_machine%&&16
1590      qxl_data                                     : REMark set up machine specific data....
1600      read_my_datas                      : REMark and read it
1610   END IF
1620   IF what_machine%&&32
1630      aurora_data                        : REMark set up machine specific data....
1640      read_my_datas                      : REMark and read it
1650   END IF
1660   IF what_machine%&&64
1670      java_data                                    : REMark set up machine specific data....
1680      read_my_datas                      : REMark and read it
1690   END IF
1700   IF what_machine%&&128
1710      ptrgen_data                        : REMark set up machine specific data....
1720      read_my_datas                      : REMark and read it
1730   END IF
1740   IF what_machine%&&256
1750      qpc_data                                : REMark set up machine specific data....
1760      read_my_datas                      : REMark and read it
1770   END IF
1780 END DEFine make_my_data
1790 :
1800 DEFine PROCedure read_my_datas
1810 REMark reads datas into the global var mdata$
1820 REMark uses data_count% from make_my_data
1830 LOCal lp%
1840   REPeat lp%
1850     READ mdata$(data_count%)
1860     IF mdata$(data_count%)="":EXIT lp%
1870     data_count%=data_count%+1
1880   END REPeat lp%
1890 END DEFine read_my_datas
1900 :
1910 DEFine PROCedure generic_data
1920 restore 1920
1930   DATA '**** -- SMSQ Generic -- ****'
1940   DATA 'smsq_smsq_loader_link'
1950   DATA 'smsq_smsq_fh_link'
1960   DATA 'smsq_smsq_link'
1970   DATA 'smsq_smsq_cache_link'
1980   DATA 'smsq_sbas_link'
1990   DATA 'smsq_smsq_lang_link'
2000   DATA 'smsq_sbas_lang_link'
2010   DATA 'smsq_sbas_procs_link'
2020   DATA 'smsq_smsq_wman_link'
2030   DATA 'smsq_smsq_hotkey_link'
2040   DATA 'smsq_smsq_vers_link'
2050   DATA 'smsq_home_link'
2060   DATA 'smsq_smsq_1mb_link',''
2070 END DEFine generic_data
2080 :
2090 DEFine PROCedure q40_data
2100 restore 2100
2110   DATA '**** -- Q40/Q60 -- ****'
2120   DATA 'smsq_q40_hwinit_link'
2130   DATA 'smsq_q40_nasty_link'
2140   DATA 'smsq_q40_cache_link'
2150   DATA 'smsq_q40_cachemode_link'
2160   DATA 'smsq_smsq_cache40c_link'
2170   DATA 'smsq_q40_driver_ser_link'
2180   DATA 'smsq_q40_driver_dv3_link'
2190   DATA 'smsq_q40_driver_ql_link'
2200   DATA 'smsq_q40_driver_16_link'
2210   DATA 'smsq_q40_kbd_lang_link'
2220   DATA 'smsq_sbas_procs_link'
2230   DATA 'smsq_q40_sysspr_link',''
2240 END DEFine q40_data
2250 :
2260 DEFine PROCedure atari_data
2270 restore 2270
2280   DATA '**** -- Atari -- ****'
2290   DATA 'smsq_atari_hwinit_link'
2300   DATA 'smsq_atari_nasty_link'
2310   DATA 'smsq_atari_driver_ql_link'
2320   DATA 'smsq_atari_driver_mo_link'
2330   DATA 'smsq_atari_driver_ser_link'
2340   DATA 'smsq_atari_driver_dv3_link'
2350   DATA 'smsq_atari_kbd_lang_link'
2360   DATA 'smsq_atari_sysspr_link'
2370   DATA 'sys_boot_st_host_link',''
2380 END DEFine atari_data
2390 :
2400 DEFine PROCedure gc_data
2410 restore 2410
2420   DATA '**** -- (Super)GoldCard -- ****'
2430   DATA 'smsq_gold_host_link'
2440   DATA 'smsq_gold_hwinit_link'
2450   DATA 'smsq_gold_nasty_link'
2460   DATA 'smsq_gold_nasty_s_link'
2470   DATA 'smsq_gold_driver_ql_link'
2480   DATA 'smsq_gold_driver_8_link'
2490   DATA 'smsq_gold_driver_most_link'
2500   DATA 'smsq_gold_driver_nd_link'
2510   DATA 'smsq_gold_driver_nds_link'
2520   DATA 'smsq_gold_driver_dv3_link'
2530   DATA 'smsq_gold_kbd_lang_link'
2540   DATA 'smsq_gold_kbd_abc_lng_link'
2550   DATA 'smsq_gold_kbd_abc_link'
2560   DATA 'smsq_gold_roms_link'
2570   DATA 'smsq_gold_qimi_link'
2580   DATA 'smsq_gold_sysspr_link',''
2590 END DEFine gc_data
2600 :
2610 DEFine PROCedure aurora_data
2620 restore 2620
2630   DATA '**** -- Aurora -- ****'
2640   DATA 'smsq_gold_host_link'
2650   DATA 'smsq_gold_hwinit_link'
2660   DATA 'smsq_gold_nasty_link'
2670   DATA 'smsq_gold_nasty_s_link'
2680   DATA 'smsq_aurora_driver_8_link'
2690   DATA 'smsq_gold_driver_ql_link'
2700   DATA 'smsq_gold_driver_most_link'
2710   DATA 'smsq_gold_driver_nd_link'
2720   DATA 'smsq_gold_driver_nds_link'
2730   DATA 'smsq_gold_driver_dv3_link'
2740   DATA 'smsq_gold_kbd_lang_link'
2750   DATA 'smsq_gold_kbd_abc_lng_link'
2760   DATA 'smsq_gold_kbd_abc_link'
2770   DATA 'smsq_gold_roms_link'
2780   DATA 'smsq_gold_qimi_link'
2790   DATA 'smsq_aurora_sysspr_link'
2800   DATA 'smsq_gold_sysspr_link',''
2810 END DEFine aurora_data
2820 :
2830 DEFine PROCedure qxl_data
2840 restore 2840
2850   DATA '**** -- QXL -- ****'
2860   DATA 'smsq_qxl_host_link'
2870   DATA 'smsq_qxl_hwinit_link'
2880   DATA 'smsq_qxl_nasty_e_link'
2890   DATA 'smsq_qxl_driver_ql_link'
2900   DATA 'smsq_qxl_driver_16_link'
2910   DATA 'smsq_qxl_driver_most_link'
2920   DATA 'smsq_qxl_driver_nd_link'
2930   DATA 'smsq_qxl_procs_link'
2940   DATA 'smsq_qxl_driver_dv3e_link'
2950   DATA 'smsq_qxl_kbd_lang_link'
2960   DATA 'smsq_qxl_ecache_link'
2970   DATA 'smsq_qxl_sysspr_link',''
2980 END DEFine qxl_data
2990 :
3000 DEFine PROCedure java_data
3010 restore 3010
3020   DATA '**** -- Java -- ****'
3030   DATA 'smsq_java_host_link'
3040   DATA 'smsq_java_smsq_link'
3050   DATA 'smsq_java_smsq_1mb_link'
3060   DATA 'smsq_java_rtc_link'
3070   DATA 'smsq_java_hwinit_link'
3080   DATA 'smsq_java_driver_ql_link'
3090   DATA 'smsq_java_driver_8_link'
3100   DATA 'smsq_java_driver_16_link'
3110   DATA 'smsq_java_driver_most_link'
3120   DATA 'smsq_java_driver_dv3e_link'
3130   DATA 'smsq_java_ip_link'
3140   DATA 'smsq_gold_kbd_lang_link'
3150   DATA 'smsq_gold_sysspr_link',''
3160 END DEFine java_data
3170 :
3180 DEFine PROCedure qpc_data
3190 restore 3190
3200   DATA "**** -- QPC -- ****"
3210   DATA 'smsq_qpc_host_link'
3220   DATA 'smsq_qpc_hwinit_link'
3230   DATA 'smsq_qpc_nasty_e_link'
3240   DATA 'smsq_qpc_driver_ql_link'
3250   DATA 'smsq_qpc_driver_16_link'
3260   DATA 'smsq_qpc_driver_8_link'
3270   DATA 'smsq_qpc_driver_ql_link'
3280   DATA 'smsq_qpc_driver_most_link'
3290   DATA 'smsq_qpc_procs_link'
3300   DATA 'smsq_qpc_driver_dv3e_link'
3310   DATA 'smsq_qpc_kbd_lang_link'
3320   DATA 'smsq_qpc_dos_link'
3330   DATA 'smsq_qpc_ip_link'
3340   DATA 'smsq_qpc_cdaudio_link'
3350   DATA 'smsq_smsq_qpc_link'
3360   DATA 'smsq_qpc_sysspr_link',''
3370 END DEFine qpc_data
3380 :
3390 DEFine PROCedure ptrgen_data
3400 restore 3400
3410   DATA "**** -- PtrGen -- ****"
3420   DATA "ee_wman_link","ee_ptr_link","ee_hot_german_link","ee_hot_french_link"
3430   DATA "ee_hot_english_link",""
3440 END DEFine ptrgen_data
3450 :
3460 DEFine PROCedure make_targets
3470 REMark targets are only made if there was no error during compilation
3480 REMark to achieve this, a primitive approach is used:
3490 REMark a check is made whether an appsub menu item status is still selected
3500 REMark if it is, then the
3510   LOCal lp%,current_target%,tot_targets%
3520   tot_targets%=0                        : REMark no targets to make yet
3530   current_target%=0                               : REMark and no target yet
3540   FOR lp%=0 TO data_count%-2
3550     IF main_mfl%(lp%,0)=16
3560       REMark we're covering a new target
3570       tot_targets%=tot_targets%+current_target% : REMark show old target
3580       current_target%=find_target%(lp%+1) : REMark get new target we're covering
3590     ELSE
3600       IF main_mfl%(lp%,0)<>0:current_target%=0
3610     END IF
3620   END FOR lp%
3630   tot_targets%=tot_targets%+current_target%
3640   REMark at the end, here, tot_targets holds a bitmap
3650   REMark of targets for which compilation was ok
3660   IF (machines%&&1) AND NOT (tot_targets% && 1):RETurn : REMark generic was asked for but not compiled correctly!
3670   IF tot_targets%&&2:atari_target
3680   IF tot_targets%&&4:gc_target
3690   IF tot_targets%&&8:qx0_target
3700   IF tot_targets%&&16:qxl_target
3710   IF tot_targets%&&32:aurora_target
3720   IF tot_targets%&&64:java_target
3730   REMark IF tot_targets%&&128:ptr_gen_target  : rem no need for this
3740   IF tot_targets%&&256:qpc_target
3750 END DEFine make_targets
3760 :
3770 DEFine PROCedure atari_target
3780   cmdl$='m'
3790   p$=rep$&'sys_boot_st_flp'
3800   f$=rep$&'sys_boot_file'
3810   ah$=rep$&'sys_boot_st_host'
3820   ld$=rep$&'smsq_smsq_loader'
3830   hi$=rep$&'smsq_atari_hwinit'
3840   os1f$=rep$&'smsq_smsq_fh_os'
3850   os1$=rep$&'smsq_smsq_os'
3860   ca$=rep$&'smsq_smsq_cache'
3870   os2$=rep$&'smsq_sbas_control'
3880   ns1$=rep$&'smsq_atari_nasty'
3890   ln1$=rep$&'smsq_smsq_lang'
3900   ln2$=rep$&'smsq_atari_kbd_lang'
3910   ln3$=rep$&'smsq_sbas_lang'
3920   ex1$=rep$&'smsq_sbas_procs_x'
3930   ex2$=rep$&'smsq_atari_driver_dv3'
3940   ex3$=rep$&'smsq_atari_driver_ser'
3950   ex4$=rep$&'smsq_atari_driver_mono'
3960   ex5$=rep$&'smsq_atari_driver_ql'
3970   ex6$=rep$&'smsq_smsq_wman'
3980   ex7$=rep$&'smsq_smsq_hotkey'
3990   ex8$=rep$&'smsq_atari_sysspr'
4000   ex9$=rep$&'smsq_home_home'
4010   nl$=rep$&'sys_boot_null'
4020   IF 'm' INSTR cmdl$ = 0: ex4$ = nl$
4030   EW f$, ah$,ld$,hi$,os1f$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$, rep$&'smsq_atari_SMSQ.PRG'
4040 END DEFine atari_target
4050 :
4060 DEFine PROCedure gc_target
4070 REMark there are  2 targets for the Goldcard!
4080   f$=rep$&'sys_boot_file'
4090   v$=rep$&'smsq_smsq_vers'
4100   h$=rep$&'smsq_gold_host'
4110   ld$=rep$&'smsq_smsq_loader'
4120   hi$=rep$&'smsq_gold_hwinit'
4130   os1$=rep$&'smsq_smsq_os'
4140   ca$=rep$&'smsq_smsq_cache'
4150   os2$=rep$&'smsq_sbas_control'
4160   ns1$=rep$&'smsq_gold_nasty'
4170   ns2$=rep$&'smsq_gold_nasty_s'
4180   ln1$=rep$&'smsq_smsq_lang'
4190   ln2$=rep$&'smsq_gold_kbd_lang'
4200   ln2a$=rep$&'smsq_gold_kbd_abc_lang'
4210   ln3$=rep$&'smsq_sbas_lang'
4220   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
4230   ex2a$=rep$&'smsq_gold_kbd_abc'
4240   ex2$=rep$&'smsq_gold_driver_most'
4250   ex3a$=rep$&'smsq_gold_driver_ql'
4260   ex38$=rep$&'smsq_gold_driver_8'
4270   ex3q$=rep$&'smsq_gold_qimi'
4280   ex3w$=rep$&'smsq_smsq_wman'
4290   ex4$=rep$&'smsq_gold_driver_dv3'
4300   ex5$=rep$&'smsq_gold_driver_nd'
4310   ex6$=rep$&'smsq_gold_driver_nds'
4320   ex7$=rep$&'smsq_smsq_hotkey'
4330   ex8$=rep$&'smsq_gold_sysspr'
4340   ex9$=rep$&'smsq_home_home'
4350   nl$=rep$&'sys_boot_null'
4360   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex3a$,ex3q$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,ex1$,ex9$, rep$&'SMSQ_GOLD_gold'
4370   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex38$,ex3q$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,ex1$,ex9$, rep$&'SMSQ_GOLD_gold8'
4380 END DEFine gc_target
4390 :
4400 DEFine PROCedure aurora_target
4410   f$=rep$&'sys_boot_file'
4420   v$=rep$&'smsq_smsq_vers'
4430   h$=rep$&'smsq_gold_host'
4440   ld$=rep$&'smsq_smsq_loader'
4450   hi$=rep$&'smsq_gold_hwinit'
4460   os1$=rep$&'smsq_smsq_os'
4470   ca$=rep$&'smsq_smsq_cache'
4480   os2$=rep$&'smsq_sbas_control'
4490   ns1$=rep$&'smsq_gold_nasty'
4500   ns2$=rep$&'smsq_gold_nasty_s'
4510   ln1$=rep$&'smsq_smsq_lang'
4520   ln2$=rep$&'smsq_gold_kbd_lang'
4530   ln2a$=rep$&'smsq_gold_kbd_abc_lang'
4540   ln3$=rep$&'smsq_sbas_lang'
4550   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
4560   ex2a$=rep$&'smsq_gold_kbd_abc'
4570   ex2$=rep$&'smsq_gold_driver_most'
4580   ex3a$=rep$&'smsq_gold_driver_ql'
4590   ex3b$=rep$&'smsq_aurora_driver_8'
4600   ex3q$=rep$&'smsq_gold_qimi'
4610   ex3w$=rep$&'smsq_smsq_wman'
4620   ex3sq$=rep$&'smsq_aurora_sysspr'
4630   ex3sh$=rep$&'smsq_gold_sysspr'
4640   ex4$=rep$&'smsq_gold_driver_dv3'
4650   ex5$=rep$&'smsq_gold_driver_nd'
4660   ex6$=rep$&'smsq_gold_driver_nds'
4670   ex8$=rep$&"smsq_home_home"
4680   ex9$=rep$&'smsq_smsq_hotkey'
4690   ex10$=rep$&'smsq_gold_roms'
4700   nl$=rep$&'sys_boot_null'
4710   EW f$, v$,h$,ld$,hi$,os1$,ca$,os2$,ns1$,ns2$,ln1$,ln2$,ln2a$,ln3$,ex2a$,ex2$,ex3a$,ex3b$,ex3q$,ex3w$,ex3sh$,ex4$,ex5$,ex6$,ex9$,ex10$,ex8$,ex1$, rep$&'SMSQ_Aurora_SMSQE'
4720 END DEFine aurora_target
4730 :
4740 DEFine PROCedure qx0_target
4750   nl$=rep$&'sys_boot_null'
4760   f$=rep$&'sys_boot_file'
4770   qr$=rep$&'sys_boot_q40_rom'
4780   ld$=rep$&'smsq_smsq_loader'
4790   hi$=rep$&'smsq_q40_hwinit'
4800   os1$=rep$&'smsq_smsq_1mb_os' : REMark change for fast memory
4810   ca$=rep$&'smsq_smsq_cache40c'
4820   caq$=rep$&'smsq_q40_cache'
4830   caq4$=rep$&'smsq_q40_cachemode'
4840   os2$=rep$&'smsq_sbas_control'
4850   ns2$=rep$&'smsq_q40_nasty'
4860   ln1$=rep$&'smsq_smsq_lang'
4870   ln2$=rep$&'smsq_q40_kbd_lang'
4880   ln3$=rep$&'smsq_sbas_lang'
4890   ex1$=rep$&'smsq_sbas_procs_x'
4900   ex2$=rep$&'smsq_q40_driver_dv3'
4910   ex3$=rep$&'smsq_q40_driver_ser'
4920   ex4a$=rep$&'smsq_q40_driver_ql'
4930   ex4b$=rep$&'smsq_q40_driver_16'
4940   ex4w$=rep$&'smsq_smsq_wman'
4950   ex5$=rep$&'smsq_q40_sysspr'
4960   ex9$=rep$&'smsq_smsq_hotkey'
4970   ex10$=rep$&'smsq_home_home'
4980   EW f$, qr$,ld$,hi$,os1$,ca$,caq$,os2$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex4a$,ex4b$,ex4w$,ex5$,ex9$,ex10$,ns2$,caq4$, rep$&'smsq_q40_rom'
4990 END DEFine qx0_target
5000 :
5010 DEFine PROCedure qxl_target
5020   IF FOP_IN (rep$&'smsq_qxl_qxlh.exe') <> -7: PRINT 'QXLH.EXE exists'; cmd$: QUIT
5030   p$   = rep$&'sys_boot_file'
5040   bl$  = rep$&'smsq_qxl_qxl2_exe'
5050   hst$ = rep$&'smsq_qxl_host'
5060   hi$  = rep$&'smsq_qxl_hwinit'
5070   ld$  = rep$&'smsq_smsq_loader'
5080   os1$ = rep$&'smsq_smsq_os'
5090   ca$  = rep$&'smsq_smsq_cache'
5100   os2$ = rep$&'smsq_sbas_control'
5110   ns1$ = rep$&'smsq_qxl_nasty_e'
5120   ln1$ = rep$&'smsq_smsq_lang'
5130   ln2$ = rep$&'smsq_qxl_kbd_lang'
5140   ln3$ = rep$&'smsq_sbas_lang'
5150   ex1$ = rep$&'smsq_qxl_procs_x'
5160   ex2$ = rep$&'smsq_qxl_driver_most'  : REMark Keyboard before CON
5170   ex3$ = rep$&'smsq_qxl_driver_ql'
5180   ex3b$ = rep$&'smsq_qxl_driver_16'
5190   ex3w$ = rep$&'smsq_smsq_wman'
5200   ex4$ = rep$&'smsq_qxl_driver_nd'
5210   ex5$ = rep$&'smsq_qxl_driver_dv3e'
5220   ex6$= rep$&'smsq_smsq_hotkey'
5230   ex7$ = rep$&'smsq_qxl_sysspr'
5240   ex8$=rep$&'smsq_home_home'
5250   last$ = rep$&'smsq_qxl_ecache'
5260   dml$ = rep$&'smsq_qxl_dummy'
5270   qm$  = rep$&'qmon_qxl_bin'
5280   r$='ram1_smsqe.exe' : f$=rep$&'smsq_qxl_smsqe.exe'
5290   cct$=rep$&"extras_exe_cct"
5300   DELETE f$
5310   EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3w$,ex4$,ex5$,ex6$,ex7$,ex8$,last$, r$
5320   EW cct$, bl$,r$, f$
5330   DELETE r$
5340   OPEN #4,f$
5350   lenf=FLEN(#4)
5360   lens% = INT ((lenf+511)/512)
5370   lenb% = lenf - lens% * 512
5380   IF lenb%: lenb% = lenb% + 512
5390   BPUT #4\2, lenb% MOD 256, lenb% DIV 256, lens% MOD 256, lens% DIV 256
5400   CLOSE #4
5410 END DEFine qxl_target
5420 :
5430 DEFine PROCedure java_target
5440   f$=rep$&'sys_boot_file'
5450   v$=rep$&'smsq_smsq_vers'
5460   h$=rep$&'smsq_java_host'
5470   ld$=rep$&'smsq_smsq_loader'
5480   hi$=rep$&'smsq_java_hwinit'
5490   os1$=rep$&'smsq_java_smsq_os'
5500   os3$=rep$&'smsq_java_smsq_1mb_os' : REMark must be after os
5510   ca$=rep$&'smsq_smsq_cache'
5520   os2$=rep$&'smsq_sbas_control'
5530   ln1$=rep$&'smsq_smsq_lang'
5540   ln2$=rep$&'smsq_gold_kbd_lang'
5550   ln3$=rep$&'smsq_sbas_lang'
5560   ex1$=rep$&'smsq_sbas_procs_x'   : REMark this must be after "roms"
5570   ex2$=rep$&'smsq_java_driver_most'
5580   ex3a$=rep$&'smsq_java_driver_ql'
5590   ex3b$=rep$&'smsq_java_driver_8'
5600   ex3c$=rep$&'smsq_java_driver_16'
5610   ex3w$=rep$&'smsq_smsq_wman'
5620   ex4$=rep$&'smsq_java_driver_dv3e'
5630   ex7$=rep$&'smsq_smsq_hotkey'
5640   ex8$=rep$&'smsq_gold_sysspr'
5650   ex9$=rep$&'smsq_home_home'
5660   ex10$ = rep$&'smsq_java_ip_x'
5670   EW f$,h$,ld$,hi$,os1$,os3$,ca$,os2$,ln1$,ln2$,ln3$,ex2$,ex3a$,ex3b$,ex3c$,ex3w$,ex4$,ex1$,ex7$,ex8$,ex9$,ex10$, rep$&'SMSQ_java_java'
5680 END DEFine java_target
5690 :
5700 DEFine PROCedure qpc_target
5710   p$   = rep$&'sys_boot_file'
5720   hst$ = rep$&'smsq_qpc_host'
5730   hi$  = rep$&'smsq_qpc_hwinit'
5740   ld$  = rep$&'smsq_smsq_loader'
5750   os1$ = rep$&'smsq_smsq_qpc_os'
5760   ca$  = rep$&'smsq_smsq_cache'
5770   os2$ = rep$&'smsq_sbas_control'
5780   ns1$ = rep$&'smsq_qpc_nasty_e'
5790   ln1$ = rep$&'smsq_smsq_lang'
5800   ln2$ = rep$&'smsq_qpc_kbd_lang'
5810   ln3$ = rep$&'smsq_sbas_lang'
5820   ex1$ = rep$&'smsq_sbas_procs_x'
5830   ex2$ = rep$&'smsq_qpc_driver_most'  : REMark Keyboard before CON
5840   ex3$ = rep$&'smsq_qpc_driver_ql'
5850   ex3b$= rep$&'smsq_qpc_driver_16'
5860   ex3c$= rep$&'smsq_qpc_driver_8'
5870   ex3w$= rep$&'smsq_smsq_wman'
5880   ex3s$= rep$&'smsq_qpc_sysspr'
5890   ex4$ = rep$&'smsq_qpc_driver_dv3e'
5900   ex5$ = rep$&'smsq_smsq_hotkey'
5910   ex6$ = rep$&'smsq_qpc_procs_x'
5920   ex7$ = rep$&'smsq_qpc_cdaudio_x'
5930   ex8$ = rep$&'smsq_qpc_dos_x'
5940   ex9$ = rep$&'smsq_qpc_ip_x'
5950   exa$ = rep$&'smsq_home_home'
5960   qm$ =  rep$&"qmon_qpc_smsq"
5970   r$='ram1_smsqe.bin'
5980   EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3c$,ex3w$,ex3s$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$,exa$, r$
5990   COPY_O r$,rep$&'smsq_qpc_smsqe.bin'
6000   DELETE r$
6010   REMark rd$='ram1_smsqe.deb'
6020   REMark EW p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3c$,ex3w$,ex3s$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$,exa$,qm$, rd$
6030   REMark COPY_O r$,qpc$&'release_smsqe.bin'
6040   REMark COPY_O rd$,qpc$&'exe_smsqe.bin'
6050 END DEFine qpc_target
6060 :
6070 DEFine PROCedure findit
6080 LOCal lp%,a$
6090 restore 6090
6100   DATA p$, hst$,ld$,hi$,os1$,ca$,os2$,ns1$,ln1$,ln2$,ln3$,ex1$,ex2$,ex3$,ex3b$,ex3c$,ex3w$,ex3s$,ex4$,ex5$,ex6$,ex7$,ex8$,ex9$,exa$, r$,""
6110   REPeat lp%
6120     READ a$:IF a$="":EXIT lp%
6130     t%=FOP_IN(a$ )
6140     IF (t%<0)
6150        PRINT "not found "& a$
6160     END IF
6170  END REPeat lp%
6180 END DEFine findit
6190 :
6200 DEFine FuNction find_target% (current%)
6210   IF "SMSQ Generic" INSTR mdata$(current%):RETurn 1
6220   IF "Atari" INSTR mdata$(current%):RETurn 2
6230   IF "GoldCard" INSTR mdata$(current%):RETurn 4
6240   IF "Q40" INSTR mdata$(current%):RETurn 8
6250   IF "QXL" INSTR mdata$(current%):RETurn 16
6260   IF "Aurora" INSTR mdata$(current%):RETurn 32
6270   IF "Java" INSTR mdata$(current%):RETurn 64
6280   IF "Ptr_Gen" INSTR mdata$(current%):RETurn 128
6290   IF "QPC" INSTR mdata$(current%):RETurn 256
6300   RETurn 0                                     : REMark huh???
6310 END DEFine find_target%
6320 :
6330 DEFine PROCedure change_size (x%,y%)
6340 LOCal xrel%,yrel%,temp,a$,ttemp%(main_lit%),abs%,yabs%,t%,bt$,mtemp%(0,0)
6350   main_lfl%(size_it%)=1                 : REMark reset to available
6360   DIM mtemp%(DIMN(main_mfl%,1),DIMN(main_mfl%,2))
6370   ARRCOPY main_lfl%,ttemp%              : REMark keep statusses
6380   ARRCOPY main_mfl%,mtemp%
6390   CH_WIN main_defn,xrel%,yrel%                    : REMark ptr displacement
6400   xsize%=xsize%-xrel%+3
6410   ysize%=ysize%-yrel%                             : REMark new sizes
6420   t%=2^4+2^5
6430   RPTR xabs%,yabs%,t%,t%,xrel%,yrel%,bt$: REMark get absolute coords
6440   IF xsize%>xsz%-16:xsize%=xsz%-16
6450   IF ysize%>ysz%-16:ysize%=ysz%-16      : REMark max values
6460   DR_UNST main_defn                               : REMark no more wdw defn...
6470   DR_REMV main_defn                               : REMark .... now!
6480   app_addr=0                                      : REMark nor any appsub wdw
6490   make_main_wdw                         : REMark make window anew
6500   ARRCOPY ttemp%,main_lfl%
6510   ARRCOPY mtemp%,main_mfl%
6520   t%=lxs*3
6530   xabs%=xabs%-t%:IF xabs%<0:xabs%=0     : REMark position ptr nicely
6540   yabs%=yabs%-8:IF yabs%<0:yabs%=0
6550   SPTR xabs%,yabs%,0                              : REMark now
6560   IF compiled
6570      DR_PPOS main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
6580   ELSE
6590      DR_PULD main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
6600   END IF
6610    POS_PTR main_defn,t%*65536+8,1       : REMark position pointer in wdw
6620 END DEFine change_size
6630 :
6640 DEFine FuNction fen_app (adresse,tableau$,nr_sects%,selk%,phys%,a_exclure$)
6650 rem make a typical QPAC2 appsub wdw, with strings in the items
6660 rem the parameters are: the address (0 at first)
6670 rem the string array, the number of sections
6680 rem a flag whether we want selection keystrokes and whether hit=do
6690 rem an integer array with x,y, sizes & origs
6700 rem a string with chars to be excluded from the selection keystrokes
6710 LOCal app_attributs%(3,1),temp%,bord%,pap%,ori_x%,ori_y%
6720 LOCal tail_x%,tail_y%,fleches%,bar%,bar_sect%,arrows%,selkey%
6730 :
6740   IF adresse:RECHP adresse              : rem if this is a second call, release old
6750   tail_x%=phys%(0)                      : rem x size  - just to make this clear
6760   tail_y%=phys%(1)                      : rem y "size"      we could use this directly
6770   ori_x%=phys%(2)                       : rem
6780   ori_y%=phys%(3)                       : rem origs x,y
6790 :
6800   adresse=AP_MAKE(selk%,tail_x%,tail_y%,ori_x%,ori_y%,nr_sects%,tableau$,a_exclure$)
6810   IF adresse<0: RETurn 0                : rem  error, return 0
6820 :
6830   app_attributs%(0,0)=1
6840   app_attributs%(0,1)=sp.appihigh       : rem current appsub wdw item border size & colour
6850 :
6860   bord%=sp.appbd                        : rem border colour
6870   pap%=sp.appbg                         : rem paper colour
6880   selkey%=9                                       : rem selection keystroke, always TAB
6890   arrows%=sp.scrbararr                            : rem arrow colour
6900   bar%=sp.scrbar                        : rem bar colour
6910   bar_sect%=sp.scrbarsec                : rem bar section
6920 :
6930   app_attributs%(0,0)=1
6940   app_attributs%(0,1)=sp.appihigh       : rem current appsub wdw item border size & colour
6950 :
6960   app_attributs%(1,0)=sp.appiunabg
6970   app_attributs%(1,1)=sp.appiunafg      : rem unavailable
6980 :
6990   app_attributs%(2,0)=sp.appiavabg
7000   app_attributs%(2,1)=sp.appiavafg      : rem available
7010 :
7020   app_attributs%(3,0)=sp.appiselbg
7030   app_attributs%(3,1)=sp.appiselfg      : rem selected
7040 :
7050   er=AP_PARS(adresse,0,bord%,pap%,selk%,arrows%,bar%,bar_sect%,app_attributs%)
7060 :
7070   IF er:RETurn 0                        : rem OOOOPS
7080 :
7090   RETurn adresse                        : rem return the address!
7100 END DEFine fen_app
7110 :
7120 def fn warning_pos% (mess$,channel%,title$,pos)
7130 rem warning wdw with pointer positioning
7140   pos_ptr main_defn,pos,-1
7150   return warning%(mess$,channel%,title$)
7160 end def warning_pos%
7170 :
7180 define function warning%(mess$,channel%,title$)
7190 rem shows a warning message (mess$) in a small window. hit esc or ok
7200 rem to return; returns 0 if esc, 1 if ok  hit
7210 local item%,lp%,chan%,y%,swnum%,infot%
7220   if channel%
7230     chan%=channel%
7240   else
7250     chan%=fopen("con")
7260   endif
7270   ch_item warn_defn,-3,0,-1,'','Attention'
7280   ch_item warn_defn,-1,0,-1,cancel$,'ESC'
7290   ch_item warn_defn,-1,1,-1,'O','OK'
7300   warn_lfl%(0)=1:warn_lfl%(1)=1
7310   poke_w warn_defn+44,sp.errmg                           : rem border colour of wdw
7320   poke_w warn_defn+46,sp.errbg                           : rem paper colour of wdw
7330   poke_w warn_wdw_addr+14,sp.errbg             : rem paper of title info wdw
7340   obj_addr=peek_l(warn_wdw_addr+16)            : rem ptr to object list
7350   poke_w obj_addr+10,sp.errfg                            : rem info object text  colour
7360   dr_puld warn_defn,-1,-1
7370   dr_iwdf #chan%,warn_defn,0
7380   wm_paper#chan%,sp.errbg
7390   wm_ink#chan%,sp.errfg
7400   cls#chan%
7410   if title$<>"":print#chan%,title$;" :"
7420   print #chan%,mess$
7430   pos_ptr warn_defn,160*65536+10,-1
7440   repeat lp%
7450     rd_ptr warn_defn,item%,swnum%,y%,y%,y%,warn_lfl%
7460     if swnum%=-1:exit lp%
7470   end repeat lp%
7480   if not channel%:close#chan%
7490   dr_unst warn_defn
7500   return item%
7510 end define warning%
7520 :
7530 def proc info_pos (mess$,channel%,pos)
7540 rem warning wdw with positioning
7550   pos_ptr main_defn,pos,-1
7560   info mess$,channel%
7570 end def info_pos
7580 :
7590 define procedure info(mess$,channel%)
7600 rem shows window containing mess$
7610 local chan%,infot%,obj_addr
7620   if channel%
7630     chan%=channel%
7640   else
7650     chan%=fopen("con")
7660   endif
7670   ch_item warn_defn,-3,0,-1,"",'  Info.  '
7680   ch_item warn_defn,-1,0,-1,"",'Info'
7690   ch_item warn_defn,-1,1,-1,"",'Info'
7700   poke_w warn_defn+44,sp.infwinbg                 : rem paper colour of wdw
7710   poke_w warn_defn+46,sp.winbd                              : rem border around wdw
7720   poke_w warn_wdw_addr+14,sp.infwinbg             : rem paper of title info wdw
7730   obj_addr=peek_l(warn_wdw_addr+16)               : rem ptr to object list
7740   poke_w obj_addr+10,sp.infwinfg                  : rem info object text        colour
7750   dr_puld warn_defn,-1,-1
7760   pos_ptr warn_defn,90*65536+70,-1
7770   dr_iwdf #chan%,warn_defn,0
7780   wm_paper#chan%,sp.infwinbg
7790   wm_ink#chan%,sp.infwinfg
7800   cls#chan%
7810   print #chan%,mess$
7820   if not channel%:close#chan%
7830   info_is_pulled_down%=1
7840 end define info
7850 :
7860 def proc info_wait(mess$,channel%)
7870 rem shows info wdw and waits until user clicks
7880   info mess$,channel%
7890   info_wait_to_close
7900 end def info_wait
7910 :
7920 def proc info_wait_to_close
7930 local item%,lp%,chan%,y%,swnum%,infot%
7940   repeat lp%
7950     rd_ptr warn_defn,item%,swnum%,y%,y%,y%,warn_lfl%
7960     if swnum%=-1:exit lp%
7970   end repeat lp%
7980   dr_unst warn_defn
7990 end def info_wait_to_close
8000 :
8010 def proc info_pos_wait (mess$,channel%,pos)
8020 rem positions & shows info wdw and waits until user clicks
8030   info_pos mess$,channel%,pos
8040   info_wait_to_close
8050 end def info_pos_wait
8060 :
8070 def proc stop_info
8080   dr_unst warn_defn
8090   info_is_pulled_down%=0
8100 end def stop_info
8110 :
8120 DEFine PROCedure button (def_trav,menu_drap%,app_drap%,control%,chaine$,type_col%)
8130 REMark les parametres sont, dans l'ordre:
8140 REMark la dƒfinition de la fen‘tre, suivie des tableaux pour
8150 REMark les postes du menu dƒliƒ, les postes de la sous-fen‘tre d'appl.
8160 REMark et la dƒfinition de contr˜le, suivi du nom du bouton et
8170 REMark du type couleurs.
8180 REMark Il est supposƒ que vous avez ouvert une fen‘tre #1 si
8190 REMark le programme est compilƒ
8200   DR_UNST def_trav
8210   IF compiled: CLOSE#1
8220   button_wait chaine$
8230 rem  BTN chaine$,type_col%
8240   IF compiled
8250     OPEN#1,"CON_"
8260     DR_PPOS def_trav,-1,-1,menu_drap%,app_drap%,control%
8270   ELSE
8280     DR_PULD def_trav,-1,-1,menu_drap%,app_drap%,control%
8290   END IF
8300 END DEFine button
8310 :
8320 DEFine PROCedure make_colours (colourway%)
8330 REMark this initialises the GLOBAL variables menu_pap, menu_brd, menu_inv,
8340 REMark menu_ink and menu_bar according to the colourway passed as parameter.
8350 REMark More importantly, this sets the sp.xxx GLOBAL variables.
8360 REMark The GLOBAl variable mycolour_mode% must have been set prior to calling
8370 REMark this function, preferrably via the function set_colours%.
8380 REMark Moreover the colours black, white,red, green,wg_stripes, etc...
8390 REMark must have been set prior to calling this procedure:
8400 REMark Preferrably, they were also set via the function set_colours%
8410 :
8420 rem this function needs the extension WL_MK16 (outptr_bin)
8430 :
8440 REMark the colourways% are:
8450 REMark 0= white/green
8460 REMark 1= black/red
8470 REMark 2= white/red
8480 REMark 3= black/green
8490 remark 4-7= use system palettes
8500 :
8510 rem the Global war mycolour_mode% is:
8520 rem 0 = we are in QL mode 4
8530 rem 1 = we are in PAL mode (256 colours)
8540 rem 2 - we are in 24 bit colour mode
8550 rem preferrably, this is set via the function set_colours%
8560 :
8570   if colourway%>7 or colourway%<0: colourway%=0: REMark must be between 0 & 7
8580 :
8590 rem first make sure that what we request is possible.
8600 rem colourways 4-7 are only possible if we are in 8 or 24 bit colour mode
8610 rem if it is requested in another mode, it is set to 0
8620 :
8630   if colourway%>3
8640      make_my_palette colourway%-4: rem set colours according to system palette
8650      return                         : rem premature exit
8660   endif
8670 :
8680 rem this now is only the old colourways
8690   select on mycolour_mode%
8700     =0                                  : rem QL 4 colours
8710       SELect ON colourway%
8720           =0:                           : remark white/green
8730                 menu_pap=white
8740                 menu_brd=green
8750                 menu_ink=black
8760                 menu_bar=wg_striped
8770                 menu_inv=red
8780           =1:                           : remark black/red
8790                 menu_pap=black
8800                 menu_brd=red
8810                 menu_ink=white
8820                 menu_bar=br_striped
8830                 menu_inv=green
8840           =2:                           : remark white/red
8850                 menu_pap=white
8860                 menu_brd=red
8870                 menu_ink=black
8880                 menu_bar=wr_striped
8890                 menu_inv=green
8900           =3:                           : remark black/green
8910                 menu_pap=black
8920                 menu_brd=green
8930                 menu_ink=white
8940                 menu_bar=bg_striped
8950                 menu_inv=red
8960       END SELect
8970     =1                                  : rem pal colours
8980       SELect ON colourway%
8990           =0:                           : remark white/green
9000                 menu_pap=white+256
9010                 menu_brd=green+256
9020                 menu_ink=black+256
9030                 menu_bar=wg_striped+256
9040                 menu_inv=red +256
9050           =1:                           : remark black/red
9060                 menu_pap=black+256
9070                 menu_brd=red  +256
9080                 menu_ink=white +256
9090                 menu_bar=br_striped +256
9100                 menu_inv=green +256
9110                 omenu_pap=black+256
9120           =2:                           : remark white/red
9130                 menu_pap=white +256
9140                 menu_brd=red   +256
9150                 menu_ink=black +256
9160                 menu_bar=wr_striped +256
9170                 menu_inv=green +256
9180           =3:                           : remark black/green
9190                 menu_pap=black +256
9200                 menu_brd=green +256
9210                 menu_ink=white +256
9220                 menu_bar=bg_striped +256
9230                 menu_inv=red   +256
9240       END SELect
9250     =2                                  : rem 24 bit colours
9260       SELect ON colourway%
9270           =0:                           : remark white/green
9280                 menu_pap=WL_MK16(white)
9290                 menu_brd=WL_MK16(green)
9300                 menu_ink=WL_MK16(black)
9310                 menu_bar=WL_MK16(wg_striped)
9320                 menu_inv=WL_MK16(red)
9330           =1:                           : remark black/red
9340                 menu_pap=WL_MK16(black)
9350                 menu_brd=WL_MK16(red)
9360                 menu_ink=WL_MK16(white)
9370                 menu_bar=WL_MK16(br_striped)
9380                 menu_inv=WL_MK16(green)
9390           =2:                           : remark white/red
9400                 menu_pap=WL_MK16(white)
9410                 menu_brd=WL_MK16(red)
9420                 menu_ink=WL_MK16(black)
9430                 menu_bar=WL_MK16(wr_striped)
9440                 menu_inv=WL_MK16(green)
9450           =3:                           : remark black/green
9460                 menu_pap=WL_MK16(black)
9470                 menu_brd=WL_MK16(green)
9480                 menu_ink=WL_MK16(white)
9490                 menu_bar=WL_MK16(bg_striped)
9500                 menu_inv=WL_MK16(red)
9510       END SELect
9520   end select
9530 :
9540   make_my_old_palette
9550 :
9560 END DEFine make_colours
9570 :
9580 def proc make_my_palette (cw%)
9590   sp.winbd            = hex('0200') : rem Window border
9600   sp.winbg            = hex('0201') : rem Window background
9610   sp.winfg            = hex('0202') : rem Window foreground
9620   sp.winmg            = hex('0203') : rem Window middleground
9630   sp.titlebg          = hex('0204') : rem Title background
9640   sp.titletextbg  = hex('0205') : rem Title text background
9650   sp.titlefg          = hex('0206') : rem Title foreground
9660   sp.litemhigh        = hex('0207') : rem Loose item highlight
9670   sp.litemavabg   = hex('0208') : rem Loose item available background
9680   sp.litemavafg   = hex('0209') : rem Loose item available foreground
9690   sp.litemselbg   = hex('020a') : rem Loose item selected background
9700   sp.litemselfg   = hex('020b') : rem Loose item selected foreground
9710   sp.litemunabg   = hex('020c') : rem Loose item unavailable background
9720   sp.litemunafg   = hex('020d') : rem Loose item unavailable foreground
9730   sp.infwinbd         = hex('020e') : rem Information window border
9740   sp.infwinbg         = hex('020f') : rem Information window background
9750   sp.infwinfg         = hex('0210') : rem Information window foreground
9760   sp.infwinmg         = hex('0211') : rem Information window middleground
9770   sp.subinfbd         = hex('0212') : rem Subsidiary information window border
9780   sp.subinfbg         = hex('0213') : rem Subsidiary information window background
9790   sp.subinffg         = hex('0214') : rem Subsidiary information window foreground
9800   sp.subinfmg         = hex('0215') : rem Subsidiary information window middleground
9810   sp.appbd            = hex('0216') : rem Application window border
9820   sp.appbg            = hex('0217') : rem Application window background
9830   sp.appfg            = hex('0218') : rem Application window foreground
9840   sp.appmg            = hex('0219') : rem Application window middleground
9850   sp.appihigh         = hex('021a') : rem Application window item highlight
9860   sp.appiavabg        = hex('021b') : rem Application window item available background
9870   sp.appiavafg        = hex('021c') : rem Application window item available foreground
9880   sp.appiselbg        = hex('021d') : rem Application window item selected background
9890   sp.appiselfg        = hex('021e') : rem Application window item selected foreground
9900   sp.appiunabg        = hex('021f') : rem Application window item unavailable background
9910   sp.appiunafg        = hex('0220') : rem Application window item unavailable foreground
9920   sp.scrbar           = hex('0221') : rem Pan/scroll bar
9930   sp.scrbarsec        = hex('0222') : rem Pan/scroll bar section
9940   sp.scrbararr        = hex('0223') : rem Pan/scroll bar arrow
9950   sp.buthigh          = hex('0224') : rem Button highlight
9960   sp.butbd            = hex('0225') : rem Button border
9970   sp.butbg            = hex('0226') : rem Button background
9980   sp.butfg            = hex('0227') : rem Button foreground
9990   sp.hintbd           = hex('0228') : rem Hint border
10000   sp.hintbg           = hex('0229') : rem Hint background
10010   sp.hintfg           = hex('022a') : rem Hint foreground
10020   sp.hintmg           = hex('022b') : rem Hint middleground
10030   sp.errbg            = hex('022c') : rem Error message background
10040   sp.errfg            = hex('022d') : rem Error message foreground
10050   sp.errmg            = hex('022e') : rem Error message middleground
10060   sp.shaded           = hex('022f') : rem sp.shaded area
10070   sp.3ddark           = hex('0230') : rem Dark 3D border shade
10080   sp.3dlight          = hex('0231') : rem Light 3D border shade
10090   sp.vertfill         = hex('0232') : rem Vertical area fill
10100   sp.subtitbg         = hex('0233')  : rem Subtitle background
10110   sp.subtittxtbg  = hex('0234')  : rem Subtitle text background
10120   sp.subtitfg         = hex('0235')  : rem Subtitle foreground
10130   sp.mindexbg         = hex('0236')  : rem Menu index background
10140   sp.mindexfg         = hex('0237')  : rem Menu index foreground
10150   sp.separator        = hex('0238')  : rem Seperator lines etc.
10160   if not (is_open(#0) or is_open(#1))
10170     open#1,'con'
10180     sp_jobpal -1,cw%
10190     close#1
10200   else
10210     sp_jobpal -1,cw%
10220   endif
10230 end def make_my_palette
10240 :
10250 def proc make_my_old_palette
10260 rem this translates the colours used in the wdws into 4 colour mode...
10270   sp.winbd          =         menu_ink   : rem Window border
10280   sp.winbg          =         menu_pap   : rem Window background
10290   sp.winfg          =         menu_pap   : rem Window foreground
10300   sp.winmg          =         menu_pap   : rem Window middleground
10310   sp.titlebg        =         menu_bar   : rem Title background
10320   sp.titletextbg=   menu_pap   : rem Title text background
10330   sp.titlefg        =         menu_ink   : rem Title foreground
10340   sp.litemhigh      =         menu_brd   : rem Loose item highlight
10350   sp.litemavabg =   menu_pap   : rem Loose item available background
10360   sp.litemavafg =   menu_ink   : rem Loex fifiose item available foreground
10370   sp.litemselbg =   menu_brd   : rem Loose item selected background
10380   sp.litemselfg =   menu_ink   : rem Loose item selected foreground
10390   sp.litemunabg =   menu_pap   : rem Loose item unavailable background
10400   sp.litemunafg =   menu_brd   : rem Loose item unavailable foreground
10410   sp.infwinbd       =         menu_brd   : rem Information window border
10420   sp.infwinbg       =         menu_pap   : rem Information window background
10430   sp.infwinfg       =         menu_ink   : rem Information window foreground
10440   sp.infwinmg       =         menu_pap   : rem Information window foreground
10450   sp.subinfbd       =         menu_brd   : rem Information window border (sub)
10460   sp.subinfbg       =         menu_pap   : rem Information window background
10470   sp.subinffg       =         menu_ink   : rem Information window foreground
10480   sp.subinfmg       =         menu_pap   : rem Information window foreground
10490   sp.appbd          =         menu_brd   : rem Application window border
10500   sp.appbg          =         menu_pap   : rem Application window background
10510   sp.appfg          =         menu_ink   : rem Application window foreground
10520   sp.appmg          =         menu_pap   : rem Application window middleground
10530   sp.appihigh       =         menu_ink   : rem Application window item highlight
10540   sp.appiavabg      =         menu_pap   : rem Application window item available background
10550   sp.appiavafg      =         menu_ink   : rem Application window item available foreground
10560   sp.appiselbg      =         menu_brd   : rem Application window item selected background
10570   sp.appiselfg      =         menu_ink   : rem Application window item selected foreground
10580   sp.appiunabg      =         menu_pap   : rem Application window item unavailable background
10590   sp.appiunafg      =         menu_brd   : rem Application window item unavailable foreground
10600   sp.scrbar         =         menu_ink   : rem Pan/scroll bar
10610   sp.scrbarsec      =         menu_pap   : rem bar section
10620   sp.scrbararr      =         menu_brd   : rem Pan/scroll bar arrow
10630   sp.buthigh        =         menu_ink   : rem Button highlight
10640   sp.butbd          =         menu_brd   : rem Button border
10650   sp.butbg          =         menu_pap   : rem Button background
10660   sp.butfg          =         menu_ink   : rem Button foreground
10670   sp.hintbd         =         menu_brd   : rem Hint border
10680   sp.hintbg         =         menu_ink   : rem Hint background
10690   sp.hintfg         =         menu_pap   : rem Hint foreground
10700   sp.hintmg         =         menu_ink   : rem Hint middleground
10710   sp.errbg          =         menu_inv   : rem Error message background
10720   sp.errfg          =         menu_ink   : rem Error message foreground
10730   sp.errmg          =         menu_ink   : rem Error message middleground
10740   sp.shaded         =         0            : rem sp.shaded area
10750   sp.3ddark         =         0            : rem Dark 3D border shade
10760   sp.3dlight        =         white        : rem Light 3D border shade
10770   sp.vertfill       =         menu_pap   : rem Vertical area fill
10780   sp.subtitbg       =         menu_pap   : rem Subtitle background
10790   sp.subtittxtbg=   menu_pap   : rem Subtitle text background
10800   sp.subtitfg       =         menu_ink   : rem Subtitle foreground
10810   sp.mindexbg       =         menu_pap   : rem Menu index background
10820   sp.mindexfg       =         menu_brd   : rem same,        foreground
10830   sp.separator      =         menu_brd   : rem Seperator lines etc.
10840 end def make_my_old_palette
10850 :
10860 DEFine FuNction centr$(ctr$,leng)
10870 rem this "centres" the string ctr$ (i.e. left padds it with spaces) so that
10880 rem it is centred in a window of leng width (leng is length in chars)
10890   LOCal l%,cta$
10900   cta$=strip_spaces$(ctr$)
10910   l%=INT(leng-LEN(cta$))/2
10920   if l%<0:return cta$
10930   return FILL$(' ',l%)&cta$&FILL$(' ',l%)
10940 END DEFine centr$
10950 :
10960 DEFine PROCedure scroll_arrows(rangees%,nbr_objets%,appfen,cty%)
10970 rem Cette procƒdure dƒcide s'il faut laisser les flches de dƒfilement
10980 rem params: le nombre de rangƒes, le nombre total d'objets
10990 rem le pointer vers la sous-fen d'app et la dƒfinition de controle
11000   IF nbr_objets%<=rangees%
11010     POKE_W appfen+150,0       : rem no y offset to start of menu
11020     cty%(1,2)=nbr_objets%     : rem nbr of columns/rows
11030 rem  ELSE
11040 rem    POKE_W appfen+150,6
11050 rem    cty%(1,2)=rangees%-1      : rem if scroll arrows are there, there is one line less in the window
11060  END IF
11070  IF DIMN(cty%,1)=2:cty%(2,2)=cty%(1,2)
11080  cty%(0,1)=1                            : rem show that definitionhas changed
11090 END DEFine scroll_arrows
11100 :
11110 DEFine FuNction getstr$(poste%,drap%,defn,a$)
11120 REMark opens and sets a window over a loose item and proposes a sting to be edited.
11130 REMark parameters:
11140 REMark - item nbr
11150 REMark - falg whether result MUST be a numbrr (flag<>0)
11160 REMark - the working definition
11170 REMark - the string o edit
11180 rem returns the string or ""
11190 rem uses the global (!) vars sp.litemselbg and sp.litemselfg
11200 rem
11210 local b$,chan%
11220   chan%=FOPEN("con")
11230   b$=getstr2$(poste%,drap%,defn,a$,chan%)
11240   close#chan%
11250   return b$
11260 END DEFine getstr$
11270 :
11280 DEFine FuNction getstr2$(poste%,drap%,defn,a$,chan%)
11290 REMark sets a window over a loose item and proposes a sting to be edited.
11300 REMark parameters:
11310 REMark - item nbr
11320 REMark - falg whether result MUST be a numbrr (flag<>0)
11330 REMark - the working definition
11340 REMark - the string o edit
11350 remark - a screen channel to be used
11360 rem returns the string or ""
11370 rem uses the global (!) vars sp.litemselbg and sp.litemselfg
11380 rem
11390   LOCal b$,res
11400   b$=" ":b$=a$           : REMark c'est curieusement nƒcessaire
11410   wm_PAPER#chan%,sp.litemselbg
11420   wm_INK#chan%,sp.litemselfg
11430   DR_LWDF#chan%,defn,poste%             : rem open channel over item
11440   CLS#chan%
11450   res=GET_STR(#chan%,drap%,b$)                                : rem get string
11460   IF res<0:b$=""                        : rem ooops
11470   CH_ITEM defn,-1,poste%,-1,"",b$
11480   RETurn b$
11490 END DEFine getstr2$
11500 :
11510 def fn showcont$(string$,length%)
11520 rem this adds '...' at the end of a string, to show that it continues
11530 local l%,lengt%
11540   l%=len(string$)
11550   if not l%:return ""                             : rem empty string
11560   lengt%=length% div 6
11570   if l%<=lengt%:return string$                    : rem entire string fits wdw
11580   return string$(1 to lengt%-3)&"..."
11590 end def showcont$
11600 :
11610 def fn get_free_text$(defn,item%,prompt$,length%)
11620 rem this opens wdw over loose-item item% of the working definition defn
11630 rem allows free text to be entered
11640 rem params: working defintion,item over which wdw is to be opened
11650 rem           first value of string, length of max string in wdw, if this is...
11660 rem              -1, the length will be determined here
11670 local chan%,string$,tempsizes%(3)
11680   chan%=fopen('con')
11690   if chan%<0:return prompt$
11700   dr_lwdf#chan%,defn,item%              : rem open wdw over item
11710   paper#chan%,menu_brd
11720   ink#chan%,menu_ink
11730   cls#chan%
11740   string$=wedit$(#chan%,prompt$)
11750   if length%=-1
11760      wsipo#chan%,tempsizes%
11770      length%=tempsizes%(0)
11780   endif
11790   close#chan%
11800   if length%
11810     ch_item defn,-1,item%,-1,"",showcont$(string$,length%)
11820   else
11830     ch_item defn,-1,item%,-1,"",string$
11840   endif
11850   return string$
11860 end def get_free_text$
11870 :
11880 def fn do_caps(a$,caps%)
11890 rem set string to capitales etc,caps% determines whether the string....
11900 REMark ...must be lower case (=0), upper case(=1), all words capitalized (=2),
11910 REMark ... first word only capitalized (=3), or nothing(=-1)
11920   SELect ON caps%
11930    =0: MIN a$
11940    =1: MAJ a$
11950    =2: CAP2 a$
11960    =3: CAP a$
11970   END SELect
11980   return a$
11990 end def do_caps
12000 :
12010 def fn set_colours% (force_mode%)
12020 rem this sets colour variables according to the colour mode wished, if possible.
12030 rem force_mode%=
12040 rem       0 if ql colours wished (mode 4 only)
12050 rem       1 if pal colours wished
12060 rem       2 if 24 bit colours wished
12070 rem This function returns the true QL mode set (4,8,32,33 etc.).
12080 rem This function also sets the GLOBAL variable mycolour_mode% to the
12090 rem mode 0,1,2 as above - whatever is possible on the machine, i.e.
12100 rem if colour_24 is wished on a 4 colour machine (which won't succeed)
12110 rem this function will set set that variable to 0, i.e. QL mode.
12120 rem
12130 rem This function needs the outptr toolkit
12140 local mode%
12150   mode%=rmode                                     : rem get current mode now
12160   select on force_mode%
12170       = 0                                         : rem mode 4 wished
12180        select on mode%
12190             = 4                                   : rem already on, so do nothing
12200             = 0,1,8 : mode 4            : rem old modes, set to 4 colours
12210             = remainder
12220               colour_ql                           : rem if higher colour mode, set to "mode 4"
12230               mode 4
12240        end select
12250        mode%=0                                    : rem new mode we're in  - 4 colour mode
12260       = 1                                         : rem PALette wished
12270        select on mode%
12280             = 16,32,33 : colour_pal:mode%=1  : rem we're in high colour mode, but PAL colours wished
12290             = remainder
12300               if mode%<>4:mode 4
12310               mode%=0
12320        end select
12330       = 2
12340        select on mode%
12350             = 32,33 : colour_24 :mode%=2          : rem we're in high col mode 24 bit colours
12360             = 16 : colour_pal : mode%=1
12370             = remainder
12380               if mode%<>4:mode 4
12390               mode%=0
12400        end select
12410   end select
12420   mycolour_mode%=mode%                            : rem set this global variable now:
12430   rem now set some colour variables
12440   select on mode%
12450        = 0                                        : rem Ql 4 mode colours
12460           Black            = 0
12470           White            = 7
12480           Red              = 2
12490           Green            = 4
12500           grey             = 255
12510           dull_pink      = 19
12520           Blue             = 0
12530           Magenta        = 2
12540           Yellow           = 7
12550           Cyan             = 0
12560           Dark_slate     = 0
12570           Slate_grey     = 255
12580           Dark_grey      = 255
12590           Light_grey     = 255
12600           Ash_grey       = 255
12610           Dark_red       = 210
12620           Light_green    = 220
12630           Mustard        = 242
12640           Dark_green     = 228
12650           Sea_blue       = 0
12660           Dark_blue      = 0
12670           Purple           = 0
12680           Shocking_pink  = 234
12690           Orange           = 234
12700           Lime_green     = 4
12710           Apple_green    = 4
12720           Bright_blue    = 0
12730           Mauve            = 0
12740           Peach            = 7
12750           Light_yellow   = 7
12760           Light_blue     = 0
12770           Sky_blue       = 0
12780           Rose_pink      = 234
12790           Pink             = 234
12800           Beige            = 7
12810           Pastel_pink    = 234
12820           Pastel_yellow  = 7
12830           Pastel_green   = 4
12840           Pastel_cyan    = 0
12850           Pastel_blue    = 0
12860           Pastel_rose    = 234
12870           Brick            = 210
12880           Light_khaki    = 7
12890           Dull_green     = 228
12900           Dull_cyan      = 0
12910           Steel_blue     = 0
12920           Dull_pink      = 234
12930           Brown            = 0
12940           Khaki            = 7
12950           Dusky_green    = 4
12960           Dusky_blue     = 0
12970           Midnight_blue  = 0
12980           Plum             = 0
12990           Dusky_pink     = 234
13000           Buff             = 0
13010           Avocado        = 4
13020           Dull_turquoise = 4
13030           Dull_blue      = 0
13040           Faded_purple   = 234
13050           Cerise           = 2
13060           Tan_normal     = 0
13070           Grass_green    = 4
13080           Sea_green      = 4
13090           Ultramarine    = 0
13100           Deep_purple    = 234
13110           Light_orange   = 7
13120           light_red      = 2
13130           wg_striped     = 92
13140           br_striped     = 82
13150           wr_striped     = 107
13160           bg_striped     = 100
13170        = 1                              : rem Colour_pal
13180           Black            = 0
13190           White            = 1
13200           Red              = 2
13210           Green            = 3
13220           Blue             = 4
13230           Magenta        = 5
13240           Yellow           = 6
13250           Cyan             = 7
13260           Dark_slate     = 8
13270           Slate_grey     = 9
13280           Dark_grey      = 10
13290           Grey             = 11
13300           Light_grey     = 12
13310           Ash_grey       = 13
13320           Dark_red       = 14
13330           Light_green    = 15
13340           Mustard        = 16
13350           Dark_green     = 17
13360           Sea_blue       = 18
13370           Dark_blue      = 19
13380           Purple           = 20
13390           Shocking_pink  = 21
13400           Orange           = 22
13410           Lime_green     = 23
13420           Apple_green    = 24
13430           Bright_blue    = 25
13440           Mauve            = 26
13450           Peach            = 27
13460           Light_yellow   = 28
13470           Light_blue     = 29
13480           Sky_blue       = 30
13490           Rose_pink      = 31
13500           Pink             = 32
13510           Beige            = 33
13520           Pastel_pink    = 34
13530           Pastel_yellow  = 35
13540           Pastel_green   = 36
13550           Pastel_cyan    = 37
13560           Pastel_blue    = 38
13570           Pastel_rose    = 39
13580           Brick            = 40
13590           Light_khaki    = 41
13600           Dull_green     = 42
13610           Dull_cyan      = 43
13620           Steel_blue     = 44
13630           Dull_pink      = 45
13640           Brown            = 46
13650           Khaki            = 47
13660           Dusky_green    = 48
13670           Dusky_blue     = 49
13680           Midnight_blue  = 50
13690           Plum             = 51
13700           Dusky_pink     = 52
13710           Buff             = 53
13720           Avocado        = 54
13730           Dull_turquoise = 55
13740           Dull_blue      = 56
13750           Faded_purple   = 57
13760           Cerise           = 58
13770           Tan_normal     = 59
13780           Grass_green    = 60
13790           Sea_green      = 61
13800           Ultramarine    = 62
13810           Deep_purple    = 63
13820           pinkish        = 77
13830           light_red      = 109
13840           Light_orange   = 208
13850           wg_striped     = light_green
13860           br_striped     = dark_red
13870           wr_striped     = light_red
13880           bg_striped     = dark_green
13890        = 2                              : rem colour_24
13900           Black            = hex('000000')
13910           White            = hex('FFFFFF')
13920           Red              = hex('FF0000')
13930           Green            = hex('00FF00')
13940           Blue             = hex('0000FF')
13950           Magenta        = hex('FF00FF')
13960           Yellow           = hex('FFFF00')
13970           Cyan             = hex('00FFFF')
13980           Dark_slate     = hex('242424')
13990           Slate_grey     = hex('494949')
14000           Dark_grey      = hex('6D6D6D')
14010           Grey             = hex('929292')
14020           Light_grey     = hex('B6B6B6')
14030           Ash_grey       = hex('DBDBDB')
14040           Dark_red       = hex('920000')
14050           Light_green    = hex('B6FFB6')
14060           Mustard        = hex('929200')
14070           Dark_green     = hex('009200')
14080           Sea_blue       = hex('009292')
14090           Dark_blue      = hex('000092')
14100           Purple           = hex('920092')
14110           Shocking_pink  = hex('FF0092')
14120           Orange           = hex('FF9200')
14130           Lime_green     = hex('92FF00')
14140           Apple_green    = hex('00FF92')
14150           Bright_blue    = hex('0092FF')
14160           Mauve            = hex('9200FF')
14170           Peach            = hex('FFB6B6')
14180           Light_yellow   = hex('FFFFB6')
14190           Light_blue     = hex('B6FFFF')
14200           Sky_blue       = hex('B6B6FF')
14210           Rose_pink      = hex('FFB6FF')
14220           Pink             = hex('FFB6DB')
14230           Beige            = hex('FFDBB6')
14240           Pastel_pink    = hex('FFDBDB')
14250           Pastel_yellow  = hex('FFFFDB')
14260           Pastel_green   = hex('DBFFDB')
14270           Pastel_cyan    = hex('DBFFFF')
14280           Pastel_blue    = hex('DBDBFF')
14290           Pastel_rose    = hex('FFDBFF')
14300           Brick            = hex('B66D6D')
14310           Light_khaki    = hex('B6B66D')
14320           Dull_green     = hex('6DB66D')
14330           Dull_cyan      = hex('6DB6B6')
14340           Steel_blue     = hex('6D6DB6')
14350           Dull_pink      = hex('B66DB6')
14360           Brown            = hex('6D2424')
14370           Khaki            = hex('6D6D24')
14380           Dusky_green    = hex('246D24')
14390           Dusky_blue     = hex('246D6D')
14400           Midnight_blue  = hex('24246D')
14410           Plum             = hex('6D246D')
14420           Dusky_pink     = hex('B64992')
14430           Buff             = hex('B69249')
14440           Avocado        = hex('92B649')
14450           Dull_turquoise = hex('49B692')
14460           Dull_blue      = hex('4992B6')
14470           Faded_purple   = hex('9249B6')
14480           Cerise           = hex('920049')
14490           Tan_normal     = hex('924900')
14500           Grass_green    = hex('499200')
14510           Sea_green      = hex('009249')
14520           Ultramarine    = hex('004992')
14530           Deep_purple    = hex('490092')
14540           Light_orange   = hex('ffb624')
14550           pinkish        = hex('C0037B')
14560           Light_red      = hex('FC1729')
14570           wg_striped     = light_green
14580           br_striped     = dark_red
14590           wr_striped     = light_red
14600           bg_striped     = dark_green
14610   end select
14620   mode%=rmode
14630   ret mode%                             : rem return current screen mode now
14640 end def set_colours%
14650 :
14660 DEFine PROCedure init
14670 LOCal temp%,targets%,start%,lp%
14680   text=0:sprite=2:blob=4:pattern=6
14690   retr=256:retn=-256:sous1=254:sous2=252:sous3=250
14700   REMark normal: hit = selects/deselects, do = returns & leaves selected
14710   REMark retr = return even on hit, reset item to available after,
14720   REMark retn = return even on hit, hit selects/deselects, do selects
14730 :
14740   hit$=CHR$(1):do$=CHR$(2):cancel$=CHR$(3):sleep$=CHR$(7):wake$=CHR$(8)
14750   help$=CHR$(4):move$=CHR$(5):size$=CHR$(6):cr$=CHR$(10)
14760 :
14770   appwinoffset%=16                      : REMark appsub win additional y offset
14780   xsize%=500                                      : REMark x size divisible by 4!
14790   ysize%=200                                      : REMark provisional y size%
14800   lxs=24:lys=12                         : REMark standard item x,y, sizes
14810   inf1y=lys+4                                     : REMark y orig of inner border
14820   xesp=lxs+2                                      : REMark loose items x spacing
14830   nbr_post%=4                                     : REMark nbr of item right & left in title bar
14840   main_coul%=SMSCFG(2)                            : REMark colourways
14850   app_coul%=main_coul%                            : REMark preset colourways
14860   temp%=set_colours% (2)                : REMark always try to set highest colours
14870   rem IF temp%<2 AND main_coul%>3:main_coul%=0 : REMark old wman
14880   make_colours main_coul%               : REMark set all colours now
14890 :
14900   rep$=SMSCFG(1)                        : REMark dir with files
14910   titre$="Dir: "&rep$&" "
14920 :
14930 restore 14930
14940 :
14950 : REMark make loose item attributes
14960 :
14970   DIM std_iattr(3,3)
14980   RD_IATT std_iattr
14990     DATA 1,sp.litemhigh : REMark current item border size & colour
15000     DATA sp.litemunabg,sp.litemunafg,0,0 : REMark unavailable item paper & ink
15010     DATA sp.litemavabg,sp.litemavafg,0,0 : REMark   available               "    "           "
15020     DATA sp.litemselbg,sp.litemselfg,0,0 : REMark   selected
15030 :
15040 : REMark make data for sprites - uses the modified RD_SPRT function
15050 : REMark
15060 : REMark
15070 :
15080   espace_vide=ALCHP(100)                : REMark GLOBAL var for 0 mask sprites!
15090 :
15100   main_sprite=RD_SPRT(0)                : REMark 0 = normal mask
15110     DATA 11,13,6,5,4
15120     DATA '     www     '
15130     DATA '     waw     '
15140     DATA '     waw     '
15150     DATA '     waw     '
15160     DATA 'wwwwwa awwwww'
15170     DATA 'waaaa   aaaaw'
15180     DATA 'wwwwwa awwwww'
15190     DATA '     waw     '
15200     DATA '     waw     '
15210     DATA '     waw     '
15220     DATA '     www     '
15230 :
15240   move_sprite=RD_SPRT(1)                : REMark 1 = 0 mask
15250   IF temp%>1:move_sprite=6              : REMark or system sprite!
15260     DATA 10,14,5,4,4
15270     DATA 'aaaaaaaaaa    '
15280     DATA 'awwwwwwwwa    '
15290     DATA 'awwrrrrwwa    '
15300     DATA 'awwrrrrwwaaaaa'
15310     DATA 'awwrrwwwwwwwwa'
15320     DATA 'awwwwwwwwrrwwa'
15330     DATA 'aaaaawwrrrrwwa'
15340     DATA '    awwrrrrwwa'
15350     DATA '    awwwwwwwwa'
15360     DATA '    aaaaaaaaaa'
15370 :
15380   zzz_sprite= RD_SPRT(1)
15390   IF temp%>1:zzz_sprite=10
15400     DATA 7,15,0,0,4
15410     DATA 'wwwww          '
15420     DATA '   w           '
15430     DATA '  w  wwww      '
15440     DATA ' w     w       '
15450     DATA 'wwwww w  wwww  '
15460     DATA '     wwww  w   '
15470     DATA '          wwww '
15480 :
15490  size_sprite=RD_SPRT(1)
15500  IF temp%>1:size_sprite=7
15510     DATA 9,14,5,4,4
15520     DATA 'aaaaaaaaaaaaaa'
15530     DATA 'awwwwwwwwwwwwa'
15540     DATA 'awwrrrrrrrrwwa'
15550     DATA 'awwrrrrrrrrwwa'
15560     DATA 'awwrrwwwwwwwwa'
15570     DATA 'awwrrwwrrrrwwa'
15580     DATA 'awwrrwwrrrrwwa'
15590     DATA 'awwwwwwwwwwwwa'
15600     DATA 'aaaaaaaaaaaaaa'
15610 :
15620   targets%=0
15630   REMark IF NOT compiled:cmd$="-tj-tg-st-mk-as"
15640   quit_always%=0:quit_no_errors%=1:autostart%=0
15650   domakeit%=0
15660   all_targets%=0
15670   force_assembly$=""
15680   IF cmd$<>''
15690       IF '-qa' INSTR cmd$:quit_always%=1
15700       IF '-q0' INSTR cmd$:quit_no_errors%=1
15710       IF '-as' INSTR cmd$:autostart%=1
15720       IF '-st' INSTR cmd$:select_all%=1
15730       IF '-mk' INSTR cmd$:do_make_it%=1
15740       IF '-tg' INSTR cmd$:taedrgets%=targets%+1 : REMark Generic
15750       IF '-ta' INSTR cmd$:targets%=targets%+2 : REMark Atari
15760       IF '-to' INSTR cmd$:targets%=targets%+4 : REMark gOld
15770       IF '-tq' INSTR cmd$:targets%=targets%+8 : REMark Q40
15780       IF '-tx' INSTR cmd$:targets%=targets%+16 : REMark qXl
15790       IF '-tu' INSTR cmd$:targets%=targets%+32 : REMark aUrora
15800       IF '-tj' INSTR cmd$:targets%=targets%+64 : REMark Java
15810       IF '-tr' INSTR cmd$:targets%=targets%+128 : REMark ptRgen
15820       IF '-tc' INSTR cmd$:targets%=targets%+256 : REMark qpC
15830       IF '-at' INSTR cmd$:targets%=255+256  : REMark all targets
15840       IF '-fa' INSTR cmd$:force_assembly$=" -f": REMark force assembly of all files
15850   ELSE
15860     targets%=1
15870   END IF
15880   make_my_data targets%
15890   make_main_wdw
15900   IF (select_all%):main_lfl%(all_it%)=129:set_statusses
15910   start%=1
15920   FOR lp%=target_its% TO target_its%+8
15930     IF targets% && start%: main_lfl%(lp%)=129
15940     start%=start%*2
15950   END FOR lp%
15960   IF do_make_it%:main_lfl%(make_it%)=129
15970 :
15980   warn_make
15990 :
16000 END DEFine init
16010 :
16020 DEFine PROCedure make_main_wdw
16030 REMark make the main window
16040 LOCal main_lot,main_iwt,main_iot1
16050 :
16060   REMark first make sure of the wdw sizes (we also come here from change_size!)
16070   xsize%=xsize%-(xsize% MOD 4)                                : REMark make into nbr divisible by 4
16080   IF xsize%<500:xsize%=500
16090 :
16100   REMark calculate the y size of the wdw so that it always looks nice, in that
16110   REMark the rows in the appsub always fill the appsub entirely
16120   ysize%=ysize%+(ysize%&&1)             : REMark make into even number
16130   IF ysize%<100:ysize%=100              : REMark predetermioned min ysize% before calculations
16140   ysize%=ysize%-inf1y-14-appwinoffset%
16150   rangees=ysize% DIV 12                 : REMark GLOBAL var: nbr of rows in appsub
16160   IF rangees>data_count%-1:rangees=data_count%-1
16170   IF rangees<3:rangees=3
16180   ysize%=rangees*12+inf1y+14 +appwinoffset%
16190 :
16200   REMark first calc info obj size - this also influences one loose item, used
16210   REMark in the title bar
16220   longueur_titre=LEN(titre$)*6                    : REMark length in pixels
16230   titre_x=INT((xsize%-longueur_titre)/2): REMark x orig
16240 :
16250   REMark first the loose items
16260 :
16270   yinf1y=inf1y+4
16280   esc%=0:move_it%=esc%+1:size_it%=move_it%+1
16290   sleep_it%=size_it%+1:all_it%=sleep_it%+1:go_it%=all_it%+1
16300   dir_it%=go_it%+1:target_its%=dir_it%+1:make_it%=target_its%+9
16310 :
16320 restore 16320
16330   REMark reset data pointer to here
16340 :
16350   temp%=(xsize%-4) DIV 9
16360   main_lit%=16:DIM main_lfl%(main_lit%)   : REMark there are 16 items!
16370   main_lot=RD_LOT(std_iattr,main_lit%)
16380     DATA lxs,lys,4+xesp*0,2,0,0,cancel$,text+retr,'ESC'
16390     DATA lxs,lys,4+xesp*1,2,0,0,move$,sprite+retr,move_sprite
16400     DATA lxs,lys,4+xesp*2,2,0,0,size$,sprite+retn,size_sprite
16410     DATA lxs,lys,xsize%-2-xesp*1,2,0,0,sleep$,sprite+retr,zzz_sprite
16420     DATA lxs,lys,xsize%-2-xesp*3,2,0,0,'A',sous1+retn,"All"
16430     DATA lxs,lys,xsize%-2-xesp*4,2,0,0,'O',sous1+retn,"OK"
16440     DATA 6*6,lys-2,titre_x+30,3,0,0   ,'D',text+retn,rep$
16450     DATA 8*6,lys,8,inf1y+2,0,0,        'G',sous1+retn,"Generic"
16460     DATA 8*6,lys,8+temp%,inf1y+2,0,0,  'T',sous2+retn,"Atari"
16470     DATA 8*6,lys,8+temp%*2,inf1y+2,0,0,'L',sous3+retn,"GoldCard"
16480     DATA 8*6,lys,8+temp%*3,inf1y+2,0,0,'Q',sous1+retn,"Q40/Q60"
16490     DATA 8*6,lys,8+temp%*4,inf1y+2,0,0,'X',sous2+retn,"QXL"
16500     DATA 8*6,lys,8+temp%*5,inf1y+2,0,0,'U',sous2+retn,"Aurora"
16510     DATA 8*6,lys,8+temp%*6,inf1y+2,0,0,'J',sous1+retn,"Java"
16520     DATA 8*6,lys,8+temp%*7,inf1y+2,0,0,'P',sous1+retn,"PtrGen"
16530     DATA 8*6,lys,8+temp%*8,inf1y+2,0,0,'C',sous3+retn,"QPC"
16540     DATA 4*6,lys,xsize%-2-xesp*2,2,0,0,'M',sous1+retn,"Make"
16550 :
16560   REMark now info wdw objects - here only one, the title
16570   REMark make the title data: calculate size and x position of this object
16580 :
16590   main_iot1=RD_IOT(0)
16600     DATA longueur_titre,10,4,INT((lys-10)/2),sous1,sp.titlefg,0,0,titre$
16610 :
16620   REMark now the info wdws
16630 :
16640   main_iwt=RD_IWT(2)                                          : REMark there are 3
16650 :
16660 REMark first the title bar
16670 :
16680     DATA xsize%-8-(2*nbr_post%*xesp),lys+2,xesp*nbr_post%+4,0               : REMark x,y sizes & origs
16690     DATA 0,0,sp.infwinbd,sp.titlebg     : REMark shadow, border size & col,paper col
16700     DATA 0                                        : REMark object pointer (none)
16710 :
16720 REMark now wdw containing title itself
16730 :
16740     DATA longueur_titre+8,lys,titre_x-4,2
16750     DATA 0,0,sp.infwindbd,sp.infwinbg
16760     DATA main_iot1                      : REMark one object
16770 :
16780 REMark inner border
16790 :
16800     DATA xsize%-8,ysize%-inf1y-2,4,inf1y
16810     DATA 0,1,sp.infwinbd,sp.infwinbg
16820     DATA 0
16830 :
16840 REMark now the appsub wdw - use the OUTPTR functions for this
16850 :
16860   app_addr=0                                      : REMark there is none yet
16870   mk_main_app 1                         : REMark (this destroys my DATA pointer)
16880   main_app_list = app_addr+180                    : REMark list built by OUTPTR
16890 :
16900 : REMark  working defn
16910 :
16920 restore 16920
16930                                                   : REMark reset DATA pointer (due to Basic Linker, restore must be ALONE on the line)
16940   main_defn=RD_WDEF
16950     DATA 2,1,sp.winbd,sp.winbg                    : REMark shad,border size & col,pap col
16960     DATA xsize%,ysize%,0,0    : REMark x,y size & initial ptr pos
16970     DATA main_sprite,main_lot,main_iwt,main_app_list : REMark sprite,loose items, info wins, appsubs
16980 END DEFine make_main_wdw
16990 :
17000 DEFine PROCedure mk_main_app
17010 REMark make main application sub window
17020 LOCal t2%,temp%,sections%,att_phys%(3)
17030   DIM main_cty%(0):DIM main_mfl%(0,0)
17040   sections%=2
17050   temp%=sections% :t2%=rangees
17060   att_phys%(0)=xsize%-16                : REMark xsize calculated on main wdw x size
17070   att_phys%(1)=t2%                      : REMark y size,nominal,in rows
17080   att_phys%(2)=4                        : REMark x origin x
17090   att_phys%(3)=inf1y +appwinoffset%     : REMark y origin
17100   app_addr=fen_app(app_addr,mdata$,temp%,1,att_phys%,"DO ")
17110   IF app_addr
17120       DIM main_cty%(sections%,2)        : REMark ctrl defn
17130       main_cty%(0,0)=1                            : REMark show 1 section
17140       main_cty%(1,2)=t2%
17150       IF sections%=2:main_cty%(2,2)=t2% : REMark but allow 2
17160       IF temp%
17170              DIM main_mfl%(temp%-1,0)   : REMark flag array
17180              set_statusses              : REMark and set all statusses
17190       ELSE
17200              POKE_W app_addr,PEEK_W(app_addr)+8
17210       END IF
17220    REMark   scroll_arrows t2%,temp%,app_addr,main_cty%
17230   END IF
17240 END DEFine mk_main_app
17250 :
17260 DEFine PROCedure set_statusses
17270 REMark set all statusses of menu appsub objects
17280 LOCal temp%,lp%,stat%
17290   temp%=DIMN(main_mfl%,1)               : REMark how many are there?
17300   stat%=main_lfl%(all_it%)              : REMark status of "all" item
17310   FOR lp%=0 TO temp%
17320     IF mdata$(lp%+1,1)='*'
17330       main_mfl%(lp%,0)=16               : REMark comments set to unavailable
17340     ELSE
17350       main_mfl%(lp%,0)=stat%            : REMark all other to status
17360     END IF
17370   END FOR lp%
17380 END DEFine set_statusses
17390 :
17400 def proc make_standard_sprites
17410 rem This makes standard move, sleep and wake sprites and sets the
17420 rem GLOBAL vars move_sprite,, zzz_sprite, wake_sprite to them.
17430 rem If there are system sprites, these are used.
17440 rem This also makes the old "main_sprite" pointer sprite.
17450 :
17460   espace_vide=ALCHP(100)         : rem for pointers with no mask
17470 :
17480 restore 17480
17490   main_sprite=RD_SPRT(0)         : REMark ici un masque normal
17500     DATA 11,13,6,5,4
17510     DATA '     www     '
17520     DATA '     waw     '
17530     DATA '     waw     '
17540     DATA '     waw     '
17550     DATA 'wwwwwa awwwww'
17560     DATA 'waaaa   aaaaw'
17570     DATA 'wwwwwa awwwww'
17580     DATA '     waw     '
17590     DATA '     waw     '
17600     DATA '     waw     '
17610     DATA '     www     '
17620 :
17630   if sys_tspr_addr(6)<>0
17640     move_sprite=6
17650   else
17660 restore 17660
17670     move_sprite=RD_SPRT(1)         : REMark ici un masque 0
17680     DATA 10,14,5,4,4
17690     DATA 'aaaaaaaaaa    '
17700     DATA 'awwwwwwwwa    '
17710     DATA 'awwrrrrwwa    '
17720     DATA 'awwrrrrwwaaaaa'
17730     DATA 'awwrrwwwwwwwwa'
17740     DATA 'awwwwwwwwrrwwa'
17750     DATA 'aaaaawwrrrrwwa'
17760     DATA '    awwrrrrwwa'
17770     DATA '    awwwwwwwwa'
17780     DATA '    aaaaaaaaaa'
17790   endif
17800 :
17810   if sys_tspr_addr(10)<>0
17820     sleep_sprite=10
17830   else
17840 restore 17840
17850     sleep_sprite= RD_SPRT(1)             : REMark ici un masque 0
17860     DATA 7,15,0,0,4
17870     DATA 'wwwww          '
17880     DATA '   w           '
17890     DATA '  w  wwww      '
17900     DATA ' w     w       '
17910     DATA 'wwwww w  wwww  '
17920     DATA '     wwww  w   '
17930     DATA '          wwww '
17940   endif
17950 :
17960   if sys_tspr_addr(11)<>0
17970     wake_sprite=11
17980   else
17990 restore 17990
18000     wake_sprite=RD_SPRT(1)          : REMark ici un masque 0
18010     DATA 9,14,0,0,4
18020     DATA "             w"
18030     DATA "            w "
18040     DATA "      w   ww  "
18050     DATA "     ww www   "
18060     DATA "    wwwwww    "
18070     DATA "   www ww     "
18080     DATA "  w   w      "
18090     DATA " w            "
18100     DATA "w             "
18110   endif
18120 :
18130   if sys_tspr_addr(7)<>0
18140     size_sprite=7
18150   else
18160 restore 18160
18170     size_sprite=RD_SPRT(1)
18180     DATA 9,14,5,4,4
18190     DATA 'aaaaaaaaaaaaaa'
18200     DATA 'awwwwwwwwwwwwa'
18210     DATA 'awwrrrrrrrrwwa'
18220     DATA 'awwrrrrrrrrwwa'
18230     DATA 'awwrrwwwwwwwwa'
18240     DATA 'awwrrwwrrrrwwa'
18250     DATA 'awwrrwwrrrrwwa'
18260     DATA 'awwwwwwwwwwwwa'
18270     DATA 'aaaaaaaaaaaaaa'
18280   endif
18290 :
18300 end def make_sprites
18310 :
18320 DEFine PROCedure warn_make
18330 rem makes warning & info window
18340 rem uses global vars sp.xxx
18350 rem SETS the global var warning_width% (nbr of chars in one line in wdw)
18360 local lys%,warn_lot,warn_iot1,warn_iwt
18370 local x%,y%,warning_paper,warning_brd
18380 restore 18380
18390   x%=200:y%=82
18400   lys%=10
18410   DIM warn_lfl%(1)
18420   warn_lot=RD_LOT(std_iattr,1)
18430     DATA 30,lys%,4,4,0,0,cancel$,text+retr,'ESC'
18440     DATA 30,lys%,x%-34,4,0,0,'O',text+retr,"OK"
18450 :
18460   warn_iot1=RD_IOT (0)
18470     DATA 12*9,10,0,1,text,sp.errfg,2,0,'Attention'
18480 :
18490   warn_iwt=RD_IWT(1)
18500     DATA x%-16,y%-32,8,24
18510     DATA 0,1,sp.winbd,sp.errbg
18520     DATA 0
18530 :
18540     DATA 110,12,(x%-110) div 2,4
18550     DATA 0,1,sp.winbd,sp.errbg
18560     DATA warn_iot1
18570 :
18580   warn_defn=RD_WDEF
18590     DATA 2,1,sp.winbd,sp.errbg
18600     DATA x%,y%,0,0
18610     DATA main_sprite,warn_lot,warn_iwt,0
18620 :
18630   warning_width%=(x%-16) div 6                    : rem width of warning window in chars
18640   info_is_pulled_down%=0
18650   warn_wdw_addr=peek_l(warn_defn+100)+20: rem ptr to 2nd info wdw
18660  rem warn_wdw_addr=peek_l(warn_wdw_addr+16): rem ptr to object list
18670 END DEFine warn_make
18680 :
18690 DEFine PROCedure about_make (titre$,version$)
18700 local lxs%,lys%,xesp%,inf1y%,abt_lot,longueur_titre%,titre_x%
18710 local abt_iwt,abt_iot1,abt_iot2
18720   ab_xt%=200:ab_yt%=120    : REMark tailles x et y: la taille x devrait
18730                                    : REMark ‘tre un chiffre divisible par 4
18740   lxs%=24:lys%=10                    : REMark tailles x et y pour postes standard
18750   inf1y%=lys%+4            : REMark posi. y de la fen. d'inf. intƒrieure
18760   xesp%=lxs%+2
18770   nbr_post%=0                      : REMark nombre de postes dans un coin
18780 :
18790 restore 18790
18800 wolfsoft_sprite=rD_SPrT(0)
18810     DATA 23,51,0,0,4
18820     DATA 'rrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrr'
18830     DATA 'rrgggggggggggggggggggggggggggggggggggggggggggggggrr'
18840     DATA 'rrggggggggrrrrrrrrrrrrrrrrgggggggggggggggggggggggrr'
18850     DATA 'rrgggggggggggggggggggggrgggggggggggggggggggggggggrr'
18860     DATA 'rrggggggggggggggggggggrggggggggggggggggggggggggggrr'
18870     DATA 'rrggggggggagggggagggrrrrrrrrrrrrrggaaaaaaaaggggggrr'
18880     DATA 'rrggggggggagggggaggggggggggggggggggagggggggggggggrr'
18890     DATA 'rrggggggggaggaggaggaaaaaaaggaggggggaaaaaaagggggggrr'
18900     DATA 'rrggggggggagaaagaggaawwaaaggaggggggagggggggggggggrr'
18910     DATA 'rrggggggggaaagaaaggaawwwwaggaggggggagggggggggggggrr'
18920     DATA 'rrggrrrrrgaagggaaggawwwwaaggaggggggagggggrrrrrrrgrr'
18930     DATA 'rrgrgggggrgggggggggaaawwaaggaggggggggggrrrgggggggrr'
18940     DATA 'rrgrggggggrggggggggaaaaaaaggaaaaaagggrrggrgggggggrr'
18950     DATA 'rrgrgggggggggggggrrrgggggggggggrrrggrggggrgggggggrr'
18960     DATA 'rrggrrrrrrggggggrgggrrgggggggrrggggggggggrgggggggrr'
18970     DATA 'rrggggggggrrrgggrgggggrrgggrrggggggggggggrgggggggrr'
18980     DATA 'rrgggggggggggrggrgggggggrggrgggggggggggggrgggggggrr'
18990     DATA 'rrggggggggggggrgrggggggggrgrrrrrrrgggggggrgggggggrr'
19000     DATA 'rrgrggggggggggrgrggggggggrgrgggggggggggggrgggggggrr'
19010     DATA 'rrggrgggggggrrgggrggggggrggrgggggggggggggrgggggggrr'
19020     DATA 'rrgggrrrrrrrggggggrrrrrrgggrgggggggggggggrgggggggrr'
19030     DATA 'rrgggggggggggggggggggggggggggggggggggggggggggggggrr'
19040     DATA 'rrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrr'
19050 :
19060   yinf1y%=inf1y%+4:
19070 :
19080   DIM abt_lfl%(0)
19090   abt_lot=RD_LOT(std_iattr,0)
19100     DATA ab_xt%-16,lys%,8,ab_yt%-lys%-8,0,0,cancel$,text+retr,'Super!'
19110 :
19120 : REMark  D'abord le titre qui est un objet d'une sous-fen‘tre d'info.
19130 :
19140   longueur_titre%=LEN(" Version "&version$)*6               : REMark on calcule la taille et la
19150   titre_x%=INT((ab_xt%-longueur_titre%)/2)  : REMark position x de cet objet
19160   abt_iot2=RD_IOT(1)
19170     DATA longueur_titre%,lys%,4,8,text,sp.winfg,0,0," Version "&version$
19180     DATA 52,24,ab_xt%-60,40,sprite,0,wolfsoft_sprite
19190   longueur_titre%=LEN(titre$)*6     : REMark on calcule la taille et la
19200   titre_x%=INT((ab_xt%-longueur_titre%)/2)  : REMark position x de cet objet
19210   abt_iot1=RD_IOT(0)
19220     DATA longueur_titre%,lys%,4,INT((lys%-10)/2),text,sp.titlefg,0,0,titre$
19230   abt_iwt=RD_IWT(2)               : REMark on veut 3 sous-fen. d'info
19240 REMark d'abord la fen‘tre contenant la barre entourant le titre
19250     DATA ab_xt%-8-(2*nbr_post%*xesp%),lys%+2,xesp%*nbr_post%+4,0  : REMark taille x,y, origines x,y
19260     DATA 0,0,sp.infwinbd,sp.titlebg     : REMark ombre, taille & couleur bordure
19270                                    : REMark puis couleur papier
19280     DATA 0                         : REMark pointeur vers objets
19290 REMark maintenant la fen‘tre qui contient le titre
19300     DATA longueur_titre%+8,lys%,titre_x%-2,2   : REMark on utilise titre_x% etc
19310     DATA 0,0, sp.infwinbd,sp.titletextbg
19320     DATA abt_iot1                 : REMark un objet, le titre
19330 REMark maintenant la fen‘tre de la bordure intƒrieure
19340     DATA ab_xt%-8,ab_yt%-inf1y%-2,4,inf1y%
19350     DATA 0,1,sp.infwinbd,sp.infwinbg
19360     DATA abt_iot2
19370   abt_defn=RD_WDEF
19380     DATA 2,1,sp.winbd,sp.winbg: REMark ombre, larg. & coul. bord., coul. papier
19390     DATA ab_xt%,ab_yt%,10,10 : REMark taille x,y et position pointeur
19400     DATA main_sprite,abt_lot,abt_iwt,0
19410 :
19420 end def about_make
19430 :
19440 DEFine PROCedure about
19450   LOCal loop%,item%,dummy%,chan%
19460   DR_PULD abt_defn,-1,-1,abt_lfl%
19470   chan%=fopen('con')
19480   dr_iwdf#chan%,abt_defn,2
19490   wm_ink#chan%,sp.infwinfg
19500   wm_paper#chan%,sp.infwinbg
19510   at#chan%,4,2
19520   print#chan%,"Copyright (c) 2014"
19530   at#chan%,5,2
19540   print#chan%,"W. Lenerz"
19550   close#chan%
19560   REPeat loop%
19570     RD_PTR abt_defn,item%,dummy%,dummy%,dummy%,dummy%,abt_lfl%
19580     SELect ON item%
19590             =0       : EXIT loop%
19600     END SELect
19610   END REPeat loop%
19620   DR_UNST abt_defn
19630 END DEFine about
19640 :
19650 def proc do_popup(string$)
19660 rem this pops up a small "hint" window showing the string$ passed
19670 rem as parameter.
19680 rem The string MUST be 1 line long and MUST fit
19690 rem inside the window (YOU must make sure of that!)
19700   do_popup2 string$,0
19710 end def do_popup
19720 :
19730 def proc popup_make
19740 rem make a small window defn
19750 local x%,y%,p_iot,popsub(0)
19760 restore 19760
19770   p_sprt=rd_sprt(0)                               : rem make an invisible pointer, all transparent
19780     data 1,2,0,0,4
19790     data '  '
19800   x%=14                                 : rem bogus window sizes
19810   y%=10
19820   popsub(0)=rd_appw                               : rem the info wdw
19830     DATA x%,y%,0,0
19840     DATA 0,0, sp.hintbd,sp.hintbg
19850     DATA 0,""
19860   popup_defn=RD_WDEF                              : rem the working defn
19870     DATA 0,1,sp.hintbd,sp.hintbg        : rem no shadow, border size & col, paper col
19880     DATA x%,y%,40,2
19890     DATA p_sprt,0,0,mk_awl(popsub)
19900 end def popup_make
19910 :
19920 def proc do_popup2(string$,main_defn)
19930 rem This pops up a small "hint" window showing the string$ passed
19940 rem as parameter.
19950 rem main_defn is the working defintion of the window over which the hint
19960 rem window is to hover. Position the pointer there where you need it before
19970 rem calling this.
19980 rem If the string is too long to fit into the window (which can only be as long
19990 rem as the outline of the working defn) then the string is split up at the
20000 rem first convenient space and put on a second line, and so on.
20010 rem If this would lead to the hint window being too high, only as many
20020 rem lines as possible will be displayed.
20030 rem !!!!!
20040 rem This presumes that a char is 10 pixels high & 6 pixels wide (standard ql 6x10 matrix)..
20050 rem !!!!!
20060 rem The string may contain the char chr$(10) - there will be a newline.
20070 rem
20080 rem note : do not try to pass a screen chaneel, open & close one here.
20090 rem
20100 local dummy%,lp%,address,con%,temp%,lastspace%,xs%,ys%,temp$
20110 local hint$(10),slen% ,maxlength%,popup_lfl%(0),popup_mfl%(0,0)
20120   if string$="":return                            : rem empty string, do nothing
20130   string$=strip_spaces$(string$)        : rem no spaces at ends
20140   con%=fopen("con")
20150   if main_defn<>0
20160     xs%=peek_w (main_defn+32)
20170     ys%=peek_w(main_defn+34)            : rem get max sizes the hint wdw may have
20180   else
20190     scr_size#con%,xs,ys%
20200   endif
20210   xs%=(xs%-4) div 6                               : rem max nbr of chars in 1 line
20220   ys%=(ys%-4) div 10                              : rem max nbr of lines in wdw
20230   if xs%=0 or ys%=0:close#con%:return   : rem nothing will really fit, just leave
20240   dim hint$(ys%,xs%)                              : rem array to contain string
20250   rem now parse the string, find newlines, make sure of string length
20260   dummy%=1                                        : rem start of current string section
20270   temp%=0                                         : rem line
20280   slen%=0                                         : rem current length of string
20290   for lp%=1 to len(string$)
20300     if string$(lp%)=chr$(10)
20310       hint$(temp%)=string$(dummy% to lp%-1): rem cut string here
20320       temp%=temp%+1
20330       if temp%=ys%:exit lp%             : rem max nbr of lines reached
20340       dummy%=lp%+1                      : rem this is where new start of string is
20350       lastspace%=lp%
20360     else
20370       if string$(lp%)=" "
20380           lastspace%=lp%                          : rem this is where last space is
20390       endif
20400     endif
20410     slen%=slen%+1                       : rem one more char
20420     if slen% > xs%
20430       hint$(temp%)=string$(dummy% to lastspace%-1) : rem too many chars in line
20440       slen%=0
20450       temp%=temp%+1                               : rem we need nother line
20460       if temp%=ys%:exit lp%             : rem max nbr of lines reached
20470       dummy%=lastspace%+1
20480     endif
20490   end for lp%
20500   if temp%<ys%
20510      hint$(temp%)=string$(dummy% to)       : rem add last part
20520      temp%=temp%+1
20530   endif
20540   maxlength%=0
20550   for lp%=0 to temp%
20560     temp$=strip_spaces$(hint$(lp%))     : rem use intermediary var, because of QLIB
20570     if maxlength%<len(temp$):maxlength%=len (temp$)
20580   end for lp%
20590   lp%=(maxlength%*6)+4                            : rem hint window length
20600   poke_w popup_defn+32,lp%              : rem set window xsize
20610   poke_w popup_defn+34,temp%*10+2       : rem
20620   address=peek_l(popup_defn+112)        : rem ptr to app sub wdw...
20630   address=peek_l(address)               : rem ...now
20640   poke_w address,lp%-2                            : rem set its length
20650   poke_w address+2,temp%*10             : rem set its height
20660   dr_puld popup_defn,-1,-1,popup_lfl%,popup_mfl%
20670   dr_awdf#con%,popup_defn,0             : rem pull wdw over info wdw
20680   wm_paper#con%,sp.hintbg
20690   wm_ink#con%,sp.hintfg
20700   for lp%=0 to dimn(hint$,1)
20710     if hint$(lp%)<>"":print#con%,strip_spaces$(hint$(lp%))
20720   end for lp%
20730   temp%=0:slen%=0:xs%=0:ys%=0:dummy%=0
20740   RD_PTR popup_defn,temp%,dummy%,slen%,xs%,ys%,popup_lfl%,popup_mfl%: rem comes back immediatley
20750   RD_PTR popup_defn,temp%,dummy%,slen%,xs%,ys%,popup_lfl%,popup_mfl%
20760   close#con%
20770   dr_unst popup_defn
20780 end def do_popup2
20790 :
20800 DEFine FuNction RD_SPRT(drap%)
20810 REMark drap% determine si masque 0
20820   LOCal tmp_patt$(32,32)
20830   LOCal xs,ys,xo,yo, md, l
20840   READ ys,xs,xo,yo,md
20850   DIM tmp_patt$(ys,xs)
20860   FOR l=0 TO ys-1:READ tmp_patt$(l)
20870   l=ALCHP(SPRSP(xs,ys))
20880   SPSET l,xo,yo,md,tmp_patt$(0 TO ys-1,1 TO xs)
20890   IF drap%
20900     POKE_L l+16,espace_vide-(l+16)
20910   END IF
20920   RETurn l
20930 END DEFine RD_SPRT
20940 :
20950 DEFine FuNction RD_WDEF
20960 rem read window defn
20970   LOCal ldef%(3), lspr, lloose, linfo, lappl, lattr%(3)
20980   RD_WATT lattr%
20990   READ ldef%(0), ldef%(1), ldef%(2), ldef%(3)
21000   READ lspr, lloose, linf, lappl
21010   RETurn MK_WDEF (ldef%, lattr%, lspr, lloose, linf, lappl)
21020 END DEFine RD_WDEF
21030 :
21040 DEFine FuNction RD_LOT (lattr,nitem)
21050   LOCal count(3)
21060   LOCal item, ltyp, a$, lsk$
21070   LOCal ldef%(nitem,6), lptr(3,nitem), lstr$(nitem,85)
21080   lsk$=''
21090   FOR item = 0 TO nitem
21100     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
21110     READ ldef%(item,4), ldef%(item,5)
21120     READ a$: lsk$=lsk$ & a$
21130     READ ltyp
21140     ldef%(item,6)=ltyp: ltyp=(ltyp MOD 256)/2
21150     IF ltyp>10 or ltyp <0:ltyp=0
21160     IF ltyp
21170       READ lptr(ltyp,count(ltyp))
21180     ELSE
21190       READ lstr$(count(0))
21200     END IF
21210     count(ltyp)=count(ltyp)+1
21220   END FOR item
21230   RETurn MK_LIL (lattr, ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), ldef%(TO, 4 TO 5), lsk$, ldef%(TO, 6), lstr$, lptr(1), lptr(2), lptr(3))
21240 END DEFine RD_LOT
21250 :
21260 DEFine FuNction RD_IWT(nitem)
21270   LOCal item,mc1
21280   LOCal ldef%(nitem,3), latt%(nitem,3), lptr(nitem)
21290   FOR item = 0 TO nitem
21300     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
21310     READ latt%(item,0), latt%(item,1), latt%(item,2), latt%(item,3)
21320     READ lptr(item)
21330   END FOR item
21340   RETurn MK_IWL (ldef%, latt%, lptr)
21350 END DEFine RD_IWT
21360 :
21370 DEFine FuNction RD_IOT(nitem)
21380   LOCal count(3)
21390   LOCal item, ltyp, work1, work2
21400   LOCal ldef%(nitem,4), lptr(3,nitem), lstr$(nitem,85)
21410   FOR item = 0 TO nitem
21420     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
21430     READ ltyp
21440     ldef%(item,4)=ltyp:  ltyp=(ltyp MOD 256)/2
21450     IF ltyp >10:ltyp=0
21460     IF ltyp
21470       READ lptr(0,item),lptr(ltyp,count(ltyp))
21480     ELSE
21490       if mycolour_mode%=2
21500           read work1,work2,work3
21510           lptr(0,item)=WL_4_IOL(work1,work2,work3)
21520       else
21530           READ work1
21540           READ work2: work1=work1*256+work2
21550           READ work2: lptr(0,item)=work1*256+work2
21560       endif
21570       READ lstr$(count(0))
21580     END IF
21590     count(ltyp) = count(ltyp) + 1
21600   END FOR item
21610   RETurn MK_IOL (ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), lptr(0), ldef%(TO, 4), lstr$, lptr(1), lptr(2), lptr(3))
21620 END DEFine RD_IOT
21630 :
21640 DEFine PROCedure RD_IATT (lattr)
21650   LOCal i
21660   READ lattr(0,0), lattr(0,1)
21670   FOR i=1 TO 3: READ lattr(i,0), lattr(i,1), lattr(i,2), lattr(i,3)
21680 END DEFine RD_IATT
21690 :
21700 DEFine PROCedure RD_WATT (lattr%)
21710   READ lattr%(0), lattr%(1),lattr%(2),lattr%(3)
21720 END DEFine RD_WATT
21730 :
21740 DEFine FuNction RD_APPW
21750   LOCal ldef%(3), lspr, sk$, lattr%(3)
21760   READ ldef%(0), ldef%(1), ldef%(2), ldef%(3)
21770   RD_WATT lattr%
21780   READ lspr, sk$
21790   RETurn MK_APPW (ldef%, lattr%, lspr,sk$)
21800 END DEFine
21810 :
