10 rem $$asmb=dev1_booty_qptr_bin,0,172
20 rem $$asmb=dev1_booty_outptr_bin,0,10
30 rem $$asmb=DEV1_booty_blinker_bin,0,10
40 rem $$stak=2000
50 compiled=NOT (IS_OPEN(#0))
60 IF NOT compiled
70     IF NOT_SBASIC:PRINT "This only works in sbasic":stop
80     CLEAR:CLCHP
90     SCR_SIZE#0,maxx%,maxy%
100     maxy%=maxy%-28
110     OUTLN#0,maxx%,maxy%,0,28
120     WSET -1
130 ELSE
140     OPEN#1,"con"
150     SCR_SIZE#1,maxx%,maxy%
160 END IF
170 :
180 IF P_ENV(#1)<>2
190    CLS#1:INK#1,2:PRINT#1,"This software works only in the Pointer Environment"
200    a$=INKEY$(#1,-1)
210    STOP
220 END IF
230 :
240 IF NOT is_extn("RCNT_INFO")
250    CLS#1:INK#1,2:PRINT#1,"This software needs the Recent thing"
260    a$=INKEY$(#1,-1)
270    STOP
280 END IF
290 :
300 init
310 main
320 IF not compiled:CLCHP
330 STOP
340 :
350 define procedure main
360   local lp%,item%,event%,swnum%,xrel%,yrel%,timeout% , chosen$
370   local displaying_jobs%
380   displaying_jobs%=0
390   timeout%=-2
400   if compiled
410      open#1,"con"
420      scr_size#1,xsz%,ysz%
430      dr_ppos main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
440   else
450      scr_size#1,xsz%,ysz%
460      dr_puld main_defn,-1,-1,main_lfl%,main_mfl%,main_cty%
470   end if
480   repeat lp%
490     rd_ptrt main_defn,item%,swnum%,event%,timeout%,xrel%,yrel%,main_lfl%,main_mfl%,main_cty%
500     if swnum%= -1
510       select on item%
520           =-1                 : next lp%                    : rem do nothing on timeout
530           =esc_it%  : exit lp%                    : rem quit
540           =move_it%           : ch_win main_defn  : rem move
550           =sleep_it%          : button main_defn,main_lfl%,main_mfl%,main_cty%,titre$
560           =wake_it%           : refresh_list                : rem re-read list
570           =jobs_it%           : show_jobs
580           =info_it%           : show_info current_job$: rem show some info on currenly selected job
590       end select
600     else
610       if item%>0 and item%<dimn(appsub_data$,1)
620           if displaying_jobs%
630             if item%=dimn(appsub_data$,1)-1
640               current_job$=""
650             else
660               current_job$=appsub_data$(item%+1)
670             endif
680             displaying_jobs%=0
690             main_lfl%(info_it%)=1
700             refresh_list
710           else
720             chosen$=appsub_data$(item%+1)
730             hot_stuff chosen$
740           endif
750       end if
760       main_mfl%(0,0)=129                          : rem this always stays selected
770     end if
780   end repeat lp%
790   dr_unst main_defn
800   if compiled:close#1
810 end define main
820 :
830 def proc refresh_list
840   make_appsub_data current_job$
850   mk_main_app
860   dr_idrw main_defn,-5                        : rem redraw inner border wdw of main wdw
870   ap_in main_defn,app_addr+180
880   dr_ascr main_defn,app_addr
890 end def refresh_list
900 :
910 def proc show_info (jobnm$)
920 local lp%,item%,event%,swnum%,xrel%,yrel%,scr_chan%,size
930 local leng%,nbr%,size,max%,tit$
940   tit$="Info on : " & jobnm$
950   if jobnm$= main_sbasic$
960     size=rcnt_info(0,nbr%,leng%,max%)   : rem get info on job
970   else
980     if jobnm$<>""
990       size=RCNT_INFO(jobnm$,nbr%,leng%,max%) : rem get info on job
1000     else
1010       tit$="Info on : General list"
1020       size=rcnt_info(-3,nbr%,leng%,max%): rem info on general list
1030     endif
1040   endif
1050   dr_puld info_defn,-1,-1,info_lfl%
1060   scr_chan%=fopen("scr")
1070   dr_iwdf#scr_chan%,info_defn,2
1080   wm_paper#scr_chan%, sp.infwinbg
1090   wm_ink#scr_chan%,sp.infwinfg
1100   print#scr_chan%\\tit$
1110   print#scr_chan%\\"Size needed for GARR/GARJ      : ";size
1120   print#scr_chan%,"Number of files curently in list: ";nbr%
1130   print#scr_chan%,"Max current file name length    : ";leng%
1140   print#scr_chan%,"Max number of files in list     : ";max%
1150   event%=0
1160   rep lp%
1170     rd_ptr info_defn,item%,swnum%,event%,xrel%,yrel%,info_lfl%
1180     if swnum%=-1
1190       if item%=imove_it%
1200            ch_win info_defn
1210       else
1220           exit lp%
1230       endif
1240     endif
1250   end rep lp%
1260   dr_unst info_defn
1270   close#scr_chan%
1280 end def show_info
1290 :
1300 DEFine FuNction make_standard_app_sub (address,tableau$,nr_sects%,selk%,phys%,a_exclure$)
1310 rem make a typical wman appsub wdw, with strings in the items
1320 rem the parameters are: the address (0 at first)
1330 rem the string array, the number of sections
1340 rem a flag whether we want selection keystrokes (flag = 1) and whether hit=do (flag = flag +256)
1350 rem an integer array with x,y, sizes & origs
1360 rem a string with chars to be excluded from the selection keystrokes
1370 rem !!!!!
1380 rem this sets a variable "temp%" to the changed return from the mc call
1390 rem !!!!
1400 LOCal app_attributs%(3,1),temp%,bord%,pap%,ori_x%,ori_y%
1410 LOCal tail_x%,tail_y%,fleches%,bar%,bar_sect%,arrows%,selkey%
1420 :
1430   IF address:RECHP address              : rem if this is a second call, release old
1440   tail_x%=phys%(0)                      : rem x size  - just to make this clear
1450   tail_y%=phys%(1)                      : rem y "size"      we could use this directly
1460   ori_x%=phys%(2)                       : rem
1470   ori_y%=phys%(3)                       : rem origs x,y
1480 :
1490   address=AP_MAKE(selk%,tail_x%,tail_y%,ori_x%,ori_y%,nr_sects%,tableau$,a_exclure$)
1500   IF address<0: RETurn 0                : rem  error, return 0
1510 :
1520   temp%=nr_sects%
1530 :
1540   RETurn address                        : rem return the address!
1550 END DEFine make_standard_app_sub
1560 :
1570 DEFine FuNction make_standard_app_sub2 (adresse,tableau$,nr_sects%,selk%,phys%)
1580 rem make a typical wman appsub wdw, with strings in the items
1590 rem the parameters are: the address (0 at first)
1600 rem the string array, the number of sections
1610 rem a flag whether we want selection keystrokes and whether hit=do
1620 rem an integer array with x,y, sizes & origs
1630 rem here there are NO strings to be excluded from selection keystrokes.
1640 rem !!!!!
1650 rem THIS MUST ONLY BE USED IN SBASIC OR COMPILED BASIC      !!!!!!!!!!!!!!!!!!!!
1660 rem !!!!!
1670 rem this sets a variable "temp%" to the changed return from the mc call
1680 rem !!!!!
1690 LOCal app_attributs%(3,1),temp%,bord%,pap%,ori_x%,ori_y%
1700 LOCal tail_x%,tail_y%,fleches%,bar%,bar_sect%,arrows%,selkey%
1710 :
1720   IF adresse:RECHP adresse              : rem if this is a second call, release old
1730   tail_x%=phys%(0)                      : rem x size  - just to make this clear
1740   tail_y%=phys%(1)                      : rem y "size"      we could use this directly
1750   ori_x%=phys%(2)                       : rem
1760   ori_y%=phys%(3)                       : rem origs x,y
1770 :
1780   adresse=AP_MAKE2(selk%,tail_x%,tail_y%,ori_x%,ori_y%,nr_sects%,tableau$)
1790   IF adresse<0: RETurn 0                : rem  error, return 0
1800 :
1810   temp%=nr_sects%
1820 :
1830   RETurn adresse                        : rem return the address!
1840 END DEFine make_standard_app_sub2
1850 :
1860 def fn warning_pos% (mess$,channel%,title$,pos)
1870 rem warning wdw with pointer positioning
1880   pos_ptr main_defn,pos,-1
1890   return warning%(mess$,channel%,title$)
1900 end def warning_pos%
1910 :
1920 define function warning%(mess$,channel%,title$)
1930 rem shows a warning message (mess$) in a small window. hit esc or ok
1940 rem to return; returns 0 if esc, 1 if ok  hit
1950 local item%,lp%,chan%,y%,swnum%,infot%
1960   stop_info
1970   if channel%
1980     chan%=channel%
1990   else
2000     chan%=fopen("con")
2010   endif
2020   ch_item warn_defn,-3,0,-1,'','Attention'
2030   ch_item warn_defn,-1,0,-1,cancel$,'ESC'
2040   ch_item warn_defn,-1,1,-1,'O','OK'
2050   warn_lfl%(0)=1:warn_lfl%(1)=1
2060   poke_w warn_defn+44,sp.errmg                           : rem border colour of wdw
2070   poke_w warn_defn+46,sp.errbg                           : rem paper colour of wdw
2080   poke_w warn_wdw_addr+14,sp.errbg             : rem paper of title info wdw
2090   obj_addr=peek_l(warn_wdw_addr+16)            : rem ptr to object list
2100   poke_w obj_addr+10,sp.errfg                            : rem info object text  colour
2110   dr_puld warn_defn,-1,-1
2120   dr_iwdf #chan%,warn_defn,0
2130   wm_paper#chan%,sp.errbg
2140   wm_ink#chan%,sp.errfg
2150   cls#chan%
2160   if title$<>"":print#chan%,title$;" :"
2170   print #chan%,mess$
2180   pos_ptr warn_defn,160*65536+10,-1
2190   repeat lp%
2200     rd_ptr warn_defn,item%,swnum%,y%,y%,y%,warn_lfl%
2210     if swnum%=-1:exit lp%
2220   end repeat lp%
2230   if not channel%:close#chan%
2240   dr_unst warn_defn
2250   return item%
2260 end define warning%
2270 :
2280 def proc info_pos (mess$,channel%,pos)
2290 rem warning wdw with positioning
2300   pos_ptr main_defn,pos,-1
2310   info mess$,channel%
2320 end def info_pos
2330 :
2340 define procedure info(mess$,channel%)
2350 rem shows window containing mess$. channel% is a screen channel, if this is 0,
2360 rem a temp screen channel will be opened.
2370 local chan%,infot%,obj_addr
2380   if channel%
2390     chan%=channel%
2400   else
2410     chan%=fopen("con")
2420   endif
2430   ch_item warn_defn,-3,0,-1,"",'  Info.  '
2440   ch_item warn_defn,-1,0,-1,"",'Info'
2450   ch_item warn_defn,-1,1,-1,"",'Info'
2460   poke_w warn_defn+44,sp.infwinbg                 : rem paper colour of wdw
2470   poke_w warn_defn+46,sp.winbd                              : rem border around wdw
2480   poke_w warn_wdw_addr+14,sp.infwinbg             : rem paper of title info wdw
2490   obj_addr=peek_l(warn_wdw_addr+16)               : rem ptr to object list
2500   poke_w obj_addr+10,sp.infwinfg                  : rem info object text        colour
2510   dr_puld warn_defn,-1,-1
2520   pos_ptr warn_defn,90*65536+70,-1
2530   dr_iwdf #chan%,warn_defn,0
2540   wm_paper#chan%,sp.infwinbg
2550   wm_ink#chan%,sp.infwinfg
2560   cls#chan%
2570   print #chan%,mess$
2580   if not channel%:close#chan%
2590   info_is_pulled_down%=1
2600 end define info
2610 :
2620 def proc info_wait(mess$,channel%)
2630 rem shows info wdw, waits until user clicks, closed info wdw
2640   show_info mess$,channel%
2650   info_wait_to_close
2660 end def info_wait
2670 :
2680 def proc info_wait_to_close
2690 local item%,lp%,chan%,y%,swnum%,infot%
2700   repeat lp%
2710     rd_ptr warn_defn,item%,swnum%,y%,y%,y%,warn_lfl%
2720     if swnum%=-1:exit lp%
2730   end repeat lp%
2740   stop_info
2750 end def info_wait_to_close
2760 :
2770 def proc info_pos_wait (mess$,channel%,pos)
2780 rem positions & shows info wdw, waits until user clicks,closes info wdw
2790   info_pos mess$,channel%,pos
2800   info_wait_to_close
2810 end def info_pos_wait
2820 :
2830 def proc stop_info
2840   if info_is_pulled_down%
2850     dr_unst warn_defn
2860     info_is_pulled_down%=0
2870   endif
2880 end def stop_info
2890 :
2900 def proc show_info (mess$,channel%)
2910 rem either opens the info wdw and shows the message or shows the message in
2920 rem the existing info wdw
2930 local chan%
2940   if channel%
2950     chan%=channel%
2960   else
2970     chan%=fopen("con")
2980   endif
2990   if info_is_pulled_down%
3000     cls#chan%
3010     wm_paper#chan%,sp.infwinbg
3020     wm_ink#chan%,sp.infwinfg
3030     pe_cls#chan%
3040     pe_print #chan%,mess$
3050   else
3060     info mess$,chan%
3070   endif
3080   if not channel%:close#chan%
3090 end def show_info
3100 :
3110 DEFine PROCedure button (def_trav,menu_drap%,app_drap%,control%,chaine$,type_col%)
3120 REMark les parametres sont, dans l'ordre:
3130 REMark la dƒfinition de la fen‘tre, suivie des tableaux pour
3140 REMark les postes du menu dƒliƒ, les postes de la sous-fen‘tre d'appl.
3150 REMark et la dƒfinition de contr˜le, suivi du nom du bouton et
3160 REMark du type couleurs.
3170 REMark Il est supposƒ que vous avez ouvert une fen‘tre #1 si
3180 REMark le programme est compilƒ
3190   DR_UNST def_trav
3200   IF compiled: CLOSE#1
3210   rem button_wait chaine$
3220   BTN chaine$,type_col%
3230   IF compiled
3240     OPEN#1,"CON_"
3250     DR_PPOS def_trav,-1,-1,menu_drap%,app_drap%,control%
3260   ELSE
3270     DR_PULD def_trav,-1,-1,menu_drap%,app_drap%,control%
3280   END IF
3290 END DEFine button
3300 :
3310 DEFine PROCedure make_colours (colourway%)
3320 REMark this initialises the GLOBAL variables menu_pap, menu_brd, menu_inv,
3330 REMark menu_ink and menu_bar according to the colourway passed as parameter.
3340 REMark More importantly, this sets the sp.xxx GLOBAL variables.
3350 REMark The GLOBAl variable mycolour_mode% must have been set prior to calling
3360 REMark this function, preferrably via the function set_colours%.
3370 REMark Moreover the colours black, white,red, green,wg_stripes, etc...
3380 REMark must have been set prior to calling this procedure:
3390 REMark Preferrably, they were also set via the function set_colours%
3400 :
3410 rem this function needs the extension WL_MK16 (outptr_bin)
3420 :
3430 REMark the colourways% are:
3440 REMark 0= white/green
3450 REMark 1= black/red
3460 REMark 2= white/red
3470 REMark 3= black/green
3480 remark 4-7= use system palettes
3490 :
3500 rem the Global war mycolour_mode% is:
3510 rem 0 = we are in QL mode 4
3520 rem 1 = we are in PAL mode (256 colours)
3530 rem 2 - we are in 24 bit colour mode
3540 rem preferrably, this is set via the function set_colours%
3550 :
3560   if colourway%>7 or colourway%<0: colourway%=0: REMark must be between 0 & 7
3570 :
3580 rem first make sure that what we request is possible.
3590 rem colourways 4-7 are only possible if we are in 8 or 24 bit colour mode
3600 rem if it is requested in another mode, it is set to 0
3610 :
3620   if colourway%>3
3630      make_my_palette colourway%-4: rem set colours according to system palette
3640      return                         : rem premature exit
3650   endif
3660 :
3670 rem this now is only the old colourways
3680   select on mycolour_mode%
3690     =0                                  : rem QL 4 colours
3700       SELect ON colourway%
3710           =0:                           : remark white/green
3720                 menu_pap=white
3730                 menu_brd=green
3740                 menu_ink=black
3750                 menu_bar=wg_striped
3760                 menu_inv=red
3770           =1:                           : remark black/red
3780                 menu_pap=black
3790                 menu_brd=red
3800                 menu_ink=white
3810                 menu_bar=br_striped
3820                 menu_inv=green
3830           =2:                           : remark white/red
3840                 menu_pap=white
3850                 menu_brd=red
3860                 menu_ink=black
3870                 menu_bar=wr_striped
3880                 menu_inv=green
3890           =3:                           : remark black/green
3900                 menu_pap=black
3910                 menu_brd=green
3920                 menu_ink=white
3930                 menu_bar=bg_striped
3940                 menu_inv=red
3950       END SELect
3960     =1                                  : rem pal colours
3970       SELect ON colourway%
3980           =0:                           : remark white/green
3990                 menu_pap=white+256
4000                 menu_brd=green+256
4010                 menu_ink=black+256
4020                 menu_bar=wg_striped+256
4030                 menu_inv=red +256
4040           =1:                           : remark black/red
4050                 menu_pap=black+256
4060                 menu_brd=red  +256
4070                 menu_ink=white +256
4080                 menu_bar=br_striped +256
4090                 menu_inv=green +256
4100                 omenu_pap=black+256
4110           =2:                           : remark white/red
4120                 menu_pap=white +256
4130                 menu_brd=red   +256
4140                 menu_ink=black +256
4150                 menu_bar=wr_striped +256
4160                 menu_inv=green +256
4170           =3:                           : remark black/green
4180                 menu_pap=black +256
4190                 menu_brd=green +256
4200                 menu_ink=white +256
4210                 menu_bar=bg_striped +256
4220                 menu_inv=red   +256
4230       END SELect
4240     =2                                  : rem 24 bit colours
4250       SELect ON colourway%
4260           =0:                           : remark white/green
4270                 menu_pap=WL_MK16(white)
4280                 menu_brd=WL_MK16(green)
4290                 menu_ink=WL_MK16(black)
4300                 menu_bar=WL_MK16(wg_striped)
4310                 menu_inv=WL_MK16(red)
4320           =1:                           : remark black/red
4330                 menu_pap=WL_MK16(black)
4340                 menu_brd=WL_MK16(red)
4350                 menu_ink=WL_MK16(white)
4360                 menu_bar=WL_MK16(br_striped)
4370                 menu_inv=WL_MK16(green)
4380           =2:                           : remark white/red
4390                 menu_pap=WL_MK16(white)
4400                 menu_brd=WL_MK16(red)
4410                 menu_ink=WL_MK16(black)
4420                 menu_bar=WL_MK16(wr_striped)
4430                 menu_inv=WL_MK16(green)
4440           =3:                           : remark black/green
4450                 menu_pap=WL_MK16(black)
4460                 menu_brd=WL_MK16(green)
4470                 menu_ink=WL_MK16(white)
4480                 menu_bar=WL_MK16(bg_striped)
4490                 menu_inv=WL_MK16(red)
4500       END SELect
4510   end select
4520 :
4530   make_my_old_palette
4540 :
4550 END DEFine make_colours
4560 :
4570 def proc make_my_palette (cw%)
4580   sp.winbd            = hex('0200') : rem Window border
4590   sp.winbg            = hex('0201') : rem Window background
4600   sp.winfg            = hex('0202') : rem Window foreground
4610   sp.winmg            = hex('0203') : rem Window middleground
4620   sp.titlebg          = hex('0204') : rem Title background
4630   sp.titletextbg  = hex('0205') : rem Title text background
4640   sp.titlefg          = hex('0206') : rem Title foreground
4650   sp.litemhigh        = hex('0207') : rem Loose item highlight
4660   sp.litemavabg   = hex('0208') : rem Loose item available background
4670   sp.litemavafg   = hex('0209') : rem Loose item available foreground
4680   sp.litemselbg   = hex('020a') : rem Loose item selected background
4690   sp.litemselfg   = hex('020b') : rem Loose item selected foreground
4700   sp.litemunabg   = hex('020c') : rem Loose item unavailable background
4710   sp.litemunafg   = hex('020d') : rem Loose item unavailable foreground
4720   sp.infwinbd         = hex('020e') : rem Information window border
4730   sp.infwinbg         = hex('020f') : rem Information window background
4740   sp.infwinfg         = hex('0210') : rem Information window foreground
4750   sp.infwinmg         = hex('0211') : rem Information window middleground
4760   sp.subinfbd         = hex('0212') : rem Subsidiary information window border
4770   sp.subinfbg         = hex('0213') : rem Subsidiary information window background
4780   sp.subinffg         = hex('0214') : rem Subsidiary information window foreground
4790   sp.subinfmg         = hex('0215') : rem Subsidiary information window middleground
4800   sp.appbd            = hex('0216') : rem Application window border
4810   sp.appbg            = hex('0217') : rem Application window background
4820   sp.appfg            = hex('0218') : rem Application window foreground
4830   sp.appmg            = hex('0219') : rem Application window middleground
4840   sp.appihigh         = hex('021a') : rem Application window item highlight
4850   sp.appiavabg        = hex('021b') : rem Application window item available background
4860   sp.appiavafg        = hex('021c') : rem Application window item available foreground
4870   sp.appiselbg        = hex('021d') : rem Application window item selected background
4880   sp.appiselfg        = hex('021e') : rem Application window item selected foreground
4890   sp.appiunabg        = hex('021f') : rem Application window item unavailable background
4900   sp.appiunafg        = hex('0220') : rem Application window item unavailable foreground
4910   sp.scrbar           = hex('0221') : rem Pan/scroll bar
4920   sp.scrbarsec        = hex('0222') : rem Pan/scroll bar section
4930   sp.scrbararr        = hex('0223') : rem Pan/scroll bar arrow
4940   sp.buthigh          = hex('0224') : rem Button highlight
4950   sp.butbd            = hex('0225') : rem Button border
4960   sp.butbg            = hex('0226') : rem Button background
4970   sp.butfg            = hex('0227') : rem Button foreground
4980   sp.hintbd           = hex('0228') : rem Hint border
4990   sp.hintbg           = hex('0229') : rem Hint background
5000   sp.hintfg           = hex('022a') : rem Hint foreground
5010   sp.hintmg           = hex('022b') : rem Hint middleground
5020   sp.errbg            = hex('022c') : rem Error message background
5030   sp.errfg            = hex('022d') : rem Error message foreground
5040   sp.errmg            = hex('022e') : rem Error message middleground
5050   sp.shaded           = hex('022f') : rem sp.shaded area
5060   sp.3ddark           = hex('0230') : rem Dark 3D border shade
5070   sp.3dlight          = hex('0231') : rem Light 3D border shade
5080   sp.vertfill         = hex('0232') : rem Vertical area fill
5090   sp.subtitbg         = hex('0233')  : rem Subtitle background
5100   sp.subtittxtbg  = hex('0234')  : rem Subtitle text background
5110   sp.subtitfg         = hex('0235')  : rem Subtitle foreground
5120   sp.mindexbg         = hex('0236')  : rem Menu index background
5130   sp.mindexfg         = hex('0237')  : rem Menu index foreground
5140   sp.separator        = hex('0238')  : rem Seperator lines etc.
5150   if not (is_open(#0) or is_open(#1))
5160     open#1,'con'
5170     sp_jobpal -1,cw%
5180     close#1
5190   else
5200     sp_jobpal -1,cw%
5210   endif
5220 end def make_my_palette
5230 :
5240 def proc make_my_old_palette
5250 rem this translates the colours used in the wdws into 4 colour mode...
5260   sp.winbd          =         menu_ink   : rem Window border
5270   sp.winbg          =         menu_pap   : rem Window background
5280   sp.winfg          =         menu_pap   : rem Window foreground
5290   sp.winmg          =         menu_pap   : rem Window middleground
5300   sp.titlebg        =         menu_bar   : rem Title background
5310   sp.titletextbg=   menu_pap   : rem Title text background
5320   sp.titlefg        =         menu_ink   : rem Title foreground
5330   sp.litemhigh      =         menu_brd   : rem Loose item highlight
5340   sp.litemavabg =   menu_pap   : rem Loose item available background
5350   sp.litemavafg =   menu_ink   : rem Loex fifiose item available foreground
5360   sp.litemselbg =   menu_brd   : rem Loose item selected background
5370   sp.litemselfg =   menu_ink   : rem Loose item selected foreground
5380   sp.litemunabg =   menu_pap   : rem Loose item unavailable background
5390   sp.litemunafg =   menu_brd   : rem Loose item unavailable foreground
5400   sp.infwinbd       =         menu_brd   : rem Information window border
5410   sp.infwinbg       =         menu_pap   : rem Information window background
5420   sp.infwinfg       =         menu_ink   : rem Information window foreground
5430   sp.infwinmg       =         menu_pap   : rem Information window foreground
5440   sp.subinfbd       =         menu_brd   : rem Information window border (sub)
5450   sp.subinfbg       =         menu_pap   : rem Information window background
5460   sp.subinffg       =         menu_ink   : rem Information window foreground
5470   sp.subinfmg       =         menu_pap   : rem Information window foreground
5480   sp.appbd          =         menu_brd   : rem Application window border
5490   sp.appbg          =         menu_pap   : rem Application window background
5500   sp.appfg          =         menu_ink   : rem Application window foreground
5510   sp.appmg          =         menu_pap   : rem Application window middleground
5520   sp.appihigh       =         menu_ink   : rem Application window item highlight
5530   sp.appiavabg      =         menu_pap   : rem Application window item available background
5540   sp.appiavafg      =         menu_ink   : rem Application window item available foreground
5550   sp.appiselbg      =         menu_brd   : rem Application window item selected background
5560   sp.appiselfg      =         menu_ink   : rem Application window item selected foreground
5570   sp.appiunabg      =         menu_pap   : rem Application window item unavailable background
5580   sp.appiunafg      =         menu_brd   : rem Application window item unavailable foreground
5590   sp.scrbar         =         menu_ink   : rem Pan/scroll bar
5600   sp.scrbarsec      =         menu_pap   : rem bar section
5610   sp.scrbararr      =         menu_brd   : rem Pan/scroll bar arrow
5620   sp.buthigh        =         menu_ink   : rem Button highlight
5630   sp.butbd          =         menu_brd   : rem Button border
5640   sp.butbg          =         menu_pap   : rem Button background
5650   sp.butfg          =         menu_ink   : rem Button foreground
5660   sp.hintbd         =         menu_brd   : rem Hint border
5670   sp.hintbg         =         menu_ink   : rem Hint background
5680   sp.hintfg         =         menu_pap   : rem Hint foreground
5690   sp.hintmg         =         menu_ink   : rem Hint middleground
5700   sp.errbg          =         menu_inv   : rem Error message background
5710   sp.errfg          =         menu_ink   : rem Error message foreground
5720   sp.errmg          =         menu_ink   : rem Error message middleground
5730   sp.shaded         =         0            : rem sp.shaded area
5740   sp.3ddark         =         0            : rem Dark 3D border shade
5750   sp.3dlight        =         white        : rem Light 3D border shade
5760   sp.vertfill       =         menu_pap   : rem Vertical area fill
5770   sp.subtitbg       =         menu_pap   : rem Subtitle background
5780   sp.subtittxtbg=   menu_pap   : rem Subtitle text background
5790   sp.subtitfg       =         menu_ink   : rem Subtitle foreground
5800   sp.mindexbg       =         menu_pap   : rem Menu index background
5810   sp.mindexfg       =         menu_brd   : rem same,        foreground
5820   sp.separator      =         menu_brd   : rem Seperator lines etc.
5830 end def make_my_old_palette
5840 :
5850 DEFine FuNction centr$(ctr$,leng)
5860 rem this "centres" the string ctr$ (i.e. left padds it with spaces) so that
5870 rem it is centred in a window of leng width (leng is length in chars)
5880   LOCal l%,cta$
5890   cta$=ctr$
5900   strip_spaces cta$
5910   l%=INT(leng-LEN(cta$))/2
5920   if l%<0:return cta$
5930   return FILL$(' ',l%)&cta$&FILL$(' ',l%)
5940 END DEFine centr$
5950 :
5960 define fn menus_init_menu (title$,array_with_prompts$,selkeys$)
5970 rem makes a menu window with menu items in 1 column
5980   return menus_init_menu_cols (title$,array_with_prompts$,selkeys$,1)
5990 end def
6000 :
6010 def fn menus_init_menu_cols (title$,array_with_prompts$,selkeys$,columns%)
6020 rem makes a menu window with menu items
6030 rem this presupposes that the colours have been set
6040 rem this prosupposes that a variable called "std_iattr" exists
6050 rem this presupposes that the variables defining the item types and event codes have been set
6060 rem this presupposes that this entire options window WILL FIT into the main
6070 rem outline. YOU must make sure of that.
6080 rem
6090 rem parameters:
6100 rem the title of the window
6110 rem the string array containing the items
6120 rem the selkeys for the items (a string of length dimn(array,1)+1)
6130 rem nbr of columns, 1 or 2
6140 rem
6150 rem returns the wdw definition, or -1 if error
6160 local temp1%,temp2%,temp3%,temp4,lp%,temp%
6170 local lxs%,lys%,xesp%,inf1y%,nbr_post%,yinf1y%
6180 local wxsize%,wysize%,wspcg%
6190 local opts_iot1,opts_iwt,opts_lot
6200 local longueur_titre%,titre_x%,opts_defn
6210 local oszsiz%(0,0),oszorgs%(0,0),oszjus%(0,0),sk$
6220 local oszblb(1),oszstr$(0),osztyp%(0),opts_lit%
6230 :
6240 rem now make window(s) ---------------------------------
6250 :
6260   if columns%<1 or columns%>2:return -1
6270   temp1%=len (selkeys$)-1     : rem number of "rows" -1
6280   if dimn(array_with_prompts$,1) <>temp1%
6290     return -1
6300   endif
6310   wxsize%=dimn(array_with_prompts$,2)+2
6320   wspcg%=wxsize%*6  +4
6330   wxsize%=wxsize%*6  +20      : rem sizes for strings + spacing
6340   if columns%=2:wxsize%=wxsize%+wspcg%
6350 :
6360   lxs%=24:lys%=12             : rem standard item sizes
6370   inf1y%=lys%+4               : rem y orig of inner wdw
6380   xesp%=lxs%+2
6390   nbr_post%=1                           : rem 1 item in each corner (2 x esc)
6400 :
6410   if columns%=1
6420     wysize%=inf1y%+4+(temp1%+1)*12+8
6430   else
6440     wysize%=inf1y%+4+(temp1%+1)*6+8
6450     if (temp1%+1) && 1:wysize%=wysize%+12
6460   endif
6470 :
6480   longueur_titre%=len(title$)*6 : rem title object x size
6490   temp2%=longueur_titre% + xesp%*nbr_post%*2 + 8 + 36 : rem min xsize of wdw
6500   if temp2%>wxsize% : wxsize%= temp2% +20
6510   titre_x%=(wxsize%-longueur_titre%)/2   : rem title x position
6520 :
6530 : rem  loose menu items. Here there are 1 standard one (esc) + 1 per menu choice
6540   yinf1y%=inf1y%+4
6550 :
6560   opts_lit%=temp1%+1:dim opts_lfl%(opts_lit%)
6570 :
6580   temp2%= dimn(array_with_prompts$,2)
6590   if temp2%<3:temp2%=3
6600   dim oszstr$(opts_lit%,temp2%) : rem strings at least 3 chars wide for "esc".
6610   dim oszblb(opts_lit%)
6620   dim oszjus%(opts_lit%,1)
6630   dim oszorg%(opts_lit%,1)
6640   dim osztyp%(opts_lit%)
6650   dim oszsiz%(opts_lit%,1)
6660   sk$=cancel$
6670 :
6680   oszstr$(0)="ESC"
6690   oszsiz%(0,0)=lxs%
6700   oszsiz%(0,1)=lys%
6710   oszorg%(0,0)=4
6720   oszorg%(0,1)=2
6730   osztyp%(0)=0+retr
6740 :
6750   temp3%=dimn(array_with_prompts$,2)*6+8 : rem x size
6760   temp%=yinf1y%
6770 :
6780   for lp%=1 to temp1%+1
6790     oszstr$(lp%)=array_with_prompts$(lp%-1)
6800     oszsiz%(lp%,0)=temp3%
6810     oszsiz%(lp%,1)=lys%
6820     if columns%=1
6830       oszorg%(lp%,0)=8
6840       oszorg%(lp%,1)=temp%
6850       temp%=temp%+lys%
6860     else
6870       if lp%&&1
6880           oszorg%(lp%,0)=8
6890           oszorg%(lp%,1)=temp%
6900       else
6910           oszorg%(lp%,0)=8 +wspcg%
6920           oszorg%(lp%,1)=temp%
6930           temp%=temp%+lys%
6940       endif
6950     endif
6960     oszjus%(lp%,0)=1                          : rem justif left
6970     temp2%=selkeys$(lp%) instr array_with_prompts$(lp%-1)
6980     if temp2%<> 0
6990        osztyp%(lp%)=retr + 256-(2*temp2%)
7000     else
7010        osztyp%(lp%)=retr
7020     endif
7030     sk$=sk$&selkeys$(lp%)
7040   end for lp%
7050   opts_lot=mk_lil(std_iattr,oszsiz%,oszorg%,oszjus%,sk$,osztyp%,oszstr$,oszblb,oszblb,oszblb)
7060 :
7070 restore 7070
7080 :
7090   rem now the info objects
7100 :
7110   rem window title
7120   opts_iot1=rd_iot(0)
7130     data longueur_titre%,10,4,int((lys%-10)/2),text,sp.titlefg,0,0,title$
7140 :
7150   remark now the info wdws
7160 :
7170   opts_iwt=rd_iwt(2)                              : remark there are 3
7180 :
7190 remark first the title bar
7200 :
7210     data wxsize%-8-(2*nbr_post%*xesp%),lys%+3,xesp%*nbr_post%+4,0       : remark x,y sizes & origs
7220     data 0,0,sp.infwinbd,sp.titlebg     : remark shadow, border size & col,paper col
7230     data 0                                        : remark object pointer (none)
7240 :
7250 remark now wdw containing title itself
7260 :
7270     data longueur_titre%+8,lys%+1,titre_x%-4,2
7280     data 0,0,sp.infwindbd,sp.infwinbg
7290     data opts_iot1                      : remark one object
7300 :
7310 remark inner border
7320 :
7330     data wxsize%-8,wysize%-inf1y%-2,4,inf1y%
7340     data 0,1,sp.infwinbd,sp.infwinbg
7350     data 0
7360 :
7370 : rem  the working definition
7380 :
7390   opts_defn=rd_wdef
7400     data 2,1,sp.winbd,sp.winbg: rem ombre, larg. & coul. bord., coul. papier
7410     data wxsize%,wysize%,wxsize%/2,wysize%/2 : rem taille x,y et position pointeur
7420     data main_sprite,opts_lot,opts_iwt,0
7430 :
7440   return opts_defn
7450 end define menus_init_menu
7460 :
7470 def fn menus_get_choice%(opts_defn,opts_values$)
7480 local lil_fl%(0),temp1%,item%,swnum%,event%,xrel%,yrel%
7490   temp1%=dimn (opts_values$,1)
7500   dim lil_fl%(temp1%+1)
7510   dr_puld opts_defn,-1,-1,lil_fl%
7520   rd_ptr opts_defn,item%,swnum%,event%,xrel%,yrel%,lil_fl%
7530   dr_unst opts_defn
7540   return item%-1
7550 end def menus_get_choice%
7560 :
7570 DEFine PROCedure scroll_arrows(rows%,nbr_objects%,appfen,cty%)
7580 rem This determined whether an appsub wdw should have scroll arrows.
7590 rem Params: number of visible rows in the appsub wdw, ,total nbr of object rows
7600 rem in the abbsub wdw (not the visible rows, but the rows with objects),
7610 rem pointer to the appsub wdw defn returned by the APP_MAKE or APP_MAKE2 function
7620 rem and the control definition
7630   IF nbr_objects%<=rows%
7640     POKE_W appfen+150,0       : rem all objects fit , so no scroll arrows
7650                                         : rem and no y offset to start of menu
7660     poke_l appfen+96,0                  : rem no scroll bars
7670     cty%(1,2)=nbr_objects%    : rem nbr of columns/rows
7680   ELSE
7690     POKE_W appfen+150,6       : rem leave space for arrows
7700     cty%(1,2)=rows%-1                   : rem if scroll arrows are there, there is one line less in the window
7710     poke_l appfen+96,hex("2210222")
7720   END IF
7730   IF DIMN(cty%,1)=2:cty%(2,2)=cty%(1,2)
7740   cty%(0,1)=1                            : rem show that definition has changed
7750 END DEFine scroll_arrows
7760 :
7770 def fn set_colours% (force_mode%)
7780 rem this sets colour variables according to the colour mode wished, if possible.
7790 rem force_mode%=
7800 rem       0 if ql colours wished (mode 4 only)
7810 rem       1 if pal colours wished
7820 rem       2 if 24 bit colours wished
7830 rem This function returns the true QL mode set (4,8,32,33 etc.).
7840 rem This function also sets the GLOBAL variable mycolour_mode% to the
7850 rem mode 0,1,2 as above - whatever is possible on the machine, i.e.
7860 rem if colour_24 is wished on a 4 colour machine (which won't succeed)
7870 rem this function will set set that variable to 0, i.e. QL mode.
7880 rem
7890 rem This function needs the outptr toolkit
7900 local mode%
7910   mode%=rmode                                     : rem get current mode now
7920   select on force_mode%
7930       = 0                                         : rem mode 4 wished
7940        select on mode%
7950             = 4                                   : rem already on, so do nothing
7960             = 0,1,8 : mode 4            : rem old modes, set to 4 colours
7970             = remainder
7980               colour_ql                           : rem if higher colour mode, set to "mode 4"
7990               mode 4
8000        end select
8010        mode%=0                                    : rem new mode we're in  - 4 colour mode
8020       = 1                                         : rem PALette wished
8030        select on mode%
8040             = 16,32,33 : colour_pal:mode%=1  : rem we're in high colour mode, but PAL colours wished
8050             = remainder
8060               if mode%<>4:mode 4
8070               mode%=0
8080        end select
8090       = 2
8100        select on mode%
8110             = 32,33 : colour_24 :mode%=2          : rem we're in high col mode 24 bit colours
8120             = 16 : colour_pal : mode%=1
8130             = remainder
8140               if mode%<>4:mode 4
8150               mode%=0
8160        end select
8170   end select
8180   mycolour_mode%=mode%                            : rem set this global variable now:
8190   rem now set some colour variables
8200   select on mode%
8210        = 0                                        : rem Ql 4 mode colours
8220           Black            = 0
8230           White            = 7
8240           Red              = 2
8250           Green            = 4
8260           grey             = 255
8270           dull_pink      = 19
8280           Blue             = 0
8290           Magenta        = 2
8300           Yellow           = 7
8310           Cyan             = 0
8320           Dark_slate     = 0
8330           Slate_grey     = 255
8340           Dark_grey      = 255
8350           Light_grey     = 255
8360           Ash_grey       = 255
8370           Dark_red       = 210
8380           Light_green    = 220
8390           Mustard        = 242
8400           Dark_green     = 228
8410           Sea_blue       = 0
8420           Dark_blue      = 0
8430           Purple           = 0
8440           Shocking_pink  = 234
8450           Orange           = 234
8460           Lime_green     = 4
8470           Apple_green    = 4
8480           Bright_blue    = 0
8490           Mauve            = 0
8500           Peach            = 7
8510           Light_yellow   = 7
8520           Light_blue     = 0
8530           Sky_blue       = 0
8540           Rose_pink      = 234
8550           Pink             = 234
8560           Beige            = 7
8570           Pastel_pink    = 234
8580           Pastel_yellow  = 7
8590           Pastel_green   = 4
8600           Pastel_cyan    = 0
8610           Pastel_blue    = 0
8620           Pastel_rose    = 234
8630           Brick            = 210
8640           Light_khaki    = 7
8650           Dull_green     = 228
8660           Dull_cyan      = 0
8670           Steel_blue     = 0
8680           Dull_pink      = 234
8690           Brown            = 0
8700           Khaki            = 7
8710           Dusky_green    = 4
8720           Dusky_blue     = 0
8730           Midnight_blue  = 0
8740           Plum             = 0
8750           Dusky_pink     = 234
8760           Buff             = 0
8770           Avocado        = 4
8780           Dull_turquoise = 4
8790           Dull_blue      = 0
8800           Faded_purple   = 234
8810           Cerise           = 2
8820           Tan_normal     = 0
8830           Grass_green    = 4
8840           Sea_green      = 4
8850           Ultramarine    = 0
8860           Deep_purple    = 234
8870           Light_orange   = 7
8880           light_red      = 2
8890           wg_striped     = 92
8900           br_striped     = 82
8910           wr_striped     = 107
8920           bg_striped     = 100
8930        = 1                              : rem Colour_pal
8940           Black            = 0
8950           White            = 1
8960           Red              = 2
8970           Green            = 3
8980           Blue             = 4
8990           Magenta        = 5
9000           Yellow           = 6
9010           Cyan             = 7
9020           Dark_slate     = 8
9030           Slate_grey     = 9
9040           Dark_grey      = 10
9050           Grey             = 11
9060           Light_grey     = 12
9070           Ash_grey       = 13
9080           Dark_red       = 14
9090           Light_green    = 15
9100           Mustard        = 16
9110           Dark_green     = 17
9120           Sea_blue       = 18
9130           Dark_blue      = 19
9140           Purple           = 20
9150           Shocking_pink  = 21
9160           Orange           = 22
9170           Lime_green     = 23
9180           Apple_green    = 24
9190           Bright_blue    = 25
9200           Mauve            = 26
9210           Peach            = 27
9220           Light_yellow   = 28
9230           Light_blue     = 29
9240           Sky_blue       = 30
9250           Rose_pink      = 31
9260           Pink             = 32
9270           Beige            = 33
9280           Pastel_pink    = 34
9290           Pastel_yellow  = 35
9300           Pastel_green   = 36
9310           Pastel_cyan    = 37
9320           Pastel_blue    = 38
9330           Pastel_rose    = 39
9340           Brick            = 40
9350           Light_khaki    = 41
9360           Dull_green     = 42
9370           Dull_cyan      = 43
9380           Steel_blue     = 44
9390           Dull_pink      = 45
9400           Brown            = 46
9410           Khaki            = 47
9420           Dusky_green    = 48
9430           Dusky_blue     = 49
9440           Midnight_blue  = 50
9450           Plum             = 51
9460           Dusky_pink     = 52
9470           Buff             = 53
9480           Avocado        = 54
9490           Dull_turquoise = 55
9500           Dull_blue      = 56
9510           Faded_purple   = 57
9520           Cerise           = 58
9530           Tan_normal     = 59
9540           Grass_green    = 60
9550           Sea_green      = 61
9560           Ultramarine    = 62
9570           Deep_purple    = 63
9580           pinkish        = 77
9590           light_red      = 109
9600           Light_orange   = 208
9610           wg_striped     = light_green
9620           br_striped     = dark_red
9630           wr_striped     = light_red
9640           bg_striped     = dark_green
9650        = 2                              : rem colour_24
9660           Black            = hex('000000')
9670           White            = hex('FFFFFF')
9680           Red              = hex('FF0000')
9690           Green            = hex('00FF00')
9700           Blue             = hex('0000FF')
9710           Magenta        = hex('FF00FF')
9720           Yellow           = hex('FFFF00')
9730           Cyan             = hex('00FFFF')
9740           Dark_slate     = hex('242424')
9750           Slate_grey     = hex('494949')
9760           Dark_grey      = hex('6D6D6D')
9770           Grey             = hex('929292')
9780           Light_grey     = hex('B6B6B6')
9790           Ash_grey       = hex('DBDBDB')
9800           Dark_red       = hex('920000')
9810           Light_green    = hex('B6FFB6')
9820           Mustard        = hex('929200')
9830           Dark_green     = hex('009200')
9840           Sea_blue       = hex('009292')
9850           Dark_blue      = hex('000092')
9860           Purple           = hex('920092')
9870           Shocking_pink  = hex('FF0092')
9880           Orange           = hex('FF9200')
9890           Lime_green     = hex('92FF00')
9900           Apple_green    = hex('00FF92')
9910           Bright_blue    = hex('0092FF')
9920           Mauve            = hex('9200FF')
9930           Peach            = hex('FFB6B6')
9940           Light_yellow   = hex('FFFFB6')
9950           Light_blue     = hex('B6FFFF')
9960           Sky_blue       = hex('B6B6FF')
9970           Rose_pink      = hex('FFB6FF')
9980           Pink             = hex('FFB6DB')
9990           Beige            = hex('FFDBB6')
10000           Pastel_pink    = hex('FFDBDB')
10010           Pastel_yellow  = hex('FFFFDB')
10020           Pastel_green   = hex('DBFFDB')
10030           Pastel_cyan    = hex('DBFFFF')
10040           Pastel_blue    = hex('DBDBFF')
10050           Pastel_rose    = hex('FFDBFF')
10060           Brick            = hex('B66D6D')
10070           Light_khaki    = hex('B6B66D')
10080           Dull_green     = hex('6DB66D')
10090           Dull_cyan      = hex('6DB6B6')
10100           Steel_blue     = hex('6D6DB6')
10110           Dull_pink      = hex('B66DB6')
10120           Brown            = hex('6D2424')
10130           Khaki            = hex('6D6D24')
10140           Dusky_green    = hex('246D24')
10150           Dusky_blue     = hex('246D6D')
10160           Midnight_blue  = hex('24246D')
10170           Plum             = hex('6D246D')
10180           Dusky_pink     = hex('B64992')
10190           Buff             = hex('B69249')
10200           Avocado        = hex('92B649')
10210           Dull_turquoise = hex('49B692')
10220           Dull_blue      = hex('4992B6')
10230           Faded_purple   = hex('9249B6')
10240           Cerise           = hex('920049')
10250           Tan_normal     = hex('924900')
10260           Grass_green    = hex('499200')
10270           Sea_green      = hex('009249')
10280           Ultramarine    = hex('004992')
10290           Deep_purple    = hex('490092')
10300           Light_orange   = hex('ffb624')
10310           pinkish        = hex('C0037B')
10320           Light_red      = hex('FC1729')
10330           wg_striped     = light_green
10340           br_striped     = dark_red
10350           wr_striped     = light_red
10360           bg_striped     = dark_green
10370   end select
10380   mode%=rmode
10390   ret mode%                             : rem return current screen mode now
10400 end def set_colours%
10410 :
10420 DEFine PROCedure init
10430 rem this initialises the following global variables:
10440 rem all the windows (_defn,_lfl%,_cty%,mfl% etc...)
10450 rem all the sprites
10460 rem item types (text, sprite) , event codes (hit$, do$)
10470 rem v. 1.04
10480 local temp1%,temp2%,temp3%,nbr_post%,temp4%
10490 local main_iot1,main_iot2,main_iwt,main_lot,main_lit%
10500 local longueur_titre%,titre_x%,tt%
10510 :
10520   text=0:sprite=2:blob=4:pattern=6
10530   retr=256:retn=-256:sous1=254:sous2=252
10540 :
10550 : rem events
10560   hit$=CHR$(1):do$=CHR$(2):cancel$=CHR$(3):sleep$=CHR$(7):wake$=CHR$(8)
10570   help$=CHR$(4):move$=CHR$(5):size$=CHR$(6):cr$=CHR$(10)
10580 :
10590   current_job$=""             : rem job currently displayed
10600   main_sbasic$="Main SBasic"  : rem name for job 0
10610   show_jobs$="xxxMake Jobsxxx"          : rem name when we want to show jobs info
10620 :
10630   main_coul%=4      : rem make colours
10640   app_coul%=0
10650   btn_coul%=0:      rem =LNKCOL(3)
10660   temp1%=set_colours% (2)                : rem always try to set highest colours
10670   rem if mycolour_mode%<2 and main_coul%>3:main_coul%=0
10680   rem if mycolour_mode%<2 and app_coul%>3:app_coul%=0
10690   rem if mycolour_mode%<2 and btn_coul%>3:btn_coul%=0
10700   make_colours main_coul%               : rem make colours now
10710 :
10720   titre$="Recent Files"
10730 :
10740 rem now make window(s) ---------------------------------
10750 :
10760   xsize%=272                            : rem x size, should be multiple of 4
10770   ysize%=-1                             : rem y size of wdw : -1 means it's calculated...
10780                                         : rem with respect to the number of rows in appsub wdw
10790   make_appsub_data current_job$ : rem data to fill appsub wdw with
10800   rangees%=dimn(appsub_data$,1) : rem GLOBAL var: nbr of visible rows in appsub
10810   if rangees%<8:rangees%=8
10820 :
10830   lxs%=24:lys%=12             : rem tailles x et y pour postes standard
10840   inf1y%=lys%+4               : rem posi. y de la fen. d'inf. intƒrieure
10850   xesp%=lxs%+2
10860   nbr_post%=3
10870 :
10880 : rem now calculate the final y size. Even if a real y size is given, this may
10890 : rem change as the y size is calculated so that the rows fit neatly inside the
10900 : rem appsub window.
10910 : rem maxx% and maxy% are the maximum sizes the wdw may have (see start_bas)
10920 : rem if both ysize% and rangees%=-1: make wdw as high as screen allows
10930 :
10940   appwinoffset%=inf1y%                  : rem y offset for appsub win within wdw
10950   if rangees%= -1
10960     if ysize%= -1
10970       rangees%=(maxy%-14-appwinoffset%) div 12 : rem max nbr of rows
10980     endif
10990   endif
11000   if ysize%= -1
11010     ysize%=rangees%*12+appwinoffset%+4
11020   else
11030     ysize%=ysize%-appwinoffset%-4
11040     ysize%=(ysize% div 12) *12 + appwinoffset%+4
11050   endif
11060   if ysize%>=maxy%-14
11070     ysize%=maxy%-appwinoffset%-10
11080     rangees%=ysize% div 12
11090     ysize%=rangees%*12+appwinoffset% +4
11100   endif
11110 :
11120   make_standard_sprites       : rem leave this before the restore
11130 :
11140 restore 11140
11150 :
11160   DIM std_iattr(3,3)
11170   RD_IATT std_iattr
11180     DATA 1,sp.litemhigh : REMark size & colour current item border
11190     DATA sp.litemunabg,sp.litemunafg,0,0 : rem unavailable item paper & ink
11200     DATA sp.litemavabg,sp.litemavafg,0,0 : REMark available
11210     DATA sp.litemselbg,sp.litemselfg,0,0 : REMark selected
11220 :
11230   longueur_titre%=LEN(titre$)*6 : rem on calcule la taille et la
11240   titre_x%=(xsize%-longueur_titre%)/2 : rem position x de cet objet
11250 :
11260 : rem  Loose items, the first 4 are standard
11270   esc_it%=0:move_it%=esc_it%+1
11280   sleep_it%=move_it%+1
11290   wake_it%=sleep_it%+1
11300   jobs_it%=wake_it%+1
11310   info_it%=jobs_it%+1
11320 :
11330   main_lit%=5:DIM main_lfl%(main_lit%)
11340   main_lot=RD_LOT(std_iattr,main_lit%)
11350     DATA lxs%,lys%,4+xesp%*0,2,0,0,cancel$,text+retr,'ESC'
11360     DATA lxs%,lys%,4+xesp%*1,2,0,0,move$,sprite+retr,move_sprite
11370     DATA lxs%,lys%,xsize%-2-xesp%*1,2,0,0,sleep$,sprite+retr,sleep_sprite
11380     DATA lxs%,lys%,xsize%-2-xesp%*2,2,0,0,wake$,sprite+retr,wake_sprite
11390     DATA lxs%,lys%,xsize%-2-xesp%*3,2,0,0,"J",sous1+retr,"Jobs"
11400     DATA lxs%,lys%,4+xesp%*2,2,0,0,"I",sous1+retr,"Info"
11410 :
11420   main_iot1=RD_IOT(0)
11430     DATA longueur_titre%,10,4,INT((lys%-10)/2)+1,text,sp.titlefg,0,0,titre$
11440 :
11450   REMark now the info wdws
11460 :
11470   main_iwt=RD_IWT(2)                              : REMark there are 3
11480 :
11490 REMark first the title bar
11500 :
11510     DATA xsize%-8-(2*nbr_post%*xesp%),lys%+3,xesp%*nbr_post%+4,0       : REMark x,y sizes & origs
11520     DATA 0,0,sp.infwinbd,sp.titlebg     : REMark shadow, border size & col,paper col
11530     DATA 0                                        : REMark object pointer (none)
11540 :
11550 REMark now wdw containing title itself
11560 :
11570     DATA longueur_titre%+8,lys%+1,titre_x%-4,2
11580     DATA 0,0,sp.infwindbd,sp.infwinbg
11590     DATA main_iot1                      : REMark one object
11600 :
11610 REMark inner border
11620 :
11630     DATA xsize%-8,ysize%-inf1y%-2,4,inf1y%
11640     DATA 0,1,sp.infwinbd,sp.infwinbg
11650     DATA 0
11660 :
11670 rem now application subwindow
11680 :
11690   main_app_list=0
11700   app_addr=0                                      : REMark there is no appsub wdw yet
11710   mk_main_app
11720   if app_addr<>0:main_app_list = app_addr+180 : REMark list built by OUTPTR
11730 :
11740 : rem  window definition
11750 :
11760   main_defn=RD_WDEF
11770     DATA 2,1,sp.winbd,sp.winbg: rem ombre, larg. & coul. bord., coul. papier
11780     DATA xsize%,ysize%,10,ysize%-6 : rem taille x,y et position pointeur
11790     DATA main_sprite,main_lot,main_iwt,main_app_list
11800 :
11810   warn_make         :         rem make warning definition
11820   init_info_wdw
11830 END DEFine init
11840 :
11850 DEFine PROCedure init_info_wdw
11860 rem make info wdw defn
11870 local info_iot1,info_iot2,info_iwt,info_lot,info_lit%
11880 local longueur_titre%,titre_x%,titre$
11890 local ixsize%,nbr_post%
11900 :
11910   ixsize%=xsize%-16                      : rem x size, should be multiple of 4
11920   iysize%=100                            : rem y size of wdw
11930   nbr_post%=1
11940   titre$='Info'
11950 :
11960 restore 11960
11970 :
11980   longueur_titre%=LEN(titre$)*6 : rem on calcule la taille et la
11990   titre_x%=(ixsize%-longueur_titre%)/2 : rem position x de cet objet
12000 :
12010 : rem  Loose items, the first 4 are standard
12020   iesc_it%=0:imove_it%=iesc_it%+1
12030 :
12040   info_lit%=1:DIM info_lfl%(info_lit%)
12050   info_lot=RD_LOT(std_iattr,info_lit%)
12060     DATA lxs%,lys%,4+xesp%*0,2,0,0,cancel$,text+retr,'ESC'
12070     DATA lxs%,lys%,ixsize%-2-xesp%*1,2,0,0,move$,sprite+retr,move_sprite
12080 :
12090   info_iot1=RD_IOT(0)
12100     DATA longueur_titre%,10,4,INT((lys%-10)/2),text,sp.titlefg,0,0,titre$
12110 :
12120   REMark now the info wdws
12130 :
12140   info_iwt=RD_IWT(2)                              : REMark there are 3
12150 :
12160 REMark first the title bar
12170 :
12180     DATA ixsize%-8-(2*nbr_post%*xesp%),lys%+3,xesp%*nbr_post%+4,0   : REMark x,y sizes & origs
12190     DATA 0,0,sp.infwinbd,sp.titlebg     : REMark shadow, border size & col,paper col
12200     DATA 0                                        : REMark object pointer (none)
12210 :
12220 REMark now wdw containing title itself
12230 :
12240     DATA longueur_titre%+8,lys%+1,titre_x%-4,2
12250     DATA 0,0,sp.infwindbd,sp.infwinbg
12260     DATA info_iot1                      : REMark one object
12270 :
12280 REMark inner border
12290 :
12300     DATA ixsize%-8,iysize%-inf1y%-2,4,inf1y%
12310     DATA 0,1,sp.infwinbd,sp.infwinbg
12320     DATA 0
12330 :
12340 : rem  window definition
12350 :
12360   info_defn=RD_WDEF
12370     DATA 2,1,sp.winbd,sp.winbg: rem ombre, larg. & coul. bord., coul. papier
12380     DATA ixsize%,iysize%,10,iysize%-6 : rem taille x,y et position pointeur
12390     DATA main_sprite,info_lot,info_iwt,0
12400 :
12410 END DEFine init_info_wdw
12420 :
12430 DEFine PROCedure mk_main_app
12440 rem make main application sub window
12450 local t2%,temp%,sections%,att_phys%(3)
12460   dim main_cty%(0):dim main_mfl%(0,0)
12470   sections%=2
12480   temp%=sections%
12490   att_phys%(0)=xsize%-16                : rem xsize calculated on main wdw x size
12500   att_phys%(1)=rangees%                 : rem y size,nominal,in rows
12510   att_phys%(2)=4                        : rem x origin
12520   att_phys%(3)=inf1y +appwinoffset%     : rem y origin
12530   app_addr=make_standard_app_sub2(app_addr,appsub_data$,temp%,257,att_phys%)
12540   if app_addr
12550       dim main_cty%(sections%,2)        : rem ctrl defn
12560       main_cty%(0,0)=1
12570       if temp%>rangees%
12580           main_cty%(1,2)=rangees%       : rem show 1 section
12590       else
12600           main_cty%(1,2)=temp%
12610       endif
12620       if sections%=2:main_cty%(2,2)=main_cty%(1,2) : rem but allow 2
12630       if temp%
12640              dim main_mfl%(temp%-1,0)   : rem flag array
12650              main_mfl%(0,0)=128
12660       else
12670              poke_w app_addr,peek_w(app_addr)+8
12680       end if
12690       scroll_arrows rangees%,temp%,app_addr,main_cty%
12700       if main_app_list<>0
12710           poke_l main_app_list+2,app_addr
12720       endif
12730   end if
12740 end define mk_main_app
12750 :
12760 def proc make_appsub_data (jobnm$)
12770 rem  this gets the data from the recent thing into an array
12780 local lp%,leng%,nbr%,size,max%,tit$
12790   if jobnm$=show_jobs$
12800      get_jobs_data                      : rem get data on jobs, not files
12810      return                                       : rem PREMATURE EXIT
12820   endif
12830   leng%=0:nbr%=0:max%=0
12840   if jobnm$= main_sbasic$
12850     size=rcnt_info(0,nbr%,leng%,max%)   : rem get info on job
12860     if size<0
12870       leng%=warning%("Couldn't find this job",0,jobnm$&":")
12880       show_jobs
12890       return
12900     endif
12910     dim appsub_data$(nbr%+1,45)         : rem array for files for this job
12920     rcnt_garj 0, appsub_data$                     : rem get names of files
12930     tit$="***--- Files for : "&jobnm$ & " ---***"
12940   else
12950     if jobnm$<>""
12960       size=RCNT_INFO(jobnm$,nbr%,leng%,max%) : rem get info on job
12970       if size<0
12980           leng%=warning%("Couldn't find this job",0,jobnm$&":")
12990           show_jobs
13000           return
13010       endif
13020       dim appsub_data$(nbr%+1,45)       : rem array for files for this job
13030       rcnt_garj jobnm$, appsub_data$    : rem get names of files
13040       tit$="***--- Files for : "&jobnm$ & " ---***"
13050     else
13060       size=rcnt_info(-3,nbr%,leng%,max%): rem info on general list
13070       dim appsub_data$(nbr%+1,45)
13080       rcnt_garr appsub_data$
13090       tit$="***--- General file list ---***"
13100     endif
13110   endif
13120   :
13130   : rem for the outptr keywords, I need the data to start at elmt 1, not elmt 0
13140   : rem so move all elements in the array 1 down. I also want to add a title,
13150   : rem so I move them 2 down
13160   :
13170   for lp%=nbr%+1 to 2 step -1
13180     appsub_data$(lp%)=appsub_data$(lp%-2)
13190   end for lp%
13200   appsub_data$(1)=tit$
13210 end def make_appsub_data
13220 :
13230 def proc get_jobs_data
13240 rem this gets a list of all jobs into the basic array appsub_data$.
13250 rem it also gets the job id into the array job_ids, which isn't used any further.
13260 local a,address,nbr%,lp%,leng%,jbid,memspace
13270   memspace=10000              : rem space for buffer
13280   rep lp%
13290     a=alchp(memspace)                   : rem get some common heap space for the buffer
13300     nbr%=rcnt_jobs(memspace,a)          : rem get all jobs
13310     if nbr%>-1:exit lp%       : rem call was ok
13320     rechp a                             : rem i need more space!
13330     memspace=memspace+10000   : rem so try with this much
13340     if memspace>100000
13350       dim appsub_data$(1,30)  : rem names of jobs
13360       appsub_data$(1)="serious error in prog"
13370       dim job_ids(1)                    : rem job ids
13380       return
13390     endif                               : rem there is something serioulsy wrong, i presume
13400   end rep lp%
13410   address= a                            : rem keep
13420   dim appsub_data$(nbr%+2,80)           : rem for names of jobs
13430   dim job_ids(nbr%+2)                   : rem job ids
13440   for lp% = 2 to nbr%+1
13450     jbid= peek_l(address)     : rem get jobid
13460     if jbid=-1:exit lp%       : rem this ** shouldn't happen **
13470     job_ids(lp%)=jbid
13480     address=address+4                   : rem point after job id
13490     leng%=peek_w (address)    : rem length of name
13500     address=address+2
13510     appsub_data$(lp%)=get_nm$(address,leng%,lp%) : rem get the name
13520     address=address+leng% +(leng%&&1)   : rem point next entry
13530   end for lp%
13540   appsub_data$(nbr%+2)="(Show general list)"
13550   rechp a                               : rem done
13560   appsub_data$(1)="***--- List of jobs ---***"
13570 end define getjobs
13580 :
13590 define function get_nm$ (address,t%,index%)
13600 rem this gets the name from the buffer, starting at address, simple peek$
13610 local lp,a$
13620   if t%<0 : return "illegal"  : rem huh?
13630   if t%=0
13640      if job_ids(index%)=0
13650           return main_sbasic$
13660      else
13670           return "Unknown job (no name)": rem job had no name
13680      endif
13690   endif
13700   return peek$(address,t%)
13710 end define get_nm$
13720 :
13730 def proc show_jobs
13740   current_job$=show_jobs$: rem show list of jobs
13750   refresh_list
13760   displaying_jobs%=1
13770   main_lfl%(info_it%)=17
13780 end def show_jobs
13790 :
13800 def proc make_standard_sprites
13810 rem This makes standard move, sleep and wake sprites and sets the
13820 rem GLOBAL vars move_sprite,, sleep_sprite, wake_sprite and size_sprite to them.
13830 rem If there are system sprites, these are used.
13840 rem This also makes the "main_sprite" pointer sprite.
13850 :
13860   espace_vide=ALCHP(100)         : rem for pointers with no mask
13870 :
13880 restore 13880
13890   main_sprite=RD_SPRT(0)         : REMark ici un masque normal
13900     data 11,13,6,5,4
13910     data '     www     '
13920     data '     waw     '
13930     data '     waw     '
13940     data '     waw     '
13950     data 'wwwwwa awwwww'
13960     data 'waaaa   aaaaw'
13970     data 'wwwwwa awwwww'
13980     data '     waw     '
13990     data '     waw     '
14000     data '     waw     '
14010     data '     www     '
14020 :
14030   if sys_tspr_addr(6)<>0
14040     move_sprite=6
14050   else
14060 restore 14060
14070     move_sprite=RD_SPRT(1)         : REMark ici un masque 0
14080     data 10,14,5,4,4
14090     data 'aaaaaaaaaa    '
14100     data 'awwwwwwwwa    '
14110     data 'awwrrrrwwa    '
14120     data 'awwrrrrwwaaaaa'
14130     data 'awwrrwwwwwwwwa'
14140     data 'awwwwwwwwrrwwa'
14150     data 'aaaaawwrrrrwwa'
14160     data '    awwrrrrwwa'
14170     data '    awwwwwwwwa'
14180     data '    aaaaaaaaaa'
14190   endif
14200 :
14210   if sys_tspr_addr(10)<>0
14220     sleep_sprite=10
14230   else
14240 restore 14240
14250     sleep_sprite= RD_SPRT(1)             : REMark ici un masque 0
14260     data 7,15,0,0,4
14270     data 'wwwww          '
14280     data '   w           '
14290     data '  w  wwww      '
14300     data ' w     w       '
14310     data 'wwwww w  wwww  '
14320     data '     wwww  w   '
14330     data '          wwww '
14340   endif
14350 :
14360   if sys_tspr_addr(11)<>0
14370     wake_sprite=11
14380   else
14390 restore 14390
14400     wake_sprite=RD_SPRT(1)          : REMark ici un masque 0
14410     data 9,14,0,0,4
14420     data "             w"
14430     data "            w "
14440     data "      w   ww  "
14450     data "     ww www   "
14460     data "    wwwwww    "
14470     data "   www ww     "
14480     data "  w   w      "
14490     data " w            "
14500     data "w             "
14510   endif
14520 :
14530   if sys_tspr_addr(7)<>0
14540     size_sprite=7
14550   else
14560 restore 14560
14570     size_sprite=RD_SPRT(1)
14580     data 9,14,5,4,4
14590     data 'aaaaaaaaaaaaaa'
14600     data 'awwwwwwwwwwwwa'
14610     data 'awwrrrrrrrrwwa'
14620     data 'awwrrrrrrrrwwa'
14630     data 'awwrrwwwwwwwwa'
14640     data 'awwrrwwrrrrwwa'
14650     data 'awwrrwwrrrrwwa'
14660     data 'awwwwwwwwwwwwa'
14670     data 'aaaaaaaaaaaaaa'
14680   endif
14690 :
14700 end def make_standard_sprites
14710 :
14720 DEFine PROCedure warn_make
14730 rem makes warning & info window
14740 rem uses global vars sp.xxx
14750 rem SETS the global var warning_width% (nbr of chars in one line in wdw)
14760 local lys%,warn_lot,warn_iot1,warn_iwt
14770 local x%,y%,warning_paper,warning_brd
14780 restore 14780
14790   x%=200:y%=82
14800   lys%=10
14810   DIM warn_lfl%(1)
14820   warn_lot=RD_LOT(std_iattr,1)
14830     DATA 30,lys%,4,4,0,0,cancel$,text+retr,'ESC'
14840     DATA 30,lys%,x%-34,4,0,0,'O',text+retr,"OK"
14850 :
14860   warn_iot1=RD_IOT (0)
14870     DATA 12*9,10,0,1,text,sp.errfg,2,0,'Attention'
14880 :
14890   warn_iwt=RD_IWT(1)
14900     DATA x%-16,y%-32,8,24
14910     DATA 0,1,sp.winbd,sp.errbg
14920     DATA 0
14930 :
14940     DATA 110,12,(x%-110) div 2,4
14950     DATA 0,1,sp.winbd,sp.errbg
14960     DATA warn_iot1
14970 :
14980   warn_defn=RD_WDEF
14990     DATA 2,1,sp.winbd,sp.errbg
15000     DATA x%,y%,0,0
15010     DATA main_sprite,warn_lot,warn_iwt,0
15020 :
15030   warning_width%=(x%-16) div 6                    : rem width of warning window in chars
15040   info_is_pulled_down%=0
15050   warn_wdw_addr=peek_l(warn_defn+100)+20: rem ptr to 2nd info wdw
15060  rem warn_wdw_addr=peek_l(warn_wdw_addr+16): rem ptr to object list
15070 END DEFine warn_make
15080 :
15090 DEFine PROCedure about_make (titre$,version$)
15100 local lxs%,lys%,xesp%,inf1y%,abt_lot,longueur_titre%,titre_x%
15110 local abt_iwt,abt_iot1,abt_iot2
15120   ab_xt%=200:ab_yt%=120    : REMark tailles x et y: la taille x devrait
15130                                    : REMark ‘tre un chiffre divisible par 4
15140   lxs%=24:lys%=10                    : REMark tailles x et y pour postes standard
15150   inf1y%=lys%+4            : REMark posi. y de la fen. d'inf. intƒrieure
15160   xesp%=lxs%+2
15170   nbr_post%=0                      : REMark nombre de postes dans un coin
15180 :
15190 restore 15190
15200 wolfsoft_sprite=rD_SPrT(0)
15210     DATA 23,51,0,0,4
15220     DATA 'rrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrr'
15230     DATA 'rrgggggggggggggggggggggggggggggggggggggggggggggggrr'
15240     DATA 'rrggggggggrrrrrrrrrrrrrrrrgggggggggggggggggggggggrr'
15250     DATA 'rrgggggggggggggggggggggrgggggggggggggggggggggggggrr'
15260     DATA 'rrggggggggggggggggggggrggggggggggggggggggggggggggrr'
15270     DATA 'rrggggggggagggggagggrrrrrrrrrrrrrggaaaaaaaaggggggrr'
15280     DATA 'rrggggggggagggggaggggggggggggggggggagggggggggggggrr'
15290     DATA 'rrggggggggaggaggaggaaaaaaaggaggggggaaaaaaagggggggrr'
15300     DATA 'rrggggggggagaaagaggaawwaaaggaggggggagggggggggggggrr'
15310     DATA 'rrggggggggaaagaaaggaawwwwaggaggggggagggggggggggggrr'
15320     DATA 'rrggrrrrrgaagggaaggawwwwaaggaggggggagggggrrrrrrrgrr'
15330     DATA 'rrgrgggggrgggggggggaaawwaaggaggggggggggrrrgggggggrr'
15340     DATA 'rrgrggggggrggggggggaaaaaaaggaaaaaagggrrggrgggggggrr'
15350     DATA 'rrgrgggggggggggggrrrgggggggggggrrrggrggggrgggggggrr'
15360     DATA 'rrggrrrrrrggggggrgggrrgggggggrrggggggggggrgggggggrr'
15370     DATA 'rrggggggggrrrgggrgggggrrgggrrggggggggggggrgggggggrr'
15380     DATA 'rrgggggggggggrggrgggggggrggrgggggggggggggrgggggggrr'
15390     DATA 'rrggggggggggggrgrggggggggrgrrrrrrrgggggggrgggggggrr'
15400     DATA 'rrgrggggggggggrgrggggggggrgrgggggggggggggrgggggggrr'
15410     DATA 'rrggrgggggggrrgggrggggggrggrgggggggggggggrgggggggrr'
15420     DATA 'rrgggrrrrrrrggggggrrrrrrgggrgggggggggggggrgggggggrr'
15430     DATA 'rrgggggggggggggggggggggggggggggggggggggggggggggggrr'
15440     DATA 'rrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrrr'
15450 :
15460   yinf1y%=inf1y%+4:
15470 :
15480   DIM abt_lfl%(0)
15490   abt_lot=RD_LOT(std_iattr,0)
15500     DATA ab_xt%-16,lys%,8,ab_yt%-lys%-8,0,0,cancel$,text+retr,'Super!'
15510 :
15520 : REMark  D'abord le titre qui est un objet d'une sous-fen‘tre d'info.
15530 :
15540   longueur_titre%=LEN(" Version "&version$)*6               : REMark on calcule la taille et la
15550   titre_x%=INT((ab_xt%-longueur_titre%)/2)  : REMark position x de cet objet
15560   abt_iot2=RD_IOT(1)
15570     DATA longueur_titre%,lys%,4,8,text,sp.winfg,0,0," Version "&version$
15580     DATA 52,24,ab_xt%-60,40,sprite,0,wolfsoft_sprite
15590   longueur_titre%=LEN(titre$)*6     : REMark on calcule la taille et la
15600   titre_x%=INT((ab_xt%-longueur_titre%)/2)  : REMark position x de cet objet
15610   abt_iot1=RD_IOT(0)
15620     DATA longueur_titre%,lys%,4,INT((lys%-10)/2),text,sp.titlefg,0,0,titre$
15630   abt_iwt=RD_IWT(2)               : REMark on veut 3 sous-fen. d'info
15640 REMark d'abord la fen‘tre contenant la barre entourant le titre
15650     DATA ab_xt%-8-(2*nbr_post%*xesp%),lys%+2,xesp%*nbr_post%+4,0  : REMark taille x,y, origines x,y
15660     DATA 0,0,sp.infwinbd,sp.titlebg     : REMark ombre, taille & couleur bordure
15670                                    : REMark puis couleur papier
15680     DATA 0                         : REMark pointeur vers objets
15690 REMark maintenant la fen‘tre qui contient le titre
15700     DATA longueur_titre%+8,lys%,titre_x%-2,2   : REMark on utilise titre_x% etc
15710     DATA 0,0, sp.infwinbd,sp.titletextbg
15720     DATA abt_iot1                 : REMark un objet, le titre
15730 REMark maintenant la fen‘tre de la bordure intƒrieure
15740     DATA ab_xt%-8,ab_yt%-inf1y%-2,4,inf1y%
15750     DATA 0,1,sp.infwinbd,sp.infwinbg
15760     DATA abt_iot2
15770   abt_defn=RD_WDEF
15780     DATA 2,1,sp.winbd,sp.winbg: REMark ombre, larg. & coul. bord., coul. papier
15790     DATA ab_xt%,ab_yt%,10,10 : REMark taille x,y et position pointeur
15800     DATA main_sprite,abt_lot,abt_iwt,0
15810 :
15820 end def about_make
15830 :
15840 DEFine PROCedure about (datet$)
15850   LOCal loop%,item%,dummy%,chan%
15860   DR_PULD abt_defn,-1,-1,abt_lfl%
15870   chan%=fopen('con')
15880   dr_iwdf#chan%,abt_defn,2
15890   wm_ink#chan%,sp.infwinfg
15900   wm_paper#chan%,sp.infwinbg
15910   at#chan%,4,2
15920   print#chan%,"Copyright (C) "
15930   at#chan%,5,2
15940   print#chan%, datet$
15950   at#chan%,6,2
15960   print#chan%,"Wolfgang H. Lenerz"
15970   close#chan%
15980   REPeat loop%
15990     RD_PTR abt_defn,item%,dummy%,dummy%,dummy%,dummy%,abt_lfl%
16000     SELect ON item%
16010             =0       : EXIT loop%
16020     END SELect
16030   END REPeat loop%
16040   DR_UNST abt_defn
16050 END DEFine about
16060 :
16070 DEFine FuNction RD_SPRT(drap%)
16080 REMark drap% determine si masque 0
16090   LOCal tmp_patt$(32,32)
16100   LOCal xs,ys,xo,yo, md, l
16110   READ ys,xs,xo,yo,md
16120   DIM tmp_patt$(ys,xs)
16130   FOR l=0 TO ys-1:READ tmp_patt$(l)
16140   l=ALCHP(SPRSP(xs,ys))
16150   SPSET l,xo,yo,md,tmp_patt$(0 TO ys-1,1 TO xs)
16160   IF drap%
16170     POKE_L l+16,espace_vide-(l+16)
16180   END IF
16190   RETurn l
16200 END DEFine RD_SPRT
16210 :
16220 DEFine FuNction RD_WDEF
16230 rem read window defn
16240   LOCal ldef%(3), lspr, lloose, linfo, lappl, lattr%(3)
16250   RD_WATT lattr%
16260   READ ldef%(0), ldef%(1), ldef%(2), ldef%(3)
16270   READ lspr, lloose, linf, lappl
16280   RETurn MK_WDEF (ldef%, lattr%, lspr, lloose, linf, lappl)
16290 END DEFine RD_WDEF
16300 :
16310 DEFine FuNction RD_LOT (lattr,nitem)
16320   LOCal count(3)
16330   LOCal item, ltyp, a$, lsk$
16340   LOCal ldef%(nitem,6), lptr(3,nitem), lstr$(nitem,85)
16350   lsk$=''
16360   FOR item = 0 TO nitem
16370     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
16380     READ ldef%(item,4), ldef%(item,5)
16390     READ a$: lsk$=lsk$ & a$
16400     READ ltyp
16410     ldef%(item,6)=ltyp: ltyp=(ltyp MOD 256)/2
16420     IF ltyp>10 or ltyp <0:ltyp=0
16430     IF ltyp
16440       READ lptr(ltyp,count(ltyp))
16450     ELSE
16460       READ lstr$(count(0))
16470     END IF
16480     count(ltyp)=count(ltyp)+1
16490   END FOR item
16500   RETurn MK_LIL (lattr, ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), ldef%(TO, 4 TO 5), lsk$, ldef%(TO, 6), lstr$, lptr(1), lptr(2), lptr(3))
16510 END DEFine RD_LOT
16520 :
16530 DEFine FuNction RD_IWT(nitem)
16540   LOCal item,mc1
16550   LOCal ldef%(nitem,3), latt%(nitem,3), lptr(nitem)
16560   FOR item = 0 TO nitem
16570     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
16580     READ latt%(item,0), latt%(item,1), latt%(item,2), latt%(item,3)
16590     READ lptr(item)
16600   END FOR item
16610   RETurn MK_IWL (ldef%, latt%, lptr)
16620 END DEFine RD_IWT
16630 :
16640 DEFine FuNction RD_IOT(nitem)
16650   LOCal count(3)
16660   LOCal item, ltyp, work1, work2
16670   LOCal ldef%(nitem,4), lptr(3,nitem), lstr$(nitem,85)
16680   FOR item = 0 TO nitem
16690     READ ldef%(item,0), ldef%(item,1), ldef%(item,2), ldef%(item,3)
16700     READ ltyp
16710     ldef%(item,4)=ltyp:  ltyp=(ltyp MOD 256)/2
16720     IF ltyp >10:ltyp=0
16730     IF ltyp
16740       READ lptr(0,item),lptr(ltyp,count(ltyp))
16750     ELSE
16760       if mycolour_mode%=2
16770           read work1,work2,work3
16780           lptr(0,item)=WL_4_IOL(work1,work2,work3)
16790       else
16800           READ work1
16810           READ work2: work1=work1*256+work2
16820           READ work2: lptr(0,item)=work1*256+work2
16830       endif
16840       READ lstr$(count(0))
16850     END IF
16860     count(ltyp) = count(ltyp) + 1
16870   END FOR item
16880   RETurn MK_IOL (ldef%(TO, 0 TO 1), ldef%(TO, 2 TO 3), lptr(0), ldef%(TO, 4), lstr$, lptr(1), lptr(2), lptr(3))
16890 END DEFine RD_IOT
16900 :
16910 DEFine PROCedure RD_IATT (lattr)
16920   LOCal i
16930   READ lattr(0,0), lattr(0,1)
16940   FOR i=1 TO 3: READ lattr(i,0), lattr(i,1), lattr(i,2), lattr(i,3)
16950 END DEFine RD_IATT
16960 :
16970 DEFine PROCedure RD_WATT (lattr%)
16980   READ lattr%(0), lattr%(1),lattr%(2),lattr%(3)
16990 END DEFine RD_WATT
17000 :
17010 DEFine FuNction RD_APPW
17020   LOCal ldef%(3), lspr, sk$, lattr%(3)
17030   READ ldef%(0), ldef%(1), ldef%(2), ldef%(3)
17040   RD_WATT lattr%
17050   READ lspr, sk$
17060   RETurn MK_APPW (ldef%, lattr%, lspr,sk$)
17070 END DEFine
17080 :
