; Set home directory for a job	v1.00		        2005  Wolfgang Lenerz
; 2005-10-25	1.01		some modifications, call vector instead of traps (mk)

	section gen_util

	xdef	gu_shome

	xref	gu_thjmp

	include 'dev8_keys_qdos_sms'
	include 'dev8_keys_thg'

;+++
; Set home directory for the given job in the home thing.
;
; Registers:
;		Entry			Exit
;	D0				error code
;	D1	Job-ID			preserved
;	A1	Pointer to file name	preserved
;---
gu_shome
homereg reg	a0-a4/d0-d3
d1stak	equ	4			; where D1 is on stack
	movem.l homereg,-(sp)		; keep my regs
	move.l	a1,a4			; save filename
	lea	home_name,a0		; point to name of thing
	moveq	#-1,d3			; wait forever
	moveq	#-1,d1			; I will use the thing
	move.l	#'SETH',d2		; extension in thing to use
	moveq	#sms.uthg,d0		; use thing
	jsr	gu_thjmp		; on return A2= ptr thg header, a1 to thg
	tst.l	d0			; ok?
	bne.s	no_thg			; no, ignore
	move.l	a1,a0			; pointer to thing (!!!)
	move.l	d1stak(sp),d1		; get job ID back
	sub.l	#12,sp			; get some space
	move.l	sp,a1			; and point to it
	move.l	d1,(a1) 		; insert ID of job
	move.l	#$c1000000,4(a1)	; thp.call+thp.str
	move.l	a4,8(a1)		; set pointer to this string
	jsr	thh_code(a0)		; call extn thing
	add.l	#12,sp			; reset stack
	lea	home_name,a0		; free thing, ignore error on call
	moveq	#sms.fthg,d0
	jsr	gu_thjmp
	moveq	#0,d0			; ignore any error
no_thg
	movem.l (sp)+,homereg
	rts

home_name
	dc.w  4,'HOME'

	end
