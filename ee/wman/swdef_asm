* Set sub-window definition  V1.00     1986  Tony Tebby   QJUMP
*
        section wman
*
        xdef    wm_swdef
*
        include dev8_keys_qdos_io
        include dev8_keys_wwork
*
*       d0  r   error return
*       a0 c  p channel ID of window
*       a3 c  p pointer to sub-window definition
*       a4 c  p pointer to working definition
*
*               all other registers preserved
*
reglist  reg    d1-d3/a1
*
wm_swdef
        movem.l reglist,-(sp)
*
        move.l  wwa_xorg(a3),d0         reset window
        add.l   ww_xorg(a4),d0          absolute origin
        move.l  d0,-(sp)
        move.l  wwa_xsiz(a3),-(sp)      set size
        move.l  sp,a1
        moveq   #iow.defw,d0            define window
        moveq   #0,d2                   no border
        moveq   #-1,d3
        trap    #3                      do trap
        tst.l   d0
        addq.l  #8,sp
        bne.s   wsw_exit
*
        moveq   #iow.spap,d0            set paper
        move.w  wwa_watt+wwa_papr(a3),d1
        trap    #3

        moveq   #iow.ssiz,d0            set size
        moveq   #0,d1
        moveq   #0,d2
        trap    #3
        tst.l   d0
*
wsw_exit
        movem.l (sp)+,reglist           restore registers
        rts
        end
