; HOTKEY Execute File	 V2.02	   1988   Tony Tebby	QJUMP
;
; 2003-03-11  2.01  Added hk_xfilid (MK)
; 2022-01-13  2.02  Added hk_xfimid, hk_xfiwid (wl)

	section hotkey

	xdef	hk_xfil
	xdef	hk_xfilid
	xdef	hk_xfimid
	xdef	hk_xfiwid


	xref	hk_dflts
	xref	hk_xname
	xref	gu_fexnm
	xref	gu_fcrnm
	xref	gu_achp0
	xref	gu_rchp

	include 'dev8_ee_hk_data'
	include 'dev8_ee_hk_xhdr'

;+++
; This routine executes a file defined by a hotkey item
;
;	a1 c  p pointer to item
;	a3 c  p pointer to pseudo Hotkey linkage
;
;	status return standard
;---
hk_xfil
	move.l	d1,-(sp)
	bsr.s	hk_xfilid
	movem.l (sp)+,d1
	rts

;+++
; This routine executes a file defined by a hotkey item
;
;	d1  r	job ID
;	a1 c  p pointer to item
;	a3 c  p pointer to pseudo Hotkey linkage
;
;	status return standard
;---
hk_xfilid
reglist reg	d2/a0/a1/a2/a3/a4/a5
	movem.l reglist,-(sp)
	moveq	#0,d1
hk_exec
	lea	gu_fexnm,a5
hk_do
	pea	hkd_buf2(a3)			 ; default buffer

	lea	hki_name(a1),a4 		 ; name
	add.w	(a4)+,a4			 ; end of it
	move.l	a4,d0
	addq.l	#1,d0 
	bclr	#0,d0
	move.l	d0,a4				 ; rounded up is start address

	jsr	hk_xname			 ; expand executable item name

	moveq	#32,d2				 ; standard priority
	swap	d2
	move.l	a4,a3				 ; start address
	move.l	a5,a4
	jsr	hk_dflts
	addq.l	#4,sp
	movem.l (sp)+,reglist
	rts


;+++
; This routine executes a file defined by a hotkey item with owner as per d1
;
;	d1 cr	owner job ID / created job ID
;	a1 c  p pointer to item
;	a3 c  p pointer to pseudo Hotkey linkage
;
;	status return standard
;---
hk_xfimid
	movem.l reglist,-(sp)
	bra.s	hk_exec


;+++
; This routine creates a job from  a file defined by a hotkey item with owner
; as per d1 w/o activating the job
;
;	d1  r	job ID of created job
;	a1 c  p pointer to item
;	a3 c  p pointer to Hotkey linkage
;
;	status return standard
;---
hk_xfiwid
	movem.l reglist,-(sp)
	moveq	#-1,d1
	lea	gu_fcrnm,a5
	bra.s	hk_do

	end
