; QLSD init code 1.00				 W. Lenerz + M. Kilgus 2018

	section init

	include 'dev8_dv3_qlsd_keys'
	include 'dev8_keys_qdos_sms'
	include 'dev8_keys_qlv'
	include 'dev8_keys_sys'
	include 'dev8_mac_proc'
	include 'dev8_mac_text'
	include 'dev8_mac_assert'

	xdef	init

	xref	win_init
	xref	dv3_addfd
	xref	qlf_table
	xref	inicrd

inirgs	reg	d7/a0/a3
ini.d7	equ	0
ini.a0	equ	4

; d7: 0 = ROM start, 1 = RESPR start
init	movem.l inirgs,-(a7)
	lea	qlf_table,a1
	jsr	dv3_addfd		; return d7 = "thing"
	jsr	win_init

	moveq	#sms.info,d0
	trap	#1
	assert	qlsd.smsq,0		; so we can reuse d0=0
	cmp.l	#sysid.sq,(a0)		; SMSQ/E
	beq.s	set_os

	moveq	#qlsd.minerva,d0
	move.l	sys_chtt(a0),a1 	; Minerva extension block lives here
	cmp.l	#'JSL1',$46(a1) 	; Magic
	beq.s	set_os			; Yes, Minerva

	moveq	#qlsd.qdos,d0		; Otherwise we assume QDOS
set_os	move.b	d0,qlsd_os(a3)

	moveq	#1,d5			; init drive 1
	jsr	inicrd
	lea	txt.nf(pc),a1
	bne.s	write_msg		; no card, don't mess with driver name

	lea	txt.in(pc),a1
	tst.l	ini.d7(sp)		; ROM start?
	bne.s	write_msg		; ... no, don't fake MDV name

	assert	qlsd.smsq,0
	tst.b	qlsd_os(a3)		; SMSQ?
	beq.s	write_msg		; ... yes, don't fake MDV name

	move.l	#'MDV0',ddl_dnuse+2(a3) ; Fake MDV name so we can auto-boot
	st	qlsd_fake(a3)		; Remember that we faked it
write_msg
	move.l	ini.a0(sp),a0		; channel ID
	move.w	ut.wtext,a2
	jsr	(a2)

	movem.l (a7)+,inirgs
	lea	procs,a1
	move.w	sb.inipr,a2
	jmp	(a2)
procs
	proc_stt
	proc_def CARD_INIT
	proc_def WIN_DRIVE
	proc_def WIN_USE
	proc_def WIN_CHECK
	proc_end
	proc_stt
	proc_end

txt.nf	mkstr	{Card 1 not found\}
txt.in	mkstr	{Card 1 initialised\}

	end
