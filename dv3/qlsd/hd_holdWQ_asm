; DV3 QLSD Hold / Release controller				 2018 M. Kilgus
; 2018-06-15  1.02  Mark card as recently used on release (wl+MK)
; 1.01 for use with the Qubide driver: must make sure that both drivers refer
; to the SAME lock (in WIN DDB) (wl)


	section dv3

	xdef	hd_hold
	xdef	hd_release

	include 'dev8_dv3_qlsd_keys'
	include 'dev8_keys_sys'
	include 'dev8_keys_err'
	include 'dev8_mac_assert'

;+++
; This routine is called to take the disk controller
;
;	d0 cr p preserved if OK
;	d7 c  p drive ID / number
;	a3 c  p linkage block
;
;	status return 0, err.nc, err.mchk
;---
hd_hold
	tst.b	hdl_err(a3)		; WIN or QUB?
	bne.s	qub_hold		; ... QUB
	bset	#7,qlsd_lock(a3)	; prevent poll from accessing device
hd_lb1	bne.s	hdh_nc
	cmp.b	d0,d0
hdh_rts
	rts

qub_hold
	move.l	a1,-(a7)
	move.l	qlsd_end(a3),a1 	; ptr to lock in WIN ddb
	bset	#7,(a1) 		; prevent poll from accessing device
	move.l	(a7)+,a1
	bra.s	hd_lb1
hdh_nc
	moveq	#err.nc,d0
	rts

;+++
; This routine is called to release the disk controller
;
;	d0 c  p
;	d7 c  p drive ID / number
;	a3 c  p linkage block
;
;	status return accoding to d0
;---
hd_release
	move.b	hdl_apnd(a3),hdl_actm(a3) ; ... let it go
	movem.l d7/a2,-(sp)
	lea	hdl_unit-1(a3),a2	; offset >8 bit, so explicit lea
	move.b	(a2,d7.w),d7		; card nbr (1...3)
	bset	d7,qlsd_crdused(a3)	; mark card as used
	tst.b	hdl_err(a3)		; WIN or QB?
	bne.s	qub_rel 		; ... QUB
	bclr	#7,qlsd_lock(a3)	; release device
hdr_out movem.l (sp)+,d7/a2
	tst.l	d0
	rts

qub_rel
	move.l	qlsd_end(a3),a2
	bclr	#7,(a2) 		; release device
	bra.s	hdr_out
	end
