; Flip the cursor			V2.00   1998  Tony Tebby
;
;	This routine flips the cursor from visible to invisible, or vice
;	versa.
;
	section con

	include 'dev8_keys_con'

	xref	cn_donl
	xref	cn_ssuba
	xref	cn_xblock
	xref	cn_crcol
	xref	cn_cksize

	xdef	cn_ecurs
	xdef	cn_dcurs
	xdef	cn_curtg

cn_dcurs
	tst.b	sd_curf(a0)		; active
	beq.s	cnc_ok			; ... no
	sf	sd_curf(a0)		; inactive now
	bgt.s	cn_curt0		; make it invisible
cnc_ok
	moveq	#0,d0
	rts

cn_ecurs
	tst.b	sd_curf(a0)		; already active
	bne.s	cnc_ok			; ... yes
	tst.b	sd_sflag(a0)		; size OK?
	beq.s	cnec_do 		; ... yes
	bgt.s	cnc_ok			; cursor out of window
	jsr	cn_cksize		; re-check size
	blo.s	cnc_ok			; ... no cursor
cnec_do
	move.b	#1,sd_curf(a0)		; set cursor to active and visible

	tst.b	sd_nlsta(a0)		; newline status
	beq.s	cn_curt0		; ... ok
	jsr	cn_donl 		; ... it is now

cn_curt0
	moveq	#0,d0

curreg	reg	d0-d4/d7/a0-a3

cn_curtg
	movem.l curreg,-(sp)
cnc_do
	moveq	#cn.bpcur,d3		; just the cursor
	jsr	cn_ssuba(pc)		; find that sub-area

	move.l	sd_scrb(a0),a1		; get base address...
	move.w	sd_linel(a0),a2 	; ...and row increment
	move.l	cn_crcol,d7
	moveq	#-1,d6			; no stipple

	jsr	cn_xblock		; XOR a block like that

	movem.l (sp)+,curreg
cnc_exit
	tst.l	d0
	rts
	end
