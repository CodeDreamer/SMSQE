* Find free space in common heap / TPA  V2.00    1986  Tony Tebby  QJUMP
*
        section mem
*
        xdef    mem_frch
        xdef    mem_frtp
*
        xref    sms_rte
*
        include dev8_keys_sys
        include dev8_keys_sbt
        include dev8_keys_chp
*
*       d0  rs  0, no errors
*       d1  r   free space, limited to 64K default
*       a6 c  p pointer to system vars
*
*       all other registers preserved
*
*************************************************
*       NOTE: returns directly through SMS return
*************************************************
*
mem_frtp
mem_frch
        move.l  sys_sbab(a6),d1
        sub.l   sys_fsbb(a6),d1
        sub.l   #$400,d1                 ; two slave blocks spare
        blt.s   mfr_zero
        cmp.l   sys_mxfr(a6),d1          ; more than max?
        ble.s   mfr_ok                   ; ... no
        move.l  sys_mxfr(a6),d1
mfr_ok
        moveq   #0,d0
        bra.l   sms_rte

mfr_zero
        moveq   #0,d1                    ; no room at all
        bra.s   mfr_ok

        end
