; FREE_FMFM AND ALFM keywords 1.00 (c) W. Lenerz 2017

	section procs

	xdef	alfm
	xdef	free_fmem

	xref	ut_rtfd1
	xref	ut_gxin1

	include dev8_keys_q68
	include dev8_keys_err

free_fmem
	lea	ut_rtfd1,a2		; float & return d1
	move.l	a2,-(a7)
ffmem2
	move.l	#q68_sramb,a2		; pointer to first free mem in fast ram
	move.l	#q68_sramt,d1		; top of ram
	move.l	(a2),d3
	sub.l	d3,d1
	subq.l	#4,d1			; remaining free mem
	bge.s	ok
	clr.l	d1			; nothing remains
ok	rts


alfm	jsr	ut_gxin1
	bne.s	ok
	move.w	(a6,a1.l),d0
	addq.l	#2,$58(a6)
	bsr.s	ffmem2			; check how much mem is free
	ble.s	oom
	sub.l	d0,d1			; we need this much mem
	blt.s	oom			; but there's not enough remaining
	move.l	d3,d1			; ponter to free mem
	add.l	d0,d3			; new ram top
	move.l	d3,(a2)
	clr.l	d0
	jmp	ut_rtfd1

oom	moveq	#err.imem,d0
	rts

	end
