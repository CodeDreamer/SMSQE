; Q68 Receive data from port   1.02   2017-2024       W. Lenerz
;
;
; 2024-05-03	 1.02	check active before checking hardware (wl)
; 2023.02.18	 1.01	Chains to tx. Can handle 2 ser ports (wl).
				     
; based on
;
; QXL Receive data from port	 2000	     Tony Tebby

	section spp

	xdef	spp_rxser

	xref	iob_pbyt
	xref	iob_room
	xref	iob_eof

	xref	spp_txser		; check 2 ports for sending
	xref	q68_txser		; check 1 port for sending


	include 'dev8_keys_q68'
	include 'dev8_keys_serparprt'
	include 'dev8_keys_sys'

;+++
; Receive data from port
;
;	a3 c  p linkage area for port 1
;---
spp_rxser
	bsr.s	srxp_get		; get data for port 1
	btst	#0,q68_mach		; Q68 or Qimsi?
	beq	q68_txser		; yes, go directly to tx FOR Q68/qimsi!
	pea	spp_txser		; go to tx routine after return
	add.w	#spd.len,a3		; point linkage for port 2

srxp_get

	move.l	spd_hbase(a3),a4	; port hardware base
	btst	#q68..rxmpty,ser_status(a4) ; receive buffer empty?
	bne.s	srxp_exit		; yes, done ->
 
	tst.b	spd_iact(a3)		; is input active?
	beq.s	srxp_exit
	move.l	spd_ibuf(a3),d0 	; input queue
	ble.s	srxp_exit		; there is none ->
	move.l	d0,a2

	jsr	iob_room		; check room left
	move.l	d0,d3			; this much is left
	ble.s	srxp_noinput		; <=0: no room left - disable input
	cmp.l	#15,d3			; not more than 16 bytes a pop
	blt.s	srxp_loop
	moveq	#15,d3			; get 16 bytes (if there are any)
srxp_loop
	move.b	ser_rxdta(a4),d1	; get byte from hardware

; now check for CTRL Z etc
rx_chk
	move.b	spb_fz(a2),d0		; <FF> or CTRL Z
	subq.b	#1,d0
	ble.s	srxp_ckcr		; ... not CTRL Z
	move.b	d1,d0
	tst.b	spb_prty(a2)		; parity?
	beq.s	srxp_ckcz		; ... no
	bclr	#7,d0			; ... ignore it
srxp_ckcz
	cmp.b	#26,d0			; is it CTRL Z?
	beq.s	srxp_ctrz		; yes, close shop
srxp_ckcr
	move.b	spb_cr(a2),d0		; cr to lf?
	beq.s	srxp_pbyt		; ... no
	cmp.b	d0,d1			; cr?
	bne.s	srxp_pbyt		; ... no
	move.b	spb_lf(a2),d1		; ... yes
	beq.s	srxp_eloop		; cr ignored
srxp_pbyt
	jsr	iob_pbyt		; put byte in buffer
srxp_eloop
	btst	#q68..rxmpty,ser_status(a4)
	bne.s	srxp_exit
srxp_dolp
	dbf	d3,srxp_loop
srxp_exit
	rts
srxp_ctrz
	jsr	iob_eof 		; mark end of file
	st	spd_ibuf(a3)		; no input buffer pointer now
srxp_noinput
	sf	spd_iact(a3)		; inactivate input but keep interrupt alive
	rts				; done

	end
