; SMSQmulator control extension thing (c) W. Lenerz 2017

************************************************************************
*	Copying to/from the Scrap to/from the clipboard
*
*	v. 1.04  2017 Apr 07 made into extn thing
*
*	copyright (C) W. Lenerz 2016-2017
*
************************************************************************


	section exten

	include dev8_keys_thg
	include dev8_keys_sbasic
	include dev8_keys_menu
	include dev8_keys_scrap
	include dev8_keys_java
	include dev8_mac_proc2
	include 'dev8_mac_thg'

	xdef	jt_sget

	xref	gu_achp0
	xref	ut_usscp
	xref	gu_rchp
	xref	ut_frscp
	xref	thp_nul

stack	equ	32060

mregs	reg	a0/a1/a4/d2

;********
; JVA_SGET
; no parameter
; GET content of system clipboard and put it into the menu extensions' scrap
;********

jt_sget thg_extn {SGET},jt_sput,thp_nul
	movem.l mregs,-(a7)
	move.l	#'PUT ',d2		; we're PUTting things into the scrap
	jsr	ut_usscp		; use extension
	bne.s	exit			; ooops
	move.l	a1,a4

	move.l	#stack,d0
	jsr	gu_achp0		; get mem
	bne.s	exit1			; ooops

	move.b	#0,scp_dtyp(a0) 	; what to put = string
	move.l	#0,scp_styp(a0) 	; source type
	move.l	#0,scp_flln(a0) 	; flag : overwrit existing string
	move.l	#0,scp_uccd(a0) 	; no additional copy routine
	lea	52(a0),a1		; point to place for string
	move.l	a1,scp_strg(a0) 	; set in parameters
	moveq	#jt5.xc2s,d0
	dc.w	jva.trp5		; get data into (a1) from java

	move.l	a0,a1			; point to params now
	jsr	thh_code(a4)		; call the scrap extension routine
	move.l	d0,-(a7)
exit3	jsr	gu_rchp 		; release space
	bra.s	exit2
exit1	move.l	d0,-(a7)
exit2	bsr	ut_frscp		; free	scrap
	move.l	(a7)+,d0
exit	movem.l (a7)+,mregs
	tst.l	d0
	rts

 
;********
; JVA_SPUT
; no parameter
; PUT content of the menu extensions' scrap into the system clipboard
;********

jt_sput thg_extn {SPUT},,thp_nul
	movem.l mregs,-(a7)
	move.l	#'GET ',d2		; we're GETting things from the scrap
	jsr	ut_usscp		; use extension
	bne.s	exit			; ooops
	move.l	a1,a4

	move.l	#stack,d0
	jsr	gu_achp0		; get mem
	bne.s	exit1			; ooops

	move.b	#0,scg_dtyp(a0) 	; what to get = string
	move.l	#32000,scg_bufl(a0)	; size of buffer
	move.l	#0,scg_uccd(a0) 	; no additional copy routine
	lea	52(a0),a1		; point to place for string
	move.l	a1,scg_dest(a0) 	; where to copy to

	move.l	a0,a1			; point to params now
	jsr	thh_code(a4)		; call the scrap extension routine
	move.l	d0,-(a7)
	bne.s	exit3
	lea	52(a0),a1

	moveq	#jt5.xs2cp,d0
	dc.w	jva.trp5		; get data at (a1) to java
	bra.s	exit3

	end
