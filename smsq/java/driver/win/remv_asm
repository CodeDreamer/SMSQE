; SMSQmulator WIN Driver: set drive removable and write protect status v. 1.01
;
; copyright (C) w. Lenerz 2020 -2024 published under the SMSQE licence

; 2024-04-24 1.01 impement WIN_WP
;	     1.00 initial version

	section nfa

	xdef	win_remv
	xdef	win_wp

	include 'dev8_keys_qlv'
	include 'dev8_keys_err'
	include 'dev8_keys_java'
	include 'dev8_keys_sbasic'
win_wp
	moveq	#jte.dwp,d7
	bra.s	common
win_remv
	moveq	#jte.remv,d7
common
	move.w sb.gtlin,a2
	jsr	(a2)
	bne.s	wr_exit
	moveq	#1,d2			; preset status - is removable / protected
	move.l	(a6,a1.l),d1		; drive nbr
	lsl.l	#2,d3
	add.l	d3,sb_arthp(a6)
	subq.w	#4,d3			; one more param?
	blt.s	wr_err			; there wasn't even one!
	beq.s	cl_java
	move.l	4(a6,a1.l),d2		; status (0 =  not removable/wp, 1=removable)
cl_java move.l	d7,d0
	dc.w	jva.trpE		; call java, sets status
	tst.l	d0
wr_exit rts
wr_err	moveq	#err.ipar,d0
	rts

	end
